<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: http/src/HTTPApp.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>http/src/HTTPApp.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright (C) 1995, Digital Equipment Corporation. 
 All rights reserved. 
 Created by steveg 
                                                                           
 Parts Copyright (C) 1997, Columbia University                             
 All rights reserved.                                                      

 * Last Modified By: Blair MacIntyre
 * Last Modified On: Mon Aug  4 14:50:46 1997
 

<P><PRE>MODULE <module><implements><A HREF="HTTPApp.i3.html">HTTPApp</A></implements></module>;

&lt;* PRAGMA LL *&gt;

IMPORT <A HREF="App.i3.html">App</A>, <A HREF="../../tcp/src/common/ConnRW.i3.html">ConnRW</A>, <A HREF="../../m3core/src/float/IEEE-default/FloatMode.i3.html">FloatMode</A>, <A HREF="../../libm3/src/fmtlex/Fmt.i3.html">Fmt</A>, <A HREF="HTTP.i3.html">HTTP</A>, <A HREF="../../tcp/src/common/IP.i3.html">IP</A>, <A HREF="../../libm3/src/fmtlex/Lex.i3.html">Lex</A>, <A HREF="../../libm3/src/rw/Rd.i3.html">Rd</A>, <A HREF="../../libm3/src/rw/RdUtils.i3.html">RdUtils</A>, <A HREF="../../tcp/src/common/TCP.i3.html">TCP</A>,
       <A HREF="../../tcp/src/common/TCPPeer.i3.html">TCPPeer</A>, <A HREF="../../m3core/src/text/Text.i3.html">Text</A>, <A HREF="../../libm3/src/text/TextExtras.i3.html">TextExtras</A>, <A HREF="../../libm3/src/rw/TextRd.i3.html">TextRd</A>, <A HREF="../../m3core/src/thread/Common/Thread.i3.html">Thread</A>, <A HREF="../../libm3/src/rw/Wr.i3.html">Wr</A>;
</PRE> ClientLog logs the message to the client (through <CODE>wr</CODE>) and 
   to the application log (through <CODE>log</CODE>)
 
<PRE>TYPE
  ClientLog = App.Log OBJECT
    appLog: App.Log;
    wr: Wr.T;
    msg: TEXT;
    serverPush: BOOLEAN;
  OVERRIDES
    log := MessageLog;
  END;

PROCEDURE <A NAME="MessageLog"><procedure>MessageLog</procedure></A> (self: ClientLog; msg: TEXT; status: App.LogStatus)
  RAISES {App.Error} =
  BEGIN
    IF status = App.LogStatus.Error THEN
      IF self.serverPush THEN
        self.msg := Fmt.F(&quot;%s%s\n&quot;, self.msg, msg);
        ServerPushFrame(self.wr, &quot;text/plain&quot;, self.msg, self.appLog);
      ELSE
        TRY
          Wr.PutText(self.wr, msg);
          Wr.PutText(self.wr, &quot;\n&quot;);
        EXCEPT
        | Wr.Failure, Thread.Alerted =&gt;
            self.appLog.log(
              &quot;Error sending message to client&quot;, App.LogStatus.Error);
        END;
      END;
    END;
    self.appLog.log(msg, status);
  END MessageLog;

PROCEDURE <A NAME="WrLog"><procedure>WrLog</procedure></A> (old: App.Log; wr: Wr.T; serverPush: BOOLEAN := FALSE):
  App.Log =
  BEGIN
    RETURN NEW(ClientLog, appLog := old, wr := wr,
               msg := &quot;&quot;, serverPush := serverPush);
  END WrLog;

VAR
  readWriteMu := NEW(MUTEX);
  readWriteCV := NEW(Thread.Condition);
  readingCnt := 0;
  writingCnt := 0;
  (* single writer, multiple reader algorithm.

     if a thread is writing, then writingCnt # 0.
     if a thread is reading, then readingCnt # 0.

     a thread can read if another thread is reading.
     a thread cannot read if another thread is writing.
     a thread can write if no thread is reading or writing.

     readingCnt and writingCnt are protected by readWriteMu.
  *)

PROCEDURE <A NAME="ReadLock"><procedure>ReadLock</procedure></A>() =
  BEGIN
    LOCK readWriteMu DO
      WHILE writingCnt # 0 DO
        Thread.Wait(readWriteMu, readWriteCV);
      END;

      INC(readingCnt);
    END;
  END ReadLock;

PROCEDURE <A NAME="ReadUnlock"><procedure>ReadUnlock</procedure></A>() =
  BEGIN
    LOCK readWriteMu DO
      DEC(readingCnt);
    END;
  END ReadUnlock;

PROCEDURE <A NAME="WriteLock"><procedure>WriteLock</procedure></A>() =
  BEGIN
    LOCK readWriteMu DO
      WHILE readingCnt # 0 OR writingCnt # 0 DO
        Thread.Wait(readWriteMu, readWriteCV);
      END;

      INC(writingCnt);
    END;
  END WriteLock;

PROCEDURE <A NAME="WriteUnlock"><procedure>WriteUnlock</procedure></A>() =
  BEGIN
    LOCK readWriteMu DO
      DEC(writingCnt);
    END;
  END WriteUnlock;

REVEAL
  <A NAME="RequestHandler">RequestHandler</A> = RequestHandlerPublic BRANDED
  &quot;HTTPApp.RequestHandler&quot; OBJECT
    port: INTEGER;
  OVERRIDES
    accept := DefaultAccept;
    request := DefaultRequest;
  END;

PROCEDURE <A NAME="DefaultAccept"><procedure>DefaultAccept</procedure></A> (&lt;* UNUSED *&gt;     self       : RequestHandler;
                         &lt;* UNUSED *&gt;     request    : HTTP.Request;
                         &lt;* UNUSED *&gt;     serverData : REFANY;
                         &lt;* UNUSED *&gt; VAR acceptState: REFANY;
                         &lt;* UNUSED *&gt;     log        : App.Log         ):
  BOOLEAN =
  BEGIN
    &lt;* ASSERT FALSE *&gt;
  END DefaultAccept;

PROCEDURE <A NAME="DefaultRequest"><procedure>DefaultRequest</procedure></A>(&lt;* UNUSED *&gt; self: RequestHandler;
                         &lt;* UNUSED *&gt; request: HTTP.Request;
                         &lt;* UNUSED *&gt; serverData, acceptState: REFANY;
                         &lt;* UNUSED *&gt; rd: Rd.T;
                         &lt;* UNUSED *&gt; wr: Wr.T;
                         &lt;* UNUSED *&gt; log: App.Log) =
  BEGIN
    &lt;* ASSERT FALSE *&gt;
  END DefaultRequest;

REVEAL
  <A NAME="ReplyHandler">ReplyHandler</A> = ReplyHandlerPublic BRANDED &quot;HTTPApp.ReplyHandler&quot; OBJECT
  OVERRIDES
    reply := DefaultReply;
  END;

PROCEDURE <A NAME="DefaultReply"><procedure>DefaultReply</procedure></A>(&lt;* UNUSED *&gt; self: ReplyHandler;
                       &lt;* UNUSED *&gt; reply: HTTP.Reply;
                       &lt;* UNUSED *&gt; rd: Rd.T;
                       &lt;* UNUSED *&gt; wr: Wr.T;
                       &lt;* UNUSED *&gt; log: App.Log) =
  BEGIN
    &lt;* ASSERT FALSE *&gt;
  END DefaultReply;

REVEAL
  <A NAME="Proxy">Proxy</A> = ProxyPublic BRANDED &quot;HTTPApp.Proxy&quot; OBJECT
          OVERRIDES
            init := InitProxy;
            add  := AddProxy;
          END;

PROCEDURE <A NAME="InitProxy"><procedure>InitProxy</procedure></A> (self: Proxy): Proxy =
  BEGIN
    self.rules := NIL;
    self.tail := NIL;
    RETURN self;
  END InitProxy;

CONST
  NotComma = SET OF CHAR{'\000' .. '\377'} - SET OF CHAR{','};

PROCEDURE <A NAME="AddProxy"><procedure>AddProxy</procedure></A> (self: Proxy; ruleTxt: TEXT; log: App.Log)
  RAISES {App.Error} =
  VAR
    rd               := TextRd.New(ruleTxt);
    rule             := NEW(ProxyRules);
    prev: ServerList := NIL;
    host: TEXT;
  BEGIN
    IF log = NIL THEN log := App.defaultLog; END;
    TRY
      rule.hostPattern := Lex.Scan(rd);
      IF Text.Length(rule.hostPattern) = 0
           OR Text.GetChar(
                rule.hostPattern, Text.Length(rule.hostPattern) - 1) = '\\' THEN
        log.log(
          Fmt.F(&quot;Bad pattern (%s) in hostPattern proxy(s) value: \&quot;%s\&quot;&quot;,
                rule.hostPattern, ruleTxt), App.LogStatus.Error);
      END;

      TRY
        LOOP
          Lex.Skip(rd);
          host := Lex.Scan(rd, NotComma);
          IF Text.Length(host) &gt; 0 THEN
            WITH server = NEW(ServerList,
                              head := NEW(Server).initParse(host, log),
                              tail := NIL) DO
              IF prev = NIL THEN
                prev := server;
                rule.proxy := prev;
              ELSE
                prev.tail := server;
                prev := prev.tail
              END;
            END;
          END;
          EVAL Rd.GetChar(rd);
        END;
      EXCEPT
      | Rd.EndOfFile =&gt;
      END;
    EXCEPT
    | Rd.Failure, Thread.Alerted =&gt;
        log.log(Fmt.F(&quot;Bad urlPattern:proxy value: \&quot;%s\&quot;&quot;, ruleTxt),
                App.LogStatus.Error);
    END;
    IF self.tail = NIL THEN
      self.rules := rule;
    ELSE
      self.tail.tail := rule;
    END;
    self.tail := rule;
  END AddProxy;

REVEAL
  <A NAME="Server">Server</A> = ServerPublic BRANDED &quot;HTTPApp.Server&quot; OBJECT
  OVERRIDES
    init := InitServer;
    initParse := InitParseServer;
  END;

PROCEDURE <A NAME="InitServer"><procedure>InitServer</procedure></A> (self  : Server;
                      server: TEXT;
                      port  : INTEGER;
                      log   : App.Log  ): Server RAISES {App.Error} =
  VAR addr: IP.Address;
  BEGIN
    IF Text.Equal(server, &quot;DIRECT&quot;) THEN
      self.server := NIL
    ELSE
      self.server := server;
      self.port := port;
      self.endpoint := IP.NullEndPoint;
      TRY
        IF NOT IP.GetHostByName(server, addr) THEN
          log.log(Fmt.F(&quot;Cannot find %s&quot;, server), App.LogStatus.Status);
          RETURN self;
        END;
      EXCEPT
      | IP.Error =&gt;
          log.log(Fmt.F(&quot;Cannot find %s&quot;, server), App.LogStatus.Status);
          RETURN self;
      END;
      self.endpoint := IP.Endpoint{addr := addr, port := port};
    END;
    RETURN self;
  END InitServer;

CONST
  NonColon = SET OF CHAR{'\000'..'\377'} - SET OF CHAR{':'};
  NonSlash = SET OF CHAR{'\000'..'\377'} - SET OF CHAR{'/'};

PROCEDURE <A NAME="InitParseServer"><procedure>InitParseServer</procedure></A> (self: Server; serverAndPort: TEXT; log: App.Log):
  Server RAISES {App.Error} =
  VAR
    server: TEXT;
    port         := 0;
  BEGIN
    IF Text.Equal(serverAndPort, &quot;DIRECT&quot;) THEN
      server := &quot;DIRECT&quot;;
    ELSE
      WITH rd = TextRd.New(serverAndPort) DO
        TRY
          server := Lex.Scan(rd, NonColon);
          IF Text.Equal(server, &quot;http&quot;) THEN
            WITH url = NEW(HTTP.URL).init(serverAndPort, log) DO
              server := url.host;
              port := url.port;
            END;
          ELSE
            IF NOT Rd.EOF(rd) THEN
              EVAL Rd.GetChar(rd);
              port := Lex.Int(rd);
            END;
          END;
        EXCEPT
        | Lex.Error, FloatMode.Trap, Rd.EndOfFile, Rd.Failure,
              Thread.Alerted =&gt;
            log.log(
              Fmt.F(&quot;Bad server and port given: \&quot;%s\&quot; (need server:port)&quot;,
                    serverAndPort), App.LogStatus.Error);
        END;
      END;
    END;
    RETURN self.init(server, port, log);
  END InitParseServer;

TYPE
  RequestHandlerList = REF RECORD
    head: RequestHandler;
    tail: RequestHandlerList;
  END;

VAR
  requestHandlerList: RequestHandlerList := NIL;

PROCEDURE <A NAME="FindRequestHandler"><procedure>FindRequestHandler</procedure></A> (              request      : HTTP.Request;
                                            serverData   : REFANY;
                              VAR (* OUT *) acceptState  : REFANY;
                                            port, service: INTEGER;
                                            log          : App.Log       ):
  RequestHandler RAISES {App.Error} =
  VAR list: RequestHandlerList;
  BEGIN
    ReadLock();
    TRY
      IF requestHandlerList = NIL THEN RETURN NIL END;
      FOR i := FIRST(RequestPriority) TO LAST(RequestPriority) DO
        list := requestHandlerList;
        REPEAT
          IF list.head.priority = i
               AND (list.head.port = service OR list.head.port = port
                      OR list.head.port = AnyPort)
               AND list.head.accept(request, serverData, acceptState, log) THEN
            RETURN list.head
          END;
          list := list.tail;
        UNTIL list = NIL;
      END;
    FINALLY
      ReadUnlock();
    END;
    RETURN NIL;
  END FindRequestHandler;

TYPE
  Closure = Thread.Closure OBJECT
              client: TCP.T;
              log   : App.Log;
              wrLog : BOOLEAN;
              data  : REFANY;
              port, service  : INTEGER;
            OVERRIDES
              apply := ServerHandler;
            END;

PROCEDURE <A NAME="GetNameDontCrash"><procedure>GetNameDontCrash</procedure></A> (host: TCP.T): TEXT RAISES {IP.Error} =
  VAR res := TCPPeer.GetName(host);
  BEGIN
    IF res = NIL THEN
      WITH ep = TCPPeer.Get(host) DO
        RETURN
          Fmt.F(&quot;%s.%s.%s.%s&quot;, Fmt.Int(ep.addr.a[0]), Fmt.Int(ep.addr.a[1]),
                Fmt.Int(ep.addr.a[2]), Fmt.Int(ep.addr.a[3]));
      END;
    END;
    RETURN res
  END GetNameDontCrash;

PROCEDURE <A NAME="ServerHandler"><procedure>ServerHandler</procedure></A> (cl: Closure): REFANY =
  VAR
    rd            : Rd.T;
    wr            : Wr.T;
    request       : HTTP.Request;
    requestHandler: RequestHandler;
    acceptState   : REFANY;
  BEGIN
    TRY
      TRY
        rd := ConnRW.NewRd(cl.client);
        wr := ConnRW.NewWr(cl.client);
        IF cl.wrLog THEN cl.log := WrLog(cl.log, wr, FALSE); END;

        request := NEW(HTTP.Request).parse(rd, cl.log);
        IF Text.Length(request.url.host) = 0 THEN
          request.url.host := App.GetHostName();
          request.url.port := cl.port;
        END;

        IF App.Verbose() THEN
          cl.log.log(Fmt.F(&quot;INCOMING REQUEST: %s&quot;,
                           request.toText(NIL, TRUE, cl.log)),
                     App.LogStatus.Verbose);
        ELSIF App.Debug() THEN
          TRY
            cl.log.log(Fmt.F(&quot;%s %s&quot;, GetNameDontCrash(cl.client),
                             request.url.toText()), App.LogStatus.Debug);
          EXCEPT
          | IP.Error =&gt;
          END;
        END;

        requestHandler := FindRequestHandler(request, cl.data, acceptState,
                                             cl.port, cl.service, cl.log);
        IF requestHandler # NIL THEN
          requestHandler.request(
            request, cl.data, acceptState, rd, wr, cl.log);
        ELSE
          TRY
            HTTP.WriteSimpleReplyHeader(
              wr, NIL, cl.log, HTTP.StatusCode[HTTP.StatusType.Not_Found],
              HTTP.StatusReason[HTTP.StatusType.Not_Found]);
            Wr.PutText(wr, &quot;Content-type: text/plain\r\n\r\n&quot;);
            Wr.PutText(wr, Fmt.F(&quot;The requested item: %s was not found\n&quot;,
                                 request.url.toText()));
          EXCEPT
          | Wr.Failure, Thread.Alerted =&gt;
          END;
        END;
      FINALLY
        TRY Wr.Close(wr); EXCEPT | Wr.Failure, Thread.Alerted =&gt; END;
        TRY Rd.Close(rd); EXCEPT | Rd.Failure, Thread.Alerted =&gt; END;
        Thread.Pause(30.0d0);
        TCP.Close(cl.client);
      END;
    EXCEPT
    | App.Error =&gt;
    END;
    RETURN NIL;
  END ServerHandler;

TYPE
  ServerPorts = REF RECORD
    port, service: INTEGER;
    next: ServerPorts;
  END;

VAR
  serverPorts: ServerPorts := NIL; &lt;* LL = readWriteMu *&gt;

PROCEDURE <A NAME="ServerPort"><procedure>ServerPort</procedure></A> (port, service: INTEGER): BOOLEAN =
  VAR sp: ServerPorts;
  BEGIN
    LOCK readWriteMu DO
      sp := serverPorts;
      WHILE sp # NIL DO
        IF sp.port = port
             AND (sp.service = service OR sp.service = AnyService) THEN
          RETURN TRUE;
        END;
        sp := sp.next;
      END;
      RETURN FALSE;
    END;
  END ServerPort;

PROCEDURE <A NAME="Serve"><procedure>Serve</procedure></A> (port, serviceValue: INTEGER; log: App.Log; data: REFANY)
  RAISES {App.Error} =
  VAR
    conn     : TCP.T;
    connector: TCP.Connector;
    nilLog                   := log = NIL;
  BEGIN
    LOCK readWriteMu DO
      serverPorts := NEW(ServerPorts, port := port, service := serviceValue,
                                    next := serverPorts);
    END;

    IF log = NIL THEN log := App.defaultLog END;
    TRY
      connector := TCP.NewConnector(
                     IP.Endpoint{addr := IP.NullAddress, port := port});
    EXCEPT
    | IP.Error (cause) =&gt;
        log.log(&quot;IP.Error making connector: &quot; &amp; RdUtils.FailureText(cause),
                App.LogStatus.Error);
        RETURN;
    END;
    log.log(
      Fmt.F(&quot;Listening on port: %s&quot;, Fmt.Int(port)), App.LogStatus.Status);

    LOOP
      TRY
        conn := TCP.Accept(connector);
        EVAL Thread.Fork(
               NEW(Closure, client := conn, log := log, wrLog := nilLog,
                   data := data, port := port, service := serviceValue));
      EXCEPT
      | IP.Error, Thread.Alerted =&gt; (* continue *)
      END;
    END;
  END Serve;

PROCEDURE <A NAME="Direct"><procedure>Direct</procedure></A> (request: HTTP.Request; log: App.Log): TCP.T
  RAISES {App.Error} =
  VAR server := NEW(Server).init(request.url.host, request.url.port, log);
  BEGIN
    TRY
      RETURN TCP.Connect(server.endpoint);
    EXCEPT
    | IP.Error, Thread.Alerted =&gt;
        log.log(Fmt.F(&quot;Unable to connect to server: %s:%s&quot;, server.server,
                      Fmt.Int(server.port)), App.LogStatus.Error);
    END;
    &lt;* ASSERT FALSE *&gt;
  END Direct;

PROCEDURE <A NAME="Client"><procedure>Client</procedure></A> (             request : HTTP.Request;
                               proxy   : Proxy;
                               style   : HTTP.Style;
                               rdClient: Rd.T;
                               wrClient: Wr.T;
                               handler : ReplyHandler;
                  &lt;* UNUSED *&gt; service : INTEGER;
                               log     : App.Log       )
  RAISES {App.Error} =
  VAR
    conn        : TCP.T;
    wrServer    : Wr.T;
    rdServer    : Rd.T;
    rules       : ProxyRules;
    proxyRequest: BOOLEAN    := FALSE;
    proxies     : ServerList;
    hp          : TEXT;
  BEGIN
    IF log = NIL THEN log := App.defaultLog END;
    IF style = NIL THEN style := HTTP.DefaultStyle() END;
    IF App.Verbose() THEN
      log.log(
        Fmt.F(&quot;OUTGOING request: %s&quot;, request.toText(style, TRUE, log)),
        App.LogStatus.Verbose);
    END;

    IF Text.Length(request.url.host) = 0 THEN
      log.log(Fmt.F(&quot;No server given in requested URL: %s&quot;,
                    request.url.toText()), App.LogStatus.Error);
    END;

    IF proxy = NIL THEN
      conn := Direct(request, log);
    ELSE
      rules := proxy.rules;
      hp := Fmt.F(&quot;%s:%s&quot;, request.url.host, Fmt.Int(request.url.port));
      WHILE rules # NIL AND conn = NIL DO
        IF TextExtras.PatternMatch(hp, rules.hostPattern) THEN
          IF App.Verbose() THEN
            IF rules.proxy.head.server = NIL THEN
              log.log(Fmt.F(&quot;proxy rule matched: %s %s DIRECT&quot;,
                            Fmt.F(&quot;%s:%s&quot;, request.url.host,
                                  Fmt.Int(request.url.port)),
                            rules.hostPattern), App.LogStatus.Verbose);
            ELSE
              log.log(
                Fmt.F(&quot;proxy rule matched: %s %s %s&quot;,
                      Fmt.F(&quot;%s:%s&quot;, request.url.host,
                            Fmt.Int(request.url.port)), rules.hostPattern,
                      rules.proxy.head.server), App.LogStatus.Verbose);
            END;
          END;
          proxies := rules.proxy;
          WHILE proxies # NIL DO
            WITH server = proxies.head DO
              IF server.server = NIL THEN
                conn := Direct(request, log);
                EXIT;
              ELSE
                TRY
                  IF server.endpoint = IP.NullEndPoint THEN
                    (* try to resolve another time *)
                    EVAL server.init(server.server, server.port, log);
                  END;
                  conn := TCP.Connect(server.endpoint);
                  proxyRequest := TRUE;
                  EXIT;
                EXCEPT
                | IP.Error, Thread.Alerted =&gt;
                    log.log(
                      Fmt.F(&quot;Unable to connect to proxy server: %s:%s&quot;,
                            server.server, Fmt.Int(server.port)),
                      App.LogStatus.Status);
                END;
              END;
            END;
            proxies := proxies.tail;
          END;
        END;
        rules := rules.tail;
      END;

      IF conn = NIL THEN
        log.log(Fmt.F(&quot;proxy rule matched: %s DIRECT (default)&quot;,
                      Fmt.F(&quot;%s:%s&quot;, request.url.host,
                            Fmt.Int(request.url.port))),
                App.LogStatus.Verbose);
        conn := Direct(request, log);
      END;
    END;

    wrServer := ConnRW.NewWr(conn);
    rdServer := ConnRW.NewRd(conn);
    TRY
      TRY
        TRY
          request.write(wrServer, style, proxyRequest, log);
          IF rdClient # NIL AND request.method = HTTP.Method.Post
               AND request.postData # NIL THEN
            Wr.PutText(wrServer, request.postData);
          END;
        FINALLY
          (* SCG/MSM 2/22/96 A CERN proxy gets confused if we close the wr
             while still using the rd.

             Symptoms:

             Sending large files though the cern proxy fail partway
             through.  The reader blocks on a read and the socket is in
             FIN_WAIT_2. *)
          Wr.Flush(wrServer);
        END;
        WITH reply = NEW(HTTP.Reply).parse(rdServer, log) DO
          IF App.Verbose() THEN
            log.log(reply.toText(NIL, log), App.LogStatus.Verbose);
          END;
          handler.reply(reply, rdServer, wrClient, log);
        END;
      FINALLY
        Wr.Close(wrServer);
        Rd.Close(rdServer);
        TCP.Close(conn);
      END;
    EXCEPT
    | Rd.Failure, Thread.Alerted, Wr.Failure =&gt;
        log.log(Fmt.F(&quot;error get url: %s&quot;, request.url.toText()),
                App.LogStatus.Error);
    END;
  END Client;

PROCEDURE <A NAME="RegisterRequestHandler"><procedure>RegisterRequestHandler</procedure></A>(port: INTEGER; handler: RequestHandler) =
  BEGIN
    WriteLock();
    TRY
      handler.port := port;
      requestHandlerList := NEW(RequestHandlerList, head := handler,
                                tail := requestHandlerList);
    FINALLY
      WriteUnlock();
    END;
  END RegisterRequestHandler;

PROCEDURE <A NAME="ServerPushSupported"><procedure>ServerPushSupported</procedure></A>(request: HTTP.Request): BOOLEAN =
  VAR
    field := request.lookupField(&quot;User-Agent&quot;);
    trd: TextRd.T;
    agent: TEXT;
  BEGIN
    TRY
      IF field # NIL THEN
        trd := TextRd.New(field.value);
        agent := Lex.Scan(trd, NonSlash);
        IF Text.Equal(agent, &quot;Mozilla&quot;) THEN RETURN TRUE END;
      END;
    EXCEPT
    | Rd.Failure, Thread.Alerted =&gt;
    END;
    RETURN FALSE;
  END ServerPushSupported;

PROCEDURE <A NAME="ServerPushFrame"><procedure>ServerPushFrame</procedure></A>(wr: Wr.T; contentType, msg: TEXT; log: App.Log) RAISES {App.Error} =
  BEGIN
    TRY
      IF App.Verbose() THEN
        log.log(Fmt.F(&quot;Serverpush: %s&quot;, msg), App.LogStatus.Verbose);
      END;
      Wr.PutText(wr, ServerPushBoundaryStringStart);
      Wr.PutText(wr, Fmt.F(&quot;Content-type: %s\r\nContent-length: %s\r\n\r\n&quot;,
                           contentType, Fmt.Int(Text.Length(msg))));
      Wr.PutText(wr, msg);
      Wr.Flush(wr);
    EXCEPT
    | Wr.Failure, Thread.Alerted =&gt;
        IF App.Debug() THEN
           log.log(Fmt.F(&quot;Failure writing \&quot;%s\&quot; server push frame&quot;, msg),
                   App.LogStatus.Debug);
        END;
    END;
  END ServerPushFrame;

VAR
  proxy: Proxy := NEW(Proxy).init();

PROCEDURE <A NAME="DefaultProxy"><procedure>DefaultProxy</procedure></A>(&lt;* UNUSED *&gt; log: App.Log): Proxy =
  BEGIN
    RETURN proxy;
  END DefaultProxy;

TYPE
  Arg = {ProxyServer};

  ArgHandler = App.ArgHandler OBJECT
  OVERRIDES
    set := SetArg;
  END;

PROCEDURE <A NAME="SetArg"><procedure>SetArg</procedure></A> (            self : ArgHandler;
                  &lt;*UNUSED *&gt; src  : App.ArgSource;
                              value: TEXT;
                              log  : App.Log        ) RAISES {App.Error} =
  BEGIN
    CASE VAL(self.id, Arg) OF
    | Arg.ProxyServer =&gt;
        IF Text.Length(value) &gt; 0 THEN proxy.add(value, log); END;
    END;
  END SetArg;

BEGIN
  EVAL NEW(ArgHandler, id := ORD(Arg.ProxyServer),
           paramName := &quot;pattern ProxyServer:port|DIRECT&quot;,
           default := &quot;&quot;).init(
           switchName := &quot;proxy&quot;, envName := &quot;HTTP_PROXY&quot;,
           configName := &quot;proxy&quot;);
END HTTPApp.
</PRE>
</inModule>
<PRE>























</PRE>
</BODY>
</HTML>
