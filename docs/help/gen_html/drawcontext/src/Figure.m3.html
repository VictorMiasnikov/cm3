<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: drawcontext/src/Figure.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>drawcontext/src/Figure.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright (c) 2000 California Institute of Technology 
 All rights reserved. See the file COPYRIGHT for a full description. 
 $Id: Figure.m3.html,v 1.3 2010-04-29 17:18:11 wagner Exp $ 

<P><PRE>MODULE <module><implements><A HREF="Figure.i3.html">Figure</A></implements></module>;
IMPORT <A HREF="LinoText.i3.html">LinoText</A>;
IMPORT <A HREF="../../ui/src/vbt/VBT.i3.html">VBT</A>;
IMPORT <A HREF="VBTDrawContext.i3.html">VBTDrawContext</A>;
IMPORT <A HREF="VBTTextBounder.i3.html">VBTTextBounder</A>;
IMPORT <A HREF="../../libm3/src/geometry/Rect.i3.html">Rect</A>;
IMPORT <A HREF="TextBounder.i3.html">TextBounder</A>;
IMPORT <A HREF="../../libm3/src/rw/Rd.i3.html">Rd</A>;
IMPORT <A HREF="PSReader.i3.html">PSReader</A>;
IMPORT <A HREF="../../libm3/src/geometry/Axis.i3.html">Axis</A>;
IMPORT <A HREF="DrawContext.i3.html">DrawContext</A>;
IMPORT <A HREF="DrawContextClass.i3.html">DrawContextClass</A>;
IMPORT <A HREF="CacheDrawContext.i3.html">CacheDrawContext</A>;
IMPORT <A HREF="RegionDrawContext.i3.html">RegionDrawContext</A>;
IMPORT <A HREF="../../cit_util/src/TextSubs.i3.html">TextSubs</A>;
IMPORT <A HREF="../../libm3/src/geometry/Point.i3.html">Point</A>;
IMPORT <A HREF="../../libm3/src/geometry/Region.i3.html">Region</A>;
IMPORT <A HREF="TransformOther.i3.html">TransformOther</A>;

TYPE
  AttributeCache = RECORD
    tb: TextBounder.T := NIL;
    region: Region.T;
  END;

  VBTCache = RECORD
    vbt: VBT.T := NIL;
    dc: VBTDrawContext.T;
  END;

REVEAL
  <A NAME="T">T</A> = ROOT BRANDED &quot;Figure&quot; OBJECT
    data: CacheDrawContext.T;
    ac: AttributeCache;
    vc: VBTCache;
  END;

PROCEDURE <A NAME="ToCache"><procedure>ToCache</procedure></A>(fig: T; static := TRUE): CacheDrawContext.T =
  BEGIN
    IF static THEN
      RETURN fig.data;
    ELSE
      RETURN FromCache(fig.data).data;
    END;
  END ToCache;

PROCEDURE <A NAME="FromPS"><procedure>FromPS</procedure></A>(rd: Rd.T; captureResDPI := 720): T =
  BEGIN
    RETURN FromCache(PSReader.New(rd, captureResDPI));
  END FromPS;

PROCEDURE <A NAME="FromCache"><procedure>FromCache</procedure></A>(cache: CacheDrawContext.T; static := TRUE): T =
  VAR
    result := NEW(T, data := cache);
  BEGIN
    IF NOT static THEN
      result := FromFigure(result);
    END;
    RETURN result;
  END FromCache;

PROCEDURE <A NAME="FromFigure"><procedure>FromFigure</procedure></A>(fig: T; where := Point.Origin;
                     subs: TextSubs.T := NIL): T =
  VAR
    result := NEW(T, data := NEW(CacheDrawContext.T,
                                 resDPI := fig.data.resDPI));
  BEGIN
    Plot(fig, result.data, where, subs);
    RETURN result;
  END FromFigure;

PROCEDURE <A NAME="FromText"><procedure>FromText</procedure></A>(text: TEXT := NIL; size := 12): T =
  VAR
    result := NEW(T, data := NEW(CacheDrawContext.T));
  BEGIN
    IF text # NIL THEN
      result.data.text(LinoText.T{Point.Origin, text, size});
    END;
    RETURN result;
  END FromText;

PROCEDURE <A NAME="Plot"><procedure>Plot</procedure></A>(fig: T; in: DrawContext.T;
               where := Point.Origin; subs: TextSubs.T := NIL) =
  BEGIN
    fig.data.recall(in, where, subs);
  END Plot;

PROCEDURE <A NAME="Size"><procedure>Size</procedure></A>(fig: T; in: DrawContext.T;
               axis := Axis.T.Hor;
               subs: TextSubs.T := NIL): CARDINAL =
  BEGIN
    RETURN Rect.Size(axis, BoundRect(fig, in, Point.Origin, subs));
  END Size;

PROCEDURE <A NAME="VBTSize"><procedure>VBTSize</procedure></A>(fig: T; vbt: VBT.T;
                  axis := Axis.T.Hor;
                  subs: TextSubs.T := NIL): CARDINAL =
  BEGIN
    IF fig.vc.vbt # vbt THEN
      fig.vc.dc := NEW(VBTDrawContext.T,
                        textBounder := NEW(VBTTextBounder.T).init(vbt));
      fig.vc.vbt := vbt;
    END;
    RETURN Size(fig, fig.vc.dc, axis, subs);
  END VBTSize;

PROCEDURE <A NAME="BoundRect"><procedure>BoundRect</procedure></A>(fig: T; in: DrawContext.T;
                    where := Point.Origin;
                    subs: TextSubs.T := NIL): Rect.T =
  BEGIN
    RETURN BoundRegion(fig, in, where, subs).r;
  END BoundRect;

PROCEDURE <A NAME="BoundRegion"><procedure>BoundRegion</procedure></A>(fig: T; in: DrawContext.T;
                      where := Point.Origin;
                      subs: TextSubs.T := NIL): Region.T =
  BEGIN
    IF subs # NIL OR fig.ac.tb # in.textBounder THEN
      VAR
        region := NEW(RegionDrawContext.T).init(in.textBounder);
        inv := TransformOther.Inverse(in.transform);
      BEGIN
        region.setTransform(in.transform);
        fig.data.recall(region, where, subs);
        fig.ac.tb := in.textBounder;
        fig.ac.region := region.toRegion();
        fig.ac.region := TransformOther.ApplyToRegion(inv, fig.ac.region);
      END;
    END;
    RETURN fig.ac.region;
  END BoundRegion;

BEGIN
END Figure.
</PRE>
</inModule>
<PRE>























</PRE>
</BODY>
</HTML>
