<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: drawcontext/src/PSDrawContext.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>drawcontext/src/PSDrawContext.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright (c) 2000 California Institute of Technology 
 All rights reserved. See the file COPYRIGHT for a full description. 
 $Id: PSDrawContext.m3.html,v 1.3 2010-04-29 17:18:11 wagner Exp $ 

<P><PRE>MODULE <module><implements><A HREF="PSDrawContext.i3.html">PSDrawContext</A></implements></module>;
IMPORT <A HREF="WYSIWYGify.i3.html">WYSIWYGify</A>;
IMPORT <A HREF="DrawContextClass.i3.html">DrawContextClass</A>;
IMPORT <A HREF="Line.i3.html">Line</A>;
IMPORT <A HREF="../derived/LineList.i3.html">LineList</A>;
IMPORT <A HREF="LinoText.i3.html">LinoText</A>;
IMPORT <A HREF="../derived/LinoTextList.i3.html">LinoTextList</A>;
IMPORT <A HREF="../../libm3/src/geometry/Rect.i3.html">Rect</A>;
IMPORT <A HREF="../../libm3/src/geometry/Point.i3.html">Point</A>;
IMPORT <A HREF="../../libm3/src/geometry/Transform.i3.html">Transform</A>;
IMPORT <A HREF="../../libm3/src/bundleintf/Bundle.i3.html">Bundle</A>;
IMPORT <A HREF="PSTextBounder.i3.html">PSTextBounder</A>;
IMPORT <A HREF="../../cit_util/src/TextSubs.i3.html">TextSubs</A>;
IMPORT <A HREF="../../libm3/src/fmtlex/Fmt.i3.html">Fmt</A>;
IMPORT <A HREF="../../libm3/src/rw/Wr.i3.html">Wr</A>;
IMPORT <A HREF="../derived/PSFormBundle.i3.html">PSFormBundle</A>;
IMPORT <A HREF="../../m3core/src/time/Common/FmtTime.i3.html">FmtTime</A>;
IMPORT <A HREF="../../m3core/src/time/Common/Time.i3.html">Time</A>;
IMPORT <A HREF="../../m3core/src/text/Text.i3.html">Text</A>;
IMPORT <A HREF="../../libm3/src/rw/TextWr.i3.html">TextWr</A>;
IMPORT <A HREF="../../m3core/src/thread/Common/Thread.i3.html">Thread</A>;

&lt;* FATAL Thread.Alerted, Wr.Failure *&gt;

REVEAL
  <A NAME="T">T</A> = Public BRANDED &quot;PSDrawContext&quot; OBJECT
    lines: LineList.T;
    texts: LinoTextList.T;
    bbox: Rect.T;
    captureRes: REAL;
  OVERRIDES
    init := Init;
    write := Write;
    gLine := GLine;
    gText := GText;
  END;

PROCEDURE <A NAME="Init"><procedure>Init</procedure></A>(self: T; captureResDPI: INTEGER): T =
  BEGIN
    self.textBounder := NEW(PSTextBounder.T).init();
    self.lines := NIL;
    self.texts := NIL;
    self.bbox := Rect.Empty;
    self.captureRes := FLOAT(captureResDPI);
    self.setTransform(Transform.IsoScale(self.captureRes/FLOAT(self.resDPI)));
    RETURN self;
  END Init;

PROCEDURE <A NAME="Write"><procedure>Write</procedure></A>(self: T; wr: Wr.T; title: TEXT) =
  VAR
    subs := NEW(TextSubs.T).init();
    form := Bundle.Get(PSFormBundle.Get(), &quot;form.ps&quot;);
    cap2points := 72.0 / FLOAT(self.captureRes);
    cpt := Fmt.Real(cap2points, style := Fmt.Style.Fix, prec := 6);
  BEGIN
    subs.add(&quot;#title#&quot;, title);
    subs.add(&quot;#cpt#&quot;, cpt);
    subs.add(&quot;#lines#&quot;, FmtLines(self));
    subs.add(&quot;#texts#&quot;, FmtTexts(self));
    subs.add(&quot;#date#&quot;, FmtTime.Long(Time.Now()));
    WITH b = self.bbox DO
      subs.int(&quot;#width#&quot;, TRUNC(FLOAT(b.east - b.west) * cap2points)+1);
      subs.int(&quot;#height#&quot;, TRUNC(FLOAT(b.south - b.north) * cap2points )+1);
      subs.int(&quot;#xoff#&quot;, ROUND(FLOAT(-b.west) * cap2points));
      subs.int(&quot;#yoff#&quot;, ROUND(FLOAT(b.south) * cap2points));
    END;
    Wr.PutText(wr, subs.apply(form));
  END Write;

PROCEDURE <A NAME="BBox"><procedure>BBox</procedure></A>(self: T; r: Rect.T) =
  BEGIN
    self.bbox := Rect.Join(self.bbox, r);
  END BBox;

PROCEDURE <A NAME="GLine"><procedure>GLine</procedure></A>(self: T; l: Line.T) =
  BEGIN
    BBox(self, Line.GetBoundRect(l));
    self.lines := LineList.Cons(l, self.lines);
  END GLine;

PROCEDURE <A NAME="GText"><procedure>GText</procedure></A>(self: T; t: LinoText.T) =
  BEGIN
    BBox(self, self.textBounder.bound(t));
    self.texts := LinoTextList.Cons(t, self.texts);
  END GText;

PROCEDURE <A NAME="FmtPoint"><procedure>FmtPoint</procedure></A>(p: Point.T): TEXT =
  BEGIN
    RETURN Fmt.Int(p.h) &amp; &quot; &quot; &amp; Fmt.Int(p.v);
  END FmtPoint;

PROCEDURE <A NAME="FmtText"><procedure>FmtText</procedure></A>(t: TEXT): TEXT =
  VAR
    wr := TextWr.New();
    c: CHAR;
  BEGIN
    FOR i := 0 TO Text.Length(t) - 1 DO
      c := Text.GetChar(t, i);
      CASE c OF
      | '(', ')' =&gt; Wr.PutChar(wr, '\134');
      ELSE
      END;
      Wr.PutChar(wr, c);
    END;
    RETURN TextWr.ToText(wr);
  END FmtText;

PROCEDURE <A NAME="FmtLines"><procedure>FmtLines</procedure></A>(self: T): TEXT =
  VAR
    wr := TextWr.New();
    cur := self.lines;
    l: Line.T;
  BEGIN
    WHILE cur # NIL DO
      l := cur.head;
      Wr.PutText(wr, Fmt.Int(l.s.thick));
      Wr.PutText(wr, &quot; slw n &quot;);
      Wr.PutText(wr, FmtPoint(l.a));
      Wr.PutText(wr, &quot; m &quot;);
      Wr.PutText(wr, FmtPoint(l.b));
      Wr.PutText(wr, &quot; l gs col0 s gr\n&quot;);
      cur := cur.tail;
    END;
    RETURN TextWr.ToText(wr);
  END FmtLines;

PROCEDURE <A NAME="FmtTexts"><procedure>FmtTexts</procedure></A>(self: T): TEXT =
  VAR
    wr := TextWr.New();
    cur := self.texts;
    t: LinoText.T;
  BEGIN
    WHILE cur # NIL DO
      t := cur.head;
      Wr.PutText(wr, &quot;tff &quot;);
      Wr.PutText(wr, WYSIWYGify.FormatTextSizeForPS(FLOAT(t.size)));
      Wr.PutText(wr, &quot; scf sf &quot;);
      Wr.PutText(wr, FmtPoint(t.a));
      Wr.PutText(wr, &quot; tx (&quot;);
      Wr.PutText(wr, FmtText(t.t));
      Wr.PutText(wr, &quot;) col0 sh gr\n&quot;);
      cur := cur.tail;
    END;
    RETURN TextWr.ToText(wr);
  END FmtTexts;

BEGIN
END PSDrawContext.
</PRE>
</inModule>
<PRE>























</PRE>
</BODY>
</HTML>
