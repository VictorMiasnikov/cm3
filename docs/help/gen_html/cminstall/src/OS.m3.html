<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: cminstall/src/OS.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>cminstall/src/OS.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright 1996-2000, Critical Mass, Inc.  All rights reserved. 
 See file COPYRIGHT-CMASS for details. 

<P><PRE>MODULE <module><implements><A HREF="#x1">OS</A></implements></module>;

IMPORT <A HREF="../../libm3/src/atom/Atom.i3.html">Atom</A>, <A HREF="../../libm3/derived/AtomList.i3.html">AtomList</A>, <A HREF="../../libm3/src/params/Env.i3.html">Env</A>, <A HREF="../../libm3/src/os/Common/File.i3.html">File</A>, <A HREF="../../libm3/src/rw/FileWr.i3.html">FileWr</A>, <A HREF="../../libm3/src/os/Common/FS.i3.html">FS</A>, <A HREF="#x2">Msg</A>, <A HREF="../../libm3/src/os/Common/Pathname.i3.html">Pathname</A>;
IMPORT <A HREF="../../libm3/src/os/Common/OSError.i3.html">OSError</A>, <A HREF="../../libm3/src/os/Common/RegularFile.i3.html">RegularFile</A>, <A HREF="#x3">Text2</A>, <A HREF="../../m3core/src/text/Text.i3.html">Text</A>, <A HREF="../../m3core/src/thread/Common/Thread.i3.html">Thread</A>, <A HREF="../../libm3/src/rw/Wr.i3.html">Wr</A>;

PROCEDURE <A NAME="IsDirectory"><procedure>IsDirectory</procedure></A> (file: TEXT): BOOLEAN =
  BEGIN
    TRY
      WITH stat = FS.Status (file) DO
        RETURN stat.type = FS.DirectoryFileType;
      END
    EXCEPT
    | OSError.E =&gt; RETURN FALSE;
    END
  END IsDirectory;

PROCEDURE <A NAME="IsExecutable"><procedure>IsExecutable</procedure></A> (file: TEXT): BOOLEAN =
  BEGIN
    TRY
      WITH stat = FS.Status (file) DO
        RETURN stat.type = RegularFile.FileType;
      END
    EXCEPT
    | OSError.E =&gt; RETURN FALSE;
    END
  END IsExecutable;

PROCEDURE <A NAME="FileNameEq"><procedure>FileNameEq</procedure></A> (a, b: TEXT): BOOLEAN =
  BEGIN
    IF (on_unix)
      THEN  RETURN Text.Equal (a, b);     (* POSIX *)
      ELSE  RETURN Text2.CIEqual (a, b);  (* WIN32 *)
    END;
  END FileNameEq;

PROCEDURE <A NAME="CleanDirName"><procedure>CleanDirName</procedure></A> (dir: TEXT): TEXT =
  CONST Slash = ARRAY BOOLEAN OF CHAR { '\134', '/' }[on_unix];
  VAR len: INTEGER;
  BEGIN
    IF (dir # NIL) AND NOT FileNameEq (Pathname.Prefix (dir), dir) THEN
      len := Text.Length (dir);
      IF (len &gt; 1) AND Text.GetChar (dir, len-1) = Slash THEN
        dir := Text.Sub (dir, 0, len-1);
      END;
    END;
    RETURN dir;
  END CleanDirName;

PROCEDURE <A NAME="FindExecutable"><procedure>FindExecutable</procedure></A> (file: TEXT): TEXT =
  CONST UnixExts = ARRAY OF TEXT { NIL };
  CONST WinExts = ARRAY OF TEXT { NIL, &quot;exe&quot;, &quot;com&quot;, &quot;cmd&quot;, &quot;bat&quot; };
  VAR path := Env.Get (&quot;PATH&quot;);
  BEGIN
    IF on_unix
      THEN RETURN SearchPath (file, path, ':', UnixExts);
      ELSE RETURN SearchPath (file, path, ';', WinExts);
    END;
  END FindExecutable;

PROCEDURE <A NAME="SearchPath"><procedure>SearchPath</procedure></A> (file, path: TEXT;   sep: CHAR;
                      READONLY exts: ARRAY OF  TEXT): TEXT =
  VAR dir, fn: TEXT;  s0, s1, len: INTEGER;  no_ext: BOOLEAN;
  BEGIN
    IF IsExecutable (file) THEN RETURN file; END;

    no_ext := Text.Equal (file, Pathname.Base (file));

    (* first try the file without looking at the path *)
    IF no_ext THEN
      FOR i := FIRST (exts) TO LAST (exts) DO
        fn := Pathname.Join (NIL, file, exts[i]);
        IF IsExecutable (fn) THEN RETURN fn; END;
      END;
    END;

    IF path = NIL THEN RETURN NIL; END;
    IF Pathname.Absolute (file) THEN RETURN NIL; END;

    (* try the search path *)
    len := Text.Length (path);  s0 := 0;
    WHILE (s0 &lt; len) DO
      s1 := Text.FindChar (path, sep, s0);
      IF (s1 &lt; 0) THEN s1 := len; END;
      IF (s0 &lt; s1) THEN
        dir := Text.Sub (path, s0, s1 - s0);
        dir := CleanDirName (dir);
        IF no_ext THEN
          FOR i := FIRST (exts) TO LAST (exts) DO
            fn := Pathname.Join (dir, file, exts[i]);
            IF IsExecutable (fn) THEN RETURN fn; END;
          END;
        ELSE
          fn := Pathname.Join (dir, file, NIL);
          IF IsExecutable (fn) THEN RETURN fn; END;
        END;
      END;
      s0 := s1 + 1;
    END;

    (* failed *)
    RETURN NIL;
  END SearchPath;

PROCEDURE <A NAME="GetAbsolutePath"><procedure>GetAbsolutePath</procedure></A> (a, b: TEXT := NIL): TEXT =
  VAR path := MakePath (a, b);
  BEGIN
    IF Pathname.Absolute (path) THEN RETURN path; END;
    TRY
      RETURN FS.GetAbsolutePathname (path);
    EXCEPT OSError.E =&gt;
      RETURN path;
    END;
  END GetAbsolutePath;

PROCEDURE <A NAME="MakePath"><procedure>MakePath</procedure></A> (a, b, c, d: TEXT := NIL): TEXT =
  VAR path := a;

  PROCEDURE Join (a, b: TEXT): TEXT =
    BEGIN
      IF Pathname.Absolute(b) THEN
        RETURN b;
      ELSE
        RETURN Pathname.Join(a, b, NIL);
      END;
    END Join;

  BEGIN (* MakePath *)
    IF (b # NIL) THEN path := Join (path, b); END;
    IF (c # NIL) THEN path := Join (path, c); END;
    IF (d # NIL) THEN path := Join (path, d); END;
    RETURN path;
  END MakePath;

PROCEDURE <A NAME="MakeDir"><procedure>MakeDir</procedure></A> (dir: TEXT): BOOLEAN =
  VAR parent: TEXT;
  BEGIN
    IF dir = NIL OR Text.Length (dir) = 0 THEN dir := &quot;.&quot;; END;
    IF IsDirectory (dir) THEN RETURN TRUE; END;

    parent := Pathname.Prefix (dir);
    IF (parent # NIL) AND NOT FileNameEq (parent, dir) THEN
      IF NOT MakeDir (parent) THEN RETURN FALSE; END;
    END;

    TRY
      FS.CreateDirectory (dir);
      RETURN TRUE;
    EXCEPT OSError.E =&gt;
      RETURN FALSE;
    END;
  END MakeDir;

PROCEDURE <A NAME="WriteFile"><procedure>WriteFile</procedure></A> (name, contents: TEXT) =
  VAR wr: Wr.T;
  BEGIN
    TRY
      wr := FileWr.Open (name);
      Wr.PutText (wr, contents);
      Wr.Close (wr);
    EXCEPT
    | OSError.E (ec) =&gt;
        Msg.Error (ec, &quot;Unable to open the file: &quot;, name);
    | Wr.Failure (ec) =&gt;
        Msg.Error (ec, &quot;Unable to write the file: &quot;, name);
    | Thread.Alerted =&gt;
        Msg.Error (NIL, &quot;Interrupted while writing the file: &quot;, name);
    END;
  END WriteFile;

PROCEDURE <A NAME="RemoveFile"><procedure>RemoveFile</procedure></A> (file: TEXT) =
  BEGIN
    TRY
      FS.DeleteFile (file);
    EXCEPT OSError.E (ec) =&gt;
      Msg.Error (ec, &quot;Unable to remove file: &quot;, file);
    END;
  END RemoveFile;

PROCEDURE <A NAME="MoveFile"><procedure>MoveFile</procedure></A> (src, dest: TEXT) =
  BEGIN
    TRY
      CopyFile (src, dest);
    EXCEPT OSError.E (ec) =&gt;
      Msg.Error (ec, &quot;Unable to copy file: &quot;, src, &quot; -&gt; &quot;, dest);
    END;
    RemoveFile (src);
  END MoveFile;

PROCEDURE <A NAME="CopyFile"><procedure>CopyFile</procedure></A> (src, dest: TEXT) RAISES {OSError.E} =
  VAR
    rd, wr : File.T := NIL;
    len    : INTEGER;
    buf    : ARRAY [0..4095] OF File.Byte;
  BEGIN
    TRY
      rd := FS.OpenFileReadonly (src);
      wr := OpenDestination (dest, rd);
      LOOP
        len := rd.read (buf);
        IF (len &lt;= 0) THEN EXIT; END;
        wr.write (SUBARRAY (buf, 0, len));
      END;
    FINALLY
      IF (wr # NIL) THEN wr.close (); END;
      IF (rd # NIL) THEN rd.close (); END;
    END;
  END CopyFile;

PROCEDURE <A NAME="OpenDestination"><procedure>OpenDestination</procedure></A> (dest: TEXT;  src: File.T): File.T RAISES {OSError.E} =
  BEGIN
    (* We need to preserve permission bits on Unix.  On Win32 it's too hard
       (Win95 doesn't have security, and WinNT w/o NTFS is broken too!),
       so we don't bother.  *)
    IF NOT on_unix THEN
      (* File permissions on Windows are broken... *)
      src := NIL;
    END;

    TRY RETURN FS.OpenFile (dest, template := src);
    EXCEPT OSError.E =&gt; (* nope. *)
    END;

    (* If we can't open the file for writing, try deleting it first
       and then opening it, sometimes that'll work instead... *)

    TRY FS.DeleteFile (dest);
    EXCEPT OSError.E =&gt; (* doesn't look very hopeful *)
    END;

    RETURN FS.OpenFile (dest, template := src);
  END OpenDestination;

PROCEDURE <A NAME="Err"><procedure>Err</procedure></A> (args: AtomList.T): TEXT =
  VAR msg : TEXT := NIL;
  BEGIN
    WHILE (args # NIL) DO
      IF (msg = NIL) THEN  msg := &quot;: &quot;;  ELSE  msg := msg &amp; &quot;  ***  &quot;;  END;
      msg  := msg &amp; Atom.ToText (args.head);
      args := args.tail;
    END;
    IF (msg = NIL) THEN msg := &quot;: ** NO INFO **&quot;; END;
    RETURN msg;
  END Err;

BEGIN
END OS.
</PRE>
</inModule>
<HR>
<A NAME="x1">interface OS is in:
</A><UL>
<LI><A HREF="../../cm3ide/src/utils/OS.i3.html#0TOP0">cm3ide/src/utils/OS.i3</A>
<LI><A HREF="OS.i3.html#0TOP0">cminstall/src/OS.i3</A>
<LI><A HREF="../../m3browser/src/OS.i3.html#0TOP0">m3browser/src/OS.i3</A>
</UL>
<P>
<HR>
<A NAME="x2">interface Msg is in:
</A><UL>
<LI><A HREF="../../cm3/src/Msg.i3.html#0TOP0">cm3/src/Msg.i3</A>
<LI><A HREF="Msg.i3.html#0TOP0">cminstall/src/Msg.i3</A>
<LI><A HREF="../../m3tohtml/src/Msg.i3.html#0TOP0">m3tohtml/src/Msg.i3</A>
</UL>
<P>
<HR>
<A NAME="x3">interface Text2 is in:
</A><UL>
<LI><A HREF="../../cm3ide/src/utils/Text2.i3.html#0TOP0">cm3ide/src/utils/Text2.i3</A>
<LI><A HREF="Text2.i3.html#0TOP0">cminstall/src/Text2.i3</A>
</UL>
<P>
<PRE>























</PRE>
</BODY>
</HTML>
