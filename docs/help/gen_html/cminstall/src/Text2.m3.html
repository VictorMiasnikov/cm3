<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: cminstall/src/Text2.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>cminstall/src/Text2.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright 1996-2000 Critical Mass, Inc. All rights reserved. 
 See file COPYRIGHT-CMASS for details. 

<P> This interface defines misc. TEXT manipulation routines. 

<P><PRE>MODULE <module><implements><A HREF="#x1">Text2</A></implements></module>;

IMPORT <A HREF="../../libm3/src/types/ASCII.i3.html">ASCII</A>, <A HREF="../../m3core/src/text/Text.i3.html">Text</A>, <A HREF="../../m3core/src/text/Text8.i3.html">Text8</A>, <A HREF="../../m3core/src/text/TextClass.i3.html">TextClass</A>, <A HREF="../../m3core/src/word/Word.i3.html">Word</A>;

PROCEDURE <A NAME="CIEqual"><procedure>CIEqual</procedure></A> (a, b: TEXT): BOOLEAN =
  VAR
    len1 := SleazyLen (a);
    len2 := SleazyLen (b);
    c1, c2: CHAR;
  BEGIN
    IF (len1 # len2) THEN RETURN FALSE; END;
    FOR i := 0 TO len1 - 1 DO
      c1 := ASCII.Upper [Text.GetChar (a, i)];
      c2 := ASCII.Upper [Text.GetChar (b, i)];
      IF (c1 # c2) THEN RETURN FALSE; END
    END;
    RETURN TRUE;
  END CIEqual;

PROCEDURE <A NAME="Trim"><procedure>Trim</procedure></A> (a: TEXT): TEXT =
  VAR start: CARDINAL := 0;  stop := SleazyLen (a);  c: CHAR;
  BEGIN
    WHILE (start &lt; stop) DO
      c := Text.GetChar (a, start);
      IF (c # ' ') AND (c # '\t') AND (c # '\r') AND  (c # '\n') THEN EXIT; END;
      INC (start);
    END;
    WHILE (start &lt; stop) DO
      c := Text.GetChar (a, stop-1);
      IF (c # ' ') AND (c # '\t') AND (c # '\r') AND  (c # '\n') THEN EXIT; END;
      DEC (stop);
    END;
    IF (start &lt; stop)
      THEN RETURN Text.Sub (a, start, stop - start);
      ELSE RETURN &quot;&quot;;
    END;
  END Trim;

PROCEDURE <A NAME="EscapeString"><procedure>EscapeString</procedure></A> (a: TEXT): TEXT =
  VAR len: CARDINAL;   buf: ARRAY [0..255] OF CHAR;
  BEGIN
    IF (a = NIL) THEN a := &quot;&quot;; END;
    len := Text.Length (a);
    IF (len &lt;= NUMBER (buf))
      THEN RETURN DoEscape (a, len, buf);
      ELSE RETURN DoEscape (a, len, NEW (REF ARRAY OF CHAR, len)^);
    END;
  END EscapeString;

PROCEDURE <A NAME="DoEscape"><procedure>DoEscape</procedure></A> (a: TEXT;  len: CARDINAL;  VAR buf: ARRAY OF CHAR): TEXT =
  CONST BackSlash = '\134';
  VAR
    n_special := 0;
    c: CHAR;
    b: Text8.T;
    bx, z: INTEGER;
  BEGIN
    Text.SetChars (buf, a);

    FOR i := 0 TO len-1 DO
      c := buf[i];
      IF (c = '\n') OR (c = '\&quot;') OR (c = '\'') OR (c = BackSlash)
        OR (c = '\r') OR (c = '\t') OR (c = '\f') THEN
        INC (n_special);
      ELSIF (c &lt; ' ') OR (c &gt; '~') THEN
        INC (n_special, 3);
      END;
    END;

    b := Text8.Create (len + n_special + 2);
    b.contents[0] := '\&quot;';  bx := 1;
    FOR i := 0 TO len-1 DO
      c := buf[i];
      IF (c = BackSlash) THEN
        b.contents[bx] := c; INC (bx); b.contents[bx] := c;  INC (bx);
      ELSIF (c = '\n') THEN
        b.contents[bx] := BackSlash; INC (bx); b.contents[bx] := 'n';  INC (bx);
      ELSIF (c = '\&quot;') THEN
        b.contents[bx] := BackSlash; INC (bx); b.contents[bx] := c;  INC (bx);
      ELSIF (c = '\'') THEN
        b.contents[bx] := BackSlash; INC (bx); b.contents[bx] := c;  INC (bx);
      ELSIF (c = '\r') THEN
        b.contents[bx] := BackSlash; INC (bx); b.contents[bx] := 'r'; INC (bx);
      ELSIF (c = '\t') THEN
        b.contents[bx] := BackSlash; INC (bx); b.contents[bx] := 't'; INC (bx);
      ELSIF (c = '\f') THEN
        b.contents[bx] := BackSlash; INC (bx); b.contents[bx] := 'f'; INC (bx);
      ELSIF (c &lt; ' ') OR (c &gt; '~') THEN
        b.contents[bx] := BackSlash;  INC (bx);
        z := Word.RightShift (Word.And (ORD (c), 8_700), 6);
        b.contents[bx] := VAL(z + ORD('0'), CHAR);  INC (bx);
        z := Word.RightShift (Word.And (ORD (c), 8_070), 3);
        b.contents[bx] := VAL(z + ORD('0'), CHAR);  INC (bx);
        z := Word.And (ORD (c), 8_007);
        b.contents[bx] := VAL(z + ORD('0'), CHAR);  INC (bx);
      ELSE
        b.contents[bx] := c;  INC (bx);
      END;
    END;
    b.contents[bx] := '\&quot;';  INC (bx);

    RETURN b;
  END DoEscape;

PROCEDURE <A NAME="SleazyLen"><procedure>SleazyLen</procedure></A> (a: TEXT): CARDINAL =
  BEGIN
    IF (a = NIL) THEN RETURN 0; END;
    RETURN Text.Length (a);
  END SleazyLen;

PROCEDURE <A NAME="FindChars"><procedure>FindChars</procedure></A>(t: TEXT; s: ASCII.Set := ASCII.Spaces) : BOOLEAN =
  BEGIN
    FOR i := 0 TO Text.Length(t) -1 DO
      IF Text.GetChar(t, i) IN s THEN
        RETURN TRUE;
      END;
    END;
    RETURN FALSE;
  END FindChars;

PROCEDURE <A NAME="RemoveChars"><procedure>RemoveChars</procedure></A>(t: TEXT; s: ASCII.Set := ASCII.Spaces) : TEXT =
  VAR
    len := Text.Length(t);
    res :  Text8.T;
    cc  := 0;
    a   : CHAR;
  BEGIN
    res := Text8.Create(len);
    FOR i := 0 TO len - 1 DO
      a := TextClass.GetChar(t, i);
      IF a IN s THEN
        INC(cc);
      ELSE
        res.contents[i-cc] := a;
      END;
    END;
    RETURN Text8.New(SUBARRAY(res.contents^, 0, len - cc));
  END RemoveChars;

BEGIN
END Text2.
</PRE>
</inModule>
<HR>
<A NAME="x1">interface Text2 is in:
</A><UL>
<LI><A HREF="../../cm3ide/src/utils/Text2.i3.html#0TOP0">cm3ide/src/utils/Text2.i3</A>
<LI><A HREF="Text2.i3.html#0TOP0">cminstall/src/Text2.i3</A>
</UL>
<P>
<PRE>























</PRE>
</BODY>
</HTML>
