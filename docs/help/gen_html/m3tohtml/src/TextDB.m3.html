<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: m3tohtml/src/TextDB.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>m3tohtml/src/TextDB.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright (C) 1992, Digital Equipment Corporation                         
 All rights reserved.                                                      
 See the file COPYRIGHT for a full description.                            
                                                                           
 Last modified on Fri Apr  8 15:54:13 PDT 1994 by kalsow                   

<P><PRE>MODULE <module><implements><A HREF="TextDB.i3.html">TextDB</A></implements></module>;

IMPORT <A HREF="../../libm3/derived/TextList.i3.html">TextList</A>, <A HREF="../../libm3/derived/RefList.i3.html">RefList</A>, <A HREF="../../libm3/derived/TextIntTbl.i3.html">TextIntTbl</A>, <A HREF="../../m3core/src/text/Text.i3.html">Text</A>;
IMPORT <A HREF="DBRd.i3.html">DBRd</A>, <A HREF="DBWr.i3.html">DBWr</A>;
</PRE>-------------------------------------------------------------- database ---

<P><PRE>REVEAL
  <A NAME="T">T</A> = T_ BRANDED OBJECT
    words     : WordSet;
    relations : RefList.T;
  OVERRIDES
    init            := InitDB;
    all_relations   := AllRelations;
    get_relation    := GetRelation;
    create_relation := CreateRelation;
    load            := LoadDB;
    dump            := DumpDB;
  END;

PROCEDURE <A NAME="InitDB"><procedure>InitDB</procedure></A> (self: T): T =
  BEGIN
    self.words     := NEW (WordSet).init ();
    self.relations := NIL;
    RETURN self;
  END InitDB;

PROCEDURE <A NAME="AllRelations"><procedure>AllRelations</procedure></A> (self: T): RefList.T =
  VAR x: RefList.T := NIL;  r := self.relations;
  BEGIN
    WHILE (r # NIL) DO
      x := RefList.Cons (r.head, x);
      r := r.tail;
    END;
    RETURN x;
  END AllRelations;

PROCEDURE <A NAME="GetRelation"><procedure>GetRelation</procedure></A> (self: T;  nm: TEXT): Relation =
  VAR x := self.relations;  r: Relation;
  BEGIN
    WHILE (x # NIL) DO
      r := x.head;
      IF Text.Equal (r.name(), nm) THEN RETURN r; END;
      x := x.tail;
    END;
    RETURN NIL;
  END GetRelation;

PROCEDURE <A NAME="CreateRelation"><procedure>CreateRelation</procedure></A> (self: T;  nm: TEXT): Relation =
  VAR r := GetRelation (self, nm);
  BEGIN
    IF (r = NIL) THEN
      r := NEW (Relation).init (nm, self.words);
      self.relations := RefList.Cons (r, self.relations);
    END;
    RETURN r;
  END CreateRelation;

PROCEDURE <A NAME="LoadDB"><procedure>LoadDB</procedure></A> (self: T;  path: TEXT) =
  VAR rd := NEW (DBRd.T).init (path);  r: Relation;
  BEGIN
    self.words.load (rd);
    FOR i := 1 TO rd.get_int () DO
      r := CreateRelation (self, rd.get_line ());
      r.load (rd);
    END;
    rd.close ();
  END LoadDB;

PROCEDURE <A NAME="DumpDB"><procedure>DumpDB</procedure></A> (self: T;  path: TEXT) =
  VAR wr := NEW (DBWr.T).init (path);
  BEGIN
    self.words.dump (wr);
    wr.put_int (RefList.Length (self.relations));
    VAR x := self.relations; BEGIN
      WHILE (x # NIL) DO
        VAR r: Relation := x.head;  BEGIN
          wr.put_line (r.name ());
          r.dump (wr);
        END;
        x := x.tail;
      END;
    END;
    wr.close ();
  END DumpDB;
</PRE>------------------------------------------------------------- relations ---

<P><PRE>TYPE
   Pair = RECORD key, value: INTEGER;  END;
   PairList = REF ARRAY OF Pair;
   PairSet = RECORD
     cnt  : CARDINAL;
     elts : PairList;
   END;
</PRE> value = MissingWord ==&gt; deleted entry 

<P><PRE>REVEAL
  <A NAME="Relation">Relation</A> = R_ BRANDED OBJECT
    id    : TEXT;
    words : WordSet;
    old   : PairSet;
    new   : PairSet;
  METHODS
    init (nm: TEXT;  words: WordSet): Relation := InitRelation;
    load (rd: DBRd.T) := LoadRelation;
    dump (wr: DBWr.T) := DumpRelation;
  OVERRIDES
    name     := RelationName;
    getValue := GetValue;
    getKey   := GetKey;
    insert   := Insert;
    delete   := Delete;
    deleteValue := DeleteValue;
  END;

PROCEDURE <A NAME="InitRelation"><procedure>InitRelation</procedure></A> (r: Relation;  nm: TEXT;  words: WordSet): Relation =
  BEGIN
    r.id       := nm;
    r.words    := words;
    r.old.cnt  := 0;
    r.old.elts := NEW (PairList, 100);
    r.new.cnt  := 0;
    r.new.elts := NEW (PairList, 100);
    RETURN r;
  END InitRelation;

PROCEDURE <A NAME="RelationName"><procedure>RelationName</procedure></A> (r: Relation): TEXT =
  BEGIN
    RETURN r.id;
  END RelationName;

PROCEDURE <A NAME="GetValue"><procedure>GetValue</procedure></A> (r: Relation;  key: TEXT): TextList.T =
  VAR w := r.words.get (key);  x: TextList.T := NIL;
  BEGIN
    IF (w = MissingWord) THEN RETURN NIL; END;

    FOR i := GetKeyIndex (r.old, w) TO r.old.cnt-1 DO
      WITH p = r.old.elts[i] DO
        IF (p.key # w) THEN EXIT; END;
        IF (p.value # MissingWord) THEN
          x := TextList.Cons (r.words.text (p.value), x);
        END;
      END;
    END;

    FOR i := GetKeyIndex (r.new, w) TO r.new.cnt-1 DO
      WITH p = r.new.elts[i] DO
        IF (p.key # w) THEN EXIT; END;
        IF (p.value # MissingWord) THEN
          x := TextList.Cons (r.words.text (p.value), x);
        END;
      END;
    END;

    RETURN x;
  END GetValue;

PROCEDURE <A NAME="GetKey"><procedure>GetKey</procedure></A> (r: Relation;  value: TEXT): TextList.T =
  VAR w := r.words.get (value);  x: TextList.T := NIL;
  BEGIN
    IF (w = MissingWord) THEN RETURN NIL; END;

    (* linear search of both sets *)
    FOR i := 0 TO r.old.cnt-1 DO
      WITH p = r.old.elts[i] DO
        IF (p.value = w) THEN
          x := TextList.Cons (r.words.text (p.key), x);
        END;
      END;
    END;
    FOR i := 0 TO r.new.cnt-1 DO
      WITH p = r.new.elts[i] DO
        IF (p.value = w) THEN
          x := TextList.Cons (r.words.text (p.key), x);
        END;
      END;
    END;

    RETURN x;
  END GetKey;

PROCEDURE <A NAME="Delete"><procedure>Delete</procedure></A> (r: Relation;  key, value: TEXT) =
  VAR k := r.words.get (key);  v := r.words.get (value);
  BEGIN
    IF (k = MissingWord) OR (v = MissingWord) THEN RETURN; END;

    FOR i := GetKeyIndex (r.old, k) TO r.old.cnt-1 DO
      WITH p = r.old.elts[i] DO
        IF (p.key # k) THEN EXIT; END;
        IF (p.value = v) THEN  p.value := MissingWord; END;
      END;
    END;

    FOR i := GetKeyIndex (r.new, k) TO r.new.cnt-1 DO
      WITH p = r.new.elts[i] DO
        IF (p.key # k) THEN EXIT; END;
        IF (p.value = v) THEN  p.value := MissingWord; END;
      END;
    END;
  END Delete;

PROCEDURE <A NAME="DeleteValue"><procedure>DeleteValue</procedure></A> (r: Relation;  value: TEXT) =
  VAR w := r.words.get (value);
  BEGIN
    IF (w = MissingWord) THEN RETURN; END;

    (* linear search of both sets *)
    FOR i := 0 TO r.old.cnt-1 DO
      WITH p = r.old.elts[i] DO
        IF (p.value = w) THEN p.value := MissingWord; END;
      END;
    END;
    FOR i := 0 TO r.new.cnt-1 DO
      WITH p = r.new.elts[i] DO
        IF (p.value = w) THEN p.value := MissingWord; END;
      END;
    END;
  END DeleteValue;

PROCEDURE <A NAME="Insert"><procedure>Insert</procedure></A> (r: Relation;  key, value: TEXT) =
  (* keep the new set sorted, by inserting each pair in place *)
  VAR k := r.words.add (key);  v := r.words.add (value);
  BEGIN
    IF (r.new.cnt &gt;= NUMBER (r.new.elts^)) THEN MergeUpdates (r); END;
    VAR i : INTEGER := r.new.cnt-1; BEGIN
      WHILE (i &gt;= 0) AND (r.new.elts[i].key &gt; k) DO
        r.new.elts[i+1] := r.new.elts[i];  DEC (i);
      END;
      WITH p = r.new.elts[i+1] DO  p.key := k;  p.value := v;  END;
    END;
    INC (r.new.cnt);
  END Insert;

PROCEDURE <A NAME="MergeUpdates"><procedure>MergeUpdates</procedure></A> (r: Relation) =
  VAR n, o: CARDINAL;
  BEGIN
    (* make sure we have enough room in &quot;old&quot; *)
    IF (r.old.cnt + r.new.cnt &gt;= NUMBER (r.old.elts^)) THEN
      VAR new := NEW (PairList, 2 * NUMBER (r.old.elts^)); BEGIN
        SUBARRAY (new^, 0, NUMBER (r.old.elts^)) := r.old.elts^;
        r.old.elts := new;
      END;
    END;

    (* move the old elements out of the way *)
    VAR i := LAST (r.old.elts^);  BEGIN
      FOR j := r.old.cnt-1 TO 0 BY -1 DO
        WITH p = r.old.elts[j] DO
          IF (p.value # MissingWord) THEN
            r.old.elts[i] := p;  DEC (i);
          END;
        END;
      END;
      o := i+1;
    END;

    (* merge the two lists *)
    n := 0;  r.old.cnt := 0;
    WHILE (n &lt; r.new.cnt) AND (o &lt; NUMBER (r.old.elts^)) DO
      (* both lists are non-empty *)
      WITH nx = r.new.elts[n],  ox = r.old.elts[o]  DO
        IF (nx.value = MissingWord) THEN  INC (n);
        ELSIF (nx.key &lt; ox.key)
          THEN r.old.elts[r.old.cnt] := nx;  INC (r.old.cnt);  INC (n);
          ELSE r.old.elts[r.old.cnt] := ox;  INC (r.old.cnt);  INC (o);
        END;
      END;
    END;

    (* add the remaining old elements *)
    WHILE (o &lt; NUMBER (r.old.elts^)) DO
      r.old.elts[r.old.cnt] := r.old.elts[o];  INC (r.old.cnt);  INC (o);
    END;

    (* add the remaining new elements *)
    WHILE (n &lt; r.new.cnt) DO
      WITH nx = r.new.elts[n] DO
        IF (nx.value # MissingWord) THEN
          r.old.elts[r.old.cnt] := nx;  INC (r.old.cnt);
        END;
        INC (n);
      END;
    END;

    r.new.cnt := 0;
  END MergeUpdates;

PROCEDURE <A NAME="GetKeyIndex"><procedure>GetKeyIndex</procedure></A> (READONLY p: PairSet;  key: INTEGER): CARDINAL =
  (* Returns the smallest i such that (p.elts[i].key = key).  If
     no such pair exists, return p.cnt. *)
  VAR
    lo   : CARDINAL := 0;
    hi   : CARDINAL := p.cnt;
    mid  : CARDINAL;
 BEGIN
    WHILE (lo &lt; hi) DO
      mid := (lo + hi) DIV 2;
      IF (key &lt; p.elts[mid].key)
        THEN hi := mid;
        ELSE lo := mid + 1;
      END;
    END;
    IF (lo &gt; 0) THEN DEC (lo) END;
    IF (p.elts[lo].key # key) THEN RETURN p.cnt; END;
    WHILE (lo &gt; 0) AND (p.elts[lo-1].key = key) DO DEC (lo); END;
    RETURN lo;
  END GetKeyIndex;

PROCEDURE <A NAME="LoadRelation"><procedure>LoadRelation</procedure></A> (r: Relation;  rd: DBRd.T) =
  VAR n := rd.get_int ();
  BEGIN
    IF (n &gt; NUMBER (r.old.elts^)) THEN
      r.old.elts := NEW (PairList, 3 * n DIV 2);
    END;
    FOR i := 0 TO n-1 DO
      WITH p = r.old.elts[i] DO
        p.key   := rd.get_int ();
        p.value := rd.get_int ();
      END;
    END;
    r.old.cnt := n;
    r.new.cnt := 0;
  END LoadRelation;

PROCEDURE <A NAME="DumpRelation"><procedure>DumpRelation</procedure></A> (r: Relation;  wr: DBWr.T) =
  BEGIN
    MergeUpdates (r);
    wr.put_int (r.old.cnt);
    FOR i := 0 TO r.old.cnt-1 DO
      WITH p = r.old.elts[i] DO
        wr.put_int (p.key);
        wr.put_int (p.value);
      END;
    END;
  END DumpRelation;
</PRE>----------------------------------------------------------------- words ---

<P><PRE>CONST
  MissingWord = FIRST(INTEGER);

TYPE
  WordSet = OBJECT
    map : TextIntTbl.T;
    cnt : INTEGER;
    word: REF ARRAY OF TEXT;
  METHODS
    init (): WordSet := InitWordSet;
    add  (word: TEXT): INTEGER := AddWord;
    get  (word: TEXT): INTEGER := GetWord;
    text (id: INTEGER): TEXT   := WordText;
    load (rd: DBRd.T)          := LoadWords;
    dump (wr: DBWr.T)          := DumpWords;
  END;

PROCEDURE <A NAME="InitWordSet"><procedure>InitWordSet</procedure></A> (w: WordSet): WordSet =
  BEGIN
    w.map := NEW (TextIntTbl.Default).init ();
    w.cnt := 0;
    w.word := NEW (REF ARRAY OF TEXT, 100);
    RETURN w;
  END InitWordSet;

PROCEDURE <A NAME="AddWord"><procedure>AddWord</procedure></A> (w: WordSet;  word: TEXT): INTEGER =
  VAR id: INTEGER;
  BEGIN
    IF NOT w.map.get (word, id) THEN
      id := w.cnt;  INC (w.cnt);
      EVAL w.map.put (word, id);
      IF (id &gt;= NUMBER (w.word^)) THEN ExpandWords (w); END;
      w.word [id] := word;
    END;
    RETURN id;
  END AddWord;

PROCEDURE <A NAME="ExpandWords"><procedure>ExpandWords</procedure></A> (w: WordSet) =
  VAR
    n := NUMBER (w.word^);
    new := NEW (REF ARRAY OF TEXT, n + n);
  BEGIN
    SUBARRAY (new^, 0, n) := w.word^;
    w.word := new;
  END ExpandWords;

PROCEDURE <A NAME="GetWord"><procedure>GetWord</procedure></A> (w: WordSet;  word: TEXT): INTEGER =
  VAR id: INTEGER;
  BEGIN
    IF w.map.get (word, id)
      THEN RETURN id;
      ELSE RETURN MissingWord;
    END;
  END GetWord;

PROCEDURE <A NAME="WordText"><procedure>WordText</procedure></A> (w: WordSet;  id: INTEGER): TEXT =
  BEGIN
    IF (0 &lt; id) AND (id &lt; w.cnt)
      THEN RETURN w.word[id];
      ELSE RETURN NIL;
    END;
  END WordText;

PROCEDURE <A NAME="LoadWords"><procedure>LoadWords</procedure></A> (w: WordSet;  rd: DBRd.T) =
  BEGIN
    FOR i := 1 TO rd.get_int () DO
      EVAL w.add (rd.get_line ());
    END;
  END LoadWords;

PROCEDURE <A NAME="DumpWords"><procedure>DumpWords</procedure></A> (w: WordSet;  wr: DBWr.T) =
  BEGIN
    wr.put_int (w.cnt);
    FOR i := 0 TO w.cnt-1 DO
      wr.put_line (w.word[i]);
    END;
  END DumpWords;

BEGIN
END TextDB.
</PRE>
</inModule>
<PRE>























</PRE>
</BODY>
</HTML>
