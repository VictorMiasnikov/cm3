<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: m3tohtml/src/M3MarkUp.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>m3tohtml/src/M3MarkUp.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright (C) 1992, Digital Equipment Corporation                         
 All rights reserved.                                                      
 See the file COPYRIGHT for a full description.                            
                                                                           
 Last modified on Thu Jun  1 13:04:48 PDT 1995 by kalsow                   

<P><PRE>MODULE <module><implements><A HREF="#x1">M3MarkUp</A></implements></module>;

IMPORT <A HREF="../../libm3/src/rw/Rd.i3.html">Rd</A>, <A HREF="../../m3core/src/text/Text.i3.html">Text</A>, <A HREF="../../libm3/src/fmtlex/Fmt.i3.html">Fmt</A>, <A HREF="#x2">M3Scanner</A>;
IMPORT <A HREF="../../libm3/derived/TextList.i3.html">TextList</A>, <A HREF="../../libm3/derived/TextListSort.i3.html">TextListSort</A>, <A HREF="../../libm3/derived/TextTextTbl.i3.html">TextTextTbl</A>;
IMPORT <A HREF="M3DB.i3.html">M3DB</A>, <A HREF="FilePath.i3.html">FilePath</A>;

CONST
  Begin_ref       = &quot;&lt;A HREF=\&quot;&quot;;
  End_file        = &quot;.html&quot;;
  End_ref         = &quot;\&quot;&gt;&quot;;
  Goto_tag        = &quot;#&quot;;

  Begin_tag       = &quot;&lt;A NAME=\&quot;&quot;;
  End_tag         = &quot;\&quot;&gt;&quot;;

  End_anchor      = &quot;&lt;/A&gt;&quot;;

  Begin_interface = &quot;&lt;interface&gt;&quot;;
  End_interface   = &quot;&lt;/interface&gt;&quot;;

  Begin_module    = &quot;&lt;module&gt;&quot;;
  End_module      = &quot;&lt;/module&gt;&quot;;

  Begin_impl      = &quot;&lt;implements&gt;&quot;;
  End_impl        = &quot;&lt;/implements&gt;&quot;;

  Begin_gen_intf  = &quot;&lt;genericInterface&gt;&quot;;
  End_gen_intf    = &quot;&lt;/genericInterface&gt;&quot;;

  Begin_gen_impl  = &quot;&lt;genericModule&gt;&quot;;
  End_gen_impl    = &quot;&lt;/genericModule&gt;&quot;;

  Begin_proc      = &quot;&lt;procedure&gt;&quot;;
  End_proc        = &quot;&lt;/procedure&gt;&quot;;

TYPE
</PRE>******************
CONST
  Suffix = ARRAY BOOLEAN, BOOLEAN OF TEXT {(* standard  generic 
    module        <PRE>ARRAY BOOLEAN OF TEXT { &quot;.m3&quot;,     &quot;.mg&quot; },
    (*interface*)     ARRAY BOOLEAN OF TEXT { &quot;.i3&quot;,     &quot;.ig&quot; }
  };
*******************)

CONST
  BeginBracket = ARRAY BOOLEAN OF TEXT { &quot;&lt;inModule&gt;\n&quot;,  &quot;&lt;inInterface&gt;\n&quot; };
  EndBracket   = ARRAY BOOLEAN OF TEXT { &quot;&lt;/inModule&gt;\n&quot;, &quot;&lt;/inInterface&gt;\n&quot; };

TYPE
  InsList = RECORD  head, tail : Insertion := NIL;  cnt: INTEGER := 0; END;

TYPE
  Info = RECORD
    path         : TEXT;
    key          : TEXT;
    lex          : M3Scanner.T;
    unit         : TEXT;
    id           : TEXT;
    id_offset    : INTEGER;
    id_length    : INTEGER;
    is_interface : BOOLEAN;
    is_generic   : BOOLEAN;
    ins          : InsList;
    choice       : InsList;
    bracket      : Insertion;
  END;

PROCEDURE <A NAME="Get"><procedure>Get</procedure></A> (rd: Rd.T;  path: TEXT): Insertion =
  VAR z: Info;
  BEGIN
    ResetCache ();

    z.path      := path;
    z.key       := NIL;
    z.lex       := NEW (M3Scanner.Default).initFromRd (rd,
                                                 skip_comments := TRUE,
                                                 split_pragmas := FALSE);
    z.unit      := NIL;
    z.id        := NIL;
    z.id_offset := -1;
    z.id_length := 0;

    z.is_interface := TRUE;
    z.is_generic   := FALSE;

    (* build a list of insertions for the header *)
    AddH (z, &quot;&lt;HTML&gt;\n&lt;HEAD&gt;\n&lt;TITLE&gt;Critical Mass Modula-3: &quot;);
    AddH (z, path);
    AddH (z, &quot;&lt;/TITLE&gt;\n&lt;/HEAD&gt;\n&lt;BODY bgcolor=\&quot;#ffffff\&quot;&gt;\n&quot;);
    AddH (z, &quot;&lt;A NAME=\&quot;0TOP0\&quot;&gt;\n&lt;H2&gt;&quot;);
    AddH (z, path);
    AddH (z, &quot;&lt;/H2&gt;&lt;/A&gt;&lt;HR&gt;\n&quot;);
    AddH (z, &quot;&quot;);   z.bracket := z.ins.tail;

    NextToken (z); (* skip the initial comment *)
    MarkUnit (z);

    (* close the unit's opening bracket *)
    z.id_offset := z.lex.offset+z.lex.length + 10000;
    AddH (z, EndBracket [z.is_interface]);

    (* add the choices *)
    Append (z.ins, z.choice);

    (* and finish (leave blank lines for xmosaic scrolling) *)
    AddH (z, &quot;&lt;PRE&gt;\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n&lt;/PRE&gt;\n&quot;);
    AddH (z, &quot;&lt;/BODY&gt;\n&lt;/HTML&gt;\n&quot;);

    RETURN SortInsertions (z);
  END Get;

PROCEDURE <A NAME="MarkUnit"><procedure>MarkUnit</procedure></A> (VAR z: Info) =
  BEGIN
    (* MarkCopyright (z); There are different copyrights now... *)
    IF z.lex.token = M3Scanner.TK_Unsafe THEN NextToken (z); END;

    IF z.lex.token = M3Scanner.TK_Generic THEN
      NextToken (z); (*GENERIC*)

      IF z.lex.token = M3Scanner.TK_Interface THEN
        NextToken (z); (*INTERFACE*)
        z.is_generic   := TRUE;
        z.is_interface := TRUE;

        IF NOT GetUnitID (z) THEN RETURN; END;
        AddH (z, Begin_gen_intf);
        z.key := &quot;generic module &quot; &amp; z.unit;
        IF UnknownRef (z) THEN GenRef (z, NIL, M3DB.GenericMod (z.unit)); END;
        AddT (z, End_gen_intf);

        SkipToSemi (z);
        MarkImports (z);
        MarkDecls (z);

      ELSIF z.lex.token = M3Scanner.TK_Module THEN
        NextToken (z); (*MODULE*)
        z.is_generic   := TRUE;
        z.is_interface := FALSE;

        IF NOT GetUnitID (z) THEN RETURN; END;
        AddH (z, Begin_gen_impl);
        z.key := &quot;generic interface &quot; &amp; z.unit;
        IF UnknownRef (z) THEN GenRef (z, NIL, M3DB.GenericIntf (z.unit)); END;
        AddT (z, End_gen_impl);

        SkipToSemi (z);
        MarkImports (z);
        MarkDecls (z);

      ELSE (* error *)
        RETURN;
      END;

    ELSIF z.lex.token = M3Scanner.TK_Interface THEN
      NextToken (z); (*INTERFACE*)
      z.is_generic   := FALSE;
      z.is_interface := TRUE;

      IF NOT GetUnitID (z) THEN RETURN; END;
      AddH (z, Begin_interface);
      z.key := z.unit &amp; &quot;'s implementation &quot;;
      IF UnknownRef (z) THEN GenRef (z, NIL, M3DB.Exports (z.unit)); END;
      AddT (z, End_interface);

      IF z.lex.token = M3Scanner.TK_Semi THEN
        NextToken (z); (* ; *)
        MarkImports (z);
        MarkDecls (z);
      ELSIF z.lex.token = M3Scanner.TK_Equal THEN
        NextToken (z); (* = *)
        MarkGenericInstance (z);
      ELSE RETURN;
      END;

    ELSIF z.lex.token = M3Scanner.TK_Module THEN
      NextToken (z); (*MODULE*)
      z.is_generic   := FALSE;
      z.is_interface := FALSE;

      IF NOT GetUnitID (z) THEN RETURN; END;
      AddH (z, Begin_module);
      IF z.lex.token # M3Scanner.TK_Exports THEN
        AddH (z, Begin_impl);
        z.key := &quot;interface &quot; &amp; z.unit;
        IF UnknownRef (z) THEN GenRef (z, NIL, M3DB.Interface (z.unit)); END;
        AddT (z, End_impl);
      END;
      AddT (z, End_module);

      MarkExports (z);
      IF z.lex.token = M3Scanner.TK_Semi THEN
        NextToken (z); (* ; *)
        MarkImports (z);
        MarkDecls (z);
      ELSIF z.lex.token = M3Scanner.TK_Equal THEN
        NextToken (z); (* = *)
        MarkGenericInstance (z);
      ELSE RETURN;
      END;

    ELSE (* error *)
      RETURN;
    END;
  END MarkUnit;

PROCEDURE <A NAME="GetUnitID"><procedure>GetUnitID</procedure></A> (VAR z: Info): BOOLEAN =
  BEGIN
    IF NOT GetID (z) THEN RETURN FALSE; END;
    z.unit           := z.id;
    z.bracket.insert := BeginBracket [z.is_interface];
    (***
    z.title.insert   := z.unit &amp; Suffix [z.is_interface, z.is_generic];
    z.header.insert  := z.title.insert;
    ***)
    RETURN TRUE;
  END GetUnitID;

PROCEDURE <A NAME="MarkExports"><procedure>MarkExports</procedure></A> (VAR z: Info) =
  BEGIN
    IF z.lex.token = M3Scanner.TK_Exports THEN
      NextToken (z); (*EXPORTS*)
      WHILE GetIntfID (z) DO
        AddH (z, Begin_impl);
        AddT (z, End_impl);
        IF z.lex.token # M3Scanner.TK_Comma THEN EXIT END;
        NextToken (z); (* , *)
      END;
    END;
  END MarkExports;

PROCEDURE <A NAME="MarkImports"><procedure>MarkImports</procedure></A> (VAR z: Info) =
  BEGIN
    LOOP
      IF z.lex.token = M3Scanner.TK_Import THEN
        NextToken (z); (*IMPORT*)
        WHILE GetIntfID (z) DO
          IF z.lex.token = M3Scanner.TK_As THEN
            NextToken (z); (*AS*)
            IF z.lex.token = M3Scanner.TK_Ident THEN
              NextToken (z); (*ID*)
            END;
          END;
          IF z.lex.token # M3Scanner.TK_Comma THEN EXIT END;
          NextToken (z); (* , *)
        END;

      ELSIF z.lex.token = M3Scanner.TK_From THEN
        NextToken (z); (*FROM*)
        EVAL GetIntfID (z);

      ELSE EXIT;
      END;
      SkipToSemi (z);
    END;
  END MarkImports;

PROCEDURE <A NAME="GetIntfID"><procedure>GetIntfID</procedure></A> (VAR z: Info): BOOLEAN =
  BEGIN
    IF NOT GetID (z) THEN RETURN FALSE; END;
    z.key := &quot;interface &quot; &amp; z.id;
    IF UnknownRef (z) THEN GenRef (z, NIL, M3DB.Interface (z.id)); END;
    RETURN TRUE;
  END GetIntfID;

PROCEDURE <A NAME="MarkGenericInstance"><procedure>MarkGenericInstance</procedure></A> (VAR z: Info) =
  BEGIN
    IF NOT GetID (z) THEN RETURN END;
    IF z.is_interface THEN
      z.key := &quot;generic interface &quot; &amp; z.id;
      IF UnknownRef (z) THEN GenRef (z, NIL, M3DB.GenericIntf (z.id)); END;
    ELSE
      z.key := &quot;generic module &quot; &amp; z.id;
      IF UnknownRef (z) THEN GenRef (z, NIL, M3DB.GenericMod (z.id)); END;
    END;

    IF z.lex.token # M3Scanner.TK_L_paren THEN RETURN END;
    NextToken (z); (* ( *)

    WHILE GetIntfID (z) DO
      IF z.lex.token # M3Scanner.TK_Comma THEN EXIT END;
      NextToken (z); (* , *)
    END;

    IF z.lex.token # M3Scanner.TK_R_paren THEN RETURN END;
    NextToken (z); (* ) *)
  END MarkGenericInstance;

PROCEDURE <A NAME="MarkDecls"><procedure>MarkDecls</procedure></A> (VAR z: Info) =
  VAR id, unit: TEXT;  eq: BOOLEAN;
  BEGIN
    LOOP
      CASE z.lex.token OF
      | M3Scanner.TK_Type =&gt;
          NextToken (z); (*TYPE*)
          WHILE FindTypeID (z, id, unit, eq) DO
            IF NOT eq THEN
              z.key := Fmt.F (&quot;opaque type %s.%s&quot;, z.unit, id);
              IF UnknownRef (z) THEN
                VAR
                  types := M3DB.RevealsType (id);
                  units : TextList.T;
                BEGIN
                  IF z.is_generic
                    THEN units := M3DB.GenericMod (z.unit);
                    ELSE units := M3DB.Exports (z.unit);
                  END;
                  units := And (units, types);
                  IF (units = NIL) THEN
                    units := And (M3DB.RevealsTo (z.unit), types);
                  END;
                  GenRef (z, id, units);
                END;
              END;
            END;
          END;

      | M3Scanner.TK_Procedure =&gt;
          NextToken (z); (*PROCEDURE*)
          IF GetID (z) THEN
            IF z.is_interface THEN
              z.key := Fmt.F (&quot;procedure %s.%s&quot;, z.unit, z.id);
              IF UnknownRef (z) THEN
                IF z.is_generic THEN
                  GenRef (z, z.id, And (M3DB.GenericMod (z.unit),
                                        M3DB.DefinesProc (z.id)));
                ELSE
                  GenRef (z, z.id, And (M3DB.Exports (z.unit),
                                        M3DB.DefinesProc (z.id)));
                END;
              END;
              SkipToSemi (z);
            ELSE
              AddH (z, Begin_tag);
              AddH (z, z.id);
              AddH (z, End_tag);
              AddH (z, Begin_proc);
              AddT (z, End_proc);
              AddT (z, End_anchor);
              SkipProc (z, z.id);
            END;
          END;

      | M3Scanner.TK_Reveal =&gt;
          NextToken (z); (*REVEALS*)
          WHILE FindTypeID (z, id, unit, eq) DO
            IF eq THEN
              AddH (z, Begin_tag);
              AddH (z, z.id);
              AddH (z, End_tag);
              AddT (z, End_anchor);
            END;
          END;

      | M3Scanner.TK_EOF, M3Scanner.TK_Error =&gt; EXIT;
      ELSE NextToken (z);
      END;
    END;
  END MarkDecls;

&lt;*UNUSED*&gt;PROCEDURE <A NAME="MarkCopyright"><procedure>MarkCopyright</procedure></A> (VAR z: Info) =
  VAR id: TEXT;
  BEGIN
    IF (z.lex.token = M3Scanner.TK_Ident) AND (z.lex.length = 9) THEN
      id := Text.FromChars (SUBARRAY (z.lex.buffer^, z.lex.offset,
                                     z.lex.length));
      IF Text.Equal (&quot;Copyright&quot;, id) THEN
        z.id_offset := z.lex.offset;
        z.id_length := z.lex.length;
        AddH (z, Begin_ref);
        AddH (z, FilePath.Normalize (&quot;COPYRIGHT.html&quot;, z.path));
        AddH (z, End_ref);
        AddT (z, &quot; (C) 1994, Digital Equipment Corp.&quot;);
        AddT (z, End_anchor);
        NextToken (z);
      END;
    END;
  END MarkCopyright;

PROCEDURE <A NAME="FindTypeID"><procedure>FindTypeID</procedure></A> (VAR z: Info;  VAR id, unit: TEXT;
                      VAR eq: BOOLEAN): BOOLEAN =
  BEGIN
    LOOP
      CASE z.lex.token OF
      | M3Scanner.TK_Ident =&gt;
          EVAL GetID (z);
          id := z.id;
          IF z.lex.token = M3Scanner.TK_Dot THEN
            NextToken (z); (* . *)
            IF z.lex.token = M3Scanner.TK_Ident THEN
              unit := z.id;
              EVAL GetID (z);
            END;
          END;
          IF z.lex.token = M3Scanner.TK_Equal THEN
            NextToken (z); (* = *)
            eq := TRUE;
            RETURN TRUE;
          ELSIF z.lex.token = M3Scanner.TK_Subtype THEN
            NextToken (z); (* &lt;: *)
            eq := FALSE;
            RETURN TRUE;
          ELSE (* skip *)
          END;

      | M3Scanner.TK_L_paren =&gt;
          SkipParens (z);

      | M3Scanner.TK_Const, M3Scanner.TK_Type, M3Scanner.TK_Exception,
        M3Scanner.TK_Var, M3Scanner.TK_Procedure, M3Scanner.TK_Reveal,
        M3Scanner.TK_Begin, M3Scanner.TK_EOF, M3Scanner.TK_Error =&gt;
          RETURN FALSE;

      ELSE
          NextToken (z);
      END; (*CASE*)
    END; (*LOOP*)
  END FindTypeID;

PROCEDURE <A NAME="SkipParens"><procedure>SkipParens</procedure></A> (VAR z: Info) =
  VAR depth: INTEGER := 0;
  BEGIN
    LOOP
      IF z.lex.token = M3Scanner.TK_L_paren THEN
        INC (depth);
      ELSIF z.lex.token = M3Scanner.TK_R_paren THEN
        DEC (depth);
        IF (depth &lt;= 0) THEN NextToken (z); RETURN END;
      ELSIF z.lex.token = M3Scanner.TK_EOF THEN
        RETURN;
      ELSIF z.lex.token = M3Scanner.TK_Error THEN
        RETURN;
      END;
      NextToken (z);
    END;
  END SkipParens;

PROCEDURE <A NAME="SkipProc"><procedure>SkipProc</procedure></A> (VAR z: Info;  proc_id: TEXT) =
  BEGIN
    LOOP
      IF z.lex.token = M3Scanner.TK_End THEN
        NextToken (z); (*END*)
        IF GetID (z) AND Text.Equal (proc_id, z.id) THEN EXIT; END;
      ELSIF z.lex.token = M3Scanner.TK_EOF OR
        z.lex.token = M3Scanner.TK_Error THEN
        EXIT;
      ELSE
        NextToken (z);
      END;
    END;
  END SkipProc;

PROCEDURE <A NAME="SkipToSemi"><procedure>SkipToSemi</procedure></A> (VAR z: Info) =
  BEGIN
    WHILE (z.lex.token # M3Scanner.TK_Semi)
      AND (z.lex.token # M3Scanner.TK_EOF)
      AND (z.lex.token # M3Scanner.TK_Error) DO
      NextToken (z);
    END;
    IF (z.lex.token = M3Scanner.TK_Semi) THEN NextToken (z); END;
  END SkipToSemi;

PROCEDURE <A NAME="GetID"><procedure>GetID</procedure></A> (VAR z: Info): BOOLEAN =
  BEGIN
    IF z.lex.token # M3Scanner.TK_Ident THEN  z.id := NIL; RETURN FALSE;  END;
    z.id_offset := z.lex.offset;
    z.id_length := z.lex.length;
    z.id := Text.FromChars (SUBARRAY(z.lex.buffer^, z.id_offset, z.id_length));
    NextToken (z);
    RETURN TRUE;
  END GetID;

PROCEDURE <A NAME="NextToken"><procedure>NextToken</procedure></A> (VAR z: Info) =
  BEGIN
    REPEAT
      z.lex.next ();
    UNTIL (z.lex.token # M3Scanner.TK_Begin_pragma);
  END NextToken;
</PRE>------------------------------------------------------- insertion lists ---

<P><PRE>PROCEDURE <A NAME="AddH"><procedure>AddH</procedure></A> (VAR x: Info;  txt: TEXT) =
  BEGIN
    AddI (x.ins, x.id_offset, txt);
  END AddH;

PROCEDURE <A NAME="AddT"><procedure>AddT</procedure></A> (VAR x: Info;  txt: TEXT) =
  BEGIN
    AddI (x.ins, x.id_offset + x.id_length, txt);
  END AddT;

PROCEDURE <A NAME="AddC"><procedure>AddC</procedure></A> (VAR x: Info;  txt: TEXT) =
  BEGIN
    AddI (x.choice, 0, txt);
  END AddC;

PROCEDURE <A NAME="AddI"><procedure>AddI</procedure></A> (VAR z: InsList;  offs: INTEGER;  txt: TEXT) =
  VAR i := NEW (Insertion, next := NIL, offset := offs, insert := txt);
  BEGIN
    IF (z.head = NIL)
      THEN  z.head := i;
      ELSE  z.tail.next := i;
    END;
    z.tail := i;
    INC (z.cnt);
  END AddI;

PROCEDURE <A NAME="Append"><procedure>Append</procedure></A> (VAR a, b: InsList) =
  VAR x := b.head;  offs := a.tail.offset;
  BEGIN
    IF (b.cnt &lt;= 0) THEN RETURN END;

    (* splice the two lists *)
    INC (a.cnt, b.cnt);
    a.tail.next := x;
    a.tail := b.tail;

    (* fix the offsets *)
    WHILE (x # NIL) DO
      x.offset := offs;
      x := x.next;
    END;

    (* empty the old list *)
    b.head := NIL;
    b.tail := NIL;
    b.cnt  := 0;
  END Append;
</PRE>------------------------------------------------------- HREF generation ---

<P><PRE>VAR href_cache := NEW (TextTextTbl.Default);
VAR next_multi := 1;

PROCEDURE <A NAME="ResetCache"><procedure>ResetCache</procedure></A> () =
  BEGIN
    EVAL href_cache.init ();
    next_multi := 1;
  END ResetCache;

PROCEDURE <A NAME="UnknownRef"><procedure>UnknownRef</procedure></A> (VAR x: Info): BOOLEAN =
  VAR dest: TEXT;
  BEGIN
    IF NOT href_cache.get (x.key, dest) THEN RETURN TRUE; END;
    EmitRef (x, dest);
    RETURN FALSE;
  END UnknownRef;

PROCEDURE <A NAME="GenRef"><procedure>GenRef</procedure></A> (VAR x: Info;  tag: TEXT;  targets: TextList.T) =
  VAR ref: TEXT;
  BEGIN
    targets := NormalizeList (targets);

    IF targets = NIL THEN
      (* no hits *)
    ELSIF targets.tail = NIL THEN
      (* direct hit *)
      ref := targets.head &amp; End_file;
      IF (tag # NIL) THEN ref := ref &amp; Goto_tag &amp; tag; END;
      EVAL href_cache.put (x.key, ref);
      EmitRef (x, ref);
    ELSE
      (* a set of hits *)
      ref := GenMultiRef (x, tag, targets);
      IF (ref # NIL) THEN
        EVAL href_cache.put (x.key, ref);
        EmitRef (x, ref);
      END;
    END;
  END GenRef;

PROCEDURE <A NAME="EmitRef"><procedure>EmitRef</procedure></A> (VAR x: Info;  dest: TEXT) =
  BEGIN
    AddH (x, Begin_ref);
    IF (Text.GetChar (dest, 0) = '#')
      THEN AddH (x, dest);
      ELSE AddH (x, FilePath.Normalize (dest, x.path));
    END;
    AddH (x, End_ref);
    AddT (x, End_anchor);
  END EmitRef;

PROCEDURE <A NAME="GenMultiRef"><procedure>GenMultiRef</procedure></A> (VAR x: Info;  tag: TEXT;  targets: TextList.T): TEXT =
  VAR label := &quot;x&quot; &amp; Fmt.Int (next_multi);
  BEGIN
    IF (tag = NIL) THEN
      (* HACK work around xmosaic bug... *)
      tag := &quot;0TOP0&quot;;
    END;
    INC (next_multi);
    AddC (x, &quot;&lt;HR&gt;\n&quot;);
    AddC (x, Begin_tag);
    AddC (x, label);
    AddC (x, End_tag);
    AddC (x, x.key);
    AddC (x, &quot; is in:\n&quot;);
    AddC (x, End_anchor);
    AddC (x, &quot;&lt;UL&gt;\n&quot;);
    WHILE (targets # NIL) DO
      AddC (x, &quot;&lt;LI&gt;&quot;);
      AddC (x, Begin_ref);
      AddC (x, FilePath.Normalize (targets.head, x.path));
      AddC (x, End_file);
      IF (tag # NIL) THEN
        AddC (x, Goto_tag);
        AddC (x, tag);
      END;
      AddC (x, End_ref);
      AddC (x, targets.head);
      AddC (x, End_anchor);
      AddC (x, &quot;\n&quot;);
      targets := targets.tail;
    END;
    AddC (x, &quot;&lt;/UL&gt;\n&lt;P&gt;\n&quot;);
    RETURN Goto_tag &amp; label;
  END GenMultiRef;

PROCEDURE <A NAME="NormalizeList"><procedure>NormalizeList</procedure></A> (x: TextList.T): TextList.T =
  VAR cur, prev: TextList.T;
  BEGIN
    x := TextListSort.SortD (x);
    cur := x;  prev := NIL;
    WHILE (cur # NIL) DO
      IF (prev # NIL) AND Text.Equal (prev.head, cur.head)
        THEN prev.tail := cur.tail;  (* delete cur *)
        ELSE prev := cur;
      END;
      cur := cur.tail;
    END;
    RETURN x;
  END NormalizeList;

PROCEDURE <A NAME="And"><procedure>And</procedure></A> (a, b: TextList.T): TextList.T =
  (* destroys 'a', and normalizes 'b' *)
  VAR c, res: TextList.T;
  BEGIN
    a := NormalizeList (a);
    b := NormalizeList (b);
    res := NIL;
    WHILE (a # NIL) AND (b # NIL) DO
      CASE Text.Compare (a.head, b.head) OF
      | -1 =&gt; (* a &lt; b *)  a := a.tail;
      | +1 =&gt; (* a &gt; b *)  b := b.tail;
      |  0 =&gt; (* a = b *)  c := a;  a := a.tail; b := b.tail;
                           c.tail := res;  res := c;
      END;
    END;
    RETURN res;
  END And;
</PRE>--------------------------------------------------------------- sorting ---

<P><PRE>PROCEDURE <A NAME="SortInsertions"><procedure>SortInsertions</procedure></A> (VAR z: Info): Insertion =
</PRE><BLOCKQUOTE><EM> Do a simple insertion sort since the list is already nearly sorted </EM></BLOCKQUOTE><PRE>
  VAR a, b, c, d, e: Insertion;
  BEGIN
    a := z.ins.head;
    b := a.next;  a.next := NIL;
    WHILE (b # NIL) DO
      (* insert 'b' *)
      c := b.next;
      d := a;  e := NIL;
      WHILE (d # NIL) AND (b.offset &lt; d.offset) DO
        e := d;
        d := d.next;
      END;
      IF (e # NIL)
        THEN e.next := b;
        ELSE a := b;
      END;
      b.next := d;
      b := c;
    END;

    (* reverse the list *)
    b := NIL;
    WHILE (a # NIL) DO
      c := a.next;
      a.next := b;
      b := a;
      a := c;
    END;

    RETURN b;
  END SortInsertions;
</PRE>---------- debug ---
*********************
PROCEDURE Out (a, b: TEXT := NIL) =
  &lt;*FATAL ANY*&gt;
  BEGIN
    IF (a  # NIL) THEN Wr.PutText (Stdio.stdout, a); END;
    IF (b  # NIL) THEN Wr.PutText (Stdio.stdout, b); END;
    Wr.PutText (Stdio.stdout, <CODE>\n</CODE>);
  END Out;
<P>
PROCEDURE OutL (a: TEXT := NIL;  l: TextList.T := NIL) =
  &lt;*FATAL ANY*&gt;
  BEGIN
    IF (a  # NIL) THEN Wr.PutText (Stdio.stdout, a); END;
    WHILE (l # NIL) DO
      Wr.PutText (Stdio.stdout, l.head);
      Wr.PutText (Stdio.stdout, <CODE> </CODE>);
      l := l.tail;
    END;
    Wr.PutText (Stdio.stdout, <CODE>\n</CODE>);
  END OutL;
*************
<PRE>BEGIN
END M3MarkUp.
</PRE>
</inModule>
<HR>
<A NAME="x1">interface M3MarkUp is in:
</A><UL>
<LI><A HREF="../../cm3ide/src/markup/M3MarkUp.i3.html#0TOP0">cm3ide/src/markup/M3MarkUp.i3</A>
<LI><A HREF="../../m3markup/src/M3MarkUp.i3.html#0TOP0">m3markup/src/M3MarkUp.i3</A>
<LI><A HREF="M3MarkUp.i3.html#0TOP0">m3tohtml/src/M3MarkUp.i3</A>
</UL>
<P>
<HR>
<A NAME="x2">interface M3Scanner is in:
</A><UL>
<LI><A HREF="../../m3scan/src/M3Scanner.i3.html#0TOP0">m3scan/src/M3Scanner.i3</A>
<LI><A HREF="../../m3scanner/src/M3Scanner.i3.html#0TOP0">m3scanner/src/M3Scanner.i3</A>
</UL>
<P>
<PRE>























</PRE>
</BODY>
</HTML>
