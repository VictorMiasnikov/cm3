<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: sortedtableextras/src/SkipListTbl.mg</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>sortedtableextras/src/SkipListTbl.mg</H2></A><HR>
<inModule>
<PRE></PRE> Created on Sat Nov 22 11:57:02 PST 1997 by heydon       
 Last modified on Sat Nov 22 20:02:35 PST 1997 by heydon 
 Copyright (C) 1997, Digital Equipment Corporation       

<P><PRE>GENERIC MODULE <genericModule><A HREF="SkipListTbl.ig.html">SkipListTbl</A></genericModule>(Key, Value, Tbl, SortedTbl);

IMPORT <A HREF="../../libm3/src/random/Common/Random.i3.html">Random</A>, <A HREF="../../m3core/src/word/Word.i3.html">Word</A>;

TYPE
  ForwardPtrs = REF ARRAY OF Node;
  Node = REF RECORD
    k: Key.T;
    v: Value.T;
    back: Node := NIL;
    forward: ForwardPtrs := NIL;
  END;

REVEAL
  <A NAME="T">T</A> = Public BRANDED Brand OBJECT
    num: CARDINAL;         (* number of elements in the table *)
    maxLevel: CARDINAL;    (* READONLY after &quot;init&quot; *)
    level: CARDINAL;       (* current list level *)
    root: Node := NIL;     (* root node; its &quot;k&quot; and &quot;v&quot; are ignored *)
    update: ForwardPtrs;   (* scratch area used in &quot;put&quot;, &quot;delete&quot; *)
    rand: Random.T := NIL; (* random number generator *)
    randBits: INTEGER;     (* cache of random bits *)
    bitsRem: CARDINAL;     (* number of random bits remaining in &quot;lastRand&quot; *)
  OVERRIDES
    keyCompare := KeyCompare;
    init := Init;
    size := Size;
    get := Get;
    put := Put;
    delete := Delete;
    iterate := Iterate;
    iterateOrdered := IterateOrdered;
  END;
</PRE> The node pointed to by <CODE>root</CODE> stores <CODE>level</CODE> forward pointers, and
   its <CODE>back</CODE> field points to the last element of the list if the list
   is non-empty.
<P>
   Invariants that hold after <CODE>init</CODE>:
<P>
<PRE>
       I0. root # NIL
       I1. (root.back = NIL) = (num = 0)
       I1. level &lt;= maxLevel
       I2. NUMBER(update^) = NUMBER(root.forward^) = maxLevel
       I3. (forall i in [level, maxLevel): root.forward[i] = NIL)
       I4. rand # NIL AND bitsRem = Word.Size


<P><PRE>CONST
  IterBrand = &quot;(Iterator &quot; &amp; Brand &amp; &quot;)&quot;;

REVEAL
  <A NAME="Iterator">Iterator</A> = IteratorPublic BRANDED IterBrand OBJECT
    t: T;       (* corresponding tree *)
    curr: Node; (* current node in iteration *)
  END;

TYPE
  IteratorUp = Iterator OBJECT OVERRIDES
    reset := ResetUp;
    next := NextUp;
    seek := SeekUp;
  END;
  IteratorDown = Iterator OBJECT OVERRIDES
    reset := ResetDown;
    next := NextDown;
    seek := SeekDown;
  END;
</PRE> Utility procedures ------------------------------------------------------ 

<P><PRE>PROCEDURE <A NAME="FlipOneFourthCoin"><procedure>FlipOneFourthCoin</procedure></A>(t: T): INTEGER =
  VAR res := Word.And(t.randBits, 2_11); BEGIN
    IF t.bitsRem &gt; 2 THEN
      t.randBits := Word.RightShift(t.randBits, 2);
      DEC(t.bitsRem, 2)
    ELSE
      t.randBits := t.rand.integer();
      t.bitsRem := Word.Size
    END;
    RETURN res;
  END FlipOneFourthCoin;

PROCEDURE <A NAME="RandLevel"><procedure>RandLevel</procedure></A>(t: T): CARDINAL =
  VAR level: CARDINAL := 1; BEGIN
    WHILE level &lt; t.maxLevel AND FlipOneFourthCoin(t) = 0 DO
      INC(level);
    END;
    RETURN level
  END RandLevel;

PROCEDURE <A NAME="NewNode"><procedure>NewNode</procedure></A>(l: CARDINAL; READONLY k: Key.T; READONLY v: Value.T): Node =
</PRE><BLOCKQUOTE><EM> Return a new node with level <CODE>l</CODE>, key <CODE>k</CODE>, and value <CODE>v</CODE>. </EM></BLOCKQUOTE><PRE>
  BEGIN
    RETURN NEW(Node, k := k, v := v, forward := NEW(ForwardPtrs, l))
  END NewNode;

PROCEDURE <A NAME="Seek"><procedure>Seek</procedure></A>(t: T; READONLY k: Key.T): Node =
</PRE><BLOCKQUOTE><EM> Return the highest node in <CODE>t</CODE> with key less than <CODE>key</CODE>. </EM></BLOCKQUOTE><PRE>
  VAR alreadyChecked: Node := NIL; n := t.root; BEGIN
    FOR i := t.level - 1 TO 0 BY -1 DO
      VAR next := n.forward[i]; BEGIN
        WHILE next # alreadyChecked AND next # NIL AND
              t.keyCompare(next.k, k) &lt; 0 DO
          n := next; next := n.forward[i]
        END;
        alreadyChecked := next;
      END
    END;
    RETURN n
  END Seek;
</PRE> Tree method implementations --------------------------------------------- 

<P><PRE>PROCEDURE <A NAME="KeyCompare"><procedure>KeyCompare</procedure></A>(&lt;*UNUSED*&gt; t: T; READONLY k1, k2: Key.T): [-1..1] =
  BEGIN RETURN Key.Compare(k1, k2) END KeyCompare;

PROCEDURE <A NAME="Init"><procedure>Init</procedure></A>(t: T; maxSizeHint: CARDINAL; fixedSeed: BOOLEAN): T =
  BEGIN
    (* compute &quot;t.maxLevel&quot; as &quot;ceiling(log_4(maxSizeHint))&quot; *)
    t.maxLevel := 0;
    WHILE maxSizeHint &gt; 0 DO
      maxSizeHint := Word.RightShift(maxSizeHint, 2);
      INC(t.maxLevel)
    END;
    t.maxLevel := MAX(t.maxLevel, 1);

    (* initialized &quot;t.root&quot; *)
    IF t.root = NIL THEN t.root := NEW(Node) END;
    IF t.root.forward = NIL OR NUMBER(t.root.forward^) # t.maxLevel THEN
      t.root.forward := NEW(ForwardPtrs, t.maxLevel);
      t.update := NEW(ForwardPtrs, t.maxLevel);
    END;
    t.root.back := NIL;
    FOR i := 0 TO t.maxLevel - 1 DO t.root.forward[i] := NIL END;

    (* initialize random number generator *)
    IF fixedSeed OR t.rand = NIL THEN
      t.rand := NEW(Random.Default).init(fixedSeed)
    END;
    t.randBits := t.rand.integer();
    t.bitsRem := Word.Size;
    &lt;* ASSERT (t.bitsRem MOD 4) = 0 *&gt;

    (* initialize other fields *)
    t.num := 0;
    t.level := 0;
    RETURN t
  END Init;

PROCEDURE <A NAME="Size"><procedure>Size</procedure></A>(t: T): CARDINAL =
  BEGIN RETURN t.num END Size;

PROCEDURE <A NAME="Get"><procedure>Get</procedure></A>(t: T; READONLY k: Key.T; VAR (*OUT*) v: Value.T): BOOLEAN =
  VAR alreadyChecked: Node := NIL; n := t.root; BEGIN
    FOR i := t.level - 1 TO 0 BY -1 DO
      VAR next := n.forward[i]; BEGIN
        WHILE next # alreadyChecked AND next # NIL AND
              t.keyCompare(next.k, k) &lt; 0 DO
          n := next; next := n.forward[i]
        END;
        alreadyChecked := next
      END
    END;
    n := n.forward[0];
    IF n # NIL AND t.keyCompare(n.k, k) = 0 THEN
      v := n.v;
      RETURN TRUE
    END;
    RETURN FALSE
  END Get;

PROCEDURE <A NAME="Put"><procedure>Put</procedure></A>(t: T; READONLY k: Key.T; READONLY v: Value.T): BOOLEAN =
  VAR alreadyChecked: Node := NIL; n := t.root; BEGIN
    FOR i := t.level - 1 TO 0 BY -1 DO
      VAR next := n.forward[i]; BEGIN
        WHILE next # alreadyChecked AND next # NIL AND
              t.keyCompare(next.k, k) &lt; 0 DO
          n := next; next := n.forward[i]
        END;
        alreadyChecked := next;
        t.update[i] := n
      END
    END;
    n := n.forward[0];
    IF n # NIL AND t.keyCompare(n.k, k) = 0 THEN
      (* key already in table; set its value *)
      n.v := v;
      RETURN TRUE
    END;
    VAR newLevel := RandLevel(t); newNode := NewNode(newLevel, k, v); BEGIN
      (* correct &quot;update&quot; array if new level is new max *)
      IF newLevel &gt; t.level THEN
        FOR i := t.level TO newLevel - 1 DO t.update[i] := t.root END;
        t.level := newLevel
      END;
      (* update forward pointers *)
      FOR i := 0 TO newLevel - 1 DO
        WITH prev = t.update[i].forward[i] DO
          newNode.forward[i] := prev;
          prev := newNode
        END
      END;
      (* update back pointers *)
      newNode.back := t.update[0];
      VAR nextNode := newNode.forward[0]; BEGIN
      	IF nextNode = NIL
      	  THEN t.root.back := newNode
      	  ELSE nextNode.back := newNode
      	END
      END
    END;
    INC(t.num);
    RETURN FALSE
  END Put;

PROCEDURE <A NAME="Delete"><procedure>Delete</procedure></A>(t: T; READONLY k: Key.T; VAR (*OUT*) v: Value.T): BOOLEAN =
  VAR alreadyChecked: Node := NIL; n := t.root; BEGIN
    FOR i := t.level - 1 TO 0 BY -1 DO
      VAR next := n.forward[i]; BEGIN
        WHILE next # alreadyChecked AND next # NIL AND
              t.keyCompare(next.k, k) &lt; 0 DO
          n := next; next := n.forward[i]
        END;
        alreadyChecked := next;
        t.update[i] := n
      END
    END;
    n := n.forward[0];
    IF n = NIL OR t.keyCompare(n.k, k) # 0 THEN
      (* key not in table *)
      RETURN FALSE
    END;
    v := n.v;
    (* update forward pointers *)
    FOR i := 0 TO t.level - 1 DO
      WITH prevPtr = t.update[i].forward[i] DO
        IF prevPtr # n THEN EXIT END;
        prevPtr := n.forward[i]
      END
    END;
    (* update back pointers *)
    VAR nextNode := n.forward[0]; BEGIN
      IF nextNode = NIL
        THEN t.root.back := n.back
        ELSE nextNode.back := n.back
      END
    END;
    DEC(t.num);
    RETURN TRUE
  END Delete;

PROCEDURE <A NAME="Iterate"><procedure>Iterate</procedure></A>(t: T): Tbl.Iterator =
  BEGIN RETURN IterateOrdered(t, TRUE) END Iterate;

PROCEDURE <A NAME="IterateOrdered"><procedure>IterateOrdered</procedure></A>(t: T; up: BOOLEAN): SortedTbl.Iterator =
  VAR res: Iterator; BEGIN
    IF up
      THEN res := NEW(IteratorUp);
      ELSE res := NEW(IteratorDown);
    END;
    res.t := t;
    res.reset();
    RETURN res
  END IterateOrdered;
</PRE> Iterator method implementations ---------------------------------------- 

<P><PRE>PROCEDURE <A NAME="ResetUp"><procedure>ResetUp</procedure></A>(it: Iterator) =
  BEGIN it.curr := it.t.root.forward[0] END ResetUp;

PROCEDURE <A NAME="ResetDown"><procedure>ResetDown</procedure></A>(it: Iterator) =
  BEGIN it.curr := it.t.root.back END ResetDown;

PROCEDURE <A NAME="NextUp"><procedure>NextUp</procedure></A>(it: Iterator; VAR (*OUT*) k: Key.T; VAR (*OUT*) v: Value.T):
    BOOLEAN =
  VAR curr := it.curr; BEGIN
    IF curr = NIL THEN RETURN FALSE END;
    k := curr.k; v := curr.v;
    it.curr := curr.forward[0];
    RETURN TRUE;
  END NextUp;

PROCEDURE <A NAME="NextDown"><procedure>NextDown</procedure></A>(it: Iterator; VAR (*OUT*) k: Key.T; VAR (*OUT*) v: Value.T):
    BOOLEAN =
  VAR curr := it.curr; BEGIN
    IF curr = it.t.root THEN RETURN FALSE END;
    k := curr.k; v := curr.v;
    it.curr := curr.back;
    RETURN TRUE;
  END NextDown;

PROCEDURE <A NAME="SeekUp"><procedure>SeekUp</procedure></A>(it: Iterator; READONLY key: Key.T) =
  VAR n := Seek(it.t, key); BEGIN
    it.curr := n.forward[0]
  END SeekUp;

PROCEDURE <A NAME="SeekDown"><procedure>SeekDown</procedure></A>(it: Iterator; READONLY key: Key.T) =
  VAR n := Seek(it.t, key); next := n.forward[0]; BEGIN
    IF next # NIL AND it.t.keyCompare(next.k, key) = 0
      THEN it.curr := next
      ELSE it.curr := n
    END
  END SeekDown;

BEGIN
END SkipListTbl.
</PRE>
</inModule>
<PRE>























</PRE>
</BODY>
</HTML>
