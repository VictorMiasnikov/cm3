<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: stablegen/src/ImportList.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>stablegen/src/ImportList.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright (C) 1989, Digital Equipment Corporation               
 All rights reserved.                                            
 See the file COPYRIGHT for a full description.                  
 Created by Susan Owicki, rewritten by Carsten Weich             
 Last modified on Fri Jan  6 12:48:02 PST 1995 by chaiken        
      modified on Fri Sep 23 14:48:47 PDT 1994 by weich          

<P> The module contains procedures to gain necessary imports to compile a
   type. 

<P><PRE>MODULE <module><implements><A HREF="#x1">ImportList</A></implements></module>;

IMPORT <A HREF="../../libm3/src/atom/Atom.i3.html">Atom</A>, <A HREF="../../libm3/derived/AtomRefTbl.i3.html">AtomRefTbl</A>, <A HREF="#x2">Type</A>;
</PRE> \subsection{Producing import lists} The proceuderes <CODE>Import...</CODE> maintain
   a list modules necessary to represent a type.  This list is a
   <CODE>AtomRefTbl.T</CODE> which is only used in a set like fashion.  There are no
   entries in the table (<CODE>NIL</CODE> is inserted together with the interface).
<P>
   The main module called from outside is <CODE>FromType()</CODE>.

<PRE>REVEAL <A NAME="T">T</A> = AtomRefTbl.Default BRANDED OBJECT END;

PROCEDURE <A NAME="FromType"><procedure>FromType</procedure></A> (type: Type.Object; methods: MethodList): T =
  VAR
    obj      : Type.Reference := type;
    importTbl: T              := NEW(T).init();
  BEGIN
    IF type.name # NIL AND type.name.intf # nullAtm THEN
      EVAL importTbl.put(type.name.intf, NIL);
    END;
    ImportRevelations(type, importTbl);
    FOR i := 0 TO LAST(methods^) DO
      WITH sig = methods[i].sig DO
        ImportFromSig(sig, importTbl);
        ImportRefsFromSig(sig, importTbl);
        (* Hmm.  StableData.m3 will not actually handle the exceptions.
        FOR j := 0 TO LAST(sig.raises^) DO
          ImportFromType(sig.raises[j].arg, importTbl);
        END;
        *)
      END                        (*WITH*)
    END;                         (*FOR*)
    WHILE obj # NIL AND ISTYPE(obj, Type.Object) DO
      IF obj.revIntf # NIL THEN EVAL importTbl.put(obj.revIntf, NIL); END;
      obj := NARROW(obj, Type.Object).super;
    END;
    RETURN importTbl;
  END FromType;

PROCEDURE <A NAME="ImportRevelations"><procedure>ImportRevelations</procedure></A> (t: Type.Reference; importTbl: T) =
  (* Add to the table &quot;importTbl&quot; the names of interfaces that provide
     revelations of &quot;t&quot; or one of its supertypes. *)
  BEGIN
    IF t.revIntf # NIL THEN EVAL importTbl.put(t.revIntf, NIL); END;
    TYPECASE t OF
    | Type.Object (obj) =&gt;
        VAR o := obj;
        BEGIN
          WHILE TRUE DO
            TYPECASE o.super OF
            | Type.Opaque =&gt; EXIT
            | Type.Object =&gt;
                IF o.revIntf # NIL THEN
                  EVAL importTbl.put(o.revIntf, NIL);
                END;
                o := o.super;
            ELSE
              &lt;*ASSERT FALSE*&gt;
            END;
          END;
        END;
    ELSE
    END;
  END ImportRevelations;

PROCEDURE <A NAME="ImportFromType"><procedure>ImportFromType</procedure></A> (t: Type.T; importTbl: T; byName: BOOLEAN := TRUE) =
  BEGIN
    IF t = NIL THEN RETURN END;
    IF t.name # NIL AND t.name.intf # nullAtm THEN
      EVAL importTbl.put(t.name.intf, NIL);
    END;
    TYPECASE t OF
    | Type.Reference (ref) =&gt; ImportRevelations(ref, importTbl);
    ELSE
    END;
    IF byName AND t.name # NIL THEN RETURN; END;
    TYPECASE t OF
    | Type.Enumeration, Type.UserDefined, Type.Real, Type.LongReal,
        Type.Extended =&gt;
    | Type.Subrange (sr) =&gt; ImportFromType(sr.base, importTbl);
    | Type.Object (ob) =&gt;
        ImportFromFields(ob.fields, importTbl);
        FOR i := 0 TO LAST(ob.methods^) DO
          ImportFromSig(ob.methods[i].sig, importTbl);
        END;
        IF ob.revIntf # NIL THEN EVAL importTbl.put(ob.revIntf, NIL); END;
    | Type.Ref (r) =&gt; ImportFromType(r.target, importTbl);
    | Type.Opaque (op) =&gt; ImportFromType(op.revealedSuperType, importTbl);
    | Type.Array (a) =&gt;
        ImportFromType(a.index, importTbl);
        ImportFromType(a.element, importTbl);
    | Type.Packed (p) =&gt; ImportFromType(p.base, importTbl);
    | Type.Record (rec) =&gt; ImportFromFields(rec.fields, importTbl);
    | Type.Set (s) =&gt; ImportFromType(s.range, importTbl);
    | Type.Procedure (p) =&gt; ImportFromSig(p.sig, importTbl);
    ELSE
      &lt;*ASSERT FALSE*&gt;
    END;
  END ImportFromType;

PROCEDURE <A NAME="ImportFromFields"><procedure>ImportFromFields</procedure></A> (fields: REF ARRAY OF Type.Field; importTbl: T) =
  BEGIN
    IF fields = NIL THEN RETURN END;
    FOR i := 0 TO LAST(fields^) DO
      ImportFromType(fields[i].type, importTbl);
    END;
  END ImportFromFields;

PROCEDURE <A NAME="ImportFromSig"><procedure>ImportFromSig</procedure></A> (sig: Type.Signature; importTbl: T) =
  BEGIN
    FOR i := 0 TO LAST(sig.formals^) DO
      ImportFromType(sig.formals[i].type, importTbl);
    END;
    ImportFromType(sig.result, importTbl);
    IF sig.raises # NIL THEN
      FOR i := 0 TO LAST(sig.raises^) DO
        EVAL importTbl.put(sig.raises[i].qid.intf, NIL);
      END;
    END;
  END ImportFromSig;

PROCEDURE <A NAME="ImportRefsFromSig"><procedure>ImportRefsFromSig</procedure></A> (sig: Type.Signature; importTbl: T) =
  BEGIN
    FOR i := 0 TO LAST(sig.formals^) DO
      ImportRefsFromType(sig.formals[i].type, importTbl);
    END;
    ImportRefsFromType(sig.result, importTbl);
  END ImportRefsFromSig;

PROCEDURE <A NAME="ImportRefsFromType"><procedure>ImportRefsFromType</procedure></A> (t: Type.T; importTbl: T) =
  BEGIN
    IF t = NIL THEN RETURN END;
    TYPECASE t OF
    | Type.Enumeration, Type.UserDefined, Type.Real, Type.LongReal,
        Type.Extended, Type.Subrange, Type.Set, Type.Procedure =&gt;
    | Type.Reference =&gt;
        IF NOT Type.MayBeRefAny(t) AND Type.NamedType(t) THEN
          ImportFromType(t, importTbl)
        END;
    | Type.Array (a) =&gt;
        ImportFromType(a.index, importTbl);
        ImportRefsFromType(a.element, importTbl);
    | Type.Packed (p) =&gt; ImportRefsFromType(p.base, importTbl);
    | Type.Record (rec) =&gt;
        IF rec.fields = NIL THEN RETURN END;
        FOR i := 0 TO LAST(rec.fields^) DO
          ImportRefsFromType(rec.fields[i].type, importTbl);
        END;
    ELSE
      &lt;*ASSERT FALSE*&gt;
    END;
  END ImportRefsFromType;
</PRE> \subsection{Procedure Add} Add an element <CODE>intf</CODE> to a import list. 
<PRE>PROCEDURE <A NAME="Add"><procedure>Add</procedure></A> (importList: T; intf: Atom.T) =
  BEGIN
    EVAL importList.put(intf, NIL)
  END Add;
</PRE> \subsection{Procedure ToText} Generate a comma separated list of
   interface names out of <CODE>imports</CODE>.  The output is suitable for an
   <CODE>IMPORT</CODE> statement.
<P>
   Iterate over the table <CODE>imports</CODE> and append to <CODE>result</CODE>.

<PRE>PROCEDURE <A NAME="ToText"><procedure>ToText</procedure></A> (imports: T): TEXT =
  VAR
    intf  : Atom.T;
    dummy : REFANY;
    iter           := imports.iterate();
    result         := &quot;&quot;;
  BEGIN
    IF iter.next(intf, dummy) THEN
      result := Atom.ToText(intf);
      WHILE iter.next(intf, dummy) DO
        result := result &amp; &quot;, &quot; &amp; Atom.ToText(intf);
      END;
    END;
    RETURN result;
  END ToText;

VAR nullAtm := Atom.FromText(&quot;&quot;);

BEGIN
END ImportList.
</PRE>
</inModule>
<HR>
<A NAME="x1">interface ImportList is in:
</A><UL>
<LI><A HREF="../../sharedobjgen/src/ImportList.i3.html#0TOP0">sharedobjgen/src/ImportList.i3</A>
<LI><A HREF="ImportList.i3.html#0TOP0">stablegen/src/ImportList.i3</A>
</UL>
<P>
<HR>
<A NAME="x2">interface Type is in:
</A><UL>
<LI><A HREF="../../cm3ide/src/nodes/Type.i3.html#0TOP0">cm3ide/src/nodes/Type.i3</A>
<LI><A HREF="../../m3front/src/types/Type.i3.html#0TOP0">m3front/src/types/Type.i3</A>
<LI><A HREF="../../sharedobjgen/src/Type.i3.html#0TOP0">sharedobjgen/src/Type.i3</A>
<LI><A HREF="Type.i3.html#0TOP0">stablegen/src/Type.i3</A>
<LI><A HREF="../../stubgen/src/Type.i3.html#0TOP0">stubgen/src/Type.i3</A>
</UL>
<P>
<PRE>























</PRE>
</BODY>
</HTML>
