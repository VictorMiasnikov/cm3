<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: m3zume/src/EventFile.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>m3zume/src/EventFile.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright (C) 1995, Digital Equipment Corporation        
 All rights reserved.                                     
 See the file COPYRIGHT for a full description.           
                                                          
 Last modified on Wed Aug 21 15:07:51 PDT 1996 by najork  
      modified on Tue Feb  7 14:51:54 PST 1995 by kalsow  

<P><PRE>MODULE <module><implements><A HREF="EventFile.i3.html">EventFile</A></implements></module>;

IMPORT <A HREF="../../m3core/src/text/Text.i3.html">Text</A>, <A HREF="../../libm3/src/rw/Rd.i3.html">Rd</A>, <A HREF="../../libm3/src/atom/Atom.i3.html">Atom</A>, <A HREF="../../libm3/derived/AtomList.i3.html">AtomList</A>, <A HREF="../../m3core/src/thread/Common/Thread.i3.html">Thread</A>, <A HREF="../../libm3/src/fmtlex/Fmt.i3.html">Fmt</A>, <A HREF="../../libm3/derived/TextTextTbl.i3.html">TextTextTbl</A>, <A HREF="../../libm3/src/rw/TextWr.i3.html">TextWr</A>, <A HREF="../../libm3/src/rw/Wr.i3.html">Wr</A>;

EXCEPTION Error (TEXT);

CONST
  EOF = '\000';

TYPE (* token type *)
  TK = { EOF, Id, Digit,
         Import, AlgData,
         Output, Procedure, Update, Feedback,
         Value, Readonly,
         Comma, Semi, Colon, R_paren, L_paren, R_bracket, L_bracket };

CONST
  TKName = ARRAY TK OF TEXT {
    &quot;&lt;EOF&gt;&quot;, &quot;&lt;identifier&gt;&quot;, &quot;&lt;digit&gt;&quot;,
    &quot;IMPORT&quot;, &quot;ALGDATA&quot;,
    &quot;OUTPUT&quot;, &quot;PROCEDURE&quot;, &quot;UPDATE&quot;, &quot;FEEDBACK&quot;,
    &quot;VALUE&quot;, &quot;READONLY&quot;,
    &quot;','&quot;, &quot;';'&quot;, &quot;':'&quot;, &quot;')'&quot;, &quot;'('&quot;, &quot;']'&quot;, &quot;'['&quot;
  };

TYPE
  State = RECORD
    rd              : Rd.T;
    ch              : CHAR;
    at_eof          : BOOLEAN := FALSE;
    tok             : TK      := TK.EOF;
    tok_line        : INTEGER := 0;
    cur_line        : INTEGER := 0;
    cur_id          : TEXT    := NIL;
    alg_data        : TEXT    := NIL;
    import_head     : Import  := NIL;
    import_tail     : Import  := NIL;
    event_head      : Event   := NIL;
    event_tail      : Event   := NIL;
  END;

TYPE
  CC = { WhiteSpace, Letter, Illegal };

VAR
  init_done := FALSE;
  CharClass : ARRAY CHAR OF CC;
  BuiltinPrinters: TextTextTbl.T := NIL;

PROCEDURE <A NAME="Parse"><procedure>Parse</procedure></A> (rd: Rd.T;  VAR(*OUT*) t: T): TEXT =
  VAR s: State;
  BEGIN
    Init ();
    t.imports  := NIL;
    t.alg_data := NIL;
    t.events   := NIL;
    TRY
      s.rd := rd;
      s.ch := ' ';
      s.cur_line := 1;
      GetToken (s);
      ParseImports (s);
      ParseAlgData (s);
      ParseEvents (s);
      IF (s.tok # TK.EOF) THEN
        IF (s.event_head # NIL) OR (s.alg_data # NIL) THEN
          ParseErr (s, &quot;OUTPUT, PROCEDURE, UPDATE, or FEEDBACK&quot;);
        ELSE
          ParseErr (s, &quot;IMPORT, ALGDATA, OUTPUT, PROCEDURE, UPDATE, or FEEDBACK&quot;);
        END;
      END;
    EXCEPT Error (msg) =&gt;
      RETURN msg;
    END;
    t.imports  := s.import_head;
    t.alg_data := s.alg_data;
    t.events   := s.event_head;
    RETURN NIL;
  END Parse;

PROCEDURE <A NAME="ParseImports"><procedure>ParseImports</procedure></A> (VAR s: State) RAISES {Error} =
  VAR im: Import;
  BEGIN
    WHILE (s.tok = TK.Import) DO
      GetToken (s); (* IMPORT *)
      WHILE (s.tok = TK.Id) DO
        im := NEW (Import, next := NIL, interface := s.cur_id);
        IF (s.import_head = NIL)
          THEN s.import_head := im;
          ELSE s.import_tail.next := im;
        END;
        s.import_tail := im;
        GetToken (s); (* ID *)
        IF (s.tok # TK.Comma) THEN EXIT; END;
        GetToken (s); (* &quot;,&quot; *)
      END;
      IF (s.tok = TK.Semi) THEN
        GetToken (s);  (* &quot;;&quot; *)
      ELSE
        ParseErr (s, &quot;',' or ';' in IMPORT list&quot;);
      END;
    END;
  END ParseImports;

PROCEDURE <A NAME="ParseAlgData"><procedure>ParseAlgData</procedure></A> (VAR s: State) RAISES {Error} =
  BEGIN
    IF (s.tok = TK.AlgData) THEN
      s.alg_data := GetSection (s);
      GetToken (s); (* load the token following the AlgData section *)
    END;
  END ParseAlgData;

PROCEDURE <A NAME="ParseEvents"><procedure>ParseEvents</procedure></A> (VAR s: State) RAISES {Error} =
  VAR e: Event;
  BEGIN
    LOOP
      e := NEW (Event, next := NIL, name := NIL, args := NIL);
      IF    (s.tok = TK.Output)    THEN  e.kind := EventKind.Output;
      ELSIF (s.tok = TK.Procedure) THEN  e.kind := EventKind.Output;
      ELSIF (s.tok = TK.Update)    THEN  e.kind := EventKind.Update;
      ELSIF (s.tok = TK.Feedback)  THEN  e.kind := EventKind.Feedback;
      ELSE EXIT;
      END;
      IF (s.event_head = NIL)
        THEN s.event_head := e;
        ELSE s.event_tail.next := e;
      END;
      s.event_tail := e;
      GetToken (s);  (* OUTPUT, PROCEDURE, UPDATE, or FEEDBACK *)

      IF (s.tok = TK.L_bracket) THEN
        GetToken (s); (* '[' *)
        IF (s.tok # TK.Id)
          OR (Text.Length (s.cur_id) # 1)
          OR (Text.GetChar (s.cur_id, 0) &lt; '0')
          OR (Text.GetChar (s.cur_id, 0) &gt; '9') THEN
          ParseErr (s, &quot;event priority [0..9]&quot;);
        END;
        e.priority := s.cur_id;
        GetToken (s); (* &lt;id&gt; *)
        IF (s.tok # TK.R_bracket) THEN ParseErr (s, &quot;closing ']'&quot;); END;
        GetToken (s); (* ']' *)
      ELSE
        e.priority := &quot;1&quot;;
      END;

      IF (s.tok # TK.Id) THEN ParseErr (s, &quot;event name&quot;); END;
      e.name := s.cur_id;
      GetToken (s);  (* &lt;ident&gt; *)

      IF (s.tok = TK.L_paren) THEN
        GetToken (s); (* '(' *)
        ParseArgs (s, e);
        IF (s.tok # TK.R_paren) THEN ParseErr (s, &quot;closing ')'&quot;); END;
        GetToken (s); (* ')' *)
      END;

      IF (s.tok # TK.Semi) THEN ParseErr (s, &quot;';'&quot;); END;
      GetToken (s);
    END;
  END ParseEvents;

PROCEDURE <A NAME="ParseArgs"><procedure>ParseArgs</procedure></A> (VAR s: State;  e: Event) RAISES {Error} =
  VAR a, b, c: EventArg;   m: ArgMode;  printer: TEXT;
  BEGIN
    LOOP
      m := ArgMode.Value;
      IF    (s.tok = TK.Value)    THEN m := ArgMode.Value;     GetToken (s);
      ELSIF (s.tok = TK.Readonly) THEN m := ArgMode.Readonly;  GetToken (s);
      ELSIF (s.tok = TK.R_paren)  THEN EXIT;
      END;

      IF (s.tok # TK.Id) THEN ParseErr (s, &quot;argument name&quot;); END;
      c := e.args; (* first old arg *)
      WHILE (s.tok = TK.Id) DO
        a := NEW (EventArg, next := e.args, mode := m,
                name := s.cur_id, type := NIL, printer := NIL);
        e.args := a;
        GetToken (s); (* &lt;id&gt; *)
        IF (s.tok # TK.Comma) THEN EXIT END;
        GetToken (s); (* ',' *)
      END;

      IF (s.tok # TK.Colon) THEN ParseErr (s, &quot;':' type name&quot;); END;
      GetToken (s); (* &quot;:&quot; *)

      IF (s.tok # TK.Id) THEN ParseErr (s, &quot;type name&quot;); END;
      b := a; REPEAT b.type := s.cur_id; b := b.next; UNTIL b = c;
      GetToken (s); (* &lt;id&gt; *)

      IF (s.tok = TK.L_bracket) THEN
        GetToken (s); (* &quot;[&quot; *)
        IF (s.tok =  TK.Id) THEN
          printer := s.cur_id;
          GetToken (s); (* &lt;id&gt; *)
        ELSE
          printer := &quot;&quot;;
        END;
        IF (s.tok # TK.R_bracket) THEN ParseErr (s, &quot;closing ']'&quot;); END;
        GetToken (s); (* &quot;]&quot; *)
      ELSIF NOT BuiltinPrinters.get (a.type, printer) THEN
        ParseErr (s, &quot;print procedure for type &quot; &amp; a.type);
      END;
      b := a; REPEAT b.printer := printer; b := b.next; UNTIL b = c;

      IF    (s.tok = TK.Semi)    THEN GetToken (s); (* &quot;;&quot; *)
      ELSIF (s.tok = TK.R_paren) THEN EXIT;
      ELSE ParseErr (s, &quot;';' or ')'&quot;);
      END;
    END;

    (* reverse the arg list *)
    b := NIL;  a := e.args;
    WHILE (a # NIL) DO
      c := a.next;
      a.next := b;
      b := a;
      a := c;
    END;
    e.args := b;

  END ParseArgs;

PROCEDURE <A NAME="ParseErr"><procedure>ParseErr</procedure></A> (VAR s: State;  expected: TEXT) RAISES {Error} =
  VAR tok := TKName [s.tok];
  BEGIN
    IF (s.tok = TK.Id) OR (s.tok = TK.Digit) THEN
      tok := tok &amp; &quot; = \&quot;&quot; &amp; s.cur_id &amp; &quot;\&quot;&quot;;
    END;
    Err (s, &quot;syntax error, expected &quot; &amp; expected &amp; &quot;, but found &quot; &amp; tok);
  END ParseErr;

PROCEDURE <A NAME="GetSection"><procedure>GetSection</procedure></A> (VAR s: State): TEXT RAISES {Error} =
  (* read lines up to a blank line. *)
  &lt;*FATAL Wr.Failure, Thread.Alerted*&gt;
  VAR wr := TextWr.New ();  blank_line : BOOLEAN;
  BEGIN
      (* skip white space *)
      WHILE (CharClass [s.ch] = CC.WhiteSpace) AND (s.ch # '\n') DO
        GetCh (s);
      END;
      IF (s.ch = '\n') THEN GetCh (s); END;

      (* accumulate bytes *)
      blank_line := TRUE;
      LOOP
        IF (s.ch = EOF) THEN RETURN TextWr.ToText (wr); END;
        Wr.PutChar (wr, s.ch);
        IF (s.ch = '\n') THEN
          IF (blank_line) THEN RETURN TextWr.ToText (wr); END;
          blank_line := TRUE;
        END;
        blank_line := blank_line AND (CharClass[s.ch] = CC.WhiteSpace);
        GetCh (s);
      END;
  END GetSection;

PROCEDURE <A NAME="GetToken"><procedure>GetToken</procedure></A> (VAR s: State) RAISES {Error} =
  VAR len: INTEGER;  buf: ARRAY [0..255] OF CHAR;
  BEGIN
    LOOP
      (* skip white space *)
      WHILE (CharClass [s.ch] = CC.WhiteSpace) DO GetCh (s); END;

      (* remember where the token started *)
      s.tok_line := s.cur_line;

      CASE s.ch OF
      | 'a'..'z', 'A'..'Z' =&gt; (* identifier *)
           len := 0;
           WHILE (CharClass [s.ch] = CC.Letter) DO
             IF (len &gt; LAST (buf)) THEN Err (s, &quot;identifier too long&quot;); END;
             buf[len] := s.ch;  INC (len);
             GetCh (s);
           END;
           s.tok := TK.Id;
           s.cur_id := Text.FromChars (SUBARRAY (buf, 0, len));
           ClassifyID (s, len);
           EXIT;

      | '0' .. '9' =&gt; (* digit *)
           s.tok := TK.Digit;
           s.cur_id := Text.FromChar (s.ch);
           EXIT;

      | '(' =&gt;
           GetCh (s);
           IF (s.ch = '*')
             THEN SkipComment (s);
             ELSE s.tok := TK.L_paren; EXIT;
           END;

      | ')' =&gt; s.tok := TK.R_paren;    GetCh (s);  EXIT;
      | '[' =&gt; s.tok := TK.L_bracket;  GetCh (s);  EXIT;
      | ']' =&gt; s.tok := TK.R_bracket;  GetCh (s);  EXIT;
      | ',' =&gt; s.tok := TK.Comma;      GetCh (s);  EXIT;
      | ';' =&gt; s.tok := TK.Semi;       GetCh (s);  EXIT;
      | ':' =&gt; s.tok := TK.Colon;      GetCh (s);  EXIT;
      | EOF =&gt; s.tok := TK.EOF;  EXIT;
      ELSE Err (s, &quot;illegal character: 8_&quot; &amp; Fmt.Int (ORD (s.ch), 8)); EXIT;
      END; (* case *)
    END; (*loop*)
  END GetToken;

PROCEDURE <A NAME="SkipComment"><procedure>SkipComment</procedure></A> (VAR s: State) RAISES {Error} =
  VAR nest: INTEGER;
  BEGIN
    GetCh (s); (* initial '*' *)
    nest := 1;
    WHILE (nest &gt; 0) DO
      IF (s.ch = '*') THEN
        GetCh (s);  IF (s.ch = ')') THEN DEC (nest); GetCh (s);  END;
      ELSIF (s.ch = '(') THEN
        GetCh (s);  IF (s.ch = '*') THEN INC (nest); GetCh (s);  END;
      ELSIF (s.ch = EOF) THEN
        Err (s, &quot;EOF encountered in comment&quot;);
        nest := 0;
      ELSE
        GetCh (s);
      END;
    END;
  END SkipComment;

PROCEDURE <A NAME="ClassifyID"><procedure>ClassifyID</procedure></A> (VAR s: State;  len: INTEGER) =
  BEGIN
    CASE len OF
    | 5 =&gt; IF    Text.Equal (s.cur_id, &quot;VALUE&quot;)     THEN s.tok := TK.Value;
           END;
    | 6 =&gt; IF    Text.Equal (s.cur_id, &quot;OUTPUT&quot;)    THEN s.tok := TK.Output;
           ELSIF Text.Equal (s.cur_id, &quot;IMPORT&quot;)    THEN s.tok := TK.Import;
           ELSIF Text.Equal (s.cur_id, &quot;UPDATE&quot;)    THEN s.tok := TK.Update;
           END;
    | 7 =&gt; IF    Text.Equal (s.cur_id, &quot;ALGDATA&quot;)   THEN s.tok := TK.AlgData;
           END;
    | 8 =&gt; IF    Text.Equal (s.cur_id, &quot;FEEDBACK&quot;)  THEN s.tok := TK.Feedback;
           ELSIF Text.Equal (s.cur_id, &quot;READONLY&quot;)  THEN s.tok := TK.Readonly;
           END;
    | 9 =&gt; IF    Text.Equal (s.cur_id, &quot;PROCEDURE&quot;) THEN s.tok := TK.Procedure;
           END;
    ELSE (* skip *)
    END;
  END ClassifyID;

PROCEDURE <A NAME="GetCh"><procedure>GetCh</procedure></A> (VAR s: State) RAISES {Error} =
  BEGIN
    IF s.at_eof THEN  s.ch := EOF; RETURN;  END;
    TRY
      s.ch := Rd.GetChar (s.rd);
      IF (s.ch = '\n') THEN INC (s.cur_line); END;
    EXCEPT
    | Rd.EndOfFile   =&gt; s.ch := EOF;  s.at_eof := TRUE;
    | Rd.Failure(ec) =&gt; Err (s, &quot;problem reading event file&quot; &amp; OSErr (ec));
    | Thread.Alerted =&gt; Err (s, &quot;interrupted while reading event file&quot;);
    END;
  END GetCh;

PROCEDURE <A NAME="Err"><procedure>Err</procedure></A> (READONLY s: State;  msg: TEXT) RAISES {Error} =
  BEGIN
    msg := &quot;line &quot; &amp; Fmt.Int (s.tok_line) &amp; &quot;: &quot; &amp; msg;
    RAISE Error (msg);
  END Err;

PROCEDURE <A NAME="OSErr"><procedure>OSErr</procedure></A> (args: AtomList.T): TEXT =
  VAR msg : TEXT := NIL;
  BEGIN
    WHILE (args # NIL) DO
      IF (msg = NIL) THEN  msg := &quot;: &quot;;  ELSE  msg := msg &amp; &quot;  ***  &quot;;  END;
      msg  := msg &amp; Atom.ToText (args.head);
      args := args.tail;
    END;
    IF (msg = NIL) THEN msg := &quot;&quot; END;
    RETURN msg;
  END OSErr;

PROCEDURE <A NAME="Init"><procedure>Init</procedure></A> () =
  BEGIN
    IF init_done THEN RETURN END;
    FOR c := FIRST (CHAR) TO LAST (CHAR) DO CharClass[c] := CC.Illegal; END;
    CharClass[' ']  := CC.WhiteSpace;
    CharClass['\t'] := CC.WhiteSpace;
    CharClass['\n'] := CC.WhiteSpace;
    CharClass['\r'] := CC.WhiteSpace;
    CharClass['.']  := CC.Letter;
    CharClass['_']  := CC.Letter;
    FOR c := 'a' TO 'z' DO CharClass[c] := CC.Letter; END;
    FOR c := 'A' TO 'Z' DO CharClass[c] := CC.Letter; END;
    FOR c := '0' TO '9' DO CharClass[c] := CC.Letter; END;
    BuiltinPrinters := NEW (TextTextTbl.Default).init ();
    EVAL BuiltinPrinters.put (&quot;INTEGER&quot;,  &quot;Fmt.Int&quot;);
    EVAL BuiltinPrinters.put (&quot;BOOLEAN&quot;,  &quot;OblFmt.Bool&quot;);
    EVAL BuiltinPrinters.put (&quot;CARDINAL&quot;, &quot;Fmt.Int&quot;);
    EVAL BuiltinPrinters.put (&quot;REAL&quot;,     &quot;OblFmt.Real&quot;);
    EVAL BuiltinPrinters.put (&quot;LONGREAL&quot;, &quot;Fmt.LongReal&quot;);
    EVAL BuiltinPrinters.put (&quot;EXTENDED&quot;, &quot;Fmt.Extended&quot;);
    EVAL BuiltinPrinters.put (&quot;Text.T&quot;,   &quot;TextConv.Encode&quot;);
    EVAL BuiltinPrinters.put (&quot;TEXT&quot;,     &quot;TextConv.Encode&quot;);
    init_done := TRUE;
  END Init;

BEGIN
END EventFile.
</PRE>
</inModule>
<PRE>























</PRE>
</BODY>
</HTML>
