<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: server/src/RCSComp.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>server/src/RCSComp.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright 1996-2003 John D. Polstra.
 * All rights reserved.
 *
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions
 * are met:
 * 1. Redistributions of source code must retain the above copyright
 *    notice, this list of conditions and the following disclaimer.
 * 2. Redistributions in binary form must reproduce the above copyright
 *    notice, this list of conditions and the following disclaimer in the
 *    documentation and/or other materials provided with the distribution.
 * 3. All advertising materials mentioning features or use of this software
 *    must display the following acknowledgment:
 *      This product includes software developed by John D. Polstra.
 * 4. The name of the author may not be used to endorse or promote products
 *    derived from this software without specific prior written permission.
 *
 * THIS SOFTWARE IS PROVIDED BY THE AUTHOR ``AS IS'' AND ANY EXPRESS OR
 * IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES
 * OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED.
 * IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR ANY DIRECT, INDIRECT,
 * INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT
 * NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
 * DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
 * THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
 * (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF
 * THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 *
 * $Id: RCSComp.m3.html,v 1.3 2010-04-29 17:19:53 wagner Exp $ 

<P><PRE>MODULE <module><implements><A HREF="RCSComp.i3.html">RCSComp</A></implements></module>;

IMPORT
  <A HREF="../../suplib/src/Attic.i3.html">Attic</A>, <A HREF="ClientClass.i3.html">ClientClass</A>, <A HREF="../../suplib/src/CVProto.i3.html">CVProto</A>, <A HREF="../../suplib/src/ErrMsg.i3.html">ErrMsg</A>, <A HREF="../../suplib/src/EscapedWr.i3.html">EscapedWr</A>, <A HREF="../../suplib/src/ExecRec.i3.html">ExecRec</A>, <A HREF="../../libm3/src/os/Common/File.i3.html">File</A>,
  <A HREF="../../suplib/src/FileAttr.i3.html">FileAttr</A>, <A HREF="../../libm3/src/rw/FileRd.i3.html">FileRd</A>, <A HREF="../../libm3/src/fmtlex/Fmt.i3.html">Fmt</A>, <A HREF="../../libm3/src/os/Common/FS.i3.html">FS</A>, <A HREF="#x1">GlobTree</A>, <A HREF="../../suplib/src/GzipRd.i3.html">GzipRd</A>, <A HREF="../../suplib/src/GzipWr.i3.html">GzipWr</A>, <A HREF="../../suplib/src/Logger.i3.html">Logger</A>,
  <A HREF="../../suplib/src/MD5.i3.html">MD5</A>, <A HREF="../../suplib/src/MD5Digest.i3.html">MD5Digest</A>, <A HREF="../../suplib/src/MD5Wr.i3.html">MD5Wr</A>, <A HREF="../../libm3/src/rw/NullWr.i3.html">NullWr</A>, <A HREF="../../libm3/src/os/Common/OSError.i3.html">OSError</A>, <A HREF="../../libm3/src/os/Common/Pathname.i3.html">Pathname</A>, <A HREF="ParsedDelta.i3.html">ParsedDelta</A>,
  <A HREF="../derived/ParsedDeltaList.i3.html">ParsedDeltaList</A>, <A HREF="../../suplib/src/RCSDate.i3.html">RCSDate</A>, <A HREF="../../suplib/src/RCSDelta.i3.html">RCSDelta</A>, <A HREF="../../suplib/derived/RCSDeltaList.i3.html">RCSDeltaList</A>, <A HREF="../derived/RCSDeltaMerger.i3.html">RCSDeltaMerger</A>,
  <A HREF="../../suplib/derived/RCSDeltaTbl.i3.html">RCSDeltaTbl</A>, <A HREF="../../suplib/src/RCSError.i3.html">RCSError</A>, <A HREF="../../suplib/src/RCSFile.i3.html">RCSFile</A>, <A HREF="../../suplib/src/RCSKeyword.i3.html">RCSKeyword</A>, <A HREF="../../suplib/src/RCSPhrase.i3.html">RCSPhrase</A>, <A HREF="../../suplib/src/RCSPhrases.i3.html">RCSPhrases</A>,
  <A HREF="../../suplib/src/RCSRevNum.i3.html">RCSRevNum</A>, <A HREF="../../suplib/src/RCSString.i3.html">RCSString</A>, <A HREF="../../suplib/src/RCSTag.i3.html">RCSTag</A>, <A HREF="../../suplib/derived/RCSTagList.i3.html">RCSTagList</A>, <A HREF="../../suplib/derived/RCSTagListSort.i3.html">RCSTagListSort</A>,
  <A HREF="../derived/RCSTagMerger.i3.html">RCSTagMerger</A>, <A HREF="../../libm3/src/rw/Rd.i3.html">Rd</A>, <A HREF="../../libm3/src/rw/RdCopy.i3.html">RdCopy</A>, <A HREF="../../suplib/src/Reaper.i3.html">Reaper</A>, <A HREF="../../suplib/src/RsyncBlock.i3.html">RsyncBlock</A>, <A HREF="../../suplib/src/RsyncFile.i3.html">RsyncFile</A>, <A HREF="../../tcp/src/common/StreamRd.i3.html">StreamRd</A>,
  <A HREF="../../tcp/src/common/StreamWr.i3.html">StreamWr</A>, <A HREF="../../suplib/derived/SortedRCSDeltaTbl.i3.html">SortedRCSDeltaTbl</A>, <A HREF="../../suplib/src/SupFileRec.i3.html">SupFileRec</A>, <A HREF="#x2">SupFileRecSeq</A>, <A HREF="../../suplib/src/SupMisc.i3.html">SupMisc</A>,
  <A HREF="../../m3core/src/text/Text.i3.html">Text</A>, <A HREF="../../m3core/src/thread/Common/Thread.i3.html">Thread</A>, <A HREF="../../suplib/src/TokScan.i3.html">TokScan</A>, <A HREF="../../m3core/src/word/Word.i3.html">Word</A>, <A HREF="../../libm3/src/rw/Wr.i3.html">Wr</A>;

IMPORT <A HREF="../../libm3/src/rw/IO.i3.html">IO</A>;

EXCEPTION Error(TEXT);

CONST
  MaxChecksumRevisions = 10;
</PRE><BLOCKQUOTE><EM> Maximum revisions to checksum in attempting to identify a client file
   in checkout mode. </EM></BLOCKQUOTE><PRE>

REVEAL
  <A NAME="T">T</A> = Public BRANDED OBJECT
    proto: CVProto.T;
    wireRd: StreamRd.T;		(* Raw reader. *)
    rd: StreamRd.T;		(* Currently active reader. *)
    wireWr: StreamWr.T;		(* Raw writer. *)
    wr: StreamWr.T;		(* Currently active writer. *)
    collections: SupFileRecSeq.T;
    clientClass: ClientClass.T;
    compLevel: [0..9];
    reaper: Reaper.T;
    logger: Logger.T;
  OVERRIDES
    apply := Apply;
    init := Init;
  END;

  <A NAME="Private">Private</A> = Thread.Closure BRANDED OBJECT END;

PROCEDURE <A NAME="Apply"><procedure>Apply</procedure></A>(self: T): REFANY =
  BEGIN
    TRY
      TRY
	CompBatch(self);  (* All the collections. *)
	CompBatch(self);  (* All the fixups. *)
      FINALLY
	IF self.reaper # NIL THEN
	  Reaper.Dying(self.reaper);
	END;
      END;
    EXCEPT
    | Error(msg) =&gt;
      RETURN &quot;RCSComp error: &quot; &amp; msg;
    | Rd.EndOfFile =&gt;
      RETURN &quot;RCSComp protocol error: Premature end of file&quot;;
    | Rd.Failure(list) =&gt;
      RETURN &quot;Network read failure: &quot; &amp; ErrMsg.StrError(list);
    | Thread.Alerted =&gt;
      RETURN &quot;Interrupted&quot;;
    | TokScan.Error(msg) =&gt;
      RETURN &quot;RCSComp protocol error: &quot; &amp; msg;
    | Wr.Failure(list) =&gt;
      RETURN &quot;Network write failure: &quot; &amp; ErrMsg.StrError(list);
    END;
    RETURN NIL;
  END Apply;

PROCEDURE <A NAME="CheckName"><procedure>CheckName</procedure></A>(self: T; name: Pathname.T): BOOLEAN
  RAISES {Thread.Alerted, Wr.Failure} =
</PRE><BLOCKQUOTE><EM> Returns <CODE>TRUE</CODE> if the given filename is safe from a security standpoint.
   Otherwise, logs a warning and returns <CODE>FALSE</CODE>. </EM></BLOCKQUOTE><PRE>
  VAR
    length := Text.Length(name);
    start, limit: INTEGER;
  BEGIN
    TRY
      IF length = 0 THEN
	RAISE Error(&quot;Invalid empty filename&quot;);
      END;
      IF Text.GetChar(name, 0) = SupMisc.SlashChar THEN
	RAISE Error(&quot;Absolute pathname \&quot;&quot; &amp; name &amp; &quot;\&quot; not allowed&quot;);
      END;
      start := 0;
      WHILE start &lt; length DO
	limit := Text.FindChar(name, SupMisc.SlashChar, start);
	IF limit = -1 THEN limit := length END;
	IF start = limit
	OR Text.Equal(Text.Sub(name, start, limit-start), Pathname.Parent)
	THEN
	  RAISE Error(&quot;Filename \&quot;&quot; &amp; name &amp; &quot;\&quot; not allowed&quot;);
	END;
	start := limit + 1;
      END;
      RETURN TRUE;
    EXCEPT Error(msg) =&gt;
      WarnBoth(self, msg);
      RETURN FALSE;
    END;
  END CheckName;

PROCEDURE <A NAME="CompBatch"><procedure>CompBatch</procedure></A>(self: T)
  RAISES {Error, Rd.EndOfFile, Rd.Failure, Thread.Alerted,
	  TokScan.Error, Wr.Failure} =
</PRE><BLOCKQUOTE><EM> Process all the collections or all the fixups. </EM></BLOCKQUOTE><PRE>
  VAR
    ts: TokScan.T;
    collection, release: TEXT;
    initialBytesIn, initialBytesOut: LONGREAL;
  BEGIN
    FOR i := 0 TO self.collections.size()-1 DO
      WITH sfr = self.collections.get(i) DO
	IF NOT SupFileRec.Option.Skip IN sfr.options THEN
	  ts := self.proto.getCmd(self.rd);
	  ts.getFolded(&quot;COLL&quot;);
	  collection := ts.getToken(&quot;collection&quot;);
	  release := ts.getToken(&quot;release&quot;);
	  SupFileRec.Check(sfr, collection, release);
	  initialBytesIn := StreamRd.ByteCount(self.rd);
	  initialBytesOut := StreamWr.ByteCount(self.wr);
	  TRY
	    CompCollection(self, sfr);
	  FINALLY
	    LOCK sfr DO
	      sfr.bytesIn := sfr.bytesIn +
		StreamRd.ByteCount(self.rd) - initialBytesIn;
	      sfr.bytesOut := sfr.bytesOut +
		StreamWr.ByteCount(self.wr) - initialBytesOut;
	    END;
	  END;
	END;
      END;
    END;
    ts := self.proto.getCmd(self.rd);
    ts.getLiteral(&quot;.&quot;);
    self.proto.putCmd(self.wr, &quot;.&quot;);
    Wr.Flush(self.wr);
  END CompBatch;

PROCEDURE <A NAME="CompCollection"><procedure>CompCollection</procedure></A>(self: T; sfr: SupFileRec.T)
  RAISES {Error, Rd.EndOfFile, Rd.Failure, Thread.Alerted,
	  TokScan.Error, Wr.Failure} =
  VAR
    ts: TokScan.T;
    compress: BOOLEAN;
    cmd: TEXT;
    cmdCh: CHAR;
    name: TEXT;
    tag: TEXT;
    date: TEXT;
    revNum: RCSRevNum.T;
    revDate: RCSDate.T;
    size: CARDINAL;
    blockSize: CARDINAL;
    cksum: TEXT;
    attrText: TEXT;
    linkTo: TEXT;
    collectionPartiallyHidden :=
      self.clientClass.collectionIsPartiallyHidden(sfr.collection);
    MsgCheckoutUnsupported := &quot;Checkout from partially hidden &quot; &amp;
                                  &quot;collection \&quot;&quot; &amp; sfr.collection &amp;
                                  &quot;\&quot; is currently not suppported.&quot;;
  BEGIN
    self.proto.putCmd(self.wr, &quot;COLL&quot;, sfr.collection, sfr.release);
    Wr.Flush(self.wr);

    compress := SupFileRec.Option.Compress IN sfr.options;

    IF compress THEN
      TRY
	self.rd := NEW(GzipRd.T).init(self.wireRd, closeChild := FALSE);
      EXCEPT OSError.E(list) =&gt;
	RAISE Error(&quot;Cannot create Gzip reader: &quot; &amp; ErrMsg.StrError(list));
      END;
    END;
    TRY
      IF compress THEN
	TRY
	  self.wr := NEW(GzipWr.T).init(self.wireWr, level := self.compLevel,
	    closeChild := FALSE);
	EXCEPT OSError.E(list) =&gt;
	  RAISE Error(&quot;Cannot create Gzip writer: &quot; &amp; ErrMsg.StrError(list));
	END;
      END;
      TRY
	LOOP
	  ts := self.proto.getCmd(self.rd);
	  cmdCh := ts.getChar(&quot;command&quot;);
	  cmd := Text.FromChar(cmdCh);
	  CASE cmdCh OF
	  | '.' =&gt;
	      EXIT;
	  | 'A' =&gt;  (* Add file. *)
	      name := ts.getToken(&quot;file name&quot;);
	      ts.getEnd(&quot;end of \&quot;&quot; &amp; cmd &amp; &quot;\&quot; command&quot;);
	      RegularSend(self, sfr, name, isFixup := FALSE);
	  | 'C' =&gt;  (* Checkout file. *)
	      name := ts.getToken(&quot;file name&quot;);
	      tag := ts.getToken(&quot;tag&quot;);
	      date := ts.getToken(&quot;date&quot;);
	      ts.getEnd(&quot;end of \&quot;&quot; &amp; cmd &amp; &quot;\&quot; command&quot;);
              IF collectionPartiallyHidden THEN
                WarnBoth(self, MsgCheckoutUnsupported);
              ELSE
                CheckoutSend(self, sfr, name, tag, date);
              END;
	  | 'D' =&gt;  (* Delete file. *)
	      name := ts.getToken(&quot;file name&quot;);
	      ts.getEnd(&quot;end of \&quot;&quot; &amp; cmd &amp; &quot;\&quot; command&quot;);
	      self.proto.putCmd(self.wr, &quot;D&quot;, name);
	  | 'H', 'h' =&gt;  (* Make hard link. *)
	      name := ts.getToken(&quot;file name&quot;);
	      linkTo := ts.getToken(&quot;hard link target&quot;);
	      ts.getEnd(&quot;end of \&quot;&quot; &amp; cmd &amp; &quot;\&quot; command&quot;);
	      SendExecutes(self, sfr, name,
		inAttic := cmdCh = 'h', isCheckout := FALSE);
	      self.proto.putCmd(self.wr, cmd, name, linkTo);
	  | 'I', 'i', 'j' =&gt;  (* Directory operations. *)
	      name := ts.getToken(&quot;file name&quot;);
	      ts.getEnd(&quot;End of \&quot;&quot; &amp; cmd &amp; &quot;\&quot; command&quot;);
	      self.proto.putCmd(self.wr, cmd, name);
	  | 'J' =&gt;  (* Set directory attributes. *)
	      name := ts.getToken(&quot;file name&quot;);
	      attrText := ts.getToken(&quot;attributes&quot;);
	      ts.getEnd(&quot;End of \&quot;&quot; &amp; cmd &amp; &quot;\&quot; command&quot;);
	      SendExecutes(self, sfr, name,
		inAttic := FALSE, isCheckout := FALSE);
	      self.proto.putCmd(self.wr, cmd, name, attrText);
	  | 'L', 'l' =&gt;  (* Update list file info for up-to-date file. *)
	      name := ts.getToken(&quot;file name&quot;);
	      attrText := ts.getToken(&quot;attributes&quot;);
	      ts.getEnd(&quot;end of \&quot;&quot; &amp; cmd &amp; &quot;\&quot; command&quot;);
	      self.proto.putCmd(self.wr, cmd, name, attrText);
	  | 'N' =&gt;  (* Update a node. *)
	      name := ts.getToken(&quot;file name&quot;);
	      ts.getEnd(&quot;end of \&quot;&quot; &amp; cmd &amp; &quot;\&quot; command&quot;);
	      NodeCompare(self, sfr, name);
	  | 'R' =&gt;  (* Update regular file. *)
	      name := ts.getToken(&quot;file name&quot;);
	      size := ts.getInt(&quot;size&quot;);
	      cksum := ts.getToken(&quot;checksum&quot;);
	      ts.getEnd(&quot;end of \&quot;&quot; &amp; cmd &amp; &quot;\&quot; command&quot;);
	      RegularCompare(self, sfr, name, size, cksum);
	  | 'r' =&gt;  (* Do Rsync update. *)
	      name := ts.getToken(&quot;file name&quot;);
	      size := ts.getInt(&quot;size&quot;);
	      blockSize := ts.getInt(&quot;blockSize&quot;);
	      ts.getEnd(&quot;end of \&quot;&quot; &amp; cmd &amp; &quot;\&quot; command&quot;);
	      RsyncCompare(self, sfr, name, size, blockSize);
	  | 'S' =&gt;  (* Update in checkout mode from checksum. *)
	      name := ts.getToken(&quot;file name&quot;);
	      tag := ts.getToken(&quot;tag&quot;);
	      date := ts.getToken(&quot;date&quot;);
	      cksum := ts.getToken(&quot;checksum&quot;);
	      ts.getEnd(&quot;end of \&quot;&quot; &amp; cmd &amp; &quot;\&quot; command&quot;);
              IF collectionPartiallyHidden THEN
                WarnBoth(self, MsgCheckoutUnsupported);
              ELSE
                UpdateFromChecksum(self, sfr, name, tag, date, cksum);
              END;
	  | 's' =&gt;  (* Update in checkout mode from revNum and checksum. *)
	      name := ts.getToken(&quot;file name&quot;);
	      tag := ts.getToken(&quot;tag&quot;);
	      date := ts.getToken(&quot;date&quot;);
	      revNum := ts.getToken(&quot;revision number&quot;);
	      cksum := ts.getToken(&quot;checksum&quot;);
	      ts.getEnd(&quot;end of \&quot;&quot; &amp; cmd &amp; &quot;\&quot; command&quot;);
              IF collectionPartiallyHidden THEN
                WarnBoth(self, MsgCheckoutUnsupported);
              ELSE
                UpdateFromChecksum(self, sfr, name, tag, date, cksum, revNum);
              END;
	  | 'U' =&gt;  (* Update in checkout mode from revNum and revDate. *)
	      name := ts.getToken(&quot;file name&quot;);
	      tag := ts.getToken(&quot;tag&quot;);
	      date := ts.getToken(&quot;date&quot;);
	      revNum := ts.getToken(&quot;revision number&quot;);
	      IF self.proto.v.sendsRevDates THEN
		revDate := ts.getToken(&quot;revDate&quot;);
	      ELSE
		revDate := &quot;.&quot;;
	      END;
	      ts.getEnd(&quot;end of \&quot;&quot; &amp; cmd &amp; &quot;\&quot; command&quot;);
              IF collectionPartiallyHidden THEN
                WarnBoth(self, MsgCheckoutUnsupported);
              ELSE
                CheckoutUpdate(self, sfr, name, tag, date, revNum, revDate);
              END;
	  | 'V' =&gt;  (* Update RCS file. *)
	      name := ts.getToken(&quot;file name&quot;);
	      ts.getEnd(&quot;end of \&quot;&quot; &amp; cmd &amp; &quot;\&quot; command&quot;);
	      RCSCompare(self, sfr, name);
	  | 'X' =&gt;  (* Fixup RCS file. *)
	      name := ts.getToken(&quot;file name&quot;);
	      ts.getEnd(&quot;end of \&quot;&quot; &amp; cmd &amp; &quot;\&quot; command&quot;);
	      RegularSend(self, sfr, name, isFixup := TRUE);
	  | 'Y' =&gt;  (* Fixup checkout-mode file. *)
	      name := ts.getToken(&quot;file name&quot;);
	      tag := ts.getToken(&quot;tag&quot;);
	      date := ts.getToken(&quot;date&quot;);
	      ts.getEnd(&quot;end of \&quot;&quot; &amp; cmd &amp; &quot;\&quot; command&quot;);
              IF collectionPartiallyHidden THEN
                WarnBoth(self, MsgCheckoutUnsupported);
              ELSE
                CheckoutSend(self, sfr, name, tag, date, isFixup := TRUE);
              END;
	  ELSE
	    RAISE TokScan.Error(&quot;Invalid command \&quot;&quot; &amp; cmd &amp; &quot;\&quot;&quot;);
	  END;

	  Wr.Flush(self.wr);
	END;

	self.proto.putCmd(self.wr, &quot;.&quot;);
	Wr.Flush(self.wr);

	IF compress THEN
	  Wr.Close(self.wr);
	  IF NOT Rd.EOF(self.rd) THEN
	    RAISE TokScan.Error(
	      &quot;Expected EOF from compressed stream, didn't get it&quot;);
	  END;
	  Rd.Close(self.rd);
	END;
      FINALLY
	IF compress THEN
	  GzipWr.Cleanup(self.wr);
	  self.wr := self.wireWr;
	END;
      END;
    FINALLY
      IF compress THEN
	GzipRd.Cleanup(self.rd);
	self.rd := self.wireRd;
      END;
    END;
  END CompCollection;

PROCEDURE <A NAME="RCSCompare"><procedure>RCSCompare</procedure></A>(self: T;
		     sfr: SupFileRec.T;
		     name: Pathname.T)
  RAISES {Error, Rd.EndOfFile, Rd.Failure, Thread.Alerted,
	  TokScan.Error, Wr.Failure} =
  CONST myname = &quot;RCSCompare: &quot;;
</PRE><BLOCKQUOTE><EM> The Rd.EndOfFile and Rd.Failure exceptions are raised only for the
   network connection. </EM></BLOCKQUOTE><PRE>
  VAR
    pathname := SupMisc.CatPath(sfr.serverPrefix, name);
    origPathname := pathname;
    rf: RCSFile.T;
    ts: TokScan.T;
    cmd: TEXT;
    cmdCh: CHAR;
    clientBranch: TEXT := NIL;
    clientDeltas: RCSDeltaList.T := NIL;
    clientTags: RCSTagList.T := NIL;
    expandMode: RCSKeyword.ExpandMode;
    doBranch, doDeltas, doExpand, doTags: BOOLEAN := FALSE;
    deletes: RCSDeltaList.T;
    adds: ParsedDeltaList.T;
    dtab: SortedRCSDeltaTbl.T := NIL;
    inAttic: BOOLEAN;
    md5: MD5.T;
  BEGIN
    (* Read all the information from the client. *)
    Trace(self, myname, name);
    LOOP
      ts := self.proto.getCmd(self.rd);
      cmdCh := ts.getChar(&quot;command&quot;);
      cmd := Text.FromChar(cmdCh);
      CASE cmdCh OF
      | '.' =&gt;
	  EXIT;
      | 'B' =&gt;  (* Default branch. *)
	  clientBranch := ts.getToken(&quot;default branch&quot;);
	  ts.getEnd(&quot;end of \&quot;&quot; &amp; cmd &amp; &quot;\&quot; command&quot;);
	  doBranch := TRUE;
      | 'b' =&gt;  (* Empty default branch. *)
	  clientBranch := &quot;&quot;;
	  ts.getEnd(&quot;end of \&quot;&quot; &amp; cmd &amp; &quot;\&quot; command&quot;);
	  doBranch := TRUE;
      | 'D' =&gt;  (* Deltas. *)
	  ts.getEnd(&quot;end of \&quot;&quot; &amp; cmd &amp; &quot;\&quot; command&quot;);
	  clientDeltas := GetClientDeltas(self);
	  doDeltas := TRUE;
      | 'E' =&gt;  (* Expand mode. *)
	  TRY
	    expandMode := RCSKeyword.DecodeExpand(
			      ts.getToken(&quot;expand mode&quot;));
	  EXCEPT RCSError.E(msg) =&gt; RAISE TokScan.Error(msg) END;
	  ts.getEnd(&quot;end of \&quot;&quot; &amp; cmd &amp; &quot;\&quot; command&quot;);
	  doExpand := TRUE;
      | 'T' =&gt;  (* Tags. *)
	  ts.getEnd(&quot;end of \&quot;&quot; &amp; cmd &amp; &quot;\&quot; command&quot;);
	  clientTags := GetClientTags(self);
	  doTags := TRUE;
      ELSE
	RAISE TokScan.Error(&quot;Invalid RCSComp command \&quot;&quot; &amp; cmd &amp; &quot;\&quot;&quot;);
      END;
    END;

    IF NOT CheckName(self, name) OR MaybeSendNode(self, sfr, name) THEN
      RETURN;
    END;

    (* Do the comparisons, and send editing commands to the Updater. *)
    TRY
      rf := Attic.RCSFileOpenReadonly(pathname);
    EXCEPT
    | OSError.E(list) =&gt;
	WarnBoth(self, &quot;Cannot open \&quot;&quot; &amp; pathname
	  &amp; &quot;\&quot;: &quot; &amp; ErrMsg.StrError(list));
	RETURN;
    | RCSError.E(text) =&gt;
	WarnBoth(self, &quot;RCS file error in \&quot;&quot; &amp; pathname &amp; &quot;\&quot;: &quot; &amp; text);
	RETURN;
    END;
    IF self.clientClass.collectionIsPartiallyHidden(sfr.collection) THEN
      dtab := AccessibleDeltas(rf, self.clientClass, sfr, self);
    END;
    TRY
      IF doDeltas THEN
	(* Parse the deltas now, so that if we have a corrupted RCS file
	   we can still recover from it and carry on. *)
	TRY
	  CompDeltas(self, sfr, clientDeltas, rf, dtab, deletes, adds);
	EXCEPT
	| RCSError.E(msg) =&gt;
	    WarnBoth(self, &quot;RCS file error in \&quot;&quot; &amp; pathname &amp;
	      &quot;\&quot;: &quot; &amp; msg);
	    RETURN;
	| Error =&gt;
            Trace(self, myname,
              &quot;different RCS file, need to start from scratch&quot;);
	    (* Date mismatch on one or more revisions.  Just replace the
	       entire file. *)
            RegularSend(self, sfr, name, isFixup := FALSE);
	    RETURN;
	END;
      END;

      inAttic := pathname # origPathname;
      IF inAttic THEN cmd := &quot;v&quot; ELSE cmd := &quot;V&quot; END;
      SendExecutes(self, sfr, name, inAttic := inAttic, isCheckout := FALSE);

      IF NOT SupFileRec.Option.StrictCheckRCS IN sfr.options
      AND self.proto.v.hasLooseRCSCheck THEN
	md5 := MD5.NewRCS();
      ELSE
	md5 := MD5.New();
      END;
      RCSFile.CalculateMD5(rf, md5);

      self.proto.putCmd(self.wr, cmd,
	name,
	AttrOrModTime(self, sfr, RCSFile.GetAttr(rf)),
	RCSFile.EncodeOptions(rf.options),
	md5.finish());

      (* DANGER - Errors beyond this point are fatal. *)

      IF doBranch THEN
	CompBranch(self, sfr, rf, clientBranch);
      END;
      IF doExpand THEN
	IF rf.expand # expandMode THEN
	  self.proto.putCmd(self.wr, &quot;E&quot;, RCSKeyword.EncodeExpand(rf.expand));
	END;
      END;
      IF doTags THEN
	IF SupFileRec.Option.ExactRCS IN sfr.options THEN
	  CompTagsExact(self, sfr, clientTags, rf, dtab);
	ELSE
	  CompTagsNonExact(self, sfr, clientTags, rf, dtab);
	END;
      END;
      IF doDeltas THEN
	WHILE deletes # NIL DO
	  self.proto.putCmd(self.wr, &quot;d&quot;, deletes.head.revision);
	  deletes := deletes.tail;
	END;

	WHILE adds # NIL DO
	  WITH pd = adds.head DO
	    SendDelta(self, pd.delta, RCSDelta.Predecessor(pd.delta),
	      pd.log, pd.text);
	  END;
	  adds := adds.tail;
	END;
      END;
      self.proto.putCmd(self.wr, &quot;.&quot;);
    FINALLY
      TRY
	RCSFile.Close(rf);
      EXCEPT OSError.E(list) =&gt;
	RAISE Error(&quot;Cannot close \&quot;&quot; &amp; pathname &amp; &quot;\&quot;: &quot; &amp;
	  ErrMsg.StrError(list));
      END;
    END;
  END RCSCompare;

PROCEDURE <A NAME="GetClientDeltas"><procedure>GetClientDeltas</procedure></A>(self: T): RCSDeltaList.T
  RAISES {Rd.EndOfFile, Rd.Failure, Thread.Alerted, TokScan.Error} =
</PRE><BLOCKQUOTE><EM> The Rd.EndOfFile and Rd.Failure exceptions are raised only for the
   network connection. </EM></BLOCKQUOTE><PRE>
  VAR
    ts: TokScan.T;
    revNum: RCSRevNum.T;
    date: TEXT;
    deltas, last: RCSDeltaList.T := NIL;
    dl: RCSDeltaList.T;
  BEGIN
    LOOP
      ts := self.proto.getCmd(self.rd);
      revNum := ts.getToken(&quot;revision number&quot;);
      IF Text.Equal(revNum, &quot;.&quot;) THEN EXIT END;
      date := ts.getToken(&quot;date&quot;);
      ts.getEnd(&quot;end of delta descriptor&quot;);
      dl := RCSDeltaList.List1(NEW(RCSDelta.T,
	revision := revNum, date := date));
      IF last # NIL THEN
	last.tail := dl;
      ELSE
	deltas := dl;
      END;
      last := dl;
    END;
    RETURN deltas;
  END GetClientDeltas;

PROCEDURE <A NAME="GetClientTags"><procedure>GetClientTags</procedure></A>(self: T): RCSTagList.T
  RAISES {Rd.EndOfFile, Rd.Failure, Thread.Alerted, TokScan.Error} =
</PRE><BLOCKQUOTE><EM> The Rd.EndOfFile and Rd.Failure exceptions are raised only for the
   network connection. </EM></BLOCKQUOTE><PRE>
  VAR
    ts: TokScan.T;
    name: TEXT;
    revNum: RCSRevNum.T;
    tags, last: RCSTagList.T := NIL;
    tl: RCSTagList.T;
  BEGIN
    (* The tags arrive in the order of their appearance in the client's
       RCS file. *)
    LOOP
      ts := self.proto.getCmd(self.rd);
      name := ts.getToken(&quot;tag name&quot;);
      IF Text.Equal(name, &quot;.&quot;) THEN EXIT END;
      revNum := ts.getToken(&quot;revision number&quot;);
      ts.getEnd(&quot;end of tag descriptor&quot;);
      tl := RCSTagList.List1(NEW(RCSTag.T, name := name, revNum := revNum));
      IF last # NIL THEN
	last.tail := tl;
      ELSE
	tags := tl;
      END;
      last := tl;
    END;
    RETURN tags;
  END GetClientTags;

PROCEDURE <A NAME="CompBranch"><procedure>CompBranch</procedure></A>(self: T;
                     sfr: SupFileRec.T;
		     rf: RCSFile.T;
		     clientBranch: TEXT)
  RAISES {Thread.Alerted, Wr.Failure} =
  VAR
    serverBranch := rf.branch;
  BEGIN
    IF serverBranch = NIL THEN serverBranch := &quot;&quot; END;
    IF NOT Text.Equal(clientBranch, serverBranch) THEN
      (* The default branch is different.  But we don't always
	 want to change it on the client.  Certain kinds of
	 changes can make it invalid, as far as CVS is concerned.
	 If we are allowing deletions of revisions on the client
	 (&quot;exact&quot; mode), then we should keep the default branch
	 the same as the server.  If not, then we change the
	 default branch ONLY if it is not currently empty on
	 the client. *)
      IF SupFileRec.Option.ExactRCS IN sfr.options
      OR NOT Text.Empty(clientBranch) THEN
	IF Text.Empty(serverBranch) THEN
	  self.proto.putCmd(self.wr, &quot;b&quot;);
	ELSE
	  self.proto.putCmd(self.wr, &quot;B&quot;, serverBranch);
	END;
      END;
    END;
  END CompBranch;

PROCEDURE <A NAME="CompDeltas"><procedure>CompDeltas</procedure></A>(self: T;
                     sfr: SupFileRec.T;
                     clientDeltas: RCSDeltaList.T;
                     rf: RCSFile.T;
                     dtab: SortedRCSDeltaTbl.T;
                     VAR deletes: RCSDeltaList.T;
                     VAR adds: ParsedDeltaList.T)
  RAISES {Error, RCSError.E} =
  CONST name = &quot;CompDeltas: &quot;;
  VAR
    dm: DeltaMerger;
    clientDelta: RCSDelta.T;
    serverDelta: RCSDelta.T;
  BEGIN
    dm := NEW(DeltaMerger).init(clientDeltas, rf, dtab);
    Trace(self, name, sfr.collection, &quot; &quot;, sfr.release);
    deletes := NIL;
    adds := NIL;
    WHILE DeltaNext(dm, clientDelta, serverDelta) DO
      IF clientDelta = NIL THEN
        Trace(self, name, &quot;add: &quot;, RCSDelta.ToText(serverDelta));
	adds := ParsedDeltaList.Cons(
	  ParsedDelta.T{
	    delta := serverDelta,
	    log := RCSDelta.GetLog(serverDelta).iterate(),
	    text := RCSDelta.GetText(serverDelta,
	      RCSDelta.Predecessor(serverDelta))},
	  adds);
      ELSIF serverDelta = NIL THEN
        Trace(self, name, &quot;del: &quot;, RCSDelta.ToText(clientDelta));
	IF SupFileRec.Option.ExactRCS IN sfr.options THEN
	  deletes := RCSDeltaList.Cons(clientDelta, deletes);
	END;
      ELSIF NOT RCSDate.Equal(clientDelta.date, serverDelta.date) THEN
        Trace(self, name, &quot;err: &quot;, RCSDelta.ToText(serverDelta), &quot; &quot;,
          RCSDelta.ToText(clientDelta));
	(* The client must have an entirely different RCS file.  We don't
	   even try to edit that.  Just replace the whole file. *)
	RAISE Error(&quot;Incorrect date on client delta&quot;);
      END;
    END;
    adds := ParsedDeltaList.ReverseD(adds);
  END CompDeltas;

PROCEDURE <A NAME="AccessibleDeltas"><procedure>AccessibleDeltas</procedure></A>(rf: RCSFile.T;
                           clientClass: ClientClass.T;
                           sfr: SupFileRec.T;
                           comp: T): SortedRCSDeltaTbl.T =

  CONST myname = &quot;AccessibleDeltas: &quot;;
  VAR (* AccessibleDeltas *)
    ti := RCSFile.IterateTags(rf);
    dt := NEW(SortedRCSDeltaTbl.Default).init();
    tag: RCSTag.T;
    delta, dd: RCSDelta.T;
    found := FALSE;
  BEGIN
    Trace(comp, myname);
    WHILE ti.next(tag) DO
      IF RCSTag.IsCVSBranch(tag) THEN
	IF clientClass.inAllowedCollectionBranches(sfr.collection, tag.name)
	THEN
          Trace(comp, &quot;  exporting branch &quot;, tag.name);
          TRY
            delta := RCSFile.GetTagDelta(rf, tag.name);
            WHILE delta # NIL DO
              found := dt.get(delta.revision, dd);
              IF found THEN
                Trace(comp, &quot;    delta &quot;, delta.revision, &quot; already there&quot;,
                      level := 2);
              ELSE
                Trace(comp, &quot;    delta &quot;, delta.revision, &quot; missing&quot;,
                      level := 2);
                EVAL dt.put(delta.revision, delta);
              END;
              IF RCSRevNum.IsTrunk(delta.revision) THEN
                IF found THEN
                  (* by construction every predecssing delta has already
                     been inserted into the table so we can stop here *)
                  delta := NIL;
                ELSE
                  delta := RCSDelta.Predecessor(delta);
                END;
              ELSE
                delta := RCSDelta.GetPrev(delta);
              END;
            END;
          EXCEPT
            RCSError.E(m) =&gt; (* FIXME: do something *)
            Trace(comp, &quot;  RCS error for &quot;, tag.name, &quot;: &quot;, m);
          END;
        ELSE
          Trace(comp, &quot;  hiding branch &quot;, tag.name);
        END;
      ELSE
        Trace(comp, &quot;  ignoring non-branch tag &quot;, tag.name, level := 4);
      END;
    END;
    (* check all trunk revisions, too *)
    IF clientClass.inAllowedCollectionBranches(sfr.collection, &quot;.&quot;) THEN
      Trace(comp, &quot;  exporting main trunk&quot;);
      TRY
        delta := RCSFile.GetTagDelta(rf);
        WHILE delta # NIL DO
          found := dt.get(delta.revision, dd);
          IF found THEN
            Trace(comp, &quot;    delta &quot;, delta.revision, &quot; already there&quot;,
                  level := 2);
            delta := NIL; (* see above *)
          ELSE
            Trace(comp, &quot;    delta &quot;, delta.revision, &quot; missing&quot;, level := 2);
            EVAL dt.put(delta.revision, delta);
            delta := RCSDelta.Predecessor(delta);
          END;
        END;
      EXCEPT
        RCSError.E(m) =&gt; (* FIXME: do something *)
        Trace(comp, &quot;  RCS error for trunk head: &quot;, m);
      END;
    ELSE
      Trace(comp, &quot;  hiding main trunk&quot;);
    END;
    RETURN dt;
  END AccessibleDeltas;

PROCEDURE <A NAME="TagIncluded"><procedure>TagIncluded</procedure></A>(dtab: SortedRCSDeltaTbl.T; tag: RCSTag.T): BOOLEAN =
  VAR d: RCSDelta.T;
  BEGIN
    RETURN dtab = NIL OR dtab.get(tag.revNum, d);
  END TagIncluded;

PROCEDURE <A NAME="CompTagsExact"><procedure>CompTagsExact</procedure></A>(self: T;
                        sfr: SupFileRec.T;
                        clientTags: RCSTagList.T;
                        rf: RCSFile.T;
                        dtab: SortedRCSDeltaTbl.T)
  RAISES {Thread.Alerted, Wr.Failure} =
  CONST myname = &quot;CompTagsExact: &quot;;
  VAR
    serverTags: RCSTagList.T := NIL;
    iter: RCSFile.TagIterator;
    tag: RCSTag.T;
  BEGIN
    Trace(self, myname);
    (* Reverse the list of client tags, and build a list of the server's
       tags, also in reverse order. *)
    clientTags := RCSTagList.ReverseD(clientTags);
    iter := RCSFile.IterateTags(rf);
    WHILE iter.next(tag) DO
      IF self.clientClass.inAllowedCollectionTags(sfr.collection, tag.name) THEN
        IF RCSTag.IsCVSBranch(tag)
	AND self.clientClass.inAllowedCollectionBranches(sfr.collection,
	  tag.name)
	OR TagIncluded(dtab, tag) THEN
          serverTags := RCSTagList.Cons(tag, serverTags);
        END;
      END;
    END;

    (* Issue edits to make the client's tag list identical to that on
       the server.  Any common tail portion can be left alone.  (That's
       why we work with reversed lists here.) *)
    WHILE clientTags # NIL DO  (* Delete the extra tags on the client. *)
      IF serverTags # NIL AND
	RCSTag.Equal(clientTags.head, serverTags.head) AND
	RCSRevNum.Equal(clientTags.head.revNum, serverTags.head.revNum) THEN
	(* Common portion of the two lists.  We don't need any edits for it. *)
	serverTags := serverTags.tail;
      ELSE  (* An extra or unmatching tag on the client side. *)
        Trace(self, myname, &quot;del tag &quot;, clientTags.head.name, &quot; &quot;,
          clientTags.head.revNum);
	self.proto.putCmd(self.wr, &quot;t&quot;,
	  clientTags.head.name,
	  clientTags.head.revNum);
      END;
      clientTags := clientTags.tail;
    END;
    WHILE serverTags # NIL DO  (* Add the missing tags from the server. *)
      IF self.clientClass.inAllowedCollectionTags(sfr.collection,
        serverTags.head.name)
      THEN
        IF RCSTag.IsCVSBranch(serverTags.head)
	AND self.clientClass.inAllowedCollectionBranches(sfr.collection,
	  serverTags.head.name)
	OR TagIncluded(dtab, serverTags.head) THEN
          Trace(self, myname, &quot;adding tag &quot;, serverTags.head.name, &quot; &quot;,
            serverTags.head.revNum);
          self.proto.putCmd(self.wr, &quot;T&quot;,
                            serverTags.head.name,
                            serverTags.head.revNum);
        ELSE
          Trace(self, myname, &quot;omitting tag &quot;, serverTags.head.name, &quot; &quot;,
            serverTags.head.revNum);
        END;
      ELSE
        Trace(self, myname, &quot;hiding tag &quot;, serverTags.head.name, &quot; &quot;,
          serverTags.head.revNum);
      END;
      serverTags := serverTags.tail;
    END;
  END CompTagsExact;

PROCEDURE <A NAME="CompTagsNonExact"><procedure>CompTagsNonExact</procedure></A>(self: T;
                           sfr: SupFileRec.T;
                           clientTags: RCSTagList.T;
                           rf: RCSFile.T;
                           dtab: SortedRCSDeltaTbl.T)
  RAISES {Thread.Alerted, Wr.Failure} =
</PRE><BLOCKQUOTE><EM> Compare tags in non-exact mode.  The client is allowed to have extra,
   local tags.  We don't insist on maintaining an exact match between the
   client's list of tags and the server's, although we do make an attempt
   to keep them similar. </EM></BLOCKQUOTE><PRE>
  CONST myname = &quot;CompTagsNonExact: &quot;;
  VAR
    tm: TagMerger;
    clientTag: RCSTag.T;
    serverTag: RCSTag.T;
    newNormalTags, newVendorTags, adds, deletes: RCSTagList.T := NIL;
    iter: RCSFile.TagIterator;
    tag: RCSTag.T;
  BEGIN
    Trace(self, myname);
    tm := NEW(TagMerger).init(clientTags, rf, 1);
    WHILE TagNext(tm, clientTag, serverTag) DO
      IF clientTag = NIL THEN
	newVendorTags := RCSTagList.Cons(serverTag, newVendorTags);
      ELSIF serverTag = NIL THEN
	(* Extra tag on client -- just ignore it. *)
      ELSIF NOT RCSRevNum.Equal(clientTag.revNum, serverTag.revNum) THEN
	deletes := RCSTagList.Cons(clientTag, deletes);
	newVendorTags := RCSTagList.Cons(serverTag, newVendorTags);
      END;
    END;

    tm := NEW(TagMerger).init(clientTags, rf, 0);
    WHILE TagNext(tm, clientTag, serverTag) DO
      IF clientTag = NIL THEN
	newNormalTags := RCSTagList.Cons(serverTag, newNormalTags);
      ELSIF serverTag = NIL THEN
	(* Extra tag on client -- just ignore it. *)
      ELSIF NOT RCSRevNum.Equal(clientTag.revNum, serverTag.revNum) THEN
	deletes := RCSTagList.Cons(clientTag, deletes);
	newNormalTags := RCSTagList.Cons(serverTag, newNormalTags);
      END;
    END;

    iter := RCSFile.IterateTags(rf);
    WHILE iter.next(tag) DO
      IF RCSRevNum.NumParts(tag.revNum) MOD 2 = 1 THEN
	IF RCSTagList.Member(newVendorTags, tag) THEN
	  adds := RCSTagList.Cons(tag, adds);
	END;
      ELSE
	IF RCSTagList.Member(newNormalTags, tag) THEN
	  adds := RCSTagList.Cons(tag, adds);
	END;
      END;
    END;

    (* Now &quot;adds&quot; contains a list of the tags we want to send, in reverse
       order relative to how they appear in the server-side RCS file.
       On the client side, each will be pushed onto the front of the
       tag list.  This will result in the proper order on the client
       side. *)

    (* First, send the deletes. *)
    WHILE deletes # NIL DO
      Trace(self, myname, &quot;del tag &quot;, deletes.head.name, &quot; &quot;,
        deletes.head.revNum);
      self.proto.putCmd(self.wr, &quot;t&quot;, deletes.head.name, deletes.head.revNum);
      deletes := deletes.tail;
    END;

    (* Now send the adds. *)
    WHILE adds # NIL DO
      IF self.clientClass.inAllowedCollectionTags(sfr.collection,
        adds.head.name)
      THEN
        IF RCSTag.IsCVSBranch(adds.head)
	AND self.clientClass.inAllowedCollectionBranches(sfr.collection,
          adds.head.name)
	OR TagIncluded(dtab, adds.head) THEN
          Trace(self, myname, &quot;adding tag &quot;, adds.head.name, &quot; &quot;,
            adds.head.revNum);
          self.proto.putCmd(self.wr, &quot;T&quot;, adds.head.name, adds.head.revNum);
        ELSE
          Trace(self, myname, &quot;omitting tag &quot;, adds.head.name, &quot; &quot;,
            adds.head.revNum);
        END;
      ELSE
        Trace(self, myname, &quot;hiding tag &quot;, adds.head.name, &quot; &quot;,
          adds.head.revNum);
      END;
      adds := adds.tail;
    END;
  END CompTagsNonExact;

PROCEDURE <A NAME="SendDelta"><procedure>SendDelta</procedure></A>(self: T;
          delta: RCSDelta.T;
          diffBase: RCSDelta.T;
	  log: RCSString.Iterator;
	  text: RCSString.Iterator)
  RAISES {Thread.Alerted, Wr.Failure} =
  VAR
    diffBaseRevNum: TEXT;
  BEGIN
    IF diffBase = NIL THEN
      diffBaseRevNum := &quot;.&quot;;
    ELSE
      diffBaseRevNum := diffBase.revision;
    END;

    self.proto.putCmd(self.wr, &quot;D&quot;,
      delta.revision,
      diffBaseRevNum,
      delta.date,
      delta.author);
    IF delta.state # NIL THEN
      self.proto.putCmd(self.wr, &quot;S&quot;, delta.state);
    END;

    IF self.proto.v.sendsDeltaPhrases THEN
      SendPhrases(self, &quot;N&quot;, RCSDelta.IterateTreePhrases(delta));
    END;

    IF log # NIL THEN
      self.proto.putCmd(self.wr, &quot;L&quot;);
      SendEscaped(self.wr, log);
    END;

    IF self.proto.v.sendsDeltaPhrases THEN
      SendPhrases(self, &quot;n&quot;, RCSDelta.IterateTextPhrases(delta));
    END;

    self.proto.putCmd(self.wr, &quot;T&quot;);
    SendEscaped(self.wr, text);

    self.proto.putCmd(self.wr, &quot;.&quot;);
  END SendDelta;

PROCEDURE <A NAME="SendEscaped"><procedure>SendEscaped</procedure></A>(wr: Wr.T;
                      iter: RCSString.Iterator;
		      withChecksum := FALSE)
  RAISES {Thread.Alerted, Wr.Failure} =
  VAR
    eWr: Wr.T;
    md5Wr: MD5Wr.T;
    line: RCSString.T;
  BEGIN
    eWr := NEW(EscapedWr.T).init(wr, closeChild := FALSE);
    IF withChecksum THEN  (* Layer a checksummer on top. *)
      md5Wr := NEW(MD5Wr.T).init(eWr, closeChild := TRUE);
      eWr := md5Wr;
    END;

    TRY
      WHILE iter.next(line) DO
	Wr.PutText(eWr, line.toText());
      END;
    FINALLY
      Wr.Close(eWr);
    END;

    IF withChecksum THEN
      SupMisc.PutCmd(wr, &quot;5&quot;, md5Wr.getSignature());
    END;
  END SendEscaped;

PROCEDURE <A NAME="SendPhrases"><procedure>SendPhrases</procedure></A>(self: T;
                      cmd: TEXT;
		      iter: RCSPhrases.Iterator)
  RAISES {Thread.Alerted, Wr.Failure} =
  VAR
    eWr: Wr.T;
    phrase: RCSPhrase.T;
    wordIter: RCSPhrase.WordIterator;
    word: TEXT;
    isString: BOOLEAN;
  BEGIN
    WHILE iter.next(phrase) DO
      self.proto.putCmd(self.wr, cmd, RCSPhrase.GetKey(phrase));
      wordIter := RCSPhrase.IterateWords(phrase);
      WHILE wordIter.next(word, isString) DO
	IF isString THEN
	  self.proto.putCmd(self.wr, &quot;S&quot;);
	  eWr := NEW(EscapedWr.T).init(self.wr, closeChild := FALSE);
	  TRY
	    Wr.PutText(eWr, word);
	  FINALLY
	    Wr.Close(eWr);
	  END;
	ELSE
	  self.proto.putCmd(self.wr, &quot;W&quot;, word);
	END;
      END;
      self.proto.putCmd(self.wr, &quot;.&quot;);
    END;
  END SendPhrases;

PROCEDURE <A NAME="Init"><procedure>Init</procedure></A>(self: T;
               proto: CVProto.T;
               rd: StreamRd.T;
               wr: StreamWr.T;
	       collections: SupFileRecSeq.T;
               clientClass: ClientClass.T;
	       compLevel: [-1..9] := -1;
               reaper: Reaper.T := NIL;
	       logger: Logger.T := NIL): T =
  BEGIN
    self.proto := proto;
    self.wireRd := rd;
    self.rd := rd;
    self.wireWr := wr;
    self.wr := wr;
    self.collections := collections;
    self.clientClass := clientClass;
    IF compLevel = -1 THEN compLevel := SupMisc.DefaultCompression END;
    self.compLevel := compLevel;
    self.reaper := reaper;
    self.logger := logger;
    RETURN self;
  END Init;
</PRE>***************************************************************************
 Merging of deltas. 
***************************************************************************

<P><PRE>TYPE
  DeltaMerger = RCSDeltaMerger.T OBJECT
    clientDeltas: RCSDeltaList.T;
    iter: RCSDeltaTbl.Iterator;
  METHODS
    init(clientDeltas: RCSDeltaList.T;
	 rf: RCSFile.T;
         dtab: SortedRCSDeltaTbl.T): DeltaMerger := DeltaMergerInit;
  OVERRIDES
    getA := DeltaFromClient;
    getB := DeltaFromServer;
  END;

PROCEDURE <A NAME="DeltaMergerInit"><procedure>DeltaMergerInit</procedure></A>(dm: DeltaMerger;
			  clientDeltas: RCSDeltaList.T;
			  rf: RCSFile.T;
                          dtab: SortedRCSDeltaTbl.T): DeltaMerger =
  BEGIN
    dm.clientDeltas := clientDeltas;
    IF dtab = NIL THEN
      dm.iter := RCSFile.IterateByNumber(rf);
    ELSE
      dm.iter := dtab.iterateOrdered();
    END;
    RETURN dm;
  END DeltaMergerInit;

PROCEDURE <A NAME="DeltaNext"><procedure>DeltaNext</procedure></A>(dm: DeltaMerger;
                    VAR clientDelta, serverDelta: RCSDelta.T): BOOLEAN =
</PRE><BLOCKQUOTE><EM> This is a wrapper to correct the <CODE>RAISES</CODE> clause for <CODE>DeltaMerger.next</CODE>.
   It raises the union of what is raised by <CODE>DeltaFromClient</CODE> and
   <CODE>DeltaFromServer</CODE>. </EM></BLOCKQUOTE><PRE>
  &lt;* FATAL ANY *&gt;
  BEGIN
    RETURN dm.next(clientDelta, serverDelta);
  END DeltaNext;

PROCEDURE <A NAME="DeltaFromClient"><procedure>DeltaFromClient</procedure></A>(dm: DeltaMerger): RCSDelta.T =
</PRE><BLOCKQUOTE><EM> If you add any exceptions, add them to <CODE>DeltaNext</CODE> also. </EM></BLOCKQUOTE><PRE>
  VAR
    delta: RCSDelta.T;
  BEGIN
    IF dm.clientDeltas = NIL THEN
      delta := NIL;
    ELSE
      delta := dm.clientDeltas.head;
      dm.clientDeltas := dm.clientDeltas.tail;
    END;
    RETURN delta;
  END DeltaFromClient;

PROCEDURE <A NAME="DeltaFromServer"><procedure>DeltaFromServer</procedure></A>(dm: DeltaMerger): RCSDelta.T =
</PRE><BLOCKQUOTE><EM> If you add any exceptions, add them to <CODE>DeltaNext</CODE> also. </EM></BLOCKQUOTE><PRE>
  VAR
    rev: RCSRevNum.T;
    delta: RCSDelta.T;
  BEGIN
    IF dm.iter.next(rev, delta) THEN RETURN delta END;
    RETURN NIL;
  END DeltaFromServer;
</PRE>***************************************************************************
 Merging of tags. 
***************************************************************************

<P><PRE>TYPE
  TagMerger = RCSTagMerger.T OBJECT
    clientTags: RCSTagList.T;
    iter: RCSFile.TagIterator;
    rem: [0..1];
  METHODS
    init(clientTags: RCSTagList.T;
	 rf: RCSFile.T;
	 rem: [0..1]): TagMerger := TagMergerInit;
  OVERRIDES
    getA := TagFromClient;
    getB := TagFromServer;
  END;

PROCEDURE <A NAME="TagMergerInit"><procedure>TagMergerInit</procedure></A>(tm: TagMerger;
			clientTags: RCSTagList.T;
                        rf: RCSFile.T;
			rem: [0..1]): TagMerger =
  BEGIN
    tm.clientTags := RCSTagListSort.Sort(clientTags);
    tm.iter := RCSFile.IterateTagsByName(rf);
    tm.rem := rem;
    RETURN tm;
  END TagMergerInit;

PROCEDURE <A NAME="TagNext"><procedure>TagNext</procedure></A>(tm: TagMerger;
                  VAR clientTag, serverTag: RCSTag.T): BOOLEAN =
</PRE><BLOCKQUOTE><EM> This is a wrapper to correct the <CODE>RAISES</CODE> clause for <CODE>TagMerger.next</CODE>.
   It raises the union of what is raised by <CODE>TagFromClient</CODE> and
   <CODE>TagFromServer</CODE>. </EM></BLOCKQUOTE><PRE>
  &lt;* FATAL ANY *&gt;
  BEGIN
    RETURN tm.next(clientTag, serverTag);
  END TagNext;

PROCEDURE <A NAME="TagFromClient"><procedure>TagFromClient</procedure></A>(tm: TagMerger): RCSTag.T =
</PRE><BLOCKQUOTE><EM> If you add any exceptions, add them to <CODE>TagNext</CODE> also. </EM></BLOCKQUOTE><PRE>
  VAR
    tag: RCSTag.T;
  BEGIN
    REPEAT
      IF tm.clientTags = NIL THEN RETURN NIL END;
      tag := tm.clientTags.head;
      tm.clientTags := tm.clientTags.tail;
    UNTIL RCSRevNum.NumParts(tag.revNum) MOD 2 = tm.rem;
    RETURN tag;
  END TagFromClient;

PROCEDURE <A NAME="TagFromServer"><procedure>TagFromServer</procedure></A>(tm: TagMerger): RCSTag.T =
</PRE><BLOCKQUOTE><EM> If you add any exceptions, add them to <CODE>TagNext</CODE> also. </EM></BLOCKQUOTE><PRE>
  VAR
    tag: RCSTag.T;
  BEGIN
    REPEAT
      IF NOT tm.iter.next(tag) THEN RETURN NIL END;
    UNTIL RCSRevNum.NumParts(tag.revNum) MOD 2 = tm.rem;
    RETURN tag;
  END TagFromServer;
</PRE>***************************************************************************
 Sending regular (non-RCS) files. 
***************************************************************************

<P><PRE>PROCEDURE <A NAME="RegularCompare"><procedure>RegularCompare</procedure></A>(self: T;
                         sfr: SupFileRec.T;
                         name: Pathname.T;
                         clientSize: CARDINAL;
                         clientCksum: TEXT)
  RAISES {Error, Thread.Alerted, Wr.Failure} =
  CONST myName = &quot;RegularCompare: &quot;;
  VAR
    pathname := SupMisc.CatPath(sfr.serverPrefix, name);
    file: File.T;
    attr: FileAttr.T;
    rd: Rd.T;
    md5: MD5.T;
    buf: ARRAY [0..8191] OF CHAR;
    nRead, nGot: CARDINAL;
    serverCksum: TEXT;
  BEGIN
    Trace(self, myName);
    IF NOT CheckName(self, name) OR MaybeSendNode(self, sfr, name) THEN
      RETURN;
    END;

    TRY
      file := FS.OpenFileReadonly(pathname);
      rd := NEW(FileRd.T).init(file);
    EXCEPT OSError.E(list) =&gt;
      WarnBoth(self, &quot;Cannot open \&quot;&quot; &amp; pathname
	&amp; &quot;\&quot;: &quot; &amp; ErrMsg.StrError(list));
      RETURN;
    END;
    TRY
      TRY
	attr := FileAttr.FromFile(file);
      EXCEPT OSError.E(list) =&gt;
	RAISE Error(&quot;Cannot stat \&quot;&quot; &amp; pathname &amp; &quot;\&quot;: &quot; &amp;
	  ErrMsg.StrError(list));
      END;

      md5 := MD5.New();

      IF clientSize &gt; 0 AND FileAttr.GetSize(attr) &gt;= clientSize THEN
	(* See if it's a simple append. *)
	nRead := 0;
	WHILE nRead &lt; clientSize DO
	  WITH nWant = MIN(NUMBER(buf), clientSize - nRead) DO
	    TRY
	      nGot := Rd.GetSub(rd, SUBARRAY(buf, 0, nWant));
	    EXCEPT Rd.Failure(list) =&gt;
	      RAISE Error(&quot;Read failure from \&quot;&quot; &amp; pathname &amp; &quot;\&quot;: &quot; &amp;
		ErrMsg.StrError(list));
	    END;
	  END;
	  IF nGot = 0 THEN EXIT END;
	  md5.update(SUBARRAY(buf, 0, nGot));
	  INC(nRead, nGot);
	END;
	serverCksum := md5.clone().finish();  (* Non-destructive. *)

	IF nRead # clientSize OR NOT Text.Equal(serverCksum, clientCksum) THEN
	  (* We're going to have to send the entire file. *)
	  TRY
	    Rd.Seek(rd, 0);
	    md5 := MD5.New();
	  EXCEPT Rd.Failure(list) =&gt;
	    RAISE Error(&quot;Cannot seek \&quot;&quot; &amp; pathname &amp; &quot;\&quot;: &quot; &amp;
	      ErrMsg.StrError(list));
	  END;
	END;
      END;

      RegularUpdate(self, sfr, name, attr, rd, pathname, md5);
    FINALLY
      TRY
	Rd.Close(rd);
      EXCEPT Rd.Failure(list) =&gt;
	RAISE Error(&quot;Cannot close \&quot;&quot; &amp; pathname &amp; &quot;\&quot;: &quot; &amp;
	  ErrMsg.StrError(list));
      END;
    END;
  END RegularCompare;

PROCEDURE <A NAME="RegularUpdate"><procedure>RegularUpdate</procedure></A>(self: T;
                        sfr: SupFileRec.T;
                        name: TEXT;
			attr: FileAttr.T;
			rd: Rd.T;
			path: Pathname.T;
			md5: MD5.T)
  RAISES {Error, Thread.Alerted, Wr.Failure} =
  CONST myName = &quot;RegularUpdate: &quot;;
  VAR
    pos := Rd.Index(rd);
    size := FileAttr.GetSize(attr);
    nBytes: CARDINAL := size - pos;
  BEGIN
    Trace(self, myName, sfr.collection, &quot;, &quot;, name, &quot;, &quot;, path);
    SendExecutes(self, sfr, name, inAttic := FALSE, isCheckout := FALSE);
    IF pos = 0 THEN  (* Replacing entire file. *)
      self.proto.putCmd(self.wr, &quot;R&quot;, name, AttrOrModTimeSize(self, sfr, attr));
    ELSE  (* Appending to the file. *)
      self.proto.putCmd(self.wr, &quot;Z&quot;, name, more := TRUE);
      IF self.proto.v.hasFileAttrs THEN
	self.proto.putCmd(self.wr, NIL,
	  FileAttr.Encode(attr, support := self.proto.v.attrSupport,
	    ignore := sfr.attrIgnore),
	  more := TRUE);
      ELSE
	self.proto.putCmd(self.wr, NIL,
	  TokScan.EncodeTime(FileAttr.GetModTime(attr)),
	  Fmt.Unsigned(nBytes, 10),
	  more := TRUE);
      END;
      self.proto.putCmd(self.wr, NIL, Fmt.Unsigned(pos, 10));
    END;
    TRY
      SendCounted(rd, self.wr, nBytes, md5);
    EXCEPT Rd.Failure(list) =&gt;
      RAISE Error(&quot;Read failure from \&quot;&quot; &amp; path &amp; &quot;\&quot;: &quot; &amp;
	ErrMsg.StrError(list));
    END;
  END RegularUpdate;

PROCEDURE <A NAME="RegularSend"><procedure>RegularSend</procedure></A>(self: T;
		      sfr: SupFileRec.T;
		      name: Pathname.T;
		      isFixup: BOOLEAN)
  RAISES {Error, Thread.Alerted, Wr.Failure} =
  CONST
    CmdTab = ARRAY BOOLEAN,BOOLEAN OF TEXT{  (* inAttic, isFixup *)
      ARRAY BOOLEAN OF TEXT{ &quot;A&quot;, &quot;X&quot; },     (* inAttic = FALSE *)
      ARRAY BOOLEAN OF TEXT{ &quot;a&quot;, &quot;x&quot; }      (* inAttic = TRUE *)
    };
    MyName = &quot;RegularSend: &quot;;
  VAR
    pathname := SupMisc.CatPath(sfr.serverPrefix, name);
    origPathname := pathname;
    file: File.T;
    attr: FileAttr.T;
    attrText: TEXT;
    rd: Rd.T;
    inAttic: BOOLEAN;
    md5: MD5.T;
  BEGIN
    Trace(self, MyName, sfr.collection, &quot; &quot;, name);
    IF NOT CheckName(self, name) OR MaybeSendNode(self, sfr, name) THEN
      RETURN;
    END;

    IF SupMisc.IsRCS(pathname)
    AND self.clientClass.collectionIsPartiallyHidden(sfr.collection) THEN
      PartialRCSSend(self, sfr, name, isFixup);
      RETURN;
    END;

    TRY
      file := Attic.FSOpenFileReadonly(pathname);
      rd := NEW(FileRd.T).init(file);
    EXCEPT OSError.E(list) =&gt;
      WarnBoth(self, &quot;Cannot open \&quot;&quot; &amp; pathname
	&amp; &quot;\&quot;: &quot; &amp; ErrMsg.StrError(list));
      RETURN;
    END;
    TRY
      TRY
	attr := FileAttr.FromFile(file);
      EXCEPT OSError.E(list) =&gt;
	RAISE Error(&quot;Cannot stat \&quot;&quot; &amp; pathname &amp; &quot;\&quot;: &quot; &amp;
	  ErrMsg.StrError(list));
      END;
      inAttic := pathname # origPathname;

      SendExecutes(self, sfr, name, inAttic := inAttic, isCheckout := FALSE);
      IF isFixup THEN
	attrText := AttrOrModTimeSize(self, sfr, attr);
      ELSE
	attrText := AttrOrModTimeSizeMode(self, sfr, attr);
      END;
      self.proto.putCmd(self.wr, CmdTab[inAttic, isFixup], name, attrText);

      md5 := MD5.New();

      TRY
	SendCounted(rd, self.wr, FileAttr.GetSize(attr), md5);
      EXCEPT Rd.Failure(list) =&gt;
	RAISE Error(&quot;Read failure from \&quot;&quot; &amp; pathname &amp; &quot;\&quot;: &quot; &amp;
	  ErrMsg.StrError(list));
      END;
    FINALLY
      TRY
	Rd.Close(rd);
      EXCEPT Rd.Failure(list) =&gt;
	RAISE Error(&quot;Cannot close \&quot;&quot; &amp; pathname &amp; &quot;\&quot;: &quot; &amp;
	  ErrMsg.StrError(list));
      END;
    END;
  END RegularSend;

PROCEDURE <A NAME="PartialRCSSend"><procedure>PartialRCSSend</procedure></A>(self: T;
			 sfr: SupFileRec.T;
			 name: Pathname.T;
			 isFixup: BOOLEAN)
  RAISES {Error, Thread.Alerted, Wr.Failure} =
  CONST
    CmdTab = ARRAY BOOLEAN,BOOLEAN OF TEXT{  (* inAttic, isFixup *)
      ARRAY BOOLEAN OF TEXT{ &quot;A&quot;, &quot;X&quot; },     (* inAttic = FALSE *)
      ARRAY BOOLEAN OF TEXT{ &quot;a&quot;, &quot;x&quot; }      (* inAttic = TRUE *)
    };
  VAR
    pathname := SupMisc.CatPath(sfr.serverPrefix, name);
    origPathname := pathname;
    rf: RCSFile.T;
    dtab: SortedRCSDeltaTbl.T;
    di: RCSDeltaTbl.Iterator;
    ti: RCSFile.TagIterator;
    revNum: RCSRevNum.T;
    d1, d2: RCSDelta.T;
    tag: RCSTag.T;
    tagHidden: BOOLEAN;
    hiddenDeltas: RCSDeltaList.T := NIL;
    hiddenTags: RCSTagList.T := NIL;
    attr: FileAttr.T;
    attrText: TEXT;
    inAttic: BOOLEAN;
    signature: TEXT;
  BEGIN
    TRY
      TRY
	rf := Attic.RCSFileOpenReadonly(pathname);
      EXCEPT OSError.E(list) =&gt;
	WarnBoth(self, &quot;Cannot open \&quot;&quot; &amp; pathname
	  &amp; &quot;\&quot;: &quot; &amp; ErrMsg.StrError(list));
	RETURN;
      END;

      TRY
	(* Enumerate the hidden deltas. *)
	dtab := AccessibleDeltas(rf, self.clientClass, sfr, self);
	di := RCSFile.IterateByNumber(rf);
	WHILE di.next(revNum, d1) DO
	  IF NOT dtab.get(revNum, d2) THEN
	    hiddenDeltas := RCSDeltaList.Cons(d1, hiddenDeltas);
	  END;
	END;

	(* Enumerate the hidden tags. *)
	ti := RCSFile.IterateTags(rf);
	WHILE ti.next(tag) DO
	  tagHidden := TRUE;
	  IF self.clientClass.inAllowedCollectionTags(sfr.collection, tag.name)
	  THEN
	    IF RCSTag.IsCVSBranch(tag)
	    AND self.clientClass.inAllowedCollectionBranches(sfr.collection,
	      tag.name)
	    OR TagIncluded(dtab, tag) THEN
	      tagHidden := FALSE;
	    END;
	  END;
	  IF tagHidden THEN
	    hiddenTags := RCSTagList.Cons(tag, hiddenTags);
	  END;
	END;

	(* Remove the hidden tags and deltas. *)
	WHILE hiddenTags # NIL DO
	  RCSFile.DeleteTag(rf, hiddenTags.head.name, hiddenTags.head.revNum);
	  hiddenTags := hiddenTags.tail;
	END;
	WHILE hiddenDeltas # NIL DO
	  RCSFile.DeleteDelta(rf, hiddenDeltas.head);
	  hiddenDeltas := hiddenDeltas.tail;
	END;

	(* Find the size of the edited file and insert it into the attributes. *)
	attr := RCSFile.GetAttr(rf);
	WITH wr = NEW(NullWr.T).init() DO
	  RCSFile.ToWr(rf, wr);
	  attr := FileAttr.Override(attr,
	    NEW(FileAttr.T).init(attr.fileType, size := Wr.Index(wr)));
	  Wr.Close(wr);
	END;

	inAttic := pathname # origPathname;
	SendExecutes(self, sfr, name, inAttic := inAttic, isCheckout := FALSE);
	IF isFixup THEN
	  attrText := AttrOrModTimeSize(self, sfr, attr);
	ELSE
	  attrText := AttrOrModTimeSizeMode(self, sfr, attr);
	END;
	self.proto.putCmd(self.wr, CmdTab[inAttic, isFixup], name, attrText);

	WITH md5wr = NEW(MD5Wr.T).init(self.wr, closeChild := FALSE) DO
	  RCSFile.ToWr(rf, md5wr);
	  Wr.Close(md5wr);
	  signature := md5wr.getSignature();
	END;
	SupMisc.PutCmd(self.wr, &quot;.&quot;);
	IF signature # NIL THEN
	  SupMisc.PutCmd(self.wr, &quot;5&quot;, signature);
	END;
      FINALLY
	TRY
	  RCSFile.Close(rf);
	EXCEPT OSError.E(list) =&gt;
	  RAISE Error(&quot;Cannot close \&quot;&quot; &amp; pathname &amp; &quot;\&quot;: &quot; &amp;
	    ErrMsg.StrError(list));
	END;
      END;
    EXCEPT RCSError.E(text) =&gt;
      WarnBoth(self, &quot;RCS file error in \&quot;&quot; &amp; pathname &amp; &quot;\&quot;: &quot; &amp; text);
    END;
  END PartialRCSSend;

PROCEDURE <A NAME="SendCounted"><procedure>SendCounted</procedure></A>(rd: Rd.T;
                      wr: Wr.T;
		      count: CARDINAL;
		      md5: MD5.T)
  RAISES {Rd.Failure, Thread.Alerted, Wr.Failure} =
  VAR
    nSent: CARDINAL;
    md5Wr: MD5Wr.T;
    signature: TEXT;
  BEGIN
    IF md5 # NIL THEN
      md5Wr := NEW(MD5Wr.T).init(wr, md5 := md5, closeChild := FALSE);
      TRY
	nSent := RdCopy.ToWriter(rd, md5Wr, count);
      FINALLY
	Wr.Close(md5Wr);
      END;
      signature := md5Wr.getSignature();
    ELSE
      nSent := RdCopy.ToWriter(rd, wr, count);
      signature := NIL;
    END;

    IF nSent &lt; count THEN  (* The file got shorter *)
      FOR i := 1 TO count-nSent DO  (* Pad it out *)
	Wr.PutChar(wr, '\000');
      END;
      SupMisc.PutCmd(wr, &quot;.&lt;&quot;);
    ELSE
      (* We used to raise an error if the file grew on the server
         while it was being transferred.  Now we just ignore that
         case and transfer the number of bytes originally decided
         upon, assuming we'll transfer the rest on the next update.
         The previous policy caused big problems for huge mail
         archive files which took a very long time to transfer but
         were also grown frequently on the server host. *)
      SupMisc.PutCmd(wr, &quot;.&quot;);
    END;

    IF signature # NIL THEN
      SupMisc.PutCmd(wr, &quot;5&quot;, signature);
    END;
  END SendCounted;
</PRE>***************************************************************************
 Rsync updates. 
***************************************************************************

<P><PRE>PROCEDURE <A NAME="RsyncCompare"><procedure>RsyncCompare</procedure></A>(self: T;
                       sfr: SupFileRec.T;
                       name: Pathname.T;
                       size: CARDINAL;
                       blockSize: CARDINAL)
  RAISES {Error, Rd.EndOfFile, Rd.Failure, Thread.Alerted,
	  TokScan.Error, Wr.Failure} =
  VAR
    pathname := SupMisc.CatPath(sfr.serverPrefix, name);
    remainder: CARDINAL;       (* Bytes at tail after all full blocks. *)
    blockCount: CARDINAL;      (* Blocks including partial block at end. *)
    fullBlockCount: CARDINAL;  (* Full blocks, excluding partial at end. *)
    blocks: REF ARRAY OF RsyncBlock.T;
    ts: TokScan.T;
    rsum: Word.T;
    md5: MD5Digest.T;
    rsf: RsyncFile.T;
    di: RsyncFile.DiffIterator;
    br: RsyncFile.BlockRange;
    wr: Wr.T;
  BEGIN
    fullBlockCount := size DIV blockSize;
    remainder := size MOD blockSize;
    IF remainder = 0 THEN
      blockCount := fullBlockCount;
    ELSE
      blockCount := fullBlockCount + 1;
    END;

    (* NOTE: The client sends information about all blocks, including
       any partial block at the end of the file.  However, at present
       we do not use the information from the partial block -- we simply
       discard it.  We might use it in the future, though. *)

    blocks := NEW(REF ARRAY OF RsyncBlock.T, fullBlockCount);

    (* Read the block info. *)
    FOR i := 0 TO blockCount-1 DO
      ts := self.proto.getCmd(self.rd);
      rsum := ts.getInt(&quot;rolling checksum&quot;, 16);
      md5 := ts.getMD5(&quot;md5 checksum&quot;);
      ts.getEnd(&quot;end of rsync block info line&quot;);
      IF i &lt; fullBlockCount THEN  (* Not the last, partial block. *)
	WITH b = blocks[i] DO
	  b.num := i;
	  b.rsum := rsum;
	  b.md5 := md5;
	END;
      END;
    END;
    ts := self.proto.getCmd(self.rd);
    ts.getLiteral(&quot;.&quot;);

    IF NOT CheckName(self, name) OR MaybeSendNode(self, sfr, name) THEN
      RETURN;
    END;

    TRY
      rsf := RsyncFile.Open(pathname, blockSize);
    EXCEPT OSError.E(l) =&gt;
      WarnBoth(self, &quot;Cannot open \&quot;&quot; &amp; pathname
	&amp; &quot;\&quot;: &quot; &amp; ErrMsg.StrError(l));
      RETURN;
    END;
    TRY
      SendExecutes(self, sfr, name, inAttic := FALSE, isCheckout := FALSE);
      self.proto.putCmd(self.wr, &quot;r&quot;,
	name,
	AttrOrModTime(self, sfr, rsf.attr),
	Fmt.Unsigned(blockSize, 10),
	RsyncFile.GetMD5(rsf));
      di := RsyncFile.IterateDiffs(rsf, blocks);
      LOOP
	wr := NEW(EscapedWr.T).init(self.wr, closeChild := FALSE);
	TRY
	  IF NOT di.next(wr, br) THEN EXIT END;
	FINALLY
	  Wr.Close(wr);
	END;
	Wr.PutText(self.wr, Fmt.Int(br.start)
	  &amp; &quot; &quot; &amp; Fmt.Int(br.count)
	  &amp; &quot;\n&quot;);
      END;
      self.proto.putCmd(self.wr, &quot;.&quot;);
    FINALLY
      TRY
	RsyncFile.Close(rsf);
      EXCEPT OSError.E(l) =&gt;
	RAISE Error(&quot;Cannot close \&quot;&quot; &amp; pathname &amp; &quot;\&quot;: &quot; &amp;
	  ErrMsg.StrError(l));
      END;
    END;
  END RsyncCompare;
</PRE>***************************************************************************
 Node operations. 
***************************************************************************

<P><PRE>PROCEDURE <A NAME="NodeCompare"><procedure>NodeCompare</procedure></A>(self: T;
                      sfr: SupFileRec.T;
		      name: Pathname.T)
  RAISES {Error, Thread.Alerted, Wr.Failure} =
  CONST myName = &quot;NodeCompare: &quot;;
  BEGIN
    Trace(self, myName, sfr.collection, &quot; &quot;, name);
    IF NOT CheckName(self, name) OR MaybeSendNode(self, sfr, name) THEN
      RETURN;
    END;
    (* Uh-oh.  The client has a node, but we don't. *)
    RegularSend(self, sfr, name, isFixup := FALSE);
  END NodeCompare;

PROCEDURE <A NAME="MaybeSendNode"><procedure>MaybeSendNode</procedure></A>(self: T;
                        sfr: SupFileRec.T;
			name: Pathname.T): BOOLEAN
  RAISES {Thread.Alerted, Wr.Failure} =
</PRE><BLOCKQUOTE><EM> If the file is a node, sends it to the client if the negotiated protocol
   allows that, then returns <CODE>TRUE</CODE>.  Returns <CODE>FALSE</CODE> if the file is a
   regular file or does not exist at all. </EM></BLOCKQUOTE><PRE>
  CONST myName = &quot;MaybeSendNode: &quot;;
  VAR
    pathname := SupMisc.CatPath(sfr.serverPrefix, name);
    origPathname := pathname;
    attr: FileAttr.T;
    cmd: TEXT;
    attrText: TEXT;
  BEGIN
    Trace(self, myName, sfr.collection, &quot; &quot;, name);
    TRY
      attr := Attic.FileAttrFromPathname(pathname,
	follow := GlobTree.Not(sfr.symlink).test(name));
      IF attr.fileType = FileAttr.FileType.File THEN RETURN FALSE END;
    EXCEPT OSError.E =&gt;
      RETURN FALSE;
    END;

    (* It is a node. *)
    IF self.proto.v.hasFileAttrs THEN (* We can send it. *)
      IF pathname = origPathname THEN cmd := &quot;N&quot; ELSE cmd := &quot;n&quot; END;
      attrText := FileAttr.Encode(attr,
	support := self.proto.v.attrSupport,
	ignore := sfr.attrIgnore);
      SendExecutes(self, sfr, name, inAttic := FALSE, isCheckout := FALSE);
      Trace(self, myName, &quot;command &quot;, cmd, &quot; &quot;, name);
      self.proto.putCmd(self.wr, cmd, name, attrText);
    END;
    RETURN TRUE;
  END MaybeSendNode;
</PRE>***************************************************************************
 Checkout mode operations. 
***************************************************************************

<P><PRE>PROCEDURE <A NAME="CheckoutSend"><procedure>CheckoutSend</procedure></A>(self: T;
                       sfr: SupFileRec.T;
                       name: Pathname.T;
                       tag: TEXT;
                       date: TEXT;
		       deleteIfDead := FALSE;
		       isFixup := FALSE)
  RAISES {Error, Thread.Alerted, Wr.Failure} =
  CONST myName = &quot;CheckoutSend: &quot;;
  VAR
    pathname := SupMisc.CatPath(sfr.serverPrefix, name);
    origPathname := pathname;
    keywordName := name;
    inAttic: BOOLEAN;
    rf: RCSFile.T;
    delta: RCSDelta.T;
    attr: FileAttr.T;
    deltaText: RCSString.Iterator;
    xTag: TEXT;
    cmd: TEXT;
  BEGIN
    Trace(self, myName, sfr.collection, &quot; &quot;, name);
    IF NOT CheckName(self, name) THEN RETURN END;
    TRY
      rf := Attic.RCSFileOpenReadonly(pathname);
    EXCEPT
    | OSError.E(list) =&gt;
	WarnBoth(self, &quot;Cannot open \&quot;&quot; &amp; pathname
	  &amp; &quot;\&quot;: &quot; &amp; ErrMsg.StrError(list));
	RETURN;
    | RCSError.E(text) =&gt;
	WarnBoth(self, &quot;RCS file error in \&quot;&quot; &amp; pathname &amp; &quot;\&quot;: &quot; &amp; text);
	RETURN;
    END;
    TRY
      inAttic := pathname # origPathname;
      IF inAttic THEN
	keywordName := SupMisc.AtticName(keywordName);
      END;
      attr := RCSFile.GetAttr(rf);

      delta := GetCheckoutDelta(rf, tag, date);
      IF delta = NIL OR RCSDelta.Dead(delta, inAttic := inAttic) THEN
	(* File is non-existent or dead for this tag and date. *)
	IF deleteIfDead THEN cmd := &quot;u&quot; ELSE cmd := &quot;c&quot; END;
	self.proto.putCmd(self.wr, cmd,
	  name,
	  tag,
	  date,
	  AttrOrModTime(self, sfr, attr));
      ELSE  (* File is alive for this tag and date. *)
	IF NOT Text.Equal(tag, &quot;.&quot;) THEN xTag := tag ELSE xTag := NIL END;
	TRY
	  deltaText := RCSDelta.GetText(delta);
	EXCEPT RCSError.E(text) =&gt;
	  WarnBoth(self, &quot;RCS file error in \&quot;&quot; &amp; pathname &amp; &quot;\&quot;: &quot; &amp; text);
	  RETURN;
	END;
	deltaText := sfr.expander.expand(deltaText,
	  hideAttic := self.proto.v.hidesAtticInCVSHeader,
	  cvsRoot := sfr.keywordPrefix,
	  name := keywordName,
	  delta := delta,
	  tag := xTag);
	IF isFixup THEN cmd := &quot;Y&quot; ELSE cmd := &quot;C&quot; END;
	SendExecutes(self, sfr, name, inAttic := inAttic, isCheckout := TRUE);
	self.proto.putCmd(self.wr, cmd,
	  name,
	  tag,
	  date,
	  delta.revision,
	  delta.date,
	  AttrOrModTime(self, sfr, attr));
	SendEscaped(self.wr, deltaText, withChecksum := TRUE);
      END;
    FINALLY
      TRY
	RCSFile.Close(rf);
      EXCEPT OSError.E(list) =&gt;
	RAISE Error(&quot;Cannot close \&quot;&quot; &amp; pathname &amp; &quot;\&quot;: &quot; &amp;
	  ErrMsg.StrError(list));
      END;
    END;
  END CheckoutSend;

PROCEDURE <A NAME="UpdateFromChecksum"><procedure>UpdateFromChecksum</procedure></A>(self: T;
                             sfr: SupFileRec.T;
                             name: Pathname.T;
                             tag: TEXT;
                             date: TEXT;
                             cksum: TEXT;
			     revNumHint: RCSRevNum.T := NIL)
  RAISES {Error, Thread.Alerted, Wr.Failure} =
  (* If &quot;revNumHint&quot; is given, then only that revision is checked. *)
  CONST myName = &quot;UpdateFromChecksum: &quot;;
  VAR
    pathname := SupMisc.CatPath(sfr.serverPrefix, name);
    origPathname := pathname;
    keywordName := name;
    rf: RCSFile.T;
    delta: RCSDelta.T;
    bp: RCSDelta.T;
    gotSum: TEXT;
    numToCheck: CARDINAL;
  BEGIN
    Trace(self, myName, sfr.collection, &quot; &quot;, name);
    IF NOT CheckName(self, name) THEN RETURN END;
    TRY
      rf := Attic.RCSFileOpenReadonly(pathname);
    EXCEPT
    | OSError.E(list) =&gt;
	WarnBoth(self, &quot;Cannot open \&quot;&quot; &amp; pathname
	  &amp; &quot;\&quot;: &quot; &amp; ErrMsg.StrError(list));
	RETURN;
    | RCSError.E(text) =&gt;
	WarnBoth(self, &quot;RCS file error in \&quot;&quot; &amp; pathname &amp; &quot;\&quot;: &quot; &amp; text);
	RETURN;
    END;
    TRY
      TRY
	IF pathname # origPathname THEN  (* File is in the Attic. *)
	  keywordName := SupMisc.AtticName(keywordName);
	END;
	IF revNumHint # NIL THEN  (* Check just the specified revision. *)
	  TRY
	    delta := RCSFile.GetDelta(rf, revNumHint);
	    numToCheck := 1;
	  EXCEPT RCSError.E =&gt;  (* Client specified a non-existent revision. *)
	    delta := NIL;
	  END;
	ELSE  (* Search backward on the specified branch. *)
	  delta := GetCheckoutDelta(rf, tag, &quot;.&quot;);
	  numToCheck := MaxChecksumRevisions;
	END;
	IF delta # NIL THEN
	  IF RCSRevNum.NumParts(delta.revision) &gt; 2 THEN
	    bp := RCSFile.GetDelta(rf,
	      RCSRevNum.Prefix(RCSRevNum.Prefix(delta.revision)));
	  ELSE
	    bp := NIL;
	  END;
	  REPEAT
	    gotSum := DeltaChecksum(self, sfr, delta, keywordName, tag);
	    DEC(numToCheck);
	    IF Text.Equal(gotSum, cksum) THEN EXIT END;
	    IF delta = bp OR numToCheck = 0 THEN
	      delta := NIL;  (* Give up. *)
	    ELSE
	      delta := RCSDelta.Predecessor(delta);
	    END;
	  UNTIL delta = NIL;
	END;
      EXCEPT RCSError.E(text) =&gt;
	WarnBoth(self, &quot;RCS file error in \&quot;&quot; &amp; pathname &amp; &quot;\&quot;: &quot; &amp; text);
	RETURN;
      END;
    FINALLY
      TRY
	RCSFile.Close(rf);
      EXCEPT OSError.E(list) =&gt;
	RAISE Error(&quot;Cannot close \&quot;&quot; &amp; pathname &amp; &quot;\&quot;: &quot; &amp;
	  ErrMsg.StrError(list));
      END;
    END;

    IF delta # NIL THEN  (* We found the matching delta. *)
      CheckoutUpdate(self, sfr, name, tag, date, delta.revision, delta.date);
    ELSE  (* Just send the whole file. *)
      CheckoutSend(self, sfr, name, tag, date, deleteIfDead := TRUE);
    END;
  END UpdateFromChecksum;

PROCEDURE <A NAME="DeltaChecksum"><procedure>DeltaChecksum</procedure></A>(self: T;
                        sfr: SupFileRec.T;
                        delta: RCSDelta.T;
			keywordName: Pathname.T;
			tag: TEXT): TEXT
  RAISES {RCSError.E} =
</PRE><BLOCKQUOTE><EM> Computes the MD5 checksum of the given delta, and returns it. </EM></BLOCKQUOTE><PRE>
  VAR
    md5: MD5.T;
    xTag: TEXT;
    iter: RCSString.Iterator;
    lineStr: RCSString.T;
    sum: TEXT;
  BEGIN
    IF NOT Text.Equal(tag, &quot;.&quot;) THEN xTag := tag ELSE xTag := NIL END;
    iter := sfr.expander.expand(RCSDelta.GetText(delta),
      hideAttic := self.proto.v.hidesAtticInCVSHeader,
      cvsRoot := sfr.keywordPrefix,
      name := keywordName,
      delta := delta,
      tag := xTag);
    md5 := MD5.New();
    TRY
      WHILE iter.next(lineStr) DO
	md5.updateText(lineStr.toText());
      END;
    FINALLY
      sum := md5.finish();
    END;
    RETURN sum;
  END DeltaChecksum;

PROCEDURE <A NAME="CheckoutUpdate"><procedure>CheckoutUpdate</procedure></A>(self: T;
                         sfr: SupFileRec.T;
                         name: Pathname.T;
                         tag: TEXT;
                         date: TEXT;
                         oldRevNum: RCSRevNum.T;
			 oldRevDate: RCSDate.T)
  RAISES {Error, Thread.Alerted, Wr.Failure} =
  CONST myName = &quot;CheckoutUpdate: &quot;;
  VAR
    pathname := SupMisc.CatPath(sfr.serverPrefix, name);
    origPathname := pathname;
    inAttic: BOOLEAN;
    keywordName := name;
    rf: RCSFile.T;
    fromAtticFlag: TEXT;
    oldDelta, newDelta: RCSDelta.T;
    attr: FileAttr.T;
    parsedDeltas: ParsedDeltaList.T := NIL;
    diffBase: RCSDelta.T;
    oldLogLines: CARDINAL;
    xTag: TEXT;
    dText: RCSString.Iterator;
    md5: MD5.T;
    sLine: RCSString.T;
    cksum: TEXT;
    checkOutWholeThing := FALSE;
  BEGIN
    Trace(self, myName, sfr.collection, &quot; &quot;, name);
    IF NOT CheckName(self, name) THEN RETURN END;
    TRY
      rf := Attic.RCSFileOpenReadonly(pathname);
    EXCEPT
    | OSError.E(list) =&gt;
	WarnBoth(self, &quot;Cannot open \&quot;&quot; &amp; pathname
	  &amp; &quot;\&quot;: &quot; &amp; ErrMsg.StrError(list));
	RETURN;
    | RCSError.E(text) =&gt;
	WarnBoth(self, &quot;RCS file error in \&quot;&quot; &amp; pathname &amp; &quot;\&quot;: &quot; &amp; text);
	RETURN;
    END;
    TRY
      inAttic := pathname # origPathname;
      IF inAttic THEN
	keywordName := SupMisc.AtticName(keywordName);
      END;
      attr := RCSFile.GetAttr(rf);

      newDelta := GetCheckoutDelta(rf, tag, date);
      IF newDelta = NIL OR RCSDelta.Dead(newDelta, inAttic := inAttic) THEN
	(* File is non-existent or dead for this tag and date. *)
	self.proto.putCmd(self.wr, &quot;u&quot;,
	  name,
	  tag,
	  date,
	  AttrOrModTime(self, sfr, attr));
      ELSE  (* File is alive for this tag and date. *)
	(* See whether the client already has the right revision number.
	   If the client sent the revDate, then verify it too. *)
	IF RCSRevNum.Equal(newDelta.revision, oldRevNum)
	AND (NOT self.proto.v.sendsRevDates OR
	     RCSDate.Equal(newDelta.date, oldRevDate))
	THEN
	  (* The RCS file's modTime has changed, but there have been
	     no changes affecting the client's checked-out version.
	     Just tell the client to update its notion of the RCS
	     file's modTime. *)
	  self.proto.putCmd(self.wr, &quot;T&quot;,
	    name,
	    tag,
	    date,
	    newDelta.revision,
	    more := TRUE);
	  IF self.proto.v.sendsRevDates THEN
	    self.proto.putCmd(self.wr, NIL,
	      newDelta.date,
	      more := TRUE);
	  END;
	  self.proto.putCmd(self.wr, NIL,
	    AttrOrModTime(self, sfr, attr));
	ELSE
	  (* We have to send some deltas to the client. *)
	  TRY
	    oldDelta := RCSFile.GetDelta(rf, oldRevNum);
	    IF self.proto.v.sendsRevDates
	    AND NOT RCSDate.Equal(oldRevDate, oldDelta.date) THEN
	      (* We found the revision the client says he has.  But it
		 doesn't have the date that the client says it should
		 have.  Probably somebody completely replaced the RCS
		 file on the server. *)
	      RAISE RCSError.E(&quot;revDate mismatch for revision &quot; &amp; oldRevNum);
	    END;
	    parsedDeltas := DeltaPath(oldDelta, newDelta);

	    (* Count the number of log lines that the client will have to
	       remove from the old version of the file.  Note that trailing
	       blank lines are discarded. *)
	    oldLogLines := 0;
	    VAR
	      line: RCSString.T;
	      text: TEXT;
	      iter := RCSDelta.GetLog(oldDelta).iterate();
	      lineNum := 0;
	    BEGIN
	      WHILE iter.next(line) DO
		text := line.toText();
		IF NOT SupMisc.IsBlankLine(text) THEN
		  oldLogLines := lineNum + 1;
		END;
		INC(lineNum);
	      END;
	    END;

	    (* Compute the checksum. *)
	    IF Text.Equal(tag, &quot;.&quot;) THEN xTag := NIL ELSE xTag := tag END;
	    dText := sfr.expander.expand(RCSDelta.GetText(newDelta),
	      hideAttic := self.proto.v.hidesAtticInCVSHeader,
	      cvsRoot := sfr.keywordPrefix,
	      name := keywordName,
	      delta := newDelta,
	      tag := xTag);
	    md5 := MD5.New();
	    TRY
	      WHILE dText.next(sLine) DO
		md5.updateText(sLine.toText());
	      END;
	    FINALLY
	      cksum := md5.finish();
	    END;
	  EXCEPT RCSError.E =&gt;
            (* For one reason or another, we weren't able to come
               up with an update from the client's revision to the
               one we want.  This happened once, when the delta the
               client said it had didn't even exist in the RCS file.
               (Somebody had clobbered the RCS file.)  Just check out
               the desired revision from scratch.  We used to warn
               about this, but it put too much scary stuff into the
               server logs on those rare occasions when it happened. *)
            checkOutWholeThing := TRUE;
	  END;
	  IF NOT checkOutWholeThing THEN
	    (* We must pass along an indication of whether the RCS
	       file was in the Attic or not, because that affects the
	       expansion of the &quot;Source&quot; and &quot;Header&quot; RCS keywords. *)
	    IF pathname = origPathname THEN
	      fromAtticFlag := &quot;0&quot;;
	    ELSE
	      fromAtticFlag := &quot;1&quot;;
	    END;
	    SendExecutes(self, sfr, name, inAttic := FALSE, isCheckout := TRUE);
	    self.proto.putCmd(self.wr, &quot;U&quot;,
	      name,
	      tag,
	      date,
	      oldRevNum,
	      fromAtticFlag,
	      Fmt.Int(oldLogLines),
	      RCSKeyword.EncodeExpand(rf.expand),
	      AttrOrModTime(self, sfr, attr),
	      cksum);

	    (* DANGER - Errors beyond this point are fatal. *)

	    diffBase := oldDelta;
	    REPEAT
	      WITH pd = parsedDeltas.head DO
		SendDelta(self, pd.delta, diffBase, pd.log, pd.text);
		diffBase := pd.delta;
	      END;
	      parsedDeltas := parsedDeltas.tail;
	    UNTIL parsedDeltas = NIL;

	    self.proto.putCmd(self.wr, &quot;.&quot;);
	  END;
	END;
      END;
    FINALLY
      TRY
	RCSFile.Close(rf);
      EXCEPT OSError.E(list) =&gt;
	RAISE Error(&quot;Cannot close \&quot;&quot; &amp; pathname &amp; &quot;\&quot;: &quot; &amp;
	  ErrMsg.StrError(list));
      END;
    END;
    IF checkOutWholeThing THEN  (* We weren't able to edit the file. *)
      CheckoutSend(self, sfr, name, tag, date, deleteIfDead := TRUE);
    END;
  END CheckoutUpdate;

PROCEDURE <A NAME="DeltaPath"><procedure>DeltaPath</procedure></A>(delta1, delta2: RCSDelta.T): ParsedDeltaList.T
  RAISES {RCSError.E} =
</PRE><BLOCKQUOTE><EM> Returns a list of parsed deltas leading from <CODE>delta1</CODE> to <CODE>delta2</CODE>.
   The text for each one is filled in, but the log is present only for
   the last one. </EM></BLOCKQUOTE><PRE>
  VAR
    d1 := delta1;
    d2 := delta2;
    list1, list2: ParsedDeltaList.T := NIL;
    l: ParsedDeltaList.T;
    diffBase: RCSDelta.T;
  BEGIN
    (* Find the paths from each delta back to a common branch. *)
    WITH commLen = SupMisc.CommonLength(d1.revision, d2.revision) DO
      (* Notice that the following loops are not quite the same.  That is
	 because we want to include &quot;delta2&quot; in our final list, but
	 not &quot;delta1&quot;. *)
      WHILE Text.Length(RCSRevNum.Prefix(d1.revision)) &gt; commLen
      AND NOT RCSRevNum.IsTrunk(d1.revision) DO
	d1 := RCSDelta.Predecessor(d1);
	list1 := ParsedDeltaList.Cons(ParsedDelta.T{delta := d1}, list1);
      END;
      WHILE Text.Length(RCSRevNum.Prefix(d2.revision)) &gt; commLen
      AND NOT RCSRevNum.IsTrunk(d2.revision) DO
	list2 := ParsedDeltaList.Cons(ParsedDelta.T{delta := d2}, list2);
	d2 := RCSDelta.Predecessor(d2);
      END;
    END;

    (* Find the connecting path along the common branch. *)
    WITH c = RCSRevNum.Compare(d1.revision, d2.revision) DO
      IF c &lt; 0 THEN
	REPEAT
	  list2 := ParsedDeltaList.Cons(ParsedDelta.T{delta := d2}, list2);
	  d2 := RCSDelta.Predecessor(d2);
	UNTIL d2 = d1;
      ELSIF c &gt; 0 THEN
	REPEAT
	  d1 := RCSDelta.Predecessor(d1);
	  list1 := ParsedDeltaList.Cons(ParsedDelta.T{delta := d1}, list1);
	UNTIL d1 = d2;
      END;
    END;

    (* Combine the paths, by popping entries from &quot;list1&quot; and pushing them
       onto &quot;list2&quot;. *)
    WHILE list1 # NIL DO
      l := list1;
      list1 := list1.tail;
      l.tail := list2;
      list2 := l;
    END;

    (* Traverse the final path, filling the texts for all the deltas, and
       the log for the last one. *)
    l := list2;
    diffBase := delta1;
    WHILE l # NIL DO
      WITH pd = l.head DO
	IF l.tail = NIL THEN  (* Last delta in the list. *)
	  pd.log := RCSDelta.GetLog(pd.delta).iterate();
	END;
	pd.text := RCSDelta.GetText(pd.delta, diffBase);
	diffBase := pd.delta;
      END;
      l := l.tail;
    END;

    RETURN list2;
  END DeltaPath;

PROCEDURE <A NAME="GetCheckoutDelta"><procedure>GetCheckoutDelta</procedure></A>(rf: RCSFile.T;
                           tag: TEXT;
                           date: RCSDate.T): RCSDelta.T =
  BEGIN
    IF Text.Equal(tag, &quot;.&quot;) THEN tag := NIL END;
    IF Text.Equal(date, &quot;.&quot;) THEN date := NIL END;
    TRY
      RETURN RCSFile.GetTagDelta(rf, tag, date);
    EXCEPT RCSError.E =&gt;
      RETURN NIL;
    END;
  END GetCheckoutDelta;
</PRE>***************************************************************************

<P><PRE>PROCEDURE <A NAME="SendExecutes"><procedure>SendExecutes</procedure></A>(self: T;
                       sfr: SupFileRec.T;
		       name: Pathname.T;
		       inAttic: BOOLEAN;
		       isCheckout: BOOLEAN)
  RAISES {Thread.Alerted, Wr.Failure} =
  CONST myName = &quot;SendExecutes: &quot;;
  VAR
    clientName: Pathname.T := NIL;
    er: ExecRec.T;
  BEGIN
    Trace(self, myName, sfr.collection, &quot; &quot;, name);
    FOR i := 0 TO sfr.executes.size()-1 DO
      er := sfr.executes.get(i);
      IF er.pattern.test(name) THEN
	IF clientName = NIL THEN  (* First match. *)
	  IF isCheckout THEN
	    clientName := SupMisc.CheckoutName(name);
	  ELSIF inAttic THEN
	    clientName := SupMisc.AtticName(name);
	  ELSE
	    clientName := name;
	  END;
	END;
	self.proto.putCmd(self.wr, &quot;E&quot;, clientName, er.command);
      END;
    END;
  END SendExecutes;
</PRE>***************************************************************************

<P> Legacy protocol support. 

<P><PRE>PROCEDURE <A NAME="AttrOrModTime"><procedure>AttrOrModTime</procedure></A>(self: T;
                        sfr: SupFileRec.T;
			attr: FileAttr.T): TEXT =
  BEGIN
    IF self.proto.v.hasFileAttrs THEN
      RETURN FileAttr.Encode(attr,
	support := self.proto.v.attrSupport,
	ignore := sfr.attrIgnore);
    ELSE
      RETURN TokScan.EncodeTime(FileAttr.GetModTime(attr));
    END;
  END AttrOrModTime;

PROCEDURE <A NAME="AttrOrModTimeSize"><procedure>AttrOrModTimeSize</procedure></A>(self: T;
                            sfr: SupFileRec.T;
			    attr: FileAttr.T): TEXT =
  BEGIN
    IF self.proto.v.hasFileAttrs THEN
      RETURN FileAttr.Encode(attr,
	support := self.proto.v.attrSupport,
	ignore := sfr.attrIgnore);
    ELSE
      RETURN TokScan.EncodeTime(FileAttr.GetModTime(attr))
	&amp; &quot; &quot; &amp; Fmt.Unsigned(FileAttr.GetSize(attr), 10);
    END;
  END AttrOrModTimeSize;

PROCEDURE <A NAME="AttrOrModTimeSizeMode"><procedure>AttrOrModTimeSizeMode</procedure></A>(self: T;
                                sfr: SupFileRec.T;
				attr: FileAttr.T): TEXT =
  BEGIN
    IF self.proto.v.hasFileAttrs THEN
      (* Note that we never ignore the file mode, since we are creating
	 a brand new file in this case. *)
      RETURN FileAttr.Encode(attr,
	support := self.proto.v.attrSupport,
	ignore := sfr.attrIgnore - FileAttr.AttrTypes{FileAttr.AttrType.Mode});
    ELSE
      RETURN TokScan.EncodeTime(FileAttr.GetModTime(attr))
	&amp; &quot; &quot; &amp; Fmt.Unsigned(FileAttr.GetSize(attr), 10)
	&amp; &quot; &quot; &amp; Fmt.Unsigned(FileAttr.GetMode(attr), 10);
    END;
  END AttrOrModTimeSizeMode;
</PRE>***************************************************************************

<P><PRE>PROCEDURE <A NAME="Warning"><procedure>Warning</procedure></A>(self: T; msg: TEXT) =
</PRE><BLOCKQUOTE><EM> Logs a warning message. </EM></BLOCKQUOTE><PRE>
  BEGIN
    IF self.logger # NIL THEN
      Logger.Put(self.logger, Logger.Priority.Warning, msg);
    END;
  END Warning;

PROCEDURE <A NAME="WarnBoth"><procedure>WarnBoth</procedure></A>(self: T; msg: TEXT)
</PRE><BLOCKQUOTE><EM> Logs a warning message and sends it to the client. </EM></BLOCKQUOTE><PRE>
  RAISES {Thread.Alerted, Wr.Failure} =
  BEGIN
    Warning(self, msg);
    WarnClient(self, msg);
  END WarnBoth;

PROCEDURE <A NAME="WarnClient"><procedure>WarnClient</procedure></A>(self: T; msg: TEXT)
</PRE><BLOCKQUOTE><EM> Sends a warning message to the client. </EM></BLOCKQUOTE><PRE>
  RAISES {Thread.Alerted, Wr.Failure} =
  BEGIN
    self.proto.putCmd(self.wr, &quot;!&quot;, msg);
    Wr.Flush(self.wr);
  END WarnClient;

PROCEDURE <A NAME="Trace"><procedure>Trace</procedure></A>(self: T; m1, m2, m3, m4, m5, m6, m7, m8: TEXT := NIL;
                level := 1) =
</PRE><BLOCKQUOTE><EM> Logs a trace message. </EM></BLOCKQUOTE><PRE>
  BEGIN
    IF traceLevel &gt;= level THEN
      VAR
        msg := &quot;&quot;;
      BEGIN
        IF m1 # NIL THEN msg := msg &amp; m1 END;
        IF m2 # NIL THEN msg := msg &amp; m2 END;
        IF m3 # NIL THEN msg := msg &amp; m3 END;
        IF m4 # NIL THEN msg := msg &amp; m4 END;
        IF m5 # NIL THEN msg := msg &amp; m5 END;
        IF m6 # NIL THEN msg := msg &amp; m6 END;
        IF m7 # NIL THEN msg := msg &amp; m7 END;
        IF m8 # NIL THEN msg := msg &amp; m8 END;
        IF self.logger # NIL THEN
          Logger.Debug(self.logger, msg);
        ELSE
          IO.Put(&quot;logger = NIL: &quot; &amp; msg &amp; &quot;\n&quot;);
        END;
      END;
    END;
  END Trace;

BEGIN
END RCSComp.
</PRE>
</inModule>
<HR>
<A NAME="x1">interface GlobTree is in:
</A><UL>
<LI><A HREF="../../patternmatching/src/GlobTree.i3.html#0TOP0">patternmatching/src/GlobTree.i3</A>
<LI><A HREF="../../suplib/src/GlobTree.i3.html#0TOP0">suplib/src/GlobTree.i3</A>
</UL>
<P>
<HR>
<A NAME="x2">interface SupFileRecSeq is in:
</A><UL>
<LI><A HREF="../../client/derived/SupFileRecSeq.i3.html#0TOP0">client/derived/SupFileRecSeq.i3</A>
<LI><A HREF="../derived/SupFileRecSeq.i3.html#0TOP0">server/derived/SupFileRecSeq.i3</A>
</UL>
<P>
<PRE>























</PRE>
</BODY>
</HTML>
