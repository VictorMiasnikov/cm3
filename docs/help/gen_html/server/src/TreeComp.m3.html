<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: server/src/TreeComp.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>server/src/TreeComp.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright 1996-2003 John D. Polstra.
 * All rights reserved.
 *
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions
 * are met:
 * 1. Redistributions of source code must retain the above copyright
 *    notice, this list of conditions and the following disclaimer.
 * 2. Redistributions in binary form must reproduce the above copyright
 *    notice, this list of conditions and the following disclaimer in the
 *    documentation and/or other materials provided with the distribution.
 * 3. All advertising materials mentioning features or use of this software
 *    must display the following acknowledgment:
 *      This product includes software developed by John D. Polstra.
 * 4. The name of the author may not be used to endorse or promote products
 *    derived from this software without specific prior written permission.
 *
 * THIS SOFTWARE IS PROVIDED BY THE AUTHOR ``AS IS'' AND ANY EXPRESS OR
 * IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES
 * OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED.
 * IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR ANY DIRECT, INDIRECT,
 * INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT
 * NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
 * DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
 * THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
 * (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF
 * THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 *
 * $Id: TreeComp.m3.html,v 1.3 2010-04-29 17:19:53 wagner Exp $ 

<P><PRE>MODULE <module><implements><A HREF="TreeComp.i3.html">TreeComp</A></implements></module>;

IMPORT
  <A HREF="ClientClass.i3.html">ClientClass</A>, <A HREF="../../suplib/src/CVProto.i3.html">CVProto</A>, <A HREF="../../suplib/src/CVTree.i3.html">CVTree</A>, <A HREF="../../suplib/src/ErrMsg.i3.html">ErrMsg</A>, <A HREF="../../suplib/src/FileAttr.i3.html">FileAttr</A>, <A HREF="../../suplib/src/FileID.i3.html">FileID</A>, <A HREF="FileInfo.i3.html">FileInfo</A>,
  <A HREF="../derived/FileInfoMerger.i3.html">FileInfoMerger</A>, <A HREF="../../libm3/src/rw/FileRd.i3.html">FileRd</A>, <A HREF="../../suplib/src/FileStatus.i3.html">FileStatus</A>, <A HREF="#x1">GlobTree</A>, <A HREF="../../suplib/src/GzipRd.i3.html">GzipRd</A>, <A HREF="../../suplib/src/GzipWr.i3.html">GzipWr</A>, <A HREF="../../libm3/src/rw/IO.i3.html">IO</A>,
  <A HREF="../derived/LinkTbl.i3.html">LinkTbl</A>, <A HREF="../../suplib/src/Logger.i3.html">Logger</A>, <A HREF="../../libm3/src/os/Common/OSError.i3.html">OSError</A>, <A HREF="../../suplib/src/PathComp.i3.html">PathComp</A>, <A HREF="../../libm3/src/os/Common/Pathname.i3.html">Pathname</A>, <A HREF="../../libm3/src/rw/Rd.i3.html">Rd</A>, <A HREF="../../suplib/src/Reaper.i3.html">Reaper</A>, <A HREF="../../tcp/src/common/StreamRd.i3.html">StreamRd</A>,
  <A HREF="../../tcp/src/common/StreamWr.i3.html">StreamWr</A>, <A HREF="../../suplib/src/SupFileRec.i3.html">SupFileRec</A>, <A HREF="#x2">SupFileRecSeq</A>, <A HREF="../../suplib/src/SupMisc.i3.html">SupMisc</A>, <A HREF="../../m3core/src/text/Text.i3.html">Text</A>, <A HREF="../../m3core/src/thread/Common/Thread.i3.html">Thread</A>, <A HREF="../../m3core/src/time/Common/Time.i3.html">Time</A>,
  <A HREF="../../suplib/src/TokScan.i3.html">TokScan</A>, <A HREF="../../libm3/src/rw/Wr.i3.html">Wr</A>;

EXCEPTION Error(TEXT);

REVEAL
  <A NAME="T">T</A> = Public BRANDED OBJECT
    proto: CVProto.T;
    wireRd: StreamRd.T;		(* Raw reader. *)
    rd: StreamRd.T;		(* Currently active reader. *)
    wireWr: StreamWr.T;		(* Raw writer. *)
    wr: StreamWr.T;		(* Currently active writer. *)
    collections: SupFileRecSeq.T;
    clientClass: ClientClass.T;
    compLevel: [0..9];
    reaper: Reaper.T;
    logger: Logger.T;
  OVERRIDES
    apply := Apply;
    init := Init;
  END;

PROCEDURE <A NAME="Apply"><procedure>Apply</procedure></A>(self: T): REFANY =
  VAR
    ts: TokScan.T;
    collection, release: TEXT;
    initialBytesIn, initialBytesOut: LONGREAL;
  BEGIN
    TRY
      TRY
	FOR i := 0 TO self.collections.size()-1 DO
	  WITH sfr = self.collections.get(i) DO
	    IF NOT SupFileRec.Option.Skip IN sfr.options THEN
	      ts := self.proto.getCmd(self.rd);
	      ts.getFolded(&quot;COLL&quot;);
	      collection := ts.getToken(&quot;collection&quot;);
	      release := ts.getToken(&quot;release&quot;);
              SupFileRec.Check(sfr, collection, release);
              initialBytesIn := StreamRd.ByteCount(self.rd);
              initialBytesOut := StreamWr.ByteCount(self.wr);
              TRY
                CompCollection(self, sfr);
              FINALLY
                LOCK sfr DO
                  sfr.bytesIn := sfr.bytesIn +
                    StreamRd.ByteCount(self.rd) - initialBytesIn;
                  sfr.bytesOut := sfr.bytesOut +
		    StreamWr.ByteCount(self.wr) - initialBytesOut;
                END;
	      END;
	    END;
	  END;
	END;
	ts := self.proto.getCmd(self.rd);
	ts.getLiteral(&quot;.&quot;);
	self.proto.putCmd(self.wr, &quot;.&quot;);
	Wr.Flush(self.wr);
      FINALLY
	IF self.reaper # NIL THEN
	  Reaper.Dying(self.reaper);
	END;
      END;
    EXCEPT
    | CVTree.Error(msg) =&gt;
      RETURN msg;
    | Error(msg) =&gt;
      RETURN msg;
    | Rd.EndOfFile =&gt;
      RETURN &quot;Premature EOF from client&quot;;
    | Rd.Failure(list) =&gt;
      RETURN &quot;Network read failure: &quot; &amp; ErrMsg.StrError(list);
    | Thread.Alerted =&gt;
      RETURN &quot;Interrupted&quot;;
    | TokScan.Error(msg) =&gt;
      RETURN &quot;TreeComp protocol error: &quot; &amp; msg;
    | Wr.Failure(list) =&gt;
      RETURN &quot;Network write failure: &quot; &amp; ErrMsg.StrError(list);
    END;
    RETURN NIL;
  END Apply;

PROCEDURE <A NAME="CompCollection"><procedure>CompCollection</procedure></A>(self: T; sfr: SupFileRec.T)
  RAISES {CVTree.Error, Error, Rd.EndOfFile, Rd.Failure, Thread.Alerted,
	  TokScan.Error, Wr.Failure} =
  VAR
    compress: BOOLEAN;
    merger: Merger;
  BEGIN
    sfr.scanTime := Time.Now();

    self.proto.putCmd(self.wr, &quot;COLL&quot;,
      sfr.collection,
      sfr.release,
      TokScan.EncodeTime(sfr.scanTime));
    Wr.Flush(self.wr);

    compress := SupFileRec.Option.Compress IN sfr.options;

    IF compress THEN
      TRY
	self.rd := NEW(GzipRd.T).init(self.wireRd, closeChild := FALSE);
      EXCEPT OSError.E(list) =&gt;
	RAISE Error(&quot;Cannot create Gzip reader: &quot; &amp; ErrMsg.StrError(list));
      END;
    END;
    TRY
      IF compress THEN
	TRY
	  self.wr := NEW(GzipWr.T).init(self.wireWr, level := self.compLevel,
	    closeChild := FALSE);
	EXCEPT OSError.E(list) =&gt;
	  RAISE Error(&quot;Cannot create Gzip writer: &quot; &amp; ErrMsg.StrError(list));
	END;
      END;
      TRY
	merger := NEW(Merger).init(self, sfr);
	TRY
	  IF SupFileRec.Option.CheckoutMode IN sfr.options THEN
	    CompCheckoutMode(self, sfr, merger);
	  ELSE
	    CompCVSMode(self, sfr, merger);
	  END;
	FINALLY
	  merger.close();
	END;

	self.proto.putCmd(self.wr, &quot;.&quot;);
	Wr.Flush(self.wr);

	IF compress THEN
	  Wr.Close(self.wr);
	  IF NOT Rd.EOF(self.rd) THEN
	    RAISE TokScan.Error(
	      &quot;Expected EOF from compressed stream, didn't get it&quot;);
	  END;
	  Rd.Close(self.rd);
	END;
      FINALLY
	IF compress THEN
	  GzipWr.Cleanup(self.wr);
	  self.wr := self.wireWr;
	END;
      END;
    FINALLY
      IF compress THEN
	GzipRd.Cleanup(self.rd);
	self.rd := self.wireRd;
      END;
    END;
  END CompCollection;

PROCEDURE <A NAME="CompCVSMode"><procedure>CompCVSMode</procedure></A>(self: T; sfr: SupFileRec.T; merger: Merger)
  RAISES {Error, Rd.EndOfFile, Rd.Failure, Thread.Alerted,
	  TokScan.Error, Wr.Failure} =
  CONST myName = &quot;CompCVSMode: &quot;;
  VAR
    linkTbl: LinkTbl.T := NIL;
    clientFile, serverFile: FileInfo.T;
  BEGIN
    Trace(self, myName, &quot; &quot;, sfr.collection);
    IF self.proto.v.hasHardLinks THEN
      linkTbl := NEW(LinkTbl.Default).init();
    END;

    WHILE GetNext(merger, clientFile, serverFile) DO
      IF clientFile = NIL THEN  (* Add file on client. *)
	IF NOT SupFileRec.Option.DoDeletesOnly IN sfr.options THEN
	  CASE serverFile.type OF
	  | FileInfo.Type.DirDown =&gt;  (* Create directory. *)
              Trace(self, myName, &quot; Create directory &quot;, serverFile.name);
	      self.proto.putCmd(self.wr, &quot;I&quot;, serverFile.name);
	  | FileInfo.Type.DirUp =&gt;  (* Set directory attributes. *)
              Trace(self, myName, &quot; Set directory attributes &quot;,
                serverFile.name);
	      self.proto.putCmd(self.wr, &quot;J&quot;,
		serverFile.name,
		EncodeAttr(self, sfr, serverFile.attr));
	  | FileInfo.Type.Live, FileInfo.Type.Dead =&gt;
	      (* Create the file if it doesn't already exist with the proper
		 attributes. *)
	      VAR
		cmd := &quot;T&quot;;
		sendAttr := serverFile.attr;
	      BEGIN
                Trace(self, myName, &quot; Create file with proper attributes &quot;,
                  serverFile.name);
		IF serverFile.type = FileInfo.Type.Dead THEN cmd := &quot;t&quot; END;
		(* If it is an RCS file, and we are using &quot;loose&quot;
		   equality for RCS files, then the sizes may
		   disagree because of harmless differences in
		   white space. *)
		IF SupMisc.IsRCS(serverFile.name)
		AND NOT SupFileRec.Option.NoRCS IN sfr.options
		AND NOT SupFileRec.Option.StrictCheckRCS IN sfr.options
		AND self.proto.v.hasLooseRCSCheck THEN
		  sendAttr := FileAttr.MaskOut(sendAttr,
		    FileAttr.AttrTypes{ FileAttr.AttrType.Size });
		END;
		IF NOT HardLink(self, linkTbl, serverFile) THEN
		  self.proto.putCmd(self.wr, cmd,
		    serverFile.name,
		    EncodeAttr(self, sfr, sendAttr));
		END;
	      END;
	  END;
	END;
      ELSIF serverFile = NIL THEN  (* Delete file on client. *)
	CASE clientFile.type OF
	| FileInfo.Type.DirDown =&gt;  (* Remove listfile entry for directory. *)
            Trace(self, myName, &quot; Remove listfile entry for directory &quot;,
              clientFile.name);
	    self.proto.putCmd(self.wr, &quot;i&quot;, clientFile.name);
	| FileInfo.Type.DirUp =&gt;  (* Remove directory. *)
            Trace(self, myName, &quot; Remove directory &quot;, clientFile.name);
	    self.proto.putCmd(self.wr, &quot;j&quot;, clientFile.name);
	| FileInfo.Type.Live, FileInfo.Type.Dead =&gt;  (* Delete file. *)
            Trace(self, myName, &quot; Delete file &quot;, clientFile.name);
	    self.proto.putCmd(self.wr, &quot;D&quot;, clientFile.name);
	END;
      ELSE  (* File exists on both the server and the client. *)
	IF NOT SupFileRec.Option.DoDeletesOnly IN sfr.options THEN
	  CASE serverFile.type OF
	  | FileInfo.Type.DirDown =&gt;
	      IF clientFile.type # FileInfo.Type.DirDown THEN
		&lt;* ASSERT clientFile.type # FileInfo.Type.DirUp *&gt;
		ReplaceFileWithDirectory(self,
		  clientFile := clientFile,
		  serverDir := serverFile);
	      END;
	      (* Otherwise, do nothing at this point.  The work will be done
		 when we reach the DirUp. *)
	  | FileInfo.Type.DirUp =&gt;
	      &lt;* ASSERT clientFile.type = FileInfo.Type.DirUp *&gt;
	      IF NOT FileAttr.Equal(clientFile.attr,
		FileAttr.Umask(serverFile.attr, sfr.umask))
	      THEN
		(* Set directory attributes. *)
               Trace(self, myName, &quot; Set directory attributes &quot;,
                 serverFile.name);
		self.proto.putCmd(self.wr, &quot;J&quot;,
		  serverFile.name,
		  EncodeAttr(self, sfr, serverFile.attr));
	      END;
	  | FileInfo.Type.Live, FileInfo.Type.Dead =&gt;
	      IF clientFile.type = FileInfo.Type.DirDown THEN
		ReplaceDirectoryWithFile(self, merger,
		  clientDir := clientFile,
		  serverFile := serverFile);
	      ELSE
		&lt;* ASSERT clientFile.type # FileInfo.Type.DirUp *&gt;
		IF NOT HardLink(self, linkTbl, serverFile) THEN
		  IF NOT FileAttr.Equal(clientFile.attr,
		    FileAttr.Umask(serverFile.attr, sfr.umask))
		  OR clientFile.type # serverFile.type
                  OR SupFileRec.Option.DetailAllRCSFiles IN sfr.options AND
                     SupMisc.IsRCS(serverFile.name) THEN
		    (* FIXME - check for BogusModTime? *)
                    IF self.clientClass.collectionIsPartiallyHidden(
		      sfr.collection)
		    THEN
                      Trace(self, myName,
                        &quot; Update partially hidden file (fix) &quot;,
                        serverFile.name);
                    ELSE
                      Trace(self, myName, &quot; Update file (fix) &quot;,
                        serverFile.name);
                    END;
                    self.proto.putCmd(self.wr, &quot;U&quot;, serverFile.name);
		  END;
		END;
	      END;
	  END;
	END;
      END;
      Wr.Flush(self.wr);
    END;
  END CompCVSMode;

PROCEDURE <A NAME="ReplaceDirectoryWithFile"><procedure>ReplaceDirectoryWithFile</procedure></A>(self: T;
				   merger: Merger;
				   clientDir: FileInfo.T;
				   serverFile: FileInfo.T)
  RAISES {Error, Rd.EndOfFile, Rd.Failure, Thread.Alerted,
	  TokScan.Error, Wr.Failure} =
  CONST myName = &quot;ReplaceDirectoryWithFile: &quot;;
  VAR
    cf, sf: FileInfo.T;
    depth: CARDINAL;
  BEGIN
    Trace(self, myName, &quot; &quot;, clientDir.name, &quot; &quot;, serverFile.name);
    (* Remove the entire directory tree on the client. *)
    (* FIXME - Make the client remove empty Attic directories automatically. *)
    cf := clientDir;
    sf := NIL;
    depth := 0;
    REPEAT
      (* We should get only client files from the merger until we have
	 consumed the entire directory tree. *)
      &lt;* ASSERT sf = NIL *&gt;
      CASE cf.type OF
      | FileInfo.Type.DirDown =&gt;  (* Remove listfile entry. *)
          Trace(self, myName, &quot; Remove listfile entry &quot;, cf.name);
	  self.proto.putCmd(self.wr, &quot;i&quot;, cf.name);
	  INC(depth);
      | FileInfo.Type.DirUp =&gt;  (* Remove directory. *)
          Trace(self, myName, &quot; Remove directory &quot;, cf.name);
	  self.proto.putCmd(self.wr, &quot;j&quot;, cf.name);
	  DEC(depth);
      | FileInfo.Type.Live, FileInfo.Type.Dead =&gt;  (* Delete file. *)
          Trace(self, myName, &quot; Delete file &quot;, cf.name);
	  self.proto.putCmd(self.wr, &quot;D&quot;, cf.name);
      END;
    UNTIL depth = 0 OR NOT GetNext(merger, cf, sf);

    IF depth # 0 THEN
      RAISE Error(&quot;Unmatched DirDown from client&quot;);
    END;

    (* Add the file. *)
    Trace(self, myName, &quot; Update file &quot;, serverFile.name);
    self.proto.putCmd(self.wr, &quot;U&quot;, serverFile.name);
  END ReplaceDirectoryWithFile;

PROCEDURE <A NAME="ReplaceFileWithDirectory"><procedure>ReplaceFileWithDirectory</procedure></A>(self: T;
				   clientFile: FileInfo.T;
				   serverDir: FileInfo.T)
  RAISES {Thread.Alerted, Wr.Failure} =
  CONST myName = &quot;ReplaceFileWithDirectory: &quot;;
  BEGIN
    Trace(self, myName, &quot; &quot;, clientFile.name, &quot; &quot;, serverDir.name);
    (* Delete the file, then create the directory. *)
    self.proto.putCmd(self.wr, &quot;D&quot;, clientFile.name);
    self.proto.putCmd(self.wr, &quot;I&quot;, serverDir.name);
  END ReplaceFileWithDirectory;

PROCEDURE <A NAME="CompCheckoutMode"><procedure>CompCheckoutMode</procedure></A>(self: T;
                           sfr: SupFileRec.T;
			   merger: Merger)
  RAISES {Rd.EndOfFile, Rd.Failure, Thread.Alerted, TokScan.Error,
          Wr.Failure} =
  CONST myName = &quot;CompCheckoutMode: &quot;;
  VAR
    clientFile, serverFile: FileInfo.T;
  BEGIN
    Trace(self, myName, sfr.collection);
    &lt;* ASSERT self.clientClass.inAllowedCollections(sfr.collection) *&gt;
    &lt;* ASSERT NOT
       self.clientClass.collectionIsPartiallyHidden(sfr.collection) *&gt;
    WHILE GetNext(merger, clientFile, serverFile) DO
      IF clientFile = NIL THEN  (* Add file on client. *)
	IF NOT FileInfo.IsDir(serverFile) THEN
          Trace(self, myName, &quot; Update file (add) &quot;, serverFile.name);
	  self.proto.putCmd(self.wr, &quot;U&quot;, serverFile.name);
	END;
      ELSIF serverFile = NIL THEN  (* Delete file on client. *)
	IF NOT FileInfo.IsDir(clientFile) THEN
          Trace(self, myName, &quot; Delete file &quot;, clientFile.name);
	  self.proto.putCmd(self.wr, &quot;D&quot;, clientFile.name);
	END;
      ELSE  (* File exists on both the server and the client. *)
	(* Note, the client records the exact attributes of the
           server's RCS file in checkout mode, unmodified by the
           client's umask.  So we need not do anything with the umask
           on this end. *)
	IF NOT FileInfo.IsDir(serverFile)
	AND NOT FileAttr.Equal(clientFile.attr, serverFile.attr)
	THEN
	  (* FIXME - check for BogusModTime? *)
          Trace(self, myName, &quot; Update file (fix)&quot;);
	  self.proto.putCmd(self.wr, &quot;U&quot;, serverFile.name);
	END;
      END;
      Wr.Flush(self.wr);
    END;
  END CompCheckoutMode;

PROCEDURE <A NAME="HardLink"><procedure>HardLink</procedure></A>(self: T;
		   linkTbl: LinkTbl.T;
                   fi: FileInfo.T): BOOLEAN
  RAISES {Thread.Alerted, Wr.Failure} =
</PRE><BLOCKQUOTE><EM> If there is a known hard link to the given file, emits a link command
   and returns <CODE>TRUE</CODE>.  Otherwise, returns <CODE>FALSE</CODE>. </EM></BLOCKQUOTE><PRE>
  VAR
    linkTo: Pathname.T;
    cmd: TEXT;
  BEGIN
    IF linkTbl # NIL AND NOT FileInfo.IsDir(fi)
    AND FileAttr.AttrType.LinkCount IN FileAttr.GetMask(fi.attr)
    AND FileAttr.GetLinkCount(fi.attr) &gt; 1 THEN
      WITH id = FileID.FromAttr(fi.attr) DO
	IF id # NIL THEN
	  IF linkTbl.get(id, linkTo) THEN
	    IF fi.type = FileInfo.Type.Live THEN cmd := &quot;H&quot; ELSE cmd := &quot;h&quot; END;
	    self.proto.putCmd(self.wr, cmd, fi.name, linkTo);
	    RETURN TRUE;
	  END;
	  EVAL linkTbl.put(id, fi.name);
	END;
      END;
    END;
    RETURN FALSE;
  END HardLink;

PROCEDURE <A NAME="DecodeAttr"><procedure>DecodeAttr</procedure></A>(&lt;*UNUSED*&gt; self: T; t: TEXT): FileAttr.T
  RAISES {TokScan.Error} =
  BEGIN
    LOOP
      TRY
	RETURN FileAttr.Decode(t);
      EXCEPT FileAttr.UnknownGroup, FileAttr.UnknownOwner =&gt;
	(* Ignore unknown attributes from the client. *)
      END;
    END;
  END DecodeAttr;

PROCEDURE <A NAME="EncodeAttr"><procedure>EncodeAttr</procedure></A>(self: T;
                      sfr: SupFileRec.T;
		      attr: FileAttr.T): TEXT =
  BEGIN
    IF self.proto.v.hasFileAttrs THEN
      RETURN FileAttr.Encode(attr,
	support := self.proto.v.attrSupport,
	ignore := sfr.attrIgnore);
    ELSE
      RETURN TokScan.EncodeTime(FileAttr.GetModTime(attr));
    END;
  END EncodeAttr;

PROCEDURE <A NAME="GetNext"><procedure>GetNext</procedure></A>(m: Merger;
                  VAR clientFile, serverFile: FileInfo.T): BOOLEAN
  RAISES {Rd.EndOfFile, Rd.Failure, Thread.Alerted, TokScan.Error} =
</PRE><BLOCKQUOTE><EM> This is a wrapper around <CODE>Merger.next()</CODE>, to correct its <CODE>RAISES</CODE>
   clause.  This procedure should raise the union of the exceptions
   raised by <CODE>GetFromClient</CODE> and <CODE>GetFromServer</CODE>. </EM></BLOCKQUOTE><PRE>
  &lt;* FATAL ANY *&gt;
  BEGIN
    RETURN m.next(clientFile, serverFile);
  END GetNext;

PROCEDURE <A NAME="Init"><procedure>Init</procedure></A>(self: T;
               proto: CVProto.T;
	       rd: StreamRd.T;
	       wr: StreamWr.T;
               collections: SupFileRecSeq.T;
               clientClass: ClientClass.T;
	       compLevel: [-1..9] := -1;
	       reaper: Reaper.T := NIL;
	       logger: Logger.T := NIL): T =
  BEGIN
    self.proto := proto;
    self.wireRd := rd;
    self.rd := rd;  (* Start out uncompressed. *)
    self.wireWr := wr;
    self.wr := wr;  (* Start out uncompressed. *)
    self.collections := collections;
    self.clientClass := clientClass;
    IF compLevel = -1 THEN compLevel := SupMisc.DefaultCompression END;
    self.compLevel := compLevel;
    self.reaper := reaper;
    self.logger := logger;
    RETURN self;
  END Init;
</PRE>***************************************************************************

<P><PRE>TYPE
  Merger = FileInfoMerger.T OBJECT
    treeComp: T;
    rd: Rd.T;
    sfr: SupFileRec.T;
    iter: CVTree.Iterator;
    serverPruning: BOOLEAN;
    clientDecomp: PathComp.Decompressor;
  METHODS
    init(self: T; sfr: SupFileRec.T): Merger
      RAISES {CVTree.Error, Thread.Alerted} := MergerInit;
    close()
      RAISES {CVTree.Error, Thread.Alerted} := MergerClose;
  OVERRIDES
    getA := GetFromClient;
    getB := GetFromServer;
  END;

PROCEDURE <A NAME="MergerInit"><procedure>MergerInit</procedure></A>(m: Merger;
                     self: T;
		     sfr: SupFileRec.T): Merger
  RAISES {CVTree.Error, Thread.Alerted} =
  BEGIN
    m.treeComp := self;
    m.rd := self.rd;
    m.sfr := sfr;
    m.iter := NIL;
    IF sfr.serverScanFile # NIL THEN  (* Use the scan file. *)
      TRY
	m.iter := NEW(FSIter).init(sfr.serverScanFile);
      EXCEPT CVTree.Error =&gt; (* Ignore. *) END;
    END;
    IF m.iter = NIL THEN  (* Do a full tree walk. *)
      m.iter := CVTree.Iterate(
	root := sfr.serverPrefix,
	follow := GlobTree.Not(sfr.symlink));
    END;
    m.serverPruning := FALSE;
    m.clientDecomp := NEW(PathComp.Decompressor).init();
    RETURN m;
  END MergerInit;

PROCEDURE <A NAME="MergerClose"><procedure>MergerClose</procedure></A>(m: Merger)
  RAISES {CVTree.Error, Thread.Alerted} =
  BEGIN
    m.iter.close();
  END MergerClose;

PROCEDURE <A NAME="GetFromClient"><procedure>GetFromClient</procedure></A>(m: Merger): FileInfo.T
  RAISES {Rd.EndOfFile, Rd.Failure, Thread.Alerted, TokScan.Error} =
</PRE><BLOCKQUOTE><EM> If you add any exceptions, add them to <CODE>GetNext</CODE> also. </EM></BLOCKQUOTE><PRE>
  VAR
    ts: TokScan.T;
    cmd: TEXT;
    name: TEXT;
    attr: FileAttr.T;
    cmdCh: CHAR;
    type: FileInfo.Type;
  BEGIN
    LOOP
      ts := m.treeComp.proto.getCmd(m.rd);
      cmdCh := ts.getChar(&quot;command&quot;);
      cmd := Text.FromChar(cmdCh);
      CASE cmdCh OF
      | '.' =&gt;
	  EXIT;
      | 'D' =&gt;  (* Down into directory. *)
	  name := ts.getToken(&quot;directory name&quot;);
	  ts.getEnd(&quot;end of \&quot;&quot; &amp; cmd &amp; &quot;\&quot; command&quot;);
	  WITH path = m.clientDecomp.put(PathComp.Type.DirDown, name) DO
	    IF m.treeComp.proto.v.dirsAreExplicit
	    AND NOT SupFileRec.Option.CheckoutMode IN m.sfr.options THEN
	      RETURN NEW(FileInfo.T,
		name := path,
		attr := NIL,
		type := FileInfo.Type.DirDown);
	    END;
	  END;
      | 'F', 'f' =&gt;  (* Live file, dead file. *)
	  IF cmdCh = 'F' THEN
	    type := FileInfo.Type.Live;
	  ELSE
	    type := FileInfo.Type.Dead;
	  END;
	  name := ts.getToken(&quot;file name&quot;);
	  IF m.treeComp.proto.v.hasFileAttrs THEN
	    attr := DecodeAttr(m.treeComp, ts.getToken(&quot;attributes&quot;));
	  ELSE
	    attr := NEW(FileAttr.T).init(FileAttr.FileType.File,
	      modTime := ts.getTime(&quot;modTime&quot;));
	  END;
	  ts.getEnd(&quot;end of \&quot;&quot; &amp; cmd &amp; &quot;\&quot; command&quot;);
	  WITH path = m.clientDecomp.put(PathComp.Type.File, name) DO
	    RETURN NEW(FileInfo.T,
	      name := path,
	      attr := attr,
	      type := type);
	  END;
      | 'U' =&gt;  (* Up out of directory. *)
	  IF m.treeComp.proto.v.dirsAreExplicit THEN
	    attr := DecodeAttr(m.treeComp, ts.getToken(&quot;attributes&quot;));
	  END;
	  ts.getEnd(&quot;end of \&quot;&quot; &amp; cmd &amp; &quot;\&quot; command&quot;);
	  WITH path = m.clientDecomp.put(PathComp.Type.DirUp, NIL) DO
	    IF m.treeComp.proto.v.dirsAreExplicit
	    AND NOT SupFileRec.Option.CheckoutMode IN m.sfr.options THEN
	      RETURN NEW(FileInfo.T,
		name := path,
		attr := attr,
		type := FileInfo.Type.DirUp);
	    END;
	  END;
      ELSE
	RAISE TokScan.Error(&quot;Invalid command \&quot;&quot; &amp; cmd &amp; &quot;\&quot;&quot;);
      END;
    END;
    RETURN NIL;
  END GetFromClient;

PROCEDURE <A NAME="GetFromServer"><procedure>GetFromServer</procedure></A>(m: Merger): FileInfo.T
  RAISES {Thread.Alerted} =
</PRE><BLOCKQUOTE><EM> If you add any exceptions, add them to <CODE>GetNext</CODE> also. </EM></BLOCKQUOTE><PRE>
  VAR
    type: CVTree.FileType;
    path: Pathname.T;
    attr: FileAttr.T;
    listIt: BOOLEAN;
  BEGIN
    LOOP
      TRY
	IF NOT m.iter.next(type, path, attr) THEN EXIT END;
	CASE type OF
	| CVTree.FileType.DirDown =&gt;
	    IF m.sfr.dirFilter.test(path) THEN
	      IF m.treeComp.proto.v.dirsAreExplicit
	      AND NOT SupFileRec.Option.CheckoutMode IN m.sfr.options THEN
		RETURN NEW(FileInfo.T,
		  name := path,
		  attr := attr,
		  type := FileInfo.Type.DirDown);
	      END;
	    ELSE
	      m.iter.prune();
	      m.serverPruning := TRUE;
	    END;
	| CVTree.FileType.File =&gt;
	    IF SupFileRec.Option.CheckoutMode IN m.sfr.options THEN
	      (* List RCS files only. *)
	      listIt := attr.fileType = FileAttr.FileType.File
		AND SupMisc.IsRCS(path);
	    ELSE
	      (* List everything that is supported by the protocol.  Ignore
		 the unsupported file types. *)
	      listIt := FileAttr.AttrType.FileType IN
		m.treeComp.proto.v.attrSupport[attr.fileType];
	    END;
	    IF listIt AND m.sfr.fileFilter.test(path) THEN
	      RETURN NEW(FileInfo.T,
		name := path,
		attr := attr,
		type := FileInfo.Type.Live);
	    END;
	| CVTree.FileType.AtticFile =&gt;
	    WITH atticPath = SupMisc.AtticName(path) DO
	      IF attr.fileType = FileAttr.FileType.File
	      AND SupMisc.IsRCS(path) THEN
		IF m.sfr.fileFilter.test(atticPath) THEN
		  RETURN NEW(FileInfo.T,
		    name := path,
		    attr := attr,
		    type := FileInfo.Type.Dead);
		END;
	      ELSE  (* Non-RCS file in the Attic?! *)
		WITH fullPath = SupMisc.CatPath(m.sfr.serverPrefix, atticPath)
		DO
		  Warning(m.treeComp,
		    &quot;Non-RCS file \&quot;&quot; &amp; fullPath &amp; &quot;\&quot; in Attic&quot;);
		END;
	      END;
	    END;
	| CVTree.FileType.DirUp =&gt;
	    IF NOT m.serverPruning THEN
	      IF m.treeComp.proto.v.dirsAreExplicit
	      AND NOT SupFileRec.Option.CheckoutMode IN m.sfr.options THEN
		RETURN NEW(FileInfo.T,
		  name := path,
		  attr := attr,
		  type := FileInfo.Type.DirUp);
	      END;
	    ELSE
	      m.serverPruning := FALSE;
	    END;
	END;
      EXCEPT CVTree.Error(msg) =&gt;
	Warning(m.treeComp, msg);
      END;
    END;
    RETURN NIL;
  END GetFromServer;
</PRE>***************************************************************************

<P><PRE>TYPE
  FSIter = CVTree.Iterator OBJECT
    path: Pathname.T;
    fsrd: FileStatus.Reader;
    pruning := FALSE;
  METHODS
    init(path: Pathname.T): FSIter
      RAISES {CVTree.Error, Thread.Alerted} := FSIterInit;
  OVERRIDES
    next := FSIterNext;
    prune := FSIterPrune;
    close := FSIterClose;
  END;

PROCEDURE <A NAME="FSIterClose"><procedure>FSIterClose</procedure></A>(self: FSIter)
  RAISES {CVTree.Error, Thread.Alerted} =
  BEGIN
    TRY
      self.fsrd.close();
    EXCEPT Rd.Failure(l) =&gt;
      RAISE CVTree.Error(&quot;Read failure on \&quot;&quot; &amp; self.path &amp; &quot;\&quot;: &quot;
	&amp; ErrMsg.StrError(l));
    END;
  END FSIterClose;

PROCEDURE <A NAME="FSIterInit"><procedure>FSIterInit</procedure></A>(self: FSIter; path: Pathname.T): FSIter
  RAISES {CVTree.Error, Thread.Alerted} =
  VAR
    rd: Rd.T;
  BEGIN
    TRY
      self.path := path;
      rd := FileRd.Open(self.path);
      self.fsrd := NIL;
      TRY
	TRY
	  self.fsrd := FileStatus.FromRd(rd);
	  IF self.fsrd.version() &lt; 5 THEN  (* Too old. *)
	    self.fsrd.close();
	    RAISE CVTree.Error(&quot;Scan file \&quot;&quot; &amp; self.path
	      &amp; &quot;\&quot; format is too old&quot;);
	  END;
	  RETURN self;
	EXCEPT
	| FileStatus.Error(msg) =&gt;
	    RAISE CVTree.Error(&quot;Error in \&quot;&quot; &amp; self.path &amp; &quot;\&quot;: &quot; &amp; msg);
	| Rd.Failure(l) =&gt;
	    RAISE CVTree.Error(&quot;Read failure on \&quot;&quot; &amp; self.path &amp; &quot;\&quot;: &quot;
	      &amp; ErrMsg.StrError(l));
	END;
      FINALLY
	IF self.fsrd = NIL THEN
	  TRY Rd.Close(rd) EXCEPT ELSE END;
	END;
      END;
    EXCEPT
    | OSError.E(l) =&gt;
	RAISE CVTree.Error(&quot;Cannot open \&quot;&quot; &amp; self.path &amp; &quot;\&quot;: &quot;
	  &amp; ErrMsg.StrError(l));
    END;
  END FSIterInit;

PROCEDURE <A NAME="FSIterNext"><procedure>FSIterNext</procedure></A>(self: FSIter;
                     VAR type: CVTree.FileType;
                     VAR name: Pathname.T;
		     VAR attr: FileAttr.T): BOOLEAN
  RAISES {CVTree.Error, Thread.Alerted} =
  VAR
    fs: FileStatus.T;
  BEGIN
    TRY
      IF self.pruning THEN
	self.pruning := FALSE;
	fs := self.fsrd.prune();
      ELSE
	fs := self.fsrd.get();
      END;

      name := fs.name;
      attr := fs.clientAttr;
      CASE fs.type OF
      | FileStatus.Type.DirDown =&gt;
	  type := CVTree.FileType.DirDown;
      | FileStatus.Type.DirUp =&gt;
	  type := CVTree.FileType.DirUp;
      | FileStatus.Type.FileLive =&gt;
	  type := CVTree.FileType.File;
      | FileStatus.Type.FileDead =&gt;
	  type := CVTree.FileType.AtticFile;
      | FileStatus.Type.CheckoutLive, FileStatus.Type.CheckoutDead =&gt;
	  RAISE CVTree.Error(&quot;Invalid checkout-mode scan file \&quot;&quot;
	    &amp; self.path &amp; &quot;\&quot;&quot;);
      END;
      RETURN TRUE;
    EXCEPT
    | FileStatus.Error(msg) =&gt;
	RAISE CVTree.Error(&quot;Error in \&quot;&quot; &amp; self.path &amp; &quot;\&quot;: &quot;
	  &amp; msg);
    | Rd.EndOfFile =&gt;
	RETURN FALSE;
    | Rd.Failure(l) =&gt;
	RAISE CVTree.Error(&quot;Read error on \&quot;&quot; &amp; self.path &amp; &quot;\&quot;: &quot;
	  &amp; ErrMsg.StrError(l));
    END;
  END FSIterNext;

PROCEDURE <A NAME="FSIterPrune"><procedure>FSIterPrune</procedure></A>(self: FSIter) =
  BEGIN
    self.pruning := TRUE;
  END FSIterPrune;
</PRE>***************************************************************************

<P><PRE>PROCEDURE <A NAME="Warning"><procedure>Warning</procedure></A>(self: T; msg: TEXT) =
</PRE><BLOCKQUOTE><EM> Logs a warning message. </EM></BLOCKQUOTE><PRE>
  BEGIN
    IF self.logger # NIL THEN
      Logger.Put(self.logger, Logger.Priority.Warning, msg);
    END;
  END Warning;

PROCEDURE <A NAME="Trace"><procedure>Trace</procedure></A>(self: T; m1, m2, m3, m4, m5, m6, m7, m8: TEXT := NIL;
                level := 1) =
</PRE><BLOCKQUOTE><EM> Logs a trace message. </EM></BLOCKQUOTE><PRE>
  BEGIN
    IF traceLevel &gt;= level THEN
      VAR
        msg := &quot;&quot;;
      BEGIN
        IF m1 # NIL THEN msg := msg &amp; m1 END;
        IF m2 # NIL THEN msg := msg &amp; m2 END;
        IF m3 # NIL THEN msg := msg &amp; m3 END;
        IF m4 # NIL THEN msg := msg &amp; m4 END;
        IF m5 # NIL THEN msg := msg &amp; m5 END;
        IF m6 # NIL THEN msg := msg &amp; m6 END;
        IF m7 # NIL THEN msg := msg &amp; m7 END;
        IF m8 # NIL THEN msg := msg &amp; m8 END;
        IF self.logger # NIL THEN
          Logger.Debug(self.logger, msg);
        ELSE
          IO.Put(&quot;logger = NIL: &quot; &amp; msg &amp; &quot;\n&quot;);
        END;
      END;
    END;
  END Trace;

BEGIN
END TreeComp.
</PRE>
</inModule>
<HR>
<A NAME="x1">interface GlobTree is in:
</A><UL>
<LI><A HREF="../../patternmatching/src/GlobTree.i3.html#0TOP0">patternmatching/src/GlobTree.i3</A>
<LI><A HREF="../../suplib/src/GlobTree.i3.html#0TOP0">suplib/src/GlobTree.i3</A>
</UL>
<P>
<HR>
<A NAME="x2">interface SupFileRecSeq is in:
</A><UL>
<LI><A HREF="../../client/derived/SupFileRecSeq.i3.html#0TOP0">client/derived/SupFileRecSeq.i3</A>
<LI><A HREF="../derived/SupFileRecSeq.i3.html#0TOP0">server/derived/SupFileRecSeq.i3</A>
</UL>
<P>
<PRE>























</PRE>
</BODY>
</HTML>
