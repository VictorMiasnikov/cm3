<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: sharedobjgen/src/StubCode.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>sharedobjgen/src/StubCode.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright (C) 1991, Digital Equipment Corporation           
 All rights reserved.                                        
 See the file COPYRIGHT for a full description.              

<P> Created by Susan Owicki                                     
 Last modified on Mon Feb 28 17:01:36 PST 1994 by wobber     
      modified on Mon May 17 14:59:49 PDT 1993 by mjordan    
      modified on Thu Apr 22 10:32:22 PDT 1993 by owicki     

<P><PRE>MODULE <module><implements><A HREF="#x1">StubCode</A></implements></module>;

IMPORT <A HREF="../../libm3/src/atom/Atom.i3.html">Atom</A>, <A HREF="#x2">CodeForType</A>, <A HREF="../../libm3/src/rw/FileWr.i3.html">FileWr</A>, <A HREF="../../libm3/src/formatter/Formatter.i3.html">Formatter</A>, <A HREF="SOxCodeFiles.i3.html">SOxCodeFiles</A>,
       <A HREF="../../libm3/src/os/Common/OSError.i3.html">OSError</A>, <A HREF="#x3">ImportList</A>, <A HREF="SOxCodeUtils.i3.html">SOxCodeUtils</A>,
       <A HREF="SOxCodeGenError.i3.html">SOxCodeGenError</A>, <A HREF="#x4">Type</A>, <A HREF="../../libm3/src/rw/Wr.i3.html">Wr</A>, <A HREF="UpdateMethodsTbl.i3.html">UpdateMethodsTbl</A>,
       <A HREF="../../libm3/derived/AtomList.i3.html">AtomList</A>, <A HREF="AtomAtomListTbl.i3.html">AtomAtomListTbl</A>;

&lt;* FATAL OSError.E, Wr.Failure *&gt;

PROCEDURE <A NAME="BrandsOK"><procedure>BrandsOK</procedure></A>(t: Type.T): BOOLEAN =
  VAR OKSoFar := TRUE;
  BEGIN
    IF t = NIL THEN RETURN TRUE END;
    IF t.visited THEN RETURN t.brandsOK END;
    t.visited := TRUE;
    TYPECASE t OF
    | Type.Reference (ref) =&gt;
      IF ref.branded AND ref.brand = NIL THEN
        SOxCodeUtils.Message(&quot;Error: Branded type with no explicit brand -- &quot;
          &amp; CodeForType.ToText(ref));
        OKSoFar := FALSE;
      END;
      TYPECASE ref OF
      | Type.Ref (r) =&gt; t.brandsOK := BrandsOK(r.target) AND OKSoFar
      | Type.Object (obj) =&gt;
          OKSoFar := BrandsOK(obj.super) AND OKSoFar;
          FOR i := 0 TO LAST(obj.fields^) DO
            OKSoFar := BrandsOK(obj.fields[i].type) AND OKSoFar;
          END;
          FOR i := 0 TO LAST(obj.methods^) DO
            OKSoFar := SigBrandsOK(obj.methods[i].sig) AND OKSoFar;
          END;
          t.brandsOK := OKSoFar
      | Type.Opaque (opq) =&gt;
          t.brandsOK := BrandsOK(opq.revealedSuperType) AND OKSoFar
      ELSE t.brandsOK := OKSoFar
      END;
    | Type.Array (arr) =&gt; t.brandsOK := BrandsOK(arr.element) AND OKSoFar
    | Type.Packed (p) =&gt; t.brandsOK := BrandsOK(p.base)AND OKSoFar
    | Type.Record (rec) =&gt;
        FOR i := 0 TO LAST(rec.fields^) DO
          OKSoFar := BrandsOK(rec.fields[i].type) AND OKSoFar;
        END;
        t.brandsOK := OKSoFar;
    | Type.Procedure (proc) =&gt;
        t.brandsOK := SigBrandsOK(proc.sig);
    ELSE t.brandsOK := TRUE
    END;
    RETURN t.brandsOK;
  END BrandsOK;

PROCEDURE <A NAME="SigBrandsOK"><procedure>SigBrandsOK</procedure></A>(sig: Type.Signature): BOOLEAN =
  VAR OKSoFar := TRUE;
  BEGIN
    FOR i := 0 TO LAST(sig.formals^) DO
      OKSoFar := BrandsOK(sig.formals[i].type) AND OKSoFar
    END;
    RETURN BrandsOK(sig.result) AND OKSoFar;
  END SigBrandsOK;

PROCEDURE <A NAME="GenCode"><procedure>GenCode</procedure></A>(basename: TEXT;
                  t_array: REF ARRAY OF Type.Object;
                  qid_array: REF ARRAY OF Type.Qid;
                  meth_array: REF ARRAY OF ImportList.MethodList;
                  umethodsTbl: UpdateMethodsTbl.T) =
    VAR
      wr: Formatter.T;
      t : Type.Qid;
      o : Type.Object;
      m : ImportList.MethodList;
      fileName, basefname : TEXT;
      umethods : AtomList.T;
      umTbl : AtomAtomListTbl.T;
      imports: ImportList.T;
    BEGIN
      TRY
        FOR ftyp := FIRST(SOxCodeFiles.T) TO LAST(SOxCodeFiles.T) DO
          IF SOxCodeUtils.DoFile(ftyp) THEN
            basefname := SOxCodeUtils.FileName(basename, ftyp);
            fileName := basefname &amp; SOxCodeUtils.FileExtension(ftyp);
            wr := Formatter.New(FileWr.Open(fileName));

            imports := ImportList.New();
            SOxCodeFiles.coderTable[ftyp].InitImports(basename, imports);
            FOR i := 0 TO LAST(qid_array^) DO
              t := qid_array[i];
              o := t_array[i];
              m := meth_array[i];
              IF t # NIL THEN
                EVAL umethodsTbl.get(t.intf, umTbl);
                EVAL umTbl.get(t.item, umethods);
                SOxCodeFiles.coderTable[ftyp].Import(o, m, umethods, imports);
                (* CodeForType.ImportRevelations(t, imports); *)
              END;
            END;

            SOxCodeFiles.coderTable[ftyp].Head(wr, fileName,
                                               basename, imports);
            FOR i := 0 TO LAST(qid_array^) DO
              t := qid_array[i];
              IF t # NIL THEN
                EVAL umethodsTbl.get(t.intf, umTbl);
                EVAL umTbl.get(t.item, umethods);
                SOxCodeFiles.coderTable[ftyp].Decls(wr, t,
                                                    t_array[i].name,
                                                    basename,
                                                    meth_array[i],
                                                    umethods);
              END;
            END;

            FOR i := 0 TO LAST(qid_array^) DO
              IF t#NIL THEN
                t := qid_array[i];
                o := t_array[i];
                EVAL umethodsTbl.get(t.intf, umTbl);
                EVAL umTbl.get(t.item, umethods);
                SOxCodeFiles.coderTable[ftyp].Main(wr, t, o,
                                                   t_array[i].name,
                                                   basename,
                                                   meth_array[i],
                                                   umethods);
              END;
            END;

            SOxCodeFiles.coderTable[ftyp].Bottom(wr, basename);
            Formatter.Close(wr);
          END;
        END;

        (* CodeForType.AddModuleImports(imports, methods, lastNewMethod); *)

      EXCEPT
        SOxCodeGenError.E =&gt;
          Formatter.Close(wr);
      END;
    END GenCode;

PROCEDURE <A NAME="PragmasForStub"><procedure>PragmasForStub</procedure></A>(): REF ARRAY OF TEXT =
  BEGIN
    RETURN stubFormalPrags;
  END PragmasForStub;

PROCEDURE <A NAME="SigForStub"><procedure>SigForStub</procedure></A>(m: Type.Signature): Type.Signature =
  VAR sig:= Type.Signature{formals := stubFormals, result := NIL,
                           raises := NIL};
      useException: REF ARRAY OF BOOLEAN;
      c := 0;
      found: BOOLEAN;
  BEGIN
    IF NUMBER(m.formals^) &lt;= 0 THEN
      sig.raises := m.raises;
    ELSIF m.raises = NIL THEN
      sig.raises := stubExceptions;
    ELSE
      useException := NEW(REF ARRAY OF BOOLEAN, NUMBER(m.raises^));
      FOR i := 0 TO LAST(m.raises^) DO
        useException[i] := FALSE;
        found := FALSE;
        FOR j := 0 TO LAST(stubExceptions^) DO
          IF m.raises[i].qid.intf = stubExceptions[j].qid.intf AND
             m.raises[i].qid.item = stubExceptions[j].qid.item THEN
             found := TRUE
          END;
        END;
        IF NOT found THEN
          useException[i] := TRUE;
          INC(c);
        END;
      END;
      sig.raises := NEW(REF ARRAY OF Type.Exception,
                         NUMBER(stubExceptions^) + c);
      FOR i := 0 TO LAST(stubExceptions^) DO
        sig.raises[i] := stubExceptions[i]
      END;
      c := NUMBER(stubExceptions^);
      FOR i := 0 TO LAST(m.raises^) DO
        IF useException[i] THEN
          sig.raises[c] := m.raises[i];
          INC(c)
        END;
      END;
    END;
    RETURN sig;
  END SigForStub;

VAR stubFormals: REF ARRAY OF Type.Formal;
    stubFormalPrags: REF ARRAY OF TEXT;
    stubExceptions: REF ARRAY OF Type.Exception;

BEGIN
  stubExceptions := NEW (REF ARRAY OF Type.Exception, 3);
  FOR i := 0 TO LAST(stubExceptions^) DO
    stubExceptions[i] := NEW(Type.Exception);
  END;
  stubExceptions[0].qid := NEW(Type.Qid, intf := Atom.FromText(&quot;SharedObj&quot;),
                               item := Atom.FromText(&quot;Error&quot;));
  stubExceptions[1].qid := NEW(Type.Qid, intf := Atom.FromText(&quot;Rd&quot;),
                               item := Atom.FromText(&quot;Failure&quot;));
  stubExceptions[2].qid := NEW(Type.Qid, intf := Atom.FromText(&quot;Thread&quot;),
                               item := Atom.FromText(&quot;Alerted&quot;));
  stubFormals := NEW(REF ARRAY OF Type.Formal, 1);
  stubFormalPrags := NEW(REF ARRAY OF TEXT, 1);
  stubFormals[0] := NEW(Type.Formal, mode := Type.Mode.Value,
                        name := Atom.FromText(&quot;in&quot;),
                        default := NIL,
                        type := NEW(Type.Object,
                            name := NEW(Type.Qid,
                                        intf:= Atom.FromText(&quot;EventStubLib&quot;),
                                        item := Atom.FromText(&quot;Handle&quot;))));
  stubFormalPrags[0] := &quot;&lt;* NOWARN *&gt; &quot;;
  (*
  stubFormals[1] := NEW(Type.Formal, mode := Type.Mode.Value,
                        name := Atom.FromText(&quot;rep&quot;),
                        default := NIL,
                        type := NEW(Type.Object,
                            name := NEW(Type.Qid,
                                        intf:= Atom.FromText(&quot;StubLib&quot;),
                                        item := Atom.FromText(&quot;DataRep&quot;))));
  stubFormalPrags[0] := &quot;&quot;;
  *)
END StubCode.
</PRE>
</inModule>
<HR>
<A NAME="x1">interface StubCode is in:
</A><UL>
<LI><A HREF="StubCode.i3.html#0TOP0">sharedobjgen/src/StubCode.i3</A>
<LI><A HREF="../../stubgen/src/StubCode.i3.html#0TOP0">stubgen/src/StubCode.i3</A>
</UL>
<P>
<HR>
<A NAME="x2">interface CodeForType is in:
</A><UL>
<LI><A HREF="CodeForType.i3.html#0TOP0">sharedobjgen/src/CodeForType.i3</A>
<LI><A HREF="../../stubgen/src/CodeForType.i3.html#0TOP0">stubgen/src/CodeForType.i3</A>
</UL>
<P>
<HR>
<A NAME="x3">interface ImportList is in:
</A><UL>
<LI><A HREF="ImportList.i3.html#0TOP0">sharedobjgen/src/ImportList.i3</A>
<LI><A HREF="../../stablegen/src/ImportList.i3.html#0TOP0">stablegen/src/ImportList.i3</A>
</UL>
<P>
<HR>
<A NAME="x4">interface Type is in:
</A><UL>
<LI><A HREF="../../cm3ide/src/nodes/Type.i3.html#0TOP0">cm3ide/src/nodes/Type.i3</A>
<LI><A HREF="../../m3front/src/types/Type.i3.html#0TOP0">m3front/src/types/Type.i3</A>
<LI><A HREF="Type.i3.html#0TOP0">sharedobjgen/src/Type.i3</A>
<LI><A HREF="../../stablegen/src/Type.i3.html#0TOP0">stablegen/src/Type.i3</A>
<LI><A HREF="../../stubgen/src/Type.i3.html#0TOP0">stubgen/src/Type.i3</A>
</UL>
<P>
<PRE>























</PRE>
</BODY>
</HTML>
