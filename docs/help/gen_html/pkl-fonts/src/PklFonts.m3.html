<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: pkl-fonts/src/PklFonts.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>pkl-fonts/src/PklFonts.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright (C) 1992, Digital Equipment Corporation                         
 All rights reserved.                                                      
 See the file COPYRIGHT for a full description.                            
                                                                           
 Last modified on Wed Dec 21 09:56:22 PST 1994 by heydon                   

<P><PRE>MODULE <module>PklFonts</module> EXPORTS <A HREF="../../m3core/src/main/Main.i3.html"><implements>Main</A></implements>;

IMPORT <A HREF="#x1">JunoValue</A>, <A HREF="#x2">PSFont</A>, <A HREF="../derived/PklFontsBundle.i3.html">PklFontsBundle</A>;
IMPORT <A HREF="../../libm3/src/fmtlex/Fmt.i3.html">Fmt</A>, <A HREF="../../libm3/src/rw/Rd.i3.html">Rd</A>, <A HREF="../../libm3/src/rw/Wr.i3.html">Wr</A>, <A HREF="../../libm3/src/rw/TextRd.i3.html">TextRd</A>, <A HREF="../../libm3/src/fmtlex/Lex.i3.html">Lex</A>, <A HREF="../../vbtkit/src/vbtkitutils/Rsrc.i3.html">Rsrc</A>, <A HREF="../../libm3/src/pickle/ver1/Pickle.i3.html">Pickle</A>, <A HREF="../../libm3/src/rw/Stdio.i3.html">Stdio</A>, <A HREF="../../m3core/src/float/IEEE-default/FloatMode.i3.html">FloatMode</A>;
IMPORT   <A HREF="../../libm3/derived/TextRefTbl.i3.html">TextRefTbl</A>, <A HREF="../derived/TextIntSeqTbl.i3.html">TextIntSeqTbl</A>, <A HREF="../../libm3/derived/IntSeq.i3.html">IntSeq</A>;
FROM <A HREF="../../m3core/src/thread/Common/Thread.i3.html">Thread</A> IMPORT Alerted;

&lt;* FATAL Rd.Failure, Wr.Failure, Alerted, Pickle.Error *&gt;

CONST
  NumFonts = 13;
  NumPtSizes = 6;

TYPE Encoding = TextIntSeqTbl.T;
</PRE><BLOCKQUOTE><EM> An encoding is a binary relation on names and integers. If <CODE>e: Encoding</CODE>,
   and <CODE>e</CODE> maps the name <CODE>n</CODE> to the sequence of integers <CODE>i1, i2, ..., iN</CODE>,
   then the relation contains the pairs <CODE>(n, i1)</CODE>, <CODE>(n, i2)</CODE>, ..., <CODE>(n, iN)</CODE>.
   Usually, there is only one integer in each sequence, but for some fonts,
   the same name may be related to multiple codes. </EM></BLOCKQUOTE><PRE>

VAR (* READONLY *)
  path: Rsrc.Path;
  isoEncoding, symEncoding, defaultEncoding: Encoding;

EXCEPTION BadMetricFile;

PROCEDURE <A NAME="ReadMetrics"><procedure>ReadMetrics</procedure></A>(
  nm: TEXT;
  encoding: Encoding;
  VAR (*OUT*) capHeight: JunoValue.Real)
  : PSFont.Metric RAISES {Rd.Failure, BadMetricFile} =
</PRE><BLOCKQUOTE><EM> Returns a new font metric structure corresponding to the PostScript font
   named <CODE>nm</CODE>, and sets <CODE>capHeight</CODE> to the height of the font's capital
   letters (measured in points) for a 1-point font. The metric data is read
   from the locally-bundled AFM file named <CODE>nm</CODE> with the extension <CODE>.afm</CODE>
   using the character encoding described by the relation <CODE>encoding</CODE>. Raises
   <CODE>Rd.Failure</CODE> if the file could not be opened, or <CODE>BadMetricFile</CODE> if the AFM
   file could not be parsed succesfully.
<P>
   The only entries in the font metric file processed by this procedure are
   those named <CODE>FontBBox</CODE>, <CODE>CapHeight</CODE>, and <CODE>StartCharMetrics</CODE>. These tokens
   are assumed to start on their own lines. The <CODE>FontBBox</CODE> entry has the form:
<P>
<PRE>
         FontBBox &lt;west&gt; &lt;south&gt; &lt;east&gt; &lt;north&gt;
</PRE>
   where each of the 4 argument is an integer in thousandths of points.
   Hence, the actual bounding box is computed from these integers for a
   1-point font by dividing them by 1,000. The <CODE>CapHeight</CODE> entry has the form:
<P>
<PRE>
         CapHeight &lt;height&gt;
</PRE>
   where <CODE>height</CODE> is the height of a capital letter in thousandths of points.
   The <CODE>StartCharMetrics</CODE> entry has the form:
<P>
<PRE>
         StartCharMetrics &lt;num&gt;
         &lt;character-metric-1&gt;
         ...
         &lt;character-metric-num&gt;
</PRE>
   where <CODE>num</CODE> is the number of character metric lines immediately following
   the <CODE>StartCharMetrics</CODE> line, and the format of the character metric lines
   is described in the <CODE>ReadCharMetrics</CODE> procedure below. </EM></BLOCKQUOTE><PRE>
  &lt;* FATAL Rd.EndOfFile, Rsrc.NotFound *&gt;
  CONST
    AFMExt = &quot;.afm&quot;;
  VAR
    rd: Rd.T := Rsrc.Open(nm &amp; AFMExt, path);
    res := NEW(PSFont.Metric);
    tRd := NEW(TextRd.T);
    readBBox, readCapHeight, readCharMetrics := FALSE;
  BEGIN
    WHILE NOT Rd.EOF(rd) DO
      EVAL tRd.init(Rd.GetLine(rd));
      IF MatchToken(tRd, &quot;FontBBox&quot;) THEN
        WITH bb = res.bbox DO
          bb.west := ScanScaled(tRd); bb.south := ScanScaled(tRd);
          bb.east := ScanScaled(tRd); bb.north := ScanScaled(tRd)
        END;
        readBBox := TRUE
      ELSIF MatchToken(tRd, &quot;CapHeight&quot;) THEN
        capHeight := ScanScaled(tRd);
        readCapHeight := TRUE
      ELSIF MatchToken(tRd, &quot;StartCharMetrics&quot;) THEN
        ReadCharMetrics(rd, encoding, ScanInt(tRd),
          res.mapped, res.width, res.charBB);
        readCharMetrics := TRUE
      END
    END;
    IF NOT readCapHeight THEN
      WITH bigT = ORD('T') DO
        IF res.mapped[bigT] AND res.charBB[bigT] # NIL THEN
          capHeight := res.charBB[bigT].north;
          readCapHeight := TRUE
        END
      END
    END;
    IF NOT (readBBox AND readCapHeight AND readCharMetrics) THEN
      RAISE BadMetricFile
    END;
    RETURN res
  END ReadMetrics;

PROCEDURE <A NAME="ReadCharMetrics"><procedure>ReadCharMetrics</procedure></A>(
    rd: Rd.T;
    encoding: Encoding;
    num: INTEGER;
    VAR (*OUT*) mapped: PSFont.CharMapped;
    VAR (*OUT*) width: PSFont.CharWidth;
    VAR (*OUT*) charBB: PSFont.CharBBox)
    RAISES {Rd.Failure, BadMetricFile} =
</PRE><BLOCKQUOTE><EM> Reads <CODE>num</CODE> character metric lines from <CODE>rd</CODE>, setting <CODE>mapped[i]</CODE> to TRUE
   for each code <CODE>i</CODE> related to a named character by the character encoding
   <CODE>encoding</CODE>, and setting <CODE>width[i]</CODE> and <CODE>charBB[i]</CODE> to the character width
   and bounding box for each such character. After reading <CODE>num</CODE> such lines,
   the next line must be <CODE>EndCharMetrics</CODE>, or else <CODE>BadMetricFile</CODE> is raised.
<P>
   Each character metric line must have the form:
<P>
<PRE>
         &lt;metrics&gt;  ::= &lt;metric&gt;
                      | &lt;metric&gt; &lt;metrics&gt;
         &lt;metric&gt;   ::= &quot;N&quot; &lt;charName&gt; &quot;;&quot;
                      | &quot;WX&quot; &lt;width&gt; &quot;;&quot;
                      | &quot;B&quot; &lt;bbox&gt; &quot;;&quot;
                      | &lt;any-number-of-non-semicolon-tokens&gt; &quot;;&quot;
         &lt;bbox&gt;     ::= &lt;west&gt; &lt;south&gt; &lt;east&gt; &lt;north&gt;
</PRE>
   This line asserts that the character with name <CODE>charName</CODE> has width <CODE>width</CODE>
   and bounding-box <CODE>bbox</CODE>. The character name is a string, and the character
   width and bounding-box values are integers. If the character name is <CODE>nm</CODE>,
   then <CODE>nm</CODE> must be in the domain of the relation denoted by <CODE>encoding</CODE>, or
   <CODE>BadMetricFile</CODE> is raised. For each pair <CODE>(nm, code)</CODE> in <CODE>encoding</CODE>,
   <CODE>mapped[code]</CODE> is set to TRUE, and <CODE>width[code]</CODE> and <CODE>charBB[code]</CODE> are set
   to the width and bounding box of the character. The character width and
   bounding-box values are expressed in thouandths of points, so they must be
   divided by 1,000 to represent the metrics of the character rendered at
   1-point. </EM></BLOCKQUOTE><PRE>
  VAR
    tRd := NEW(TextRd.T);
    name: TEXT;				 (* character name *)
    w: JunoValue.Real;			 (* scaled width *)
    sawW, sawBBox: BOOLEAN;		 (* width, bbox specified? *)
    bbox: REF PSFont.BBox;		 (* scaled character bounding box *)
  BEGIN
    TRY
      WHILE num &gt; 0 DO
        (* initialize line *)
        EVAL tRd.init(Rd.GetLine(rd));
        name := NIL; sawW := FALSE; sawBBox := FALSE;

        (* read character info *)
        LOOP
          Lex.Skip(tRd);
          IF Rd.EOF(tRd) THEN EXIT END;
          IF MatchToken(tRd, &quot;C&quot;) THEN
            EVAL Lex.Int(tRd);		 (* discard character code *)
          ELSIF MatchToken(tRd, &quot;N&quot;) THEN
            Lex.Skip(tRd);
            name := Lex.Scan(tRd);
          ELSIF MatchToken(tRd, &quot;WX&quot;) THEN
            sawW := TRUE;
            w := ScanScaled(tRd)
          ELSIF MatchToken(tRd, &quot;B&quot;) THEN
            sawBBox := TRUE;
            bbox := NEW(REF PSFont.BBox);
            bbox.west  := ScanScaled(tRd);
            bbox.south := ScanScaled(tRd);
            bbox.east  := ScanScaled(tRd);
            bbox.north := ScanScaled(tRd);
            IF bbox.east &lt;= bbox.west AND
               bbox.north &lt;= bbox.south THEN
              bbox := NIL
            END
          ELSE
            VAR c: CHAR; BEGIN
              TRY REPEAT c := Rd.GetChar(tRd) UNTIL c = ';' EXCEPT
                Rd.EndOfFile =&gt; RAISE BadMetricFile
              END;
              Rd.UnGetChar(tRd)
            END
          END;
          Lex.Skip(tRd); Lex.Match(tRd, &quot;;&quot;);
        END;

        (* save width and bbox info for this character *)
        VAR seq: IntSeq.T; BEGIN
          IF encoding.get(name, seq) THEN
            IF sawW AND sawBBox THEN
              VAR code: INTEGER; BEGIN
          	FOR i := 0 TO seq.size() - 1 DO
          	  code := seq.get(i);
          	  mapped[code] := TRUE;
          	  width[code] := w;
          	  charBB[code] := bbox
          	END
              END
            ELSE
              RAISE BadMetricFile
            END
          ELSE
            Wr.PutText(Stdio.stderr, &quot;    Unknown character: &quot;);
            Wr.PutText(Stdio.stderr, name);
            Wr.PutChar(Stdio.stderr, '\n');
            Wr.Flush(Stdio.stderr)
          END
        END;
        DEC(num)
      END; (* WHILE *)

      (* check that &quot;EndCharMetrics&quot; is next line *)
      EVAL tRd.init(Rd.GetLine(rd));
      IF NOT MatchToken(tRd, &quot;EndCharMetrics&quot;) THEN RAISE BadMetricFile END
    EXCEPT
      Lex.Error, FloatMode.Trap, Rd.EndOfFile =&gt; RAISE BadMetricFile
    END;
  END ReadCharMetrics;

PROCEDURE <A NAME="MatchToken"><procedure>MatchToken</procedure></A>(rd: Rd.T; token: TEXT): BOOLEAN =
</PRE><BLOCKQUOTE><EM> If the prefix of <CODE>rd</CODE> contains <CODE>token</CODE> concatenated with a non-trivial
   amount of whitespace, or if it contains <CODE>token</CODE> followed immediately by
   end-of-file, then skip past the token and any whitespace and return <CODE>TRUE</CODE>.
   Otherwise, return <CODE>FALSE</CODE> and leave <CODE>rd</CODE> unchanged. This routine requires
   that <CODE>rd</CODE> is seekable. </EM></BLOCKQUOTE><PRE>
  VAR start := Rd.Index(rd); BEGIN
    TRY
      Lex.Match(rd, token);
      VAR end := Rd.Index(rd); BEGIN
        Lex.Skip(rd, Lex.Blanks);
        IF Rd.Index(rd) = end AND NOT Rd.EOF(rd) THEN RAISE Lex.Error END
      END
    EXCEPT Lex.Error =&gt;
      Rd.Seek(rd, start);
      RETURN FALSE
    END;
    RETURN TRUE
  END MatchToken;

PROCEDURE <A NAME="ScanScaled"><procedure>ScanScaled</procedure></A>(rd: Rd.T): REAL RAISES {BadMetricFile} =
</PRE><BLOCKQUOTE><EM> Read an integer from <CODE>rd</CODE>, first skipping whitespace on <CODE>rd</CODE>, and return
   the value of the integer divided by 1,000. </EM></BLOCKQUOTE><PRE>
  VAR val := ScanInt(rd); BEGIN
    RETURN FLOAT(val, REAL) / 1000.0
  END ScanScaled;

PROCEDURE <A NAME="ScanInt"><procedure>ScanInt</procedure></A>(rd: Rd.T): INTEGER RAISES {BadMetricFile} =
</PRE><BLOCKQUOTE><EM> Read and return an integer from <CODE>rd</CODE>, first skipping whitespace on <CODE>rd</CODE>.
   Raises <CODE>BadMetricFile</CODE> if an integer could not be read. </EM></BLOCKQUOTE><PRE>
  VAR res: INTEGER; BEGIN
    TRY res := Lex.Int(rd) EXCEPT
      Lex.Error, FloatMode.Trap =&gt; RAISE BadMetricFile
    END;
    RETURN res
  END ScanInt;

VAR
  fontTbl := NEW(TextRefTbl.Default).init(sizeHint := NumPtSizes * NumFonts);
  metricTbl := NEW(TextRefTbl.Default).init(sizeHint := NumFonts);
  fontData := NEW(PSFont.Data, fontTbl := fontTbl, metricTbl := metricTbl);

TYPE
  Size = [0..5];
  CapHeights = ARRAY Size OF CARDINAL;

CONST
  YScale = 72.0 / 104.2; (* points / pixel *)
  (* This is the vertical scale factor for converting from screen-dependent
     units (pixels) to screen-independent units (points) on a display with a
     vertical resolution of 102.4 pixels / inch (as reported by the xdpyinfo(1)
     command). This value must correspond to the display on which the X font
     sizes that appear in the &quot;CapHeights&quot; arrays below were measured. *)

CONST
  TimesHeights     = CapHeights{7, 10, 11, 13, 17, 23};
  HelveticaHeights = CapHeights{8, 11, 12, 14, 19, 25};
  CourierHeights1  = CapHeights{6,  9, 10, 11, 14, 19};
  CourierHeights2  = CapHeights{6,  9, 10, 11, 15, 20};
  SymbolHeights    = TimesHeights;
</PRE> These values are the heights of a capital letter in pixels at each of the 6
   different X font sizes (8, 10, 12, 14, 18, 24). Their sizes in the
   screen-independent units of points are therefore the pixel size times
   the constant <CODE>YScale</CODE>. 

<P><PRE>PROCEDURE <A NAME="AddFont"><procedure>AddFont</procedure></A>(ps, x: TEXT; READONLY ch: CapHeights;
  encoding: Encoding := NIL) =
</PRE><BLOCKQUOTE><EM> Binds the name <CODE>ps</CODE> to the metric data for the font with PostScript name
   <CODE>ps</CODE> (e.g., <CODE>Times-Roman</CODE>) in the global table <CODE>metricTbl</CODE> using the
   character encoding <CODE>encoding</CODE> (or <CODE>defaultEncoding</CODE> if <CODE>encoding</CODE> is NIL);
   for each of the font's sizes <CODE>i</CODE> (in the range <CODE>[0..5]</CODE>), binds the name
   <CODE>ps &amp; i</CODE> (e.g., <CODE>Times-Roman2</CODE>) to the name of the corresponding X font in
   the global table <CODE>fontTbl</CODE>. The name of the corresponding X font is formed
   from <CODE>x</CODE> and the font's point size: <CODE>x</CODE> must be the <CODE>-</CODE>-separated string
   consisting of the X font family, weight, and slant for the PostScript font
   <CODE>ps</CODE> (e.g., <CODE>times-bold-i</CODE>). </EM></BLOCKQUOTE><PRE>
  CONST
    Sizes = ARRAY OF INTEGER{8, 10, 12, 14, 18, 24};
    Prefix = &quot;-*-&quot;; Middle = &quot;-normal-*-*-&quot;; Suffix = &quot;0-*-*-*-*-*-*&quot;;
  VAR psCapHeight: JunoValue.Real; BEGIN
    Wr.PutText(Stdio.stderr, &quot;  &quot; &amp; ps &amp; &quot;\n&quot;); Wr.Flush(Stdio.stderr);
    IF encoding = NIL THEN encoding := defaultEncoding END;
    &lt;* FATAL Rd.Failure, BadMetricFile *&gt; BEGIN
      EVAL metricTbl.put(ps, ReadMetrics(ps, encoding, psCapHeight))
    END;
    FOR i := FIRST(Sizes) TO LAST(Sizes) DO
      WITH sz = Fmt.Int(i), ptSz = Fmt.Int(Sizes[i]) DO
        VAR xInfo := NEW(PSFont.XInfo); BEGIN
          (* The size for the corresponding PostScript font is computed by
             first converting the height of the screen font's capital letters
             from pixels to points, and then dividing by the height of capital
             letters in the corresponding 1-point PostScript font. *)
          xInfo.name := Prefix &amp; x &amp; Middle &amp; ptSz &amp; Suffix;
          xInfo.ptSize := (FLOAT(ch[i], JunoValue.Real)*YScale) / psCapHeight;
          EVAL fontTbl.put(ps &amp; sz, xInfo)
        END
      END
    END
  END AddFont;

PROCEDURE <A NAME="ReadEncoding"><procedure>ReadEncoding</procedure></A>(filename: TEXT): Encoding =
</PRE><BLOCKQUOTE><EM> Create and return a new character encoding as described in the file
   <CODE>filename</CODE>. In this file, any blank line or line beginning with a <CODE>#</CODE> is
   ignored. All other lines should be of the form:
<P>
<PRE>
         &lt;name&gt; &lt;whitespace&gt; &lt;octal-code&gt;
</PRE>
   If the &lt;octal-code&gt; is a non-negative number, this has the effect of
   relating &lt;name&gt; to the integer with octal value &lt;octal-code&gt; in the
   resulting encoding. </EM></BLOCKQUOTE><PRE>
  &lt;* FATAL Rd.EndOfFile, Rsrc.NotFound, FloatMode.Trap, Lex.Error *&gt;
  VAR
    res := NEW(TextIntSeqTbl.Default).init(sizeHint := 210);
    rd: Rd.T := Rsrc.Open(filename, path);
    tRd := NEW(TextRd.T);
    name: TEXT;
    value: INTEGER;
    seq: IntSeq.T;
  BEGIN
    WHILE NOT Rd.EOF(rd) DO
      EVAL tRd.init(Rd.GetLine(rd));
      IF NOT Rd.EOF(tRd) AND Rd.GetChar(tRd) # '#' THEN
        Rd.UnGetChar(tRd);
        name := Lex.Scan(tRd);
        Lex.Skip(tRd);
        value := Lex.Int(tRd, defaultBase := 8);
        IF NOT res.get(name, seq) THEN
          seq := NEW(IntSeq.T).init(sizeHint := 1);
          EVAL res.put(name, seq)
        END;
        IF value &gt;= 0 THEN seq.addhi(value) END
      END
    END;
    RETURN res
  END ReadEncoding;

BEGIN
  (* initialize resource path *)
  path := Rsrc.BuildPath(&quot;$PklFontsPATH&quot;, PklFontsBundle.Get());

  (* read encodings *)
  isoEncoding := ReadEncoding(&quot;ISOLatin1Encoding&quot;);
  symEncoding := ReadEncoding(&quot;SymbolEncoding&quot;);
  defaultEncoding := isoEncoding;

  (* fill in &quot;fontTbl&quot; and &quot;metricTbl&quot; *)
  Wr.PutText(Stdio.stderr, &quot;Building font metrics...\n&quot;);
  AddFont(&quot;Times-Roman&quot;,           &quot;times-medium-r&quot;,     TimesHeights);
  AddFont(&quot;Times-Bold&quot;,            &quot;times-bold-r&quot;,       TimesHeights);
  AddFont(&quot;Times-Italic&quot;,          &quot;times-medium-i&quot;,     TimesHeights);
  AddFont(&quot;Times-BoldItalic&quot;,      &quot;times-bold-i&quot;,       TimesHeights);
  AddFont(&quot;Helvetica&quot;,             &quot;helvetica-medium-r&quot;, HelveticaHeights);
  AddFont(&quot;Helvetica-Bold&quot;,        &quot;helvetica-bold-r&quot;,   HelveticaHeights);
  AddFont(&quot;Helvetica-Oblique&quot;,     &quot;helvetica-medium-o&quot;, HelveticaHeights);
  AddFont(&quot;Helvetica-BoldOblique&quot;, &quot;helvetica-bold-o&quot;,   HelveticaHeights);
  AddFont(&quot;Courier&quot;,               &quot;courier-medium-r&quot;,   CourierHeights1);
  AddFont(&quot;Courier-Bold&quot;,          &quot;courier-bold-r&quot;,     CourierHeights2);
  AddFont(&quot;Courier-Oblique&quot;,       &quot;courier-medium-o&quot;,   CourierHeights1);
  AddFont(&quot;Courier-BoldOblique&quot;,   &quot;courier-bold-o&quot;,     CourierHeights2);
  AddFont(&quot;Symbol&quot;,                &quot;symbol-medium-r&quot;,    SymbolHeights,
    encoding := symEncoding);

  (* write out the pickle *)
  Wr.PutText(Stdio.stderr, &quot;Pickling font metrics...\n&quot;);
  Wr.Flush(Stdio.stderr);
  Pickle.Write(Stdio.stdout, fontData);
  Wr.Close(Stdio.stdout);
  Wr.PutText(Stdio.stderr, &quot;Done.\n&quot;);
END PklFonts.
</PRE>
</inModule>
<HR>
<A NAME="x1">interface JunoValue is in:
</A><UL>
<LI><A HREF="../../juno-machine/src/JunoValue.i3.html#0TOP0">juno-machine/src/JunoValue.i3</A>
<LI><A HREF="JunoValue.i3.html#0TOP0">pkl-fonts/src/JunoValue.i3</A>
</UL>
<P>
<HR>
<A NAME="x2">interface PSFont is in:
</A><UL>
<LI><A HREF="../../juno-app/src/PSFont.i3.html#0TOP0">juno-app/src/PSFont.i3</A>
<LI><A HREF="PSFont.i3.html#0TOP0">pkl-fonts/src/PSFont.i3</A>
</UL>
<P>
<PRE>























</PRE>
</BODY>
</HTML>
