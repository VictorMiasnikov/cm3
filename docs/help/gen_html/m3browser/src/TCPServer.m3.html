<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: m3browser/src/TCPServer.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>m3browser/src/TCPServer.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright (C) 1995, Digital Equipment Corporation             
 All rights reserved.                                          
 See the file COPYRIGHT for a full description.                
                                                               
 Last modified on Tue Mar  7 14:41:39 PST 1995 by kalsow       
                                                               
 Enhanced by Peter Klein (pk@i3.informatik.rwth-aachen.de) to  
 reject connections from outside this domain.  - Mar 7, 1995   

<P><PRE>MODULE <module><implements><A HREF="#x1">TCPServer</A></implements></module>;

IMPORT <A HREF="../../m3core/src/text/Text.i3.html">Text</A>, <A HREF="../../m3core/src/thread/Common/Thread.i3.html">Thread</A>, <A HREF="../../m3core/src/time/Common/Time.i3.html">Time</A>, <A HREF="../../tcp/src/common/TCP.i3.html">TCP</A>, <A HREF="../../tcp/src/common/IP.i3.html">IP</A>, <A HREF="../../tcp/src/common/ConnFD.i3.html">ConnFD</A>, <A HREF="../../libm3/src/rw/Rd.i3.html">Rd</A>, <A HREF="../../libm3/src/rw/Wr.i3.html">Wr</A>, <A HREF="../../libm3/src/atom/Atom.i3.html">Atom</A>, <A HREF="../../libm3/derived/AtomList.i3.html">AtomList</A>;
IMPORT <A HREF="../../tcp/src/common/TCPPeer.i3.html">TCPPeer</A>;
IMPORT <A HREF="../../http/src/HTTP.i3.html">HTTP</A>, <A HREF="../../http/src/App.i3.html">App</A>;

REVEAL
  <A NAME="T">T</A> = Thread.Closure BRANDED OBJECT
    workers   : REF ARRAY OF Thread.T := NIL;
    port      : TCP.Connector         := NIL;
    handler   : RequestHandler        := NIL;
    refresher : Refresher             := NIL;
    timeout   : Time.T                := 0.0d0;
    err_log   : ErrorLogger           := NIL;
    address   : IP.Address;
    maskBits  : [0 .. 32];
  OVERRIDES
    apply := Server;
  END;

TYPE
  TT = Thread.Closure OBJECT
    self: T := NIL;
  OVERRIDES
    apply := Refresh;
  END;
</PRE>---------------------------------------------------- external interface ---

<P><PRE>PROCEDURE <A NAME="Fork"><procedure>Fork</procedure></A> (socket    : CARDINAL;
                n_threads : CARDINAL;
                handler   : RequestHandler;
                refresher : Refresher;
                refresh_interval: Time.T;
                err_log   : ErrorLogger;
                address   : IP.Address := IP.NullAddress;
                maskBits  : [0 .. 32] := 0): T =

  VAR t := NEW (T);
  BEGIN
    IF (err_log = NIL) THEN err_log := DumpErr; END;
    t.workers   := NEW (REF ARRAY OF Thread.T, n_threads+1);
    t.handler   := handler;
    t.refresher := refresher;
    t.timeout   := refresh_interval;
    t.err_log   := err_log;
    t.address   := address;
    t.maskBits  := maskBits;

    (* open a TCP connection *)
    TRY
      t.port := TCP.NewConnector (IP.Endpoint {IP.GetHostAddr (), socket});
    EXCEPT IP.Error(ec) =&gt;
      err_log (&quot;cannot open TCP connection&quot; &amp; OSErr (ec));
      RETURN NIL;
    END;

    (* fire up the refresh thread *)
    IF (refresher # NIL) AND (refresh_interval &gt; 0.0d0)
      THEN t.workers[0] := Thread.Fork (NEW (TT, self := t));
      ELSE t.workers[0] := NIL;
    END;

    (* fire up the server threads *)
    FOR i := 1 TO n_threads DO  t.workers[i] := Thread.Fork (t);  END;

    RETURN t;
  END Fork;

PROCEDURE <A NAME="Join"><procedure>Join</procedure></A> (t: T) =
  VAR z: Thread.T;
  BEGIN
    IF (t = NIL) THEN RETURN END;
    FOR i := 0 TO LAST (t.workers^) DO
      z := t.workers [i];
      IF (z # NIL) THEN
        EVAL Thread.Join (z);
        t.workers[i] := NIL;
      END;
    END;
    IF (t.port # NIL) THEN
      (** TCP.CloseConnector (t.port); *** NOT YET IMPLEMENTED 2/8/95 ***)
      t.port := NIL;
    END;
  END Join;

PROCEDURE <A NAME="Abort"><procedure>Abort</procedure></A> (t: T) =
  BEGIN
    Alert (t);
    Join (t);
  END Abort;
</PRE>------------------------------------------------- request server thread ---

<P><PRE>PROCEDURE <A NAME="Server"><procedure>Server</procedure></A> (closure: Thread.Closure): REFANY =
  CONST Second = 1000.0d0;
  VAR
    self    : T := closure;
    channel : TCP.T;
    len, j, n : INTEGER;
    request : TEXT;
    buf     : ARRAY [0..2047] OF CHAR;
  BEGIN
    TRY
      LOOP
        TRY
          request := &quot;&quot;; (* give the collector a chance while we wait... *)
          channel := TCP.Accept (self.port);
          TRY

            IF DomainOK (channel,self.address,self.maskBits) THEN
              (* read a new-line terminated request *)
              REPEAT
                len := channel.get (buf, 30.0d0 * Second);
                j := 0;  WHILE (j &lt; len) AND (buf[j] # '\n') DO INC (j) END;
                request := request &amp; Text.FromChars (SUBARRAY (buf, 0, j));
              UNTIL (j &lt; len OR len = 0);

              (* recover escaped content *)
              TRY
                request := HTTP.UnescapeURLEntry(request, App.defaultLog);
              EXCEPT
                App.Error(e) =&gt;
                request :=
                    &quot;HTTP/1.0 400 Bad request: &quot; &amp; e &amp; &quot;\r\n&quot;;
                self.err_log(&quot;bad request from &quot; &amp; TCPPeer.GetName(channel));
              END;
              (* process it *)
              request := self.handler (self, request);
            ELSE
              request :=
                &quot;HTTP/1.0 403 Service not available from outside, sorry\r\n&quot;;
              self.err_log(&quot;illegal request from &quot; &amp; TCPPeer.GetName(channel));
            END;

            (* send the reply *)
            len := Text.Length (request);
            j := 0;
            WHILE (j &lt; len) DO
              n := MIN (NUMBER (buf), len-j);
              FOR k := 0 TO n-1 DO  buf[k] := Text.GetChar (request, k+j); END;
              channel.put (SUBARRAY (buf, 0, n));
              INC (j, NUMBER (buf));
            END;

          FINALLY
            TCP.Close (channel);
          END;
        EXCEPT
        | ConnFD.TimedOut =&gt;
            self.err_log (&quot;TCPServer: ConnFD.TimedOut =&gt; client is non-responsive&quot;);
        | IP.Error(ec) =&gt;
            IF FatalError (self, ec, &quot;IP.Error&quot;) THEN RETURN NIL; END;
        | Rd.Failure(ec) =&gt;
            IF FatalError (self, ec, &quot;Rd.Failure&quot;) THEN RETURN NIL; END;
        | Wr.Failure(ec) =&gt;
            IF FatalError (self, ec, &quot;Wr.Failure&quot;) THEN RETURN NIL; END;
        END;
      END;
    EXCEPT Thread.Alerted =&gt;
      (* bail out... *)
      self.err_log (&quot;TCPServer: server thread was alerted&quot;);
      Alert (self);
    END;
    RETURN NIL;
  END Server;

PROCEDURE <A NAME="DomainOK"><procedure>DomainOK</procedure></A> (channel: TCP.T; address: IP.Address; maskBits: [0 .. 32]):
    BOOLEAN RAISES {IP.Error} =
  BEGIN
    RETURN TCPPeer.Match (channel, address, maskBits);
  END DomainOK;

PROCEDURE <A NAME="FatalError"><procedure>FatalError</procedure></A> (self: T;  ec: AtomList.T;  msg: TEXT): BOOLEAN =
  BEGIN
    self.err_log (&quot;TCPServer: &quot; &amp; msg &amp; OSErr (ec));
    IF (ec # NIL) THEN
      IF (ec.head = TCP.Refused)  THEN RETURN FALSE; END;
      IF (ec.head = TCP.Closed)   THEN RETURN FALSE; END;
      IF (ec.head = TCP.Timeout)  THEN RETURN FALSE; END;
      IF (ec.head = TCP.ConnLost) THEN RETURN FALSE; END;
    END;

    (* Don't know what's happening =&gt; bail out ... *)
    self.err_log (&quot;TCPServer: aborting...&quot;);
    Alert (self);
    RETURN TRUE;
  END FatalError;
</PRE>----------------------------------------------- periodic refresh thread ---

<P><PRE>PROCEDURE <A NAME="Refresh"><procedure>Refresh</procedure></A> (closure: Thread.Closure): REFANY =
  VAR tt: TT := closure;  self := tt.self;
  BEGIN
    TRY
      LOOP
        Thread.AlertPause (self.timeout);
        self.refresher (self);
      END;
    EXCEPT Thread.Alerted =&gt;
      (* bail out... *)
      self.err_log (&quot;TCPServer: refresh thread was alerted&quot;);
      Alert (self);
    END;
    RETURN NIL;
  END Refresh;
</PRE>------------------------------------------------------------------ misc ---

<P><PRE>PROCEDURE <A NAME="Alert"><procedure>Alert</procedure></A> (t: T) =
  VAR z: Thread.T;
  BEGIN
    IF (t = NIL) THEN RETURN END;
    FOR i := 0 TO LAST (t.workers^) DO
      z := t.workers[i];
      IF (z # NIL) THEN Thread.Alert (z); END;
    END;
  END Alert;

PROCEDURE <A NAME="OSErr"><procedure>OSErr</procedure></A> (args: AtomList.T): TEXT =
  VAR msg : TEXT := NIL;
  BEGIN
    WHILE (args # NIL) DO
      IF (msg = NIL) THEN  msg := &quot;: &quot;;  ELSE  msg := msg &amp; &quot;  ***  &quot;;  END;
      msg  := msg &amp; Atom.ToText (args.head);
      args := args.tail;
    END;
    IF (msg = NIL) THEN msg := &quot;: ** NO INFO **&quot;; END;
    RETURN msg;
  END OSErr;

PROCEDURE <A NAME="DumpErr"><procedure>DumpErr</procedure></A> (&lt;*UNUSED*&gt; x: TEXT) =
  BEGIN
  END DumpErr;

BEGIN
END TCPServer.
</PRE>
</inModule>
<HR>
<A NAME="x1">interface TCPServer is in:
</A><UL>
<LI><A HREF="../../cm3ide/src/server/TCPServer.i3.html#0TOP0">cm3ide/src/server/TCPServer.i3</A>
<LI><A HREF="TCPServer.i3.html#0TOP0">m3browser/src/TCPServer.i3</A>
</UL>
<P>
<PRE>























</PRE>
</BODY>
</HTML>
