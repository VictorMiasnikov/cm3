<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: suplib/src/GlobTree.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>suplib/src/GlobTree.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright 1996-2003 John D. Polstra.
 * All rights reserved.
 *
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions
 * are met:
 * 1. Redistributions of source code must retain the above copyright
 *    notice, this list of conditions and the following disclaimer.
 * 2. Redistributions in binary form must reproduce the above copyright
 *    notice, this list of conditions and the following disclaimer in the
 *    documentation and/or other materials provided with the distribution.
 * 3. All advertising materials mentioning features or use of this software
 *    must display the following acknowledgment:
 *      This product includes software developed by John D. Polstra.
 * 4. The name of the author may not be used to endorse or promote products
 *    derived from this software without specific prior written permission.
 *
 * THIS SOFTWARE IS PROVIDED BY THE AUTHOR ``AS IS'' AND ANY EXPRESS OR
 * IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES
 * OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED.
 * IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR ANY DIRECT, INDIRECT,
 * INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT
 * NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
 * DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
 * THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
 * (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF
 * THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 *
 * $Id: GlobTree.m3.html,v 1.3 2010-04-29 17:20:02 wagner Exp $ 

<P><PRE>MODULE <module><implements><A HREF="#x1">GlobTree</A></implements></module>;

IMPORT <A HREF="#x2">Glob</A>, <A HREF="#x3">RegEx</A>;

TYPE
  (* The base node type.  For simplicity, all nodes have left and right
     child pointers even if they are not used. *)
  Node = T OBJECT
    left, right: T := NIL;
  END;

  (* Internal nodes (operators). *)
  TreeNode = Node OBJECT OVERRIDES
    test := TreeTest;
  END;
  (* The following node types are branded because they are structurally
     identical but we need to be able to distinguish between them with
     TYPECASE. *)
  NotNode = TreeNode BRANDED OBJECT END;
  AndNode = TreeNode BRANDED OBJECT END;
  OrNode = TreeNode BRANDED OBJECT END;

  (* Leaf nodes. *)
  MatchNode = Node OBJECT
    pattern: TEXT;
    options: Glob.MatchOptions;
  OVERRIDES
    test := MatchTest;
  END;

  RegExNode = Node OBJECT
    pattern: RegEx.Pattern;
  OVERRIDES
    test := RegExTest;
  END;

  TrueNode = Node OBJECT OVERRIDES
    test := TrueTest;
  END;

  FalseNode = Node OBJECT OVERRIDES
    test := FalseTest;
  END;
</PRE> Constructors. 

<P><PRE>PROCEDURE <A NAME="Match"><procedure>Match</procedure></A>(pattern: TEXT; options := Glob.MatchOptions{}): T =
  BEGIN
    RETURN NEW(MatchNode, pattern := pattern, options := options);
  END Match;

PROCEDURE <A NAME="RegExMatch"><procedure>RegExMatch</procedure></A>(pattern: TEXT): T RAISES {RegEx.Error} =
  BEGIN
    RETURN NEW(RegExNode, pattern := RegEx.Compile(pattern));
  END RegExMatch;

PROCEDURE <A NAME="And"><procedure>And</procedure></A>(left, right: T): T =
  BEGIN
    IF left = False OR right = False THEN
      RETURN False;
    ELSIF left = True THEN
      RETURN right;
    ELSIF right = True THEN
      RETURN left;
    ELSE
      RETURN NEW(AndNode, left := left, right := right);
    END;
  END And;

PROCEDURE <A NAME="Or"><procedure>Or</procedure></A>(left, right: T): T =
  BEGIN
    IF left = True OR right = True THEN
      RETURN True;
    ELSIF left = False THEN
      RETURN right;
    ELSIF right = False THEN
      RETURN left;
    ELSE
      RETURN NEW(OrNode, left := left, right := right);
    END;
  END Or;

PROCEDURE <A NAME="Not"><procedure>Not</procedure></A>(child: T): T =
  BEGIN
    IF child = True THEN
      RETURN False;
    ELSIF child = False THEN
      RETURN True;
    ELSE
      RETURN NEW(NotNode, left := child);
    END;
  END Not;
</PRE> Leaf evaluators. 

<P><PRE>PROCEDURE <A NAME="MatchTest"><procedure>MatchTest</procedure></A>(self: MatchNode; filename: TEXT): BOOLEAN =
  BEGIN
    RETURN Glob.Match(self.pattern, filename, self.options);
  END MatchTest;

PROCEDURE <A NAME="RegExTest"><procedure>RegExTest</procedure></A>(self: RegExNode; filename: TEXT): BOOLEAN =
  BEGIN
    RETURN RegEx.Execute(self.pattern, filename) &gt;= 0;
  END RegExTest;

PROCEDURE <A NAME="TrueTest"><procedure>TrueTest</procedure></A>(&lt;*UNUSED*&gt; self: T; &lt;*UNUSED*&gt; filename: TEXT): BOOLEAN =
  BEGIN
    RETURN TRUE;
  END TrueTest;

PROCEDURE <A NAME="FalseTest"><procedure>FalseTest</procedure></A>(&lt;*UNUSED*&gt; self: T; &lt;*UNUSED*&gt; filename: TEXT): BOOLEAN =
  BEGIN
    RETURN FALSE;
  END FalseTest;
</PRE> Evaluation of more complex trees. 

<P><PRE>TYPE
  State = { DoingLeft, DoingRight };

  StackElem = REF RECORD
    next: StackElem;
    node: Node;
    state: State;
  END;

PROCEDURE <A NAME="TreeTest"><procedure>TreeTest</procedure></A>(self: TreeNode; filename: TEXT): BOOLEAN =
  VAR
    stack: StackElem := NIL;
    cur: Node;
    state: State;
    val: BOOLEAN;
  PROCEDURE Push(node: Node; state: State) =
    BEGIN
      stack := NEW(StackElem, node := node, state := state, next := stack);
    END Push;
  PROCEDURE Pop(VAR node: Node; VAR state: State) =
    BEGIN
      node := stack.node;
      state := stack.state;
      stack := stack.next;
    END Pop;
  BEGIN
    cur := self;
    LOOP
      (* Descend to the left until we hit bottom. *)
      WHILE cur.left # NIL DO
	Push(cur, State.DoingLeft);
	cur := cur.left;
      END;
      (* Now cur is a leaf node.  Evaluate it. *)
      val := cur.test(filename);
      (* Ascend, propagating the value through operator nodes. *)
      LOOP
	IF stack = NIL THEN
	  RETURN val;
	END;
	Pop(cur, state);
	TYPECASE cur OF
	| NotNode =&gt;
	    val := NOT val;
	| AndNode =&gt;
	    (* If we haven't yet evaluated the right subtree and the partial
	       result is TRUE, descend to the right.  Otherwise the result
	       is already determined to be val. *)
	    IF state = State.DoingLeft AND val THEN
	      Push(cur, State.DoingRight);
	      cur := cur.right;
	      EXIT;
	    END;
	| OrNode =&gt;
	    (* If we haven't yet evaluated the right subtree and the partial
	       result is FALSE, descend to the right.  Otherwise the result
	       is already determined to be val. *)
	    IF state = State.DoingLeft AND NOT val THEN
	      Push(cur, State.DoingRight);
	      cur := cur.right;
	      EXIT;
	    END;
	ELSE
	  (* We only push nodes that have children -- i.e., operator nodes. *)
	  &lt;*ASSERT FALSE *&gt;
	END;
      END;
    END;
  END TreeTest;

BEGIN
  True := NEW(TrueNode);
  False := NEW(FalseNode);
END GlobTree.
</PRE>
</inModule>
<HR>
<A NAME="x1">interface GlobTree is in:
</A><UL>
<LI><A HREF="../../patternmatching/src/GlobTree.i3.html#0TOP0">patternmatching/src/GlobTree.i3</A>
<LI><A HREF="GlobTree.i3.html#0TOP0">suplib/src/GlobTree.i3</A>
</UL>
<P>
<HR>
<A NAME="x2">interface Glob is in:
</A><UL>
<LI><A HREF="../../patternmatching/src/Glob.i3.html#0TOP0">patternmatching/src/Glob.i3</A>
<LI><A HREF="Glob.i3.html#0TOP0">suplib/src/Glob.i3</A>
</UL>
<P>
<HR>
<A NAME="x3">interface RegEx is in:
</A><UL>
<LI><A HREF="../../patternmatching/src/RegEx.i3.html#0TOP0">patternmatching/src/RegEx.i3</A>
<LI><A HREF="RegEx.i3.html#0TOP0">suplib/src/RegEx.i3</A>
</UL>
<P>
<PRE>























</PRE>
</BODY>
</HTML>
