<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: suplib/src/FileStatus.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>suplib/src/FileStatus.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright 1996-2003 John D. Polstra.
 * All rights reserved.
 *
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions
 * are met:
 * 1. Redistributions of source code must retain the above copyright
 *    notice, this list of conditions and the following disclaimer.
 * 2. Redistributions in binary form must reproduce the above copyright
 *    notice, this list of conditions and the following disclaimer in the
 *    documentation and/or other materials provided with the distribution.
 * 3. All advertising materials mentioning features or use of this software
 *    must display the following acknowledgment:
 *      This product includes software developed by John D. Polstra.
 * 4. The name of the author may not be used to endorse or promote products
 *    derived from this software without specific prior written permission.
 *
 * THIS SOFTWARE IS PROVIDED BY THE AUTHOR ``AS IS'' AND ANY EXPRESS OR
 * IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES
 * OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED.
 * IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR ANY DIRECT, INDIRECT,
 * INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT
 * NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
 * DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
 * THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
 * (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF
 * THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 *
 * $Id: FileStatus.m3.html,v 1.3 2010-04-29 17:20:02 wagner Exp $ 

<P><PRE>MODULE <module>FileStatus</module> EXPORTS <A HREF="FileStatus.i3.html"><implements>FileStatus</A></implements>, <A HREF="FileStatusRaw.i3.html"><implements>FileStatusRaw</A></implements>;

IMPORT
  <A HREF="FileAttr.i3.html">FileAttr</A>, <A HREF="../../libm3/src/fmtlex/Fmt.i3.html">Fmt</A>, <A HREF="PathComp.i3.html">PathComp</A>, <A HREF="../../libm3/src/rw/Rd.i3.html">Rd</A>, <A HREF="SupMisc.i3.html">SupMisc</A>, <A HREF="../../m3core/src/text/Text.i3.html">Text</A>, <A HREF="../../m3core/src/thread/Common/Thread.i3.html">Thread</A>, <A HREF="../../m3core/src/time/Common/Time.i3.html">Time</A>, <A HREF="TokScan.i3.html">TokScan</A>, <A HREF="../../libm3/src/rw/Wr.i3.html">Wr</A>;

REVEAL
  <A NAME="T">T</A> = Public BRANDED OBJECT
    line: TEXT := NIL;
    ts: TokScan.T := NIL;
    lineNum := 0;
  END;

  <A NAME="Reader">Reader</A> = RawRdPublic BRANDED OBJECT
  END;

  <A NAME="Writer">Writer</A> = RawWrPublic BRANDED OBJECT
  END;

CONST
  Version = 5;  (* File format version number. *)

VAR (*CONST*)
  HugeText := Text.FromChars( ARRAY [0..15] OF CHAR{ LAST(CHAR), .. } );
</PRE><BLOCKQUOTE><EM> Assumed to compare greater than any pathname component we might encounter. </EM></BLOCKQUOTE><PRE>

PROCEDURE <A NAME="Compare"><procedure>Compare</procedure></A>(a, b: T): [-1..1] =
  VAR
    aName := a.name;
    bName := b.name;
  BEGIN
    IF a.type = Type.DirUp THEN
      aName := SupMisc.CatPath(aName, HugeText);
    END;
    IF b.type = Type.DirUp THEN
      bName := SupMisc.CatPath(bName, HugeText);
    END;
    RETURN SupMisc.PathCompare(aName, bName);
  END Compare;

PROCEDURE <A NAME="Equal"><procedure>Equal</procedure></A>(a, b: T): BOOLEAN =
  BEGIN
    RETURN Text.Equal(a.name, b.name)
      AND (a.type = Type.DirUp) = (b.type = Type.DirUp);
  END Equal;

PROCEDURE <A NAME="MakeCooked"><procedure>MakeCooked</procedure></A>(fs: T; version: CARDINAL)
  RAISES {Error} =
  BEGIN
    IF fs.line = NIL THEN  (* Already cooked. *)
      RETURN;
    END;
    TRY
      CASE fs.type OF
      | Type.DirDown =&gt;
	  (* Nothing to do. *)
      | Type.DirUp =&gt;
	  IF version &gt;= 5 THEN
	    fs.clientAttr := GetAttr(fs.ts, version, &quot;directory attributes&quot;);
	  ELSE
	    fs.clientAttr := FileAttr.Bogus;
	  END;
	  fs.serverAttr := fs.clientAttr;
      | Type.CheckoutLive =&gt;
	  fs.tag := fs.ts.getToken(&quot;tag&quot;);
	  fs.date := fs.ts.getToken(&quot;date&quot;);
	  fs.serverAttr := GetAttr(fs.ts, version, &quot;server file attributes&quot;);
	  fs.revNum := fs.ts.getToken(&quot;revision number&quot;);
	  IF version &gt;= 4 THEN
	    fs.revDate := fs.ts.getToken(&quot;revDate&quot;);
	  ELSE
	    fs.revDate := &quot;.&quot;;
	  END;
	  fs.clientAttr :=
	    GetAttr(fs.ts, version, &quot;client file attributes&quot;);
      | Type.CheckoutDead =&gt;
	  fs.tag := fs.ts.getToken(&quot;tag&quot;);
	  fs.date := fs.ts.getToken(&quot;date&quot;);
	  fs.serverAttr := GetAttr(fs.ts, version, &quot;server file attributes&quot;);
      | Type.FileLive =&gt;
	  fs.clientAttr := GetAttr(fs.ts, version, &quot;file attributes&quot;);
	  fs.serverAttr := fs.clientAttr;
      | Type.FileDead =&gt;
	  fs.clientAttr := GetAttr(fs.ts, version, &quot;file attributes&quot;);
	  fs.serverAttr := fs.clientAttr;
      END;
      fs.line := NIL;
      fs.ts := NIL;
    EXCEPT
    | Error(msg) =&gt;
	RAISE Error(Fmt.Int(fs.lineNum) &amp; &quot;: &quot; &amp; msg);
    | TokScan.Error(msg) =&gt;
	RAISE Error(Fmt.Int(fs.lineNum) &amp; &quot;: &quot; &amp; msg);
    END;
  END MakeCooked;

PROCEDURE <A NAME="GetAttr"><procedure>GetAttr</procedure></A>(ts: TokScan.T; version: CARDINAL; what: TEXT): FileAttr.T
  RAISES {Error, TokScan.Error} =
  BEGIN
    IF version &lt; 3 THEN
      RETURN NEW(FileAttr.T).init(FileAttr.FileType.File,
	modTime := ts.getTime(what));
    ELSE
      TRY
	RETURN FileAttr.Decode(ts.getToken(what));
      EXCEPT
      | FileAttr.UnknownGroup(name) =&gt;
	  RAISE Error(&quot;Unknown group name \&quot;&quot; &amp; name &amp; &quot;\&quot;&quot;);
      | FileAttr.UnknownOwner(name) =&gt;
	  RAISE Error(&quot;Unknown user name \&quot;&quot; &amp; name &amp; &quot;\&quot;&quot;);
      END;
    END;
  END GetAttr;
</PRE>***************************************************************************

<P><PRE>TYPE
  RdReader = Reader OBJECT
    rd: Rd.T;
    vers: CARDINAL;
    time: Time.T;
    statusCode: CHAR;
    originHost: TEXT;
    delta: Time.T;
    lineNum := 0;
    comp: PathComp.Compressor := NIL;
    decomp: PathComp.Decompressor := NIL;
    previous: T := NIL;
    current: T := NIL;
    eofSeen := FALSE;
    depth: CARDINAL := 0;
  OVERRIDES
    version := RdVersion;
    get := RdGet;
    getRaw := RdGetRaw;
    prune := RdPrune;
    scanTime := RdScanTime;
    status := RdStatus;
    origin := RdOrigin;
    timeDelta := RdTimeDelta;
    close := RdClose;
  END;

PROCEDURE <A NAME="FromRd"><procedure>FromRd</procedure></A>(rd: Rd.T): Reader
  RAISES {Error, Rd.Failure, Thread.Alerted} =
  VAR
    rr := NEW(RdReader, rd := rd);
    line: TEXT;
    ts: TokScan.T;
  BEGIN
    TRY
      TRY
	line := SupMisc.GetCmdLine(rd);
	INC(rr.lineNum);
	ts := TokScan.NewDec(line);
	ts.getLiteral(&quot;F&quot;);
	rr.vers := ts.getInt(&quot;file format version number&quot;);
	IF rr.vers &lt; 1 OR rr.vers &gt; Version THEN
	  RAISE Error(&quot;Invalid format version &quot; &amp; Fmt.Int(rr.vers));
	END;
	rr.time := ts.getTime(&quot;scan time&quot;);
	TRY
	  rr.statusCode := ts.getChar(&quot;status&quot;);
	  rr.originHost := ts.getToken(&quot;origin&quot;);
	  rr.delta := ts.getTime(&quot;timeDelta&quot;);
	EXCEPT TokScan.Error =&gt;
	  rr.statusCode := '?';
	  rr.originHost := &quot;?&quot;;
	  rr.delta := 0.0d0;
	END;
	IF rr.vers &lt; 3 THEN  (* No explicit directories in the file. *)
	  rr.comp := NEW(PathComp.Compressor).init();
	  rr.decomp := NEW(PathComp.Decompressor).init();
	END;
	RETURN rr;
      EXCEPT
      | Rd.EndOfFile =&gt;
	  RAISE Error(&quot;Premature EOF&quot;);
      | TokScan.Error(msg) =&gt;
	  RAISE Error(msg);
      END;
    EXCEPT Error(msg) =&gt;
      RAISE Error(Fmt.Int(rr.lineNum) &amp; &quot;: &quot; &amp; msg);
    END;
  END FromRd;

PROCEDURE <A NAME="RdClose"><procedure>RdClose</procedure></A>(rr: RdReader)
  RAISES {Rd.Failure, Thread.Alerted} =
  BEGIN
    Rd.Close(rr.rd);
  END RdClose;

PROCEDURE <A NAME="RdGet"><procedure>RdGet</procedure></A>(rr: RdReader): T
  RAISES {Error, Rd.EndOfFile, Rd.Failure, Thread.Alerted} =
  BEGIN
    WITH fs = rr.getRaw() DO
      MakeCooked(fs, rr.vers);
      RETURN fs;
    END;
  END RdGet;

PROCEDURE <A NAME="RdGetRaw"><procedure>RdGetRaw</procedure></A>(rr: RdReader): T
  RAISES {Error, Rd.EndOfFile, Rd.Failure, Thread.Alerted} =
  VAR
    line: TEXT;
    typeCh: CHAR;
    pcType: PathComp.Type;
    pcName: TEXT;
  BEGIN
    TRY
      LOOP
	IF rr.comp # NIL THEN  (* Synthesizing directory entries. *)
	  (* If anything is queued up in the compressor, return it. *)
	  IF rr.comp.get(pcType, pcName) THEN
	    WITH fullName = rr.decomp.put(pcType, pcName) DO
	      CASE pcType OF
	      | PathComp.Type.DirDown =&gt;
		  RETURN NEW(T,
		    type := Type.DirDown,
		    name := fullName,
		    line := &quot;D &quot; &amp; fullName);
	      | PathComp.Type.DirUp =&gt;
		  RETURN NEW(T,
		    type := Type.DirUp,
		    name := fullName,
		    line := &quot;U &quot; &amp; fullName);
	      | PathComp.Type.File =&gt;
		  &lt;* ASSERT Text.Equal(fullName, rr.current.name) *&gt;
		  rr.previous := rr.current;
		  rr.current := NIL;
		  RETURN rr.previous;
	      END;
	    END;
	  END;
	END;

	(* Get the next record from the file. *)
	IF rr.eofSeen THEN
	  IF rr.depth # 0 THEN
	    RAISE Error(&quot;File is truncated&quot;);
	  END;
	  RAISE Rd.EndOfFile;
	END;
	TRY
	  line := SupMisc.GetCmdLine(rr.rd);
	  INC(rr.lineNum);
	  rr.current := NEW(T,
	    line := line,
	    lineNum := rr.lineNum,
	    ts := TokScan.NewDec(line));

	  typeCh := rr.current.ts.getChar(&quot;file type&quot;);
	  rr.current.name := rr.current.ts.getToken(&quot;filename&quot;);

	  CASE typeCh OF
	  | 'D' =&gt;
	      rr.current.type := Type.DirDown;
	      IF rr.comp # NIL THEN
		RAISE Error(&quot;Unexpected directory entry in version &quot;
		  &amp; Fmt.Int(rr.vers) &amp; &quot; file&quot;);
	      END;
	      INC(rr.depth);
	  | 'C' =&gt;
	      rr.current.type := Type.CheckoutLive;
	      IF rr.vers &lt; 2 THEN
		rr.current.name := SupMisc.RCSName(rr.current.name);
	      END;
	  | 'c' =&gt;
	      rr.current.type := Type.CheckoutDead;
	      IF rr.vers &lt; 2 THEN
		rr.current.name := SupMisc.RCSName(rr.current.name);
	      END;
	  | 'V' =&gt;
	      rr.current.type := Type.FileLive;
	  | 'v' =&gt;
	      rr.current.type := Type.FileDead;
	  | 'U' =&gt;
	      rr.current.type := Type.DirUp;
	      IF rr.comp # NIL THEN
		RAISE Error(&quot;Unexpected directory entry in version &quot;
		  &amp; Fmt.Int(rr.vers) &amp; &quot; file&quot;);
	      END;
	      IF rr.depth &lt;= 0 THEN
		RAISE Error(&quot;\&quot;U\&quot; entry has no matching \&quot;D\&quot;&quot;);
	      END;
	      DEC(rr.depth);
	  ELSE
	    RAISE Error(&quot;Invalid file type \&quot;&quot; &amp; Text.FromChar(typeCh) &amp; &quot;\&quot;&quot;);
	  END;

	  IF rr.previous # NIL AND Compare(rr.previous, rr.current) &gt;= 0 THEN
	    RAISE Error(&quot;File is not sorted properly&quot;);
	  END;

	  IF rr.comp # NIL THEN  (* Synthesizing directory entries. *)
	    TRY
	      CASE rr.current.type OF
	      | Type.DirDown =&gt;	pcType := PathComp.Type.DirDown;
	      | Type.DirUp =&gt;	pcType := PathComp.Type.DirUp;
	      ELSE
		pcType := PathComp.Type.File;
	      END;
	      rr.comp.put(pcType, rr.current.name);
	    EXCEPT PathComp.Error(msg) =&gt;
	      RAISE Error(msg);
	    END;
	  ELSE
	    rr.previous := rr.current;
	    rr.current := NIL;
	    RETURN rr.previous;
	  END;
	EXCEPT
	| Rd.EndOfFile =&gt;
	    rr.eofSeen := TRUE;
	    IF rr.comp # NIL THEN
	      rr.comp.finish();
	    END;
	| TokScan.Error(msg) =&gt;
	    RAISE Error(msg);
	END;
      END;
    EXCEPT Error(msg) =&gt;
      RAISE Error(Fmt.Int(rr.lineNum) &amp; &quot;: &quot; &amp; msg);
    END;
  END RdGetRaw;

PROCEDURE <A NAME="RdPrune"><procedure>RdPrune</procedure></A>(rr: RdReader): T
  RAISES {Error, Rd.EndOfFile, Rd.Failure, Thread.Alerted} =
  VAR
    fs: T;
    pruneDepth := rr.depth;
  BEGIN
    WHILE rr.depth &gt;= pruneDepth DO
      fs := rr.getRaw();
    END;
    &lt;* ASSERT fs.type = Type.DirUp *&gt;
    MakeCooked(fs, rr.vers);
    RETURN fs;
  END RdPrune;

PROCEDURE <A NAME="RdScanTime"><procedure>RdScanTime</procedure></A>(rr: RdReader): Time.T =
  BEGIN
    RETURN rr.time;
  END RdScanTime;

PROCEDURE <A NAME="RdStatus"><procedure>RdStatus</procedure></A>(rr: RdReader): CHAR =
  BEGIN
    RETURN rr.statusCode;
  END RdStatus;

PROCEDURE <A NAME="RdOrigin"><procedure>RdOrigin</procedure></A>(rr: RdReader): TEXT =
  BEGIN
    RETURN rr.originHost;
  END RdOrigin;

PROCEDURE <A NAME="RdTimeDelta"><procedure>RdTimeDelta</procedure></A>(rr: RdReader): Time.T =
  BEGIN
    RETURN rr.delta;
  END RdTimeDelta;

PROCEDURE <A NAME="RdVersion"><procedure>RdVersion</procedure></A>(rr: RdReader): CARDINAL =
  BEGIN
    RETURN rr.vers;
  END RdVersion;
</PRE>***************************************************************************

<P><PRE>PROCEDURE <A NAME="FromNull"><procedure>FromNull</procedure></A>(): Reader =
  BEGIN
    RETURN NEW(Reader,
      version := NullVersion,
      scanTime := NullScanTime,
      get := NullGet,
      getRaw := NullGetRaw,
      close := NullClose);
  END FromNull;

PROCEDURE <A NAME="NullClose"><procedure>NullClose</procedure></A>(&lt;*UNUSED*&gt; r: Reader) =
  BEGIN
  END NullClose;

PROCEDURE <A NAME="NullGet"><procedure>NullGet</procedure></A>(&lt;*UNUSED*&gt; r: Reader): T
  RAISES {Rd.EndOfFile} =
  BEGIN
    RAISE Rd.EndOfFile;
  END NullGet;

PROCEDURE <A NAME="NullGetRaw"><procedure>NullGetRaw</procedure></A>(&lt;*UNUSED*&gt; r: Reader): T
  RAISES {Rd.EndOfFile} =
  BEGIN
    RAISE Rd.EndOfFile;
  END NullGetRaw;

PROCEDURE <A NAME="NullScanTime"><procedure>NullScanTime</procedure></A>(&lt;*UNUSED*&gt; r: Reader): Time.T =
  BEGIN
    RETURN -1.0d0;
  END NullScanTime;

PROCEDURE <A NAME="NullVersion"><procedure>NullVersion</procedure></A>(&lt;*UNUSED*&gt; r: Reader): CARDINAL =
  BEGIN
    RETURN Version;
  END NullVersion;
</PRE>***************************************************************************

<P><PRE>TYPE
  WrWriter = Writer OBJECT
    wr: Wr.T;
    current: T := NIL;
    comp: PathComp.Compressor;
    decomp: PathComp.Decompressor;
    cannotWrite := FALSE;
  OVERRIDES
    version := WrVersion;
    put := WrPut;
    putRaw := WrPutRaw;
    close := WrClose;
  END;

PROCEDURE <A NAME="ToWr"><procedure>ToWr</procedure></A>(wr: Wr.T; scanTime: Time.T): Writer
  RAISES {Thread.Alerted, Wr.Failure} =
  VAR
    ww := NEW(WrWriter,
      wr := wr,
      comp := NEW(PathComp.Compressor).init(),
      decomp := NEW(PathComp.Decompressor).init());
  BEGIN
    SupMisc.PutCmd(wr, &quot;F&quot;,
      Fmt.Int(Version),
      TokScan.EncodeTime(scanTime),
      encode := TRUE);
    RETURN ww;
  END ToWr;

PROCEDURE <A NAME="WrClose"><procedure>WrClose</procedure></A>(ww: WrWriter)
  RAISES {Thread.Alerted, Wr.Failure} =
  VAR
    pcType: PathComp.Type;
    pcName: TEXT;
  BEGIN
    IF NOT ww.cannotWrite THEN
      (* Close off all the open directories. *)
      ww.comp.finish();
      WHILE ww.comp.get(pcType, pcName) DO
	WITH fullName = ww.decomp.put(pcType, pcName) DO
	  CASE pcType OF
	  | PathComp.Type.DirDown =&gt;
	      SupMisc.PutCmd(ww.wr, &quot;D&quot;,
		fullName,
		encode := TRUE);
	  | PathComp.Type.DirUp =&gt;
	      SupMisc.PutCmd(ww.wr, &quot;U&quot;,
		fullName,
		FileAttr.Encode(FileAttr.Bogus),
		encode := TRUE);
	  | PathComp.Type.File =&gt;  (* Should never happen. *)
	      &lt;* ASSERT FALSE *&gt;
	  END;
	END;
      END;
    END;
    Wr.Close(ww.wr);
  END WrClose;

PROCEDURE <A NAME="WrPut"><procedure>WrPut</procedure></A>(ww: WrWriter; fs: T)
  RAISES {Error, Thread.Alerted, Wr.Failure} =
  VAR
    pcType: PathComp.Type;
    pcName: TEXT;
    dirUpAttr: FileAttr.T;
  BEGIN
    IF ww.cannotWrite THEN  (* Don't even try. *)
      RETURN;
    END;
    IF ww.current # NIL THEN  (* Don't ever write the records out of order. *)
      &lt;* ASSERT Compare(ww.current, fs) &lt; 0 *&gt;
    END;
    ww.current := fs;

    TRY
      (* Rather than rely on the proper DirDown and DirUp entries being
	 supplied, we synthesize them on our own.  This is necessary in
	 certain situations when we are reading from an older version of
	 the status file which did not have explicit directory entries. *)

      TRY
	dirUpAttr := FileAttr.Bogus;

	CASE fs.type OF
	| Type.DirDown =&gt;
	    ww.comp.put(PathComp.Type.DirDown, fs.name);
	| Type.DirUp =&gt;
	    ww.comp.put(PathComp.Type.DirUp, fs.name);
	    dirUpAttr := fs.clientAttr;
	ELSE
	  ww.comp.put(PathComp.Type.File, fs.name);
	END;

	WHILE ww.comp.get(pcType, pcName) DO
	  WITH fullName = ww.decomp.put(pcType, pcName) DO
	    CASE pcType OF
	    | PathComp.Type.DirDown =&gt;
		SupMisc.PutCmd(ww.wr, &quot;D&quot;,
		  fullName,
		  encode := TRUE);
	    | PathComp.Type.DirUp =&gt;
		SupMisc.PutCmd(ww.wr, &quot;U&quot;,
		  fullName,
		  FileAttr.Encode(dirUpAttr),
		  encode := TRUE);
		dirUpAttr := FileAttr.Bogus;
	    | PathComp.Type.File =&gt;
		(* Just swallow it. *)
		&lt;* ASSERT Text.Equal(fullName, fs.name) *&gt;
	    END;
	  END;
	END;
      EXCEPT PathComp.Error(msg) =&gt;
	RAISE Error(msg);
      END;

      CASE fs.type OF
      | Type.DirDown, Type.DirUp =&gt;
	  (* Already emitted above. *)
      | Type.CheckoutLive =&gt;
	  SupMisc.PutCmd(ww.wr, &quot;C&quot;,
	    fs.name,
	    fs.tag,
	    fs.date,
	    FileAttr.Encode(fs.serverAttr),
	    fs.revNum,
	    fs.revDate,
	    FileAttr.Encode(fs.clientAttr),
	    encode := TRUE);
      | Type.CheckoutDead =&gt;
	  SupMisc.PutCmd(ww.wr, &quot;c&quot;,
	    fs.name,
	    fs.tag,
	    fs.date,
	    FileAttr.Encode(fs.serverAttr),
	    encode := TRUE);
      | Type.FileLive =&gt;
	  SupMisc.PutCmd(ww.wr, &quot;V&quot;,
	    fs.name,
	    FileAttr.Encode(fs.clientAttr),
	    encode := TRUE);
      | Type.FileDead =&gt;
	  SupMisc.PutCmd(ww.wr, &quot;v&quot;,
	    fs.name,
	    FileAttr.Encode(fs.clientAttr),
	    encode := TRUE);
      END;
    EXCEPT Wr.Failure(l) =&gt;
      ww.cannotWrite := TRUE;
      RAISE Wr.Failure(l);
    END;
  END WrPut;

PROCEDURE <A NAME="WrPutRaw"><procedure>WrPutRaw</procedure></A>(ww: WrWriter; fs: T)
  RAISES {Error, Thread.Alerted, Wr.Failure} =
  VAR
    pcType: PathComp.Type;
    pcName: TEXT;
    gotOne: BOOLEAN;
  BEGIN
    IF ww.cannotWrite THEN  (* Don't even try. *)
      RETURN;
    END;
    TRY
      (* Unfortunately, we have to do the work to keep the pathname
	 compressor in sync. *)
      TRY
	CASE fs.type OF
	| Type.DirDown =&gt;	ww.comp.put(PathComp.Type.DirDown, fs.name);
	| Type.DirUp =&gt;	ww.comp.put(PathComp.Type.DirUp, fs.name);
	ELSE
	  ww.comp.put(PathComp.Type.File, fs.name);
	END;
	(* Calls to putRaw() always originate from a getRaw().  Therefore,
	   we expect them to have all the necessary DirDowns and DirUps.
	   The compressor should return exactly one value, which is whatever
	   we put into it. *)
	gotOne := ww.comp.get(pcType, pcName);
	&lt;* ASSERT gotOne *&gt;
	EVAL ww.decomp.put(pcType, pcName);
	gotOne := ww.comp.get(pcType, pcName);
	&lt;* ASSERT NOT gotOne *&gt;
      EXCEPT PathComp.Error(msg) =&gt;
	RAISE Error(msg);
      END;

      &lt;* ASSERT fs.line # NIL *&gt;
      Wr.PutText(ww.wr, fs.line &amp; &quot;\n&quot;);
    EXCEPT Wr.Failure(l) =&gt;
      ww.cannotWrite := TRUE;
      RAISE Wr.Failure(l);
    END;
  END WrPutRaw;

PROCEDURE <A NAME="WrVersion"><procedure>WrVersion</procedure></A>(&lt;*UNUSED*&gt; ww: WrWriter): CARDINAL =
  BEGIN
    RETURN Version;
  END WrVersion;

BEGIN
END FileStatus.
</PRE>
</inModule>
<PRE>























</PRE>
</BODY>
</HTML>
