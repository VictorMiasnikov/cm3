<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: suplib/src/MD5.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>suplib/src/MD5.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright 1996-2003 John D. Polstra.
 * All rights reserved.
 *
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions
 * are met:
 * 1. Redistributions of source code must retain the above copyright
 *    notice, this list of conditions and the following disclaimer.
 * 2. Redistributions in binary form must reproduce the above copyright
 *    notice, this list of conditions and the following disclaimer in the
 *    documentation and/or other materials provided with the distribution.
 * 3. All advertising materials mentioning features or use of this software
 *    must display the following acknowledgment:
 *      This product includes software developed by John D. Polstra.
 * 4. The name of the author may not be used to endorse or promote products
 *    derived from this software without specific prior written permission.
 *
 * THIS SOFTWARE IS PROVIDED BY THE AUTHOR ``AS IS'' AND ANY EXPRESS OR
 * IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES
 * OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED.
 * IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR ANY DIRECT, INDIRECT,
 * INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT
 * NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
 * DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
 * THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
 * (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF
 * THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 *
 * $Id: MD5.m3.html,v 1.3 2010-04-29 17:20:02 wagner Exp $ 

<P><PRE>UNSAFE MODULE <module><implements><A HREF="MD5.i3.html">MD5</A></implements></module>;

IMPORT
  <A HREF="CText.i3.html">CText</A>, <A HREF="../../m3core/src/C/Common/Ctypes.i3.html">Ctypes</A>, <A HREF="../../m3core/src/C/Common/M3toC.i3.html">M3toC</A>, <A HREF="MD5Digest.i3.html">MD5Digest</A>, <A HREF="../../libm3/src/os/Common/OSError.i3.html">OSError</A>, <A HREF="../../libm3/src/os/POSIX/OSErrorPosix.i3.html">OSErrorPosix</A>, <A HREF="../../libm3/src/os/Common/Pathname.i3.html">Pathname</A>,
  <A HREF="../../m3core/src/text/Text.i3.html">Text</A>, <A HREF="Umd5.i3.html">Umd5</A>;
</PRE>***************************************************************************
 Code common to all subtypes. 
***************************************************************************

<P><PRE>TYPE
  CUCS = Ctypes.const_unsigned_char_star;

  Common = T OBJECT
    ctx: Umd5.MD5_CTX;
    initted := FALSE;
  METHODS
    init(): Common := Init;
  OVERRIDES
    finish := Finish;
    finishRaw := FinishRaw;
    update := Update;
    updateText := UpdateText;
  END;

PROCEDURE <A NAME="Finish"><procedure>Finish</procedure></A>(self: Common): TEXT =
  VAR
    str: Ctypes.char_star;
    t: TEXT;
  BEGIN
    &lt;* ASSERT self.initted *&gt;
    str := Umd5.MD5End(ADR(self.ctx), NIL);
    t := M3toC.CopyStoT(str);
    DISPOSE(str);
    self.initted := FALSE;
    RETURN t;
  END Finish;

PROCEDURE <A NAME="FinishRaw"><procedure>FinishRaw</procedure></A>(self: Common; VAR rslt: MD5Digest.T) =
  BEGIN
    &lt;* ASSERT self.initted *&gt;
    Umd5.MD5Final(ADR(rslt[0]), ADR(self.ctx));
    self.initted := FALSE;
  END FinishRaw;

PROCEDURE <A NAME="Init"><procedure>Init</procedure></A>(self: Common): Common =
  BEGIN
    Umd5.MD5Init(ADR(self.ctx));
    self.initted := TRUE;
    RETURN self;
  END Init;

PROCEDURE <A NAME="Update"><procedure>Update</procedure></A>(self: Common; READONLY data: ARRAY OF CHAR) =
  BEGIN
    &lt;* ASSERT self.initted *&gt;
    IF NUMBER(data) # 0 THEN
      self.updateRaw(ADR(data[0]), NUMBER(data));
    END;
  END Update;

PROCEDURE <A NAME="UpdateText"><procedure>UpdateText</procedure></A>(self: Common; text: TEXT) =
  VAR
    textStr := CText.SharedTtoS(text);
  BEGIN
    &lt;* ASSERT self.initted *&gt;
    self.updateRaw(LOOPHOLE(textStr, UNTRACED REF CHAR), Text.Length(text));
    CText.FreeSharedS(text, textStr);
  END UpdateText;
</PRE>***************************************************************************
 A subtype for computing MD5 checksums over uninterpreted characters. 
***************************************************************************

<P><PRE>TYPE
  Raw = Common OBJECT OVERRIDES
    clone := Clone;
    updateRaw := UpdateRaw;
  END;

PROCEDURE <A NAME="New"><procedure>New</procedure></A>(): T =
  VAR
    self := NEW(Raw);
  BEGIN
    EVAL Common.init(self);
    RETURN self;
  END New;

PROCEDURE <A NAME="Clone"><procedure>Clone</procedure></A>(self: Raw): T =
  BEGIN
    RETURN NEW(Raw, ctx := self.ctx, initted := self.initted);
  END Clone;

PROCEDURE <A NAME="UpdateRaw"><procedure>UpdateRaw</procedure></A>(self: Raw; data: UNTRACED REF CHAR; count: CARDINAL) =
  BEGIN
    &lt;* ASSERT self.initted *&gt;
    Umd5.MD5Update(ADR(self.ctx), LOOPHOLE(data, CUCS), count);
  END UpdateRaw;
</PRE>***************************************************************************
 A subtype for computing MD5 checksums over RCS files, carefully ignoring
   unimportant details of white space. 
***************************************************************************

<P><PRE>TYPE
  RCS = Common OBJECT
    lastCh := ';';
    state := State.InPrint;
  OVERRIDES
    clone := RCSClone;
    updateRaw := RCSUpdateRaw;
  END;

  State = { InPrint, InWS, InString, AtSeen };

PROCEDURE <A NAME="NewRCS"><procedure>NewRCS</procedure></A>(): T =
  VAR
    self := NEW(RCS);
  BEGIN
    EVAL Common.init(self);
    RETURN self;
  END NewRCS;

PROCEDURE <A NAME="RCSClone"><procedure>RCSClone</procedure></A>(self: RCS): T =
  BEGIN
    RETURN NEW(RCS,
      ctx := self.ctx,
      initted := self.initted,
      lastCh := self.lastCh,
      state := self.state);
  END RCSClone;

PROCEDURE <A NAME="RCSUpdateRaw"><procedure>RCSUpdateRaw</procedure></A>(self: RCS; data: UNTRACED REF CHAR; count: CARDINAL) =
  CONST
    WS = SET OF CHAR{' ', '\010', '\t', '\n', '\013', '\f', '\r'};
    Print = SET OF CHAR{FIRST(CHAR)..LAST(CHAR)} - WS - SET OF CHAR{'@'};
    Special = SET OF CHAR{'$', ',', ':', ';', '@'};	(* Self-delimiting. *)
    (* '@' isn't strictly self-delimiting, because two in a row is
       special.  But that is handled by special-case code below. *)
  VAR
    Sp := ' ';  (* CONST *)
    ptr := data;
    lim: UNTRACED REF CHAR := ptr + count;
    start: UNTRACED REF CHAR;
  BEGIN
    &lt;* ASSERT self.initted *&gt;
    WHILE ptr &lt; lim DO
      CASE self.state OF
      | State.InPrint =&gt;
	  start := ptr;
	  WHILE ptr &lt; lim AND ptr^ IN Print DO
	    self.lastCh := ptr^;
	    INC(ptr);
	  END;
	  Umd5.MD5Update(ADR(self.ctx), LOOPHOLE(start, CUCS), ptr - start);
	  IF ptr &lt; lim THEN
	    IF ptr^ = '@' THEN
	      Umd5.MD5Update(ADR(self.ctx), LOOPHOLE(ptr, CUCS), 1);
	      INC(ptr);
	      self.state := State.InString;
	    ELSE
	      self.state := State.InWS;
	    END;
	  END;
      | State.InWS =&gt;
	  WHILE ptr &lt; lim AND ptr^ IN WS DO
	    INC(ptr);
	  END;
	  IF ptr &lt; lim THEN
	    IF ptr^ = '@' THEN
	      IF self.lastCh = '@' THEN
		Umd5.MD5Update(ADR(self.ctx), ADR(Sp), 1);
	      END;
	      Umd5.MD5Update(ADR(self.ctx), LOOPHOLE(ptr, CUCS), 1);
	      INC(ptr);
	      self.state := State.InString;
	    ELSE
	      IF NOT self.lastCh IN Special AND NOT ptr^ IN Special THEN
		Umd5.MD5Update(ADR(self.ctx), ADR(Sp), 1);
	      END;
	      self.state := State.InPrint;
	    END;
	  END;
      | State.InString =&gt;
	  start := ptr;
	  WHILE ptr &lt; lim AND ptr^ # '@' DO
	    INC(ptr);
	  END;
	  Umd5.MD5Update(ADR(self.ctx), LOOPHOLE(start, CUCS), ptr - start);
	  IF ptr &lt; lim THEN
	    Umd5.MD5Update(ADR(self.ctx), LOOPHOLE(ptr, CUCS), 1);
	    INC(ptr);
	    self.state := State.AtSeen;
	  END;
      | State.AtSeen =&gt;
	  IF ptr^ = '@' THEN
	    Umd5.MD5Update(ADR(self.ctx), LOOPHOLE(ptr, CUCS), 1);
	    INC(ptr);
	    self.state := State.InString;
	  ELSIF ptr^ IN WS THEN
	    self.lastCh := '@';
	    self.state := State.InWS;
	  ELSE
	    self.state := State.InPrint;
	  END;
      END;
    END;
  END RCSUpdateRaw;
</PRE>***************************************************************************
 Convenience procedures. 
***************************************************************************

<P><PRE>PROCEDURE <A NAME="FileSignature"><procedure>FileSignature</procedure></A>(name: Pathname.T): TEXT
  RAISES {OSError.E} =
  VAR
    nameStr := CText.SharedTtoS(name);
    str: Ctypes.char_star;
    t: TEXT;
  BEGIN
    str := Umd5.MD5File(nameStr, NIL);
    CText.FreeSharedS(name, nameStr);
    IF str = NIL THEN
      OSErrorPosix.Raise();
    END;
    t := M3toC.CopyStoT(str);
    DISPOSE(str);
    RETURN t;
  END FileSignature;

BEGIN
END MD5.
</PRE>
</inModule>
<PRE>























</PRE>
</BODY>
</HTML>
