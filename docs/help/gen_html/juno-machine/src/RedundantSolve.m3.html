<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: juno-machine/src/RedundantSolve.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>juno-machine/src/RedundantSolve.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright (C) 1992, Digital Equipment Corporation         
 All rights reserved.                                      
 See the file COPYRIGHT for a full description.            
                                                           
 Last modified on Thu Apr 10 09:52:22 PDT 1997 by heydon   
      modified on Thu Dec  8 15:44:33 PST 1994 by gnelson  
      modified on Mon Oct 31 18:34:31 PST 1994 by isard    

<P><PRE>MODULE <module><implements><A HREF="RedundantSolve.i3.html">RedundantSolve</A></implements></module>;

IMPORT <A HREF="RedundantLSolve.i3.html">RedundantLSolve</A>, <A HREF="#x1">JunoValue</A>, <A HREF="NonLinearSolveRep.i3.html">NonLinearSolveRep</A>;
FROM <A HREF="RedundantLSolve.i3.html">RedundantLSolve</A> IMPORT logWr;
FROM <A HREF="#x1">JunoValue</A> IMPORT Real;
FROM <A HREF="NonLinearSolveRep.i3.html">NonLinearSolveRep</A> IMPORT conAvail, conInUse, ConType;

IMPORT <A HREF="../../libm3/src/rw/IO.i3.html">IO</A>, <A HREF="../../libm3/src/rw/Wr.i3.html">Wr</A>, <A HREF="../../libm3/src/fmtlex/Fmt.i3.html">Fmt</A>, <A HREF="../../m3core/src/text/Text.i3.html">Text</A>, <A HREF="../../libm3/src/rw/Stdio.i3.html">Stdio</A>;
FROM <A HREF="../../m3core/src/thread/Common/Thread.i3.html">Thread</A> IMPORT Alerted;
&lt;* FATAL Wr.Failure, Alerted *&gt;

VAR debug := 0; oneStep := FALSE; logFileName := &quot;solver.log&quot;;
</PRE><BLOCKQUOTE><EM> debug &gt;= 1 =&gt; show input &amp; solution
   debug &gt;= 2 =&gt; show values &amp; deltas at each iteration
   debug &gt;= 3 =&gt; show values and roundoff errors at each iteration
   oneStep =&gt; take only one Newton step each solve; always report success
   logFile =&gt; name of file logging output is sent to; if NIL, output is
              sent to stdout
</EM></BLOCKQUOTE><PRE>

TYPE T = Real;

PROCEDURE <A NAME="NewCon"><procedure>NewCon</procedure></A>(type: ConType): Constraint =
  VAR res: Constraint; BEGIN
    IF conAvail # NIL
      THEN res := conAvail; conAvail := conAvail.availLink
      ELSE res := NEW(Constraint)
    END;
    res.availLink := conInUse;
    conInUse := res;
    res.type := type;
    RETURN res
  END NewCon;

PROCEDURE <A NAME="NewPlus"><procedure>NewPlus</procedure></A>(): Constraint =
  BEGIN RETURN NewCon(ConType.Plus) END NewPlus;

PROCEDURE <A NAME="NewMinus"><procedure>NewMinus</procedure></A>(): Constraint =
  BEGIN RETURN NewCon(ConType.Minus) END NewMinus;

PROCEDURE <A NAME="NewHalve"><procedure>NewHalve</procedure></A>(): Constraint =
  BEGIN RETURN NewCon(ConType.Halve) END NewHalve;

PROCEDURE <A NAME="NewTimes"><procedure>NewTimes</procedure></A>(): Constraint =
  BEGIN RETURN NewCon(ConType.Times) END NewTimes;

PROCEDURE <A NAME="NewSin"><procedure>NewSin</procedure></A>(): Constraint =
  BEGIN RETURN NewCon(ConType.Sin) END NewSin;

PROCEDURE <A NAME="NewCos"><procedure>NewCos</procedure></A>(): Constraint =
  BEGIN RETURN NewCon(ConType.Cos) END NewCos;

PROCEDURE <A NAME="NewAtan"><procedure>NewAtan</procedure></A>(): Constraint =
  BEGIN RETURN NewCon(ConType.Atan) END NewAtan;

PROCEDURE <A NAME="NewMultTan"><procedure>NewMultTan</procedure></A>(): Constraint =
  BEGIN RETURN NewCon(ConType.MultTan) END NewMultTan;

PROCEDURE <A NAME="NewExp"><procedure>NewExp</procedure></A>(): Constraint =
  BEGIN RETURN NewCon(ConType.Exp) END NewExp;

PROCEDURE <A NAME="Dispose"><procedure>Dispose</procedure></A>() =
  VAR l := conInUse; BEGIN
    IF l # NIL THEN
      WHILE l.availLink # NIL DO l := l.availLink END;
      l.availLink := conAvail;
      conAvail := conInUse;
      conInUse := NIL
    END
  END Dispose;

VAR
  MaxIterations := 20;
  MinDelta := 1.0e-7;
  RoundFudge := FLOAT(16.0, JunoValue.Real);
</PRE> A constraint is considered satisfied if its error is at most <CODE>RoundFudge</CODE>
   times the estimated round-off error involved in computing it.
<P>
   A change to a coordinate of the solution that has relative value less than
   <CODE>MinDelta</CODE> is considered tiny.  When all changes are tiny, the iteration
   stops. 

<P><PRE>CONST
  InitRows = 20;
  InitCols = 40;
  InitErrB = 100;

VAR
  a := NEW(REF RedundantLSolve.Matrix, InitRows, InitCols + 1); (* matrix *)
  x := NEW(REF RedundantLSolve.Vector, InitCols);        (* solution vector *)
  errorVec := NEW(REF RedundantLSolve.Vector, InitRows); (* one/constraint *)
  errb := NEW(REF RedundantLSolve.Vector, InitErrB);     (* error bound *)

CONST
  NameWidth = 9;
  Prec = 3;
  FieldWidth = Prec + 8;

PROCEDURE <A NAME="ShowVector"><procedure>ShowVector</procedure></A>(
    name: TEXT;
    READONLY v: RedundantLSolve.Vector;
    size: CARDINAL) =
  BEGIN
    Wr.PutText(logWr, &quot;  &quot;);
    IF size &gt; 0 THEN
      Wr.PutText(logWr, Fmt.Pad(name, NameWidth - 2, align := Fmt.Align.Left));
      FOR i := 0 TO size - 1 DO
    	Wr.PutText(logWr,
          Fmt.Pad(Fmt.Real(v[i], Fmt.Style.Sci, prec := 3), FieldWidth))
      END
    ELSE
      Wr.PutText(logWr, &quot;&lt;none&gt;&quot;)
    END;
    Wr.PutChar(logWr, '\n');
  END ShowVector;

PROCEDURE <A NAME="EtpLogP0"><procedure>EtpLogP0</procedure></A>(&lt;*UNUSED*&gt; true_cnt, nn, ghost_cnt, iterations: CARDINAL) =
</PRE><BLOCKQUOTE><EM> true_cnt = # of true constraints; nn = # of true vars; ghost_cnt = # of
   ghost vars and constraints, <CODE>iterations</CODE> is the number of iterations. </EM></BLOCKQUOTE><PRE>
  BEGIN END EtpLogP0;

PROCEDURE <A NAME="EvalRHS"><procedure>EvalRHS</procedure></A>(
   c: Constraint;
   READONLY v: ARRAY OF T;
   READONLY errb: ARRAY OF T;
   n: CARDINAL;
   VAR (*OUT*) res: T;
   VAR (*OUT*) err: T;
   supressNewline := FALSE) =
 (* Set &quot;res&quot; and &quot;err&quot; to the value and estimated round-off error of the
    right side of &quot;c&quot;, assuming that &quot;v&quot; and &quot;errb&quot; give the value and
    round-off errors for the variables appearing in the right side of &quot;c&quot;.
    The argument &quot;res&quot; may be aliased with an element of &quot;v&quot; that doesn't
    occur in the right side of &quot;c&quot;, and &quot;err&quot; may be aliased to an element of
    &quot;errb&quot; that doesn't occur in the right side of &quot;c&quot;. *)
 BEGIN
   WITH
     arg = c.arg,
     y = v[arg[1]],
     x = v[arg[2]],
     dely = errb[arg[1]],
     delx = errb[arg[2]]
   DO
     (* res := c.type(y,x) *)
     CASE c.type OF &lt;*NOWARN*&gt;
     | ConType.Plus =&gt;
         res := y + x;
         err := dely + delx
     | ConType.Minus =&gt;
         res := y - x;
         err := dely + delx
     | ConType.Halve =&gt;
         res := y * 0.5;
         err := dely * 0.5
     | ConType.Times =&gt;
         res := y * x;
         err := dely * ABS(x) + delx * ABS(y)
     | ConType.Atan =&gt;
         res := JunoValue.Atan(y, x);
         err := (delx * ABS(y) + dely * ABS(x)) / ABS(x*x+y*y)
     | ConType.Sin =&gt;
         res := JunoValue.Sin(y);
         err := ABS(JunoValue.Cos(y)) * dely
     | ConType.Cos =&gt;
         res := JunoValue.Cos(y);
         err := ABS(JunoValue.Sin(y)) * dely
     | ConType.MultTan =&gt;
         WITH
           tanx = JunoValue.Tan(x),
           cosx = JunoValue.Cos(x)
         DO
           res := y * tanx;
           err := dely * ABS(tanx) + delx / (cosx * cosx)
         END
     | ConType.Exp =&gt;
         WITH
           expy = JunoValue.Exp(y)
         DO
           res := expy;
           err := expy * dely
         END
     END;
     IF c.type # ConType.Halve THEN
       err := err + JunoValue.HalfEps * ABS(res)
     END;
     IF debug &gt;= 3 THEN
       ShowConstraint(c, v, n, supressNewline)
     END
   END
 END EvalRHS;

PROCEDURE <A NAME="IndexVal"><procedure>IndexVal</procedure></A>(i, n: CARDINAL; READONLY v: ARRAY OF T): TEXT =
  BEGIN
    IF i &gt;= n THEN RETURN Fmt.Real(v[i]) END;
    VAR res := &quot;&quot;; BEGIN
      LOOP
        res := Text.FromChar(VAL((i MOD 26) + ORD('a'), CHAR)) &amp; res;
        IF i &lt; 26 THEN EXIT END;
        i := i DIV 26
      END;
    RETURN res
  END
END IndexVal;

PROCEDURE <A NAME="ShowConstraint"><procedure>ShowConstraint</procedure></A>(
    con: Constraint;
    READONLY v: ARRAY OF T;
    n: CARDINAL;
    supressNewline := FALSE) =
  BEGIN
    Wr.PutText(logWr, &quot;  &quot; &amp; IndexVal(con.arg[0], n, v) &amp; &quot; = &quot;);
    CASE con.type OF &lt;* NOWARN *&gt;
      ConType.Plus =&gt;
        Wr.PutText(logWr, IndexVal(con.arg[1],n,v)
          &amp; &quot; + &quot; &amp; IndexVal(con.arg[2],n,v))
    | ConType.Minus =&gt;
        Wr.PutText(logWr, IndexVal(con.arg[1],n,v)
          &amp; &quot; - &quot; &amp; IndexVal(con.arg[2],n,v))
    | ConType.Halve =&gt;
        Wr.PutText(logWr, IndexVal(con.arg[1],n,v) &amp; &quot; / 2&quot;)
    | ConType.Times =&gt;
        Wr.PutText(logWr, IndexVal(con.arg[1],n,v)
          &amp; &quot; * &quot; &amp; IndexVal(con.arg[2],n,v))
    | ConType.Atan =&gt;
        Wr.PutText(logWr, &quot;ATAN(&quot; &amp; IndexVal(con.arg[1],n,v)
          &amp; &quot;, &quot; &amp; IndexVal(con.arg[2],n,v) &amp; &quot;)&quot;)
    | ConType.Sin =&gt;
        Wr.PutText(logWr, &quot;SIN(&quot; &amp; IndexVal(con.arg[1],n,v) &amp; &quot;)&quot;)
    | ConType.Cos =&gt;
        Wr.PutText(logWr, &quot;COS(&quot; &amp; IndexVal(con.arg[1],n,v) &amp; &quot;)&quot;)
    | ConType.MultTan =&gt;
        Wr.PutText(logWr, IndexVal(con.arg[1],n,v)
          &amp; &quot;* TAN(&quot;&amp; IndexVal(con.arg[2],n,v) &amp; &quot;)&quot;)
    | ConType.Exp =&gt;
        Wr.PutText(logWr, &quot;EXP(&quot; &amp; IndexVal(con.arg[1],n,v) &amp; &quot;)&quot;)
    END;
    IF NOT supressNewline THEN Wr.PutChar(logWr, '\n') END
  END ShowConstraint;

PROCEDURE <A NAME="P"><procedure>P</procedure></A>(
    nn, n: CARDINAL;
    VAR v: ARRAY OF T;
    READONLY c: ARRAY OF Constraint): BOOLEAN =
</PRE><BLOCKQUOTE><EM><P>
<PRE>
      ON ENTRY:
      
      	    v[]
      	  ________
      	 |        |
      	 |  True  |
      	 |  Vars  |
      	 |        |        c[]
      	 |________|    _____________
       nn -&gt; |        |   |             |
      	 |        |   |             |
      	 |  Ghost |   |    Ghost    |
      	 |  Vars  |   | Constraints |
      	 |        |   |             |
      	 |________|   |_____________|
        n -&gt; |........|   |             | &lt;- ghost_cnt
      	 |........|   |    True     |
      	 |________|   | Constraints |
      	 |        |   |             |
      	 | Consts |   |_____________|
      	 |________|                   &lt;- m (total # of constraints)
      
      MATRIX ORGANIZATION:
      
      		x[]
      	____________________         __
           |                    |       |..|
           |____________________|       |..| = unused
                                        |__|
           | &lt;------ nn ------&gt; |
      
      
      	       a[][]             errorVec[]
            _______________________      __
       ^   |                    |  |    |..|
       |   |    Ghost           |  |    |..|
       |   |    Equations       |  |    |..|
       |   |____________________|__|    |__|
       m   |                    |  |    |  | &lt;- ghost_cnt
       |   |    True            |  |    |  |
       |   |    Equations       |  |    |  |
       V   |____________________|__|    |__|
                                             &lt;- m
           | &lt;------ nn + 1 -----&gt; |
</EM></BLOCKQUOTE><PRE>
  VAR
    m := NUMBER(c);			 (* number of constraints (total) *)
    ghost_cnt := n - nn;		 (* number of ghost constraints *)
    true_cnt := m - ghost_cnt;		 (* number of true constraints *)
    allUpdatesTiny: BOOLEAN;             (* set by &quot;UpdateVars&quot; *)
    goodEnough: BOOLEAN;                 (* set by &quot;EvalConstraints&quot; *)

  PROCEDURE EvalConstraints(): T =
  (* Evaluate the ghost variables, and set &quot;errorVec[i]&quot; to the error in
     true constraint &quot;i&quot;, for &quot;i&quot; in &quot;[ghost_cnt..m-1]&quot;. Return the maximum
     absolute error of any true constraint.  Also set &quot;errb[i]&quot; to the
     estimated round-off error in the value of &quot;v[i]&quot;, for each &quot;i&quot;
     in &quot;[0..n-1]&quot;, and set &quot;goodEnough&quot; to record whether all constraints
     were met as closely as can be expected given the rounding error
     involved in computing them. *)
    VAR result: T := 0.0; rhsval, rhserr, diff: T; BEGIN
      goodEnough := TRUE;
      IF debug &gt;= 3 THEN IO.Put(&quot;Evaluating constraints:\n&quot;, logWr) END;
      FOR i := FIRST(v) TO LAST(v) DO
        errb[i] := JunoValue.HalfEps * ABS(v[i])
      END;
      FOR i := 0 TO ghost_cnt - 1 DO
        WITH ci = c[i], arg0 = ci.arg[0] DO
          EvalRHS(ci, v, errb^, n, (*OUT*) v[arg0], (*OUT*) errb[arg0])
        END
      END;
      FOR i := ghost_cnt TO m - 1 DO
        WITH ci = c[i], arg0 = ci.arg[0] DO
          EvalRHS(ci, v, errb^, n, (*OUT*) rhsval, (*OUT*) rhserr,
            supressNewline := (debug &gt;= 3));
          IF debug &gt;= 3 THEN
            IO.Put(&quot; = &quot;, logWr);   IO.PutReal(rhsval, logWr);
            IO.Put(&quot; +- &quot;, logWr);  IO.PutReal(rhserr, logWr);
            IO.Put(&quot; vs. &quot;, logWr); IO.PutReal(v[arg0], logWr);
            IO.Put(&quot; +- &quot;, logWr);  IO.PutReal(errb[arg0], logWr)
          END;
          diff := rhsval - v[arg0];
          errorVec[i] := diff;
          diff := ABS(diff);
          IF diff &gt; RoundFudge * (rhserr + errb[arg0]) THEN
            goodEnough := FALSE;
            IF debug &gt;= 3 THEN IO.Put(&quot; -&gt; BAD&quot;, logWr) END
          END
        END;
        IF debug &gt;= 3 THEN IO.Put(&quot;\n&quot;, logWr) END;
        IF diff &gt; result THEN result := diff END
      END;
      RETURN result
    END EvalConstraints;

  PROCEDURE ZeroMatrix() =
    BEGIN
      FOR i := 0 TO m - 1 DO
        WITH row = a[i] DO
          FOR j := 0 TO nn - 1 DO
            row[j] := 0.0
          END
        END
      END
    END ZeroMatrix;

  PROCEDURE BuildMatrix() =
  (* Fill in the matrix &quot;a&quot; to solve one step of the iteration for the
     constraints &quot;c&quot; using the current values of the variables &quot;v&quot;. *)
    BEGIN
      ZeroMatrix();
      (* compute gradients of ghost constraints *)
      FOR i := 0 TO ghost_cnt - 1 DO
        WITH con = c[i] DO
          GradCon(con, con.arg[0] - nn)
        END
      END;
      (* compute gradients of true constraints *)
      FOR i := ghost_cnt TO m - 1 DO
        WITH con = c[i] DO
          GradCon(con, i);
          GradVar(con.arg[0], i, -1.0);
          a[i, nn] := -(errorVec[i])
        END
      END;
    END BuildMatrix;

  PROCEDURE GradCon(con: Constraint; i: CARDINAL) =
  (* Set &quot;row&quot; to the gradient of the right hand side of &quot;con&quot;. *)
    BEGIN
      CASE con.type OF &lt;* NOWARN *&gt;
      | ConType.Plus =&gt;
          GradVar(con.arg[1], i, 1.0);
          GradVar(con.arg[2], i, 1.0)
      | ConType.Minus =&gt;
          GradVar(con.arg[1], i,  1.0);
          GradVar(con.arg[2], i, -1.0)
      | ConType.Halve =&gt;
          GradVar(con.arg[1], i, 0.5)
      | ConType.Times =&gt;
          WITH arg1 = con.arg[1], arg2 = con.arg[2] DO
            GradVar(arg1, i, v[arg2]);
            GradVar(arg2, i, v[arg1])
          END
      | ConType.Sin =&gt;
          WITH arg1 = con.arg[1] DO
            GradVar(arg1, i, JunoValue.Cos(v[arg1]))
          END
      | ConType.Cos =&gt;
          WITH arg1 = con.arg[1] DO
            GradVar(arg1, i, -(JunoValue.Sin(v[arg1])))
          END
      | ConType.Atan =&gt;
          (* d(atan2(y, x))
             = d(atan(y/x))
             = (1 / (1 + ((y/x)^2))) * ((x dy - y dx) / (x^2))
             = (1 / (x^2 + y^2)) * (x dy - y dx) *)
          WITH arg1 = con.arg[1], arg2 = con.arg[2] DO
            VAR y := v[arg1]; x := v[arg2]; denom := x*x + y*y; BEGIN
              GradVar(arg1, i,  x / denom);
              GradVar(arg2, i, -y / denom)
            END
          END
      | ConType.MultTan =&gt;
          (* d(x * tan(y))
             = tan(y) dx + x * sec^2(y) dy
             = tan(y) dx + x * (1 + tan^2(y)) *)
          WITH arg1 = con.arg[1], arg2 = con.arg[2] DO
            VAR k := JunoValue.Tan(v[arg2]); BEGIN
              GradVar(arg1, i, k);
              GradVar(arg2, i, v[arg1] * (1.0 + k*k))
            END
          END
      | ConType.Exp =&gt;
          WITH arg1 = con.arg[1] DO
            GradVar(arg1, i, JunoValue.Exp(v[arg1]))
          END
      END
    END GradCon;

  PROCEDURE GradVar(var, i: CARDINAL; k: T) =
  (* Add &quot;k&quot; to the coefficient of variable &quot;var&quot; in &quot;row&quot;. However, if &quot;var&quot;
     is a ghost variable, we add &quot;k&quot; times the row in &quot;a&quot; for &quot;var&quot; to &quot;row&quot;.
  *)
    BEGIN
      IF var &lt; nn THEN
        (* true variable *)
        WITH row = a[i] DO
          row[var] := row[var] + k
        END
      ELSIF var &lt; n THEN
        (* ghost variable *)
        WITH row = a[i], varRow = a[var - nn] DO
          FOR j := 0 TO nn DO
            row[j] := row[j] + (k * varRow[j])
          END
        END
      END
    END GradVar;

  PROCEDURE UpdateVars() =
    BEGIN
      allUpdatesTiny := TRUE;
      FOR i := 0 TO nn - 1 DO
        WITH val = v[i] DO
          allUpdatesTiny := allUpdatesTiny AND (ABS(x[i]) &lt; MinDelta*ABS(val));
          val := val + x[i]
        END
      END
    END UpdateVars;

  PROCEDURE ShowInput() =
    PROCEDURE P(lo, hi: INTEGER; kind: TEXT) =
      BEGIN
        Wr.PutText(logWr, &quot;RedundantSolve.P &quot; &amp; kind &amp; &quot; constraints:\n&quot;);
        IF lo &gt; hi
          THEN Wr.PutText(logWr, &quot;  &lt;none&gt;\n&quot;)
          ELSE FOR i := lo TO hi DO ShowConstraint(c[i], v, n) END
        END
      END P;
    BEGIN
      IF debug &gt;= 1 THEN
        IO.Put(&quot;\n&quot; &amp; Fmt.Pad(&quot;&quot;, length := 45, padChar := '*') &amp; &quot;\n&quot;, logWr)
      END;
      P(0, ghost_cnt - 1, &quot;ghost&quot;);
      P(ghost_cnt, LAST(c), &quot;true&quot;);
      IO.Put(&quot;RedundantSolve.P hints:\n&quot;, logWr);
      IF nn &gt; 0 THEN
      	Wr.PutText(logWr, Fmt.Pad(&quot;&quot;, NameWidth));
      	FOR i := 0 TO nn - 1 DO
      	  Wr.PutText(logWr,
            Fmt.Pad(IndexVal(i, n, v), FieldWidth))
      	END;
        Wr.PutChar(logWr, '\n')
      END;
      ShowVector(&quot;Values&quot;, v, nn);
      Wr.Flush(logWr)
    END ShowInput;

  PROCEDURE <A NAME="ShowValuesAndErrors"><procedure>ShowValuesAndErrors</procedure></A>(itNum: INTEGER) =
    BEGIN
      Wr.PutText(logWr, &quot;Iteration &quot; &amp; Fmt.Int(itNum) &amp; &quot;:\n&quot;);
      ShowVector(&quot;Values:&quot;, v, nn);
      WITH used_err_vec = SUBARRAY(errorVec^, ghost_cnt, true_cnt) DO
        ShowVector(&quot;Errors:&quot;, used_err_vec, true_cnt)
      END
    END ShowValuesAndErrors;

  PROCEDURE <A NAME="ShowSolution"><procedure>ShowSolution</procedure></A>() =
    BEGIN
      Wr.PutText(logWr, &quot;Solution Found:\n&quot;);
      ShowVector(&quot;Values&quot;, v, nn)
    END ShowSolution;

  PROCEDURE <A NAME="GrowArrays"><procedure>GrowArrays</procedure></A>() =
  (* If necessary, make the arrays &quot;x&quot;, &quot;a&quot;, and &quot;errorVec&quot; larger. *)
    BEGIN
      IF nn &gt; NUMBER(x^) OR m &gt; NUMBER(errorVec^) THEN
	VAR
	  n2 := MAX(nn, 2 * NUMBER(x^));
	  m2 := MAX(m, 2 * NUMBER(errorVec^));
	BEGIN
	  x := NEW(REF RedundantLSolve.Vector, n2);
	  a := NEW(REF RedundantLSolve.Matrix, m2, n2 + 1);
	  errorVec := NEW(REF RedundantLSolve.Vector, m2)
	END
      END;
      IF NUMBER(v) &gt; NUMBER(errb^) THEN
        VAR
          n2 := MAX(NUMBER(v), 2 * NUMBER(errb^));
        BEGIN
          errb := NEW(REF RedundantLSolve.Vector, n2)
        END
      END
    END GrowArrays;

  (* PROCEDURE P *)
  BEGIN
    IF logWr = NIL AND debug &gt; 0 THEN
      IF logFileName # NIL
        THEN logWr := IO.OpenWrite(logFileName)
        ELSE logWr := Stdio.stdout
      END
    END;
    &lt;* ASSERT NUMBER(x^) + 1 = NUMBER(a[0]) *&gt;
    &lt;* ASSERT NUMBER(errorVec^) = NUMBER(a^) *&gt;
    GrowArrays();
    IF debug &gt;= 1 THEN ShowInput() END;
    VAR
      cnt := MaxIterations;
      error := EvalConstraints();
    BEGIN
      IF true_cnt &gt; 0 THEN
        TRY
          WHILE (NOT goodEnough) AND (cnt &gt; 0) DO
            IF debug &gt;= 2 THEN ShowValuesAndErrors(MaxIterations - cnt) END;
            BuildMatrix();
            WITH matrix = SUBARRAY(a^, ghost_cnt, true_cnt) DO
              RedundantLSolve.P(true_cnt, nn, matrix, x^)
            END;
            IF debug &gt;= 2 THEN ShowVector(&quot;Deltas&quot;, x^, nn) END;
      	    UpdateVars();
      	    IF allUpdatesTiny THEN EXIT END;
      	    IF oneStep THEN RETURN TRUE END;
      	    error := EvalConstraints();
      	    DEC(cnt)
      	  END;
          IF debug &gt;= 1 THEN
            IF goodEnough
              THEN ShowSolution()
              ELSE IO.Put(&quot;No Solution Found.\n\n&quot;, logWr)
            END
      	  END;
          RETURN goodEnough
      	FINALLY
      	  EtpLogP0(true_cnt, nn, ghost_cnt, MaxIterations-cnt);
      	  IF debug &gt;= 2 THEN Wr.Flush(logWr) END
        END (* TRY *)
      END; (* IF *)
      RETURN TRUE
    END
  END P;
</PRE><P>
PROCEDURE SolveLoop(): BOOLEAN =
  CONST
    MaxCount = 20;
  VAR
    count := 0;
    goodEnough, allUpdatesTiny: BOOLEAN;
  BEGIN
    LOOP
      EVAL EvalConstraints();
      IF goodEnough OR (count = MaxCount) THEN EXIT END;
      BuildMatrix();
      EVAL RedundantLSolve();
      UpdateVars();
      IF allUpdatesTiny THEN EXIT END;
      INC(count)
    END;
    RETURN goodEnough
  END
END;


<P><PRE>BEGIN
END RedundantSolve.
</PRE>
</inModule>
<HR>
<A NAME="x1">interface JunoValue is in:
</A><UL>
<LI><A HREF="JunoValue.i3.html#0TOP0">juno-machine/src/JunoValue.i3</A>
<LI><A HREF="../../pkl-fonts/src/JunoValue.i3.html#0TOP0">pkl-fonts/src/JunoValue.i3</A>
</UL>
<P>
<PRE>























</PRE>
</BODY>
</HTML>
