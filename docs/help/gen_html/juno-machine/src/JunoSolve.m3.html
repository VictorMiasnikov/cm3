<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: juno-machine/src/JunoSolve.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>juno-machine/src/JunoSolve.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright (C) 1992, Digital Equipment Corporation                         
 All rights reserved.                                                      
 See the file COPYRIGHT for a full description.                            
                                                                           
 Last modified on Fri Jun  9 10:55:11 PDT 1995 by heydon                       
      modified on Thu Dec  8 15:46:40 1994 by gnelson                      
      modified on Mon Oct 31 18:34:30 PST 1994 by isard                    
      modified on Tue Jul 21 01:55:06 PDT 1992 by myers                    
<PRE>&lt;* PRAGMA LL *&gt;

MODULE <module><implements><A HREF="JunoSolve.i3.html">JunoSolve</A></implements></module>;

IMPORT <A HREF="Egraph.i3.html">Egraph</A>, <A HREF="Equiv.i3.html">Equiv</A>, <A HREF="RedundantSolve.i3.html">RedundantSolve</A> AS NonLinearSolve, <A HREF="RTVal.i3.html">RTVal</A>;
IMPORT <A HREF="../../libm3/derived/RefSeq.i3.html">RefSeq</A>, <A HREF="../../m3core/src/word/Word.i3.html">Word</A>;

EXCEPTION Unsolvable;

TYPE
  Type = { Any, Pair, Num, Text, Null };
  (* There is a flat partial order on types, with &quot;Type.Any&quot; as bottom. *)

VAR (* CONST *)
  PlusOp, TimesOp, SinOp, CosOp, AtanOp, ExpOp, PairOp := Init(NEW(Var));
  zero := RTVal.FromReal(0.0);
  emptyString := RTVal.FromText(&quot;&quot;);

TYPE
  EC = Private;
  (* The type &quot;EC&quot; represents an equivalence class with an extra field to
     record if the class contains a pair expression. *)

REVEAL
  <A NAME="Private">Private</A> = Egraph.T BRANDED &quot;JunoSolve.Private&quot; OBJECT
    cons: EC;
  OVERRIDES
    union := Merge;
  END;
  <A NAME="Var">Var</A> = Public BRANDED &quot;JunoSolve.Var&quot; OBJECT
    type: Type;
    index: INTEGER;
    marked: BOOLEAN;
    uses: UseList;
    next, availLink: Var;
  END;

TYPE
  UseList = REF RECORD
    c: Constraint;
    mask := 0;
    next, availLink: UseList
  END;
</PRE> A <CODE>cons node</CODE> is a node whose <CODE>car</CODE> is <CODE>PairOp</CODE>. It represents a pair
   expression <CODE>(x, y)</CODE>.
<P>
   The call <CODE>v.union(w)</CODE> where <CODE>v</CODE> is of type <CODE>EC</CODE> requires that <CODE>v.root #
   w.root</CODE>.
<P>
   The following are invariants on valid <CODE>EC</CODE>'s and <CODE>Var</CODE>'s:
<P>
   P1: If an equivalence class whose root is <CODE>rt</CODE> contains a cons node, then
       <CODE>rt.cons</CODE> points to a cons node in <CODE>rt</CODE>'s class. Conversely, if <CODE>rt</CODE>'s
       class does not contain a cons node, then <CODE>rt.cons = NIL</CODE>.
<P>
   P2: The <CODE>type</CODE> field of a <CODE>Var</CODE> records its type; it is only valid for a
       variable that is the root of its equivalence class.
<P>
   P3: For any variable <CODE>rt</CODE> that is the root of its equivalence class,
       <CODE>rt.known =&gt; rt.type # Type.Any</CODE>. (The converse is not true, since a
       REAL or TEXT constraint can set the type of an unknown.)
<P>
   P4: The <CODE>index</CODE> field of a <CODE>Var</CODE> <CODE>v</CODE> reflects the index in the global array
       <CODE>numeric_vars</CODE> of the numeric unknown represented by <CODE>v</CODE>. It is <CODE>-1</CODE> if
       <CODE>v</CODE> does not correspond to any variable in the array.
<P>
   P5: The <CODE>uses</CODE> and <CODE>next</CODE> fields of a <CODE>Var</CODE> are relevant only in
       <CODE>NumericSolve()</CODE>, and they are valid only for <CODE>Var</CODE>'s that are the
       roots of their equivalence classes. 

<P><PRE>TYPE
  Args = ARRAY [0..2] OF Var;
  ConType = { Equal, Cons, Plus, Times, Atan, Sin, Cos, Exp, Real, Text };

REVEAL
  <A NAME="Constraint">Constraint</A> = BRANDED &quot;JunoSolve.Constraint&quot; OBJECT
    type: ConType;
    arg: Args;
    hintCnt: CARDINAL;
    mask: CARDINAL;
    nls_con: NonLinearSolve.Constraint;
    unknown: Var;
    nls_con_mask: CARDINAL;
    next: Constraint;
    availLink: Constraint
  END;
  (* These fields are only valid during the procedure &quot;NumericSolve&quot;. *)

VAR
  mu := NEW(MUTEX);
  &lt;* LL &gt;= { mu } *&gt;
  unifyList := NEW(RefSeq.T).init(sizeHint := 100);
  numeric_vars := NEW(Vars, 60);
  numeric_vals := NEW(REF ARRAY OF RTVal.Real, 60);
  numeric_con := NEW(REF ARRAY OF NonLinearSolve.Constraint, 30);
</PRE> The <CODE>unify</CODE> list <CODE>(u1, v1, u2, v2, ...)</CODE> contains all pairs <CODE>(ui, vi)</CODE> of
   cons nodes that are equivalent, but have not yet been merged.
<P>
   All <CODE>Var</CODE>'s in <CODE>numeric_vars</CODE> are roots of their equivalence classes. 

<P><PRE>PROCEDURE <A NAME="New"><procedure>New</procedure></A>(known := FALSE; val: RTVal.T := NIL): Var =
  BEGIN RETURN NewPair(NIL, NIL, known, val) END New;

VAR
  varAvail, varInUse: Var := NIL;
  conAvail, conInUse: Constraint := NIL;
  useAvail, useInUse: UseList := NIL;

PROCEDURE <A NAME="NewPair"><procedure>NewPair</procedure></A>(car, cdr: Var; known := FALSE; val: RTVal.T := NIL): Var=
  VAR res: Var; BEGIN
    IF varAvail # NIL
      THEN res := varAvail; varAvail := varAvail.availLink
      ELSE res := NEW(Var)
    END;
    res.availLink := varInUse;
    varInUse := res;
    res.known := known;
    res.val := val;
    res.car := car;
    res.cdr := cdr;
    RETURN Init(res)
  END NewPair;

PROCEDURE <A NAME="NewCon"><procedure>NewCon</procedure></A>(type: ConType): Constraint =
  VAR res: Constraint; BEGIN
    IF conAvail # NIL
      THEN res := conAvail; conAvail := conAvail.availLink
      ELSE res := NEW(Constraint)
    END;
    res.availLink := conInUse;
    conInUse := res;
    res.type := type;
    RETURN res
  END NewCon;

PROCEDURE <A NAME="NewEqual"><procedure>NewEqual</procedure></A>(x, y: Var): Constraint =
  VAR res := NewCon(ConType.Equal); BEGIN
    res.arg[0] := x; res.arg[1] := y;
    RETURN res
  END NewEqual;

PROCEDURE <A NAME="NewCons"><procedure>NewCons</procedure></A>(x, y, z: Var): Constraint =
  VAR res := NewCon(ConType.Cons); BEGIN
    res.arg[0] := x; res.arg[1] := y; res.arg[2] := z;
    RETURN res
  END NewCons;

PROCEDURE <A NAME="NewPlus"><procedure>NewPlus</procedure></A>(x, y, z: Var): Constraint =
  VAR res := NewCon(ConType.Plus); BEGIN
    res.arg[0] := x; res.arg[1] := y; res.arg[2] := z;
    RETURN res
  END NewPlus;

PROCEDURE <A NAME="NewTimes"><procedure>NewTimes</procedure></A>(x, y, z: Var): Constraint =
  VAR res := NewCon(ConType.Times); BEGIN
    res.arg[0] := x; res.arg[1] := y; res.arg[2] := z;
    RETURN res
  END NewTimes;

PROCEDURE <A NAME="NewAtan"><procedure>NewAtan</procedure></A>(x, y, z: Var): Constraint =
  VAR res := NewCon(ConType.Atan); BEGIN
    res.arg[0] := x; res.arg[1] := y; res.arg[2] := z;
    RETURN res
  END NewAtan;

PROCEDURE <A NAME="NewSin"><procedure>NewSin</procedure></A>(x, y: Var): Constraint =
  VAR res := NewCon(ConType.Sin); BEGIN
    res.arg[0] := x; res.arg[1] := y;
    RETURN res
  END NewSin;

PROCEDURE <A NAME="NewCos"><procedure>NewCos</procedure></A>(x, y: Var): Constraint =
  VAR res := NewCon(ConType.Cos); BEGIN
    res.arg[0] := x; res.arg[1] := y;
    RETURN res
  END NewCos;

PROCEDURE <A NAME="NewExp"><procedure>NewExp</procedure></A>(x, y: Var): Constraint =
  VAR res := NewCon(ConType.Exp); BEGIN
    res.arg[0] := x; res.arg[1] := y;
    RETURN res
  END NewExp;

PROCEDURE <A NAME="NewReal"><procedure>NewReal</procedure></A>(x: Var): Constraint =
  VAR res := NewCon(ConType.Real); BEGIN
    res.arg[0] := x;
    RETURN res
  END NewReal;

PROCEDURE <A NAME="NewText"><procedure>NewText</procedure></A>(x: Var): Constraint =
  VAR res := NewCon(ConType.Text); BEGIN
    res.arg[0] := x;
    RETURN res
  END NewText;

PROCEDURE <A NAME="NewUse"><procedure>NewUse</procedure></A>(con: Constraint; next: UseList): UseList =
  VAR res: UseList; BEGIN
    IF useAvail # NIL THEN
      res := useAvail; useAvail := useAvail.availLink;
      res.c := con; res.mask := 0; res.next := next
    ELSE
      res := NEW(UseList, c := con, next := next)
    END;
    res.availLink := useInUse;
    useInUse := res;
    RETURN res
  END NewUse;

PROCEDURE <A NAME="Dispose"><procedure>Dispose</procedure></A>() =
  BEGIN
    VAR l := varInUse; BEGIN
      IF l # NIL THEN
      	WHILE l.availLink # NIL DO l := l.availLink END;
      	l.availLink := varAvail;
      	varAvail := varInUse;
      	varInUse := NIL
      END
    END;
    VAR l := conInUse; BEGIN
      IF l # NIL THEN
      	WHILE l.availLink # NIL DO l := l.availLink END;
      	l.availLink := conAvail;
      	conAvail := conInUse;
      	conInUse := NIL
      END
    END;
    VAR l := useInUse; BEGIN
      IF l # NIL THEN
      	WHILE l.availLink # NIL DO l := l.availLink END;
      	l.availLink := useAvail;
      	useAvail := useInUse;
      	useInUse := NIL
      END
    END
  END Dispose;

CONST NoIndex = -1;

PROCEDURE <A NAME="Init"><procedure>Init</procedure></A>(v: Var): Var =
</PRE><BLOCKQUOTE><EM> Modify <CODE>v</CODE> into a variable <CODE>v'</CODE> such that <CODE>v'.root = v'</CODE> and such that
   P1 - P5 hold of <CODE>v'</CODE>. Return <CODE>v'</CODE>. </EM></BLOCKQUOTE><PRE>
  BEGIN
    EVAL Egraph.T.init(v);
    v.index := NoIndex;
    v.marked := FALSE;
    v.uses := NIL;
    v.next := NIL;
    IF v.known THEN
      TYPECASE v.val OF &lt;* NOWARN *&gt;
      | RTVal.Null =&gt; v.type := Type.Null
      | RTVal.Number =&gt; v.type := Type.Num
      | RTVal.Text =&gt; v.type := Type.Text
      | RTVal.Pair =&gt; v.type := Type.Pair
      END
    ELSE
      v.type := Type.Any
    END;
    IF v.car = PairOp
      THEN v.cons := v
      ELSE v.cons := NIL
    END;
    RETURN v
  END Init;

PROCEDURE <A NAME="P"><procedure>P</procedure></A>(READONLY c: ARRAY OF Constraint): BOOLEAN =
  BEGIN
    LOCK mu DO
      IF NUMBER(numeric_vars^) &lt; 3 * NUMBER(c) THEN
         numeric_vars := NEW(Vars, 3 * NUMBER(c));
      END;
      TRY
        EVAL unifyList.init();
        ConstructECs(c);
        UnifyClose();
        NumericSolve(c);
        ConstructSoln(c);
      EXCEPT
        Equiv.Forbidden, Unsolvable =&gt; RETURN FALSE
      END;
      RETURN TRUE
    END
  END P;

PROCEDURE <A NAME="EtpLogConstraint"><procedure>EtpLogConstraint</procedure></A>(&lt;*UNUSED*&gt;type: INTEGER) =
</PRE><BLOCKQUOTE><EM> Types are: 0 = Equal; 1 = CONS; 2 = +; 3 = *; 4 = REAL; 5 = TEXT; 6 = ATAN;
   7 = SIN; 8 = COS; 9 = EXP. </EM></BLOCKQUOTE><PRE>
  BEGIN END EtpLogConstraint;

PROCEDURE <A NAME="ConstructECs"><procedure>ConstructECs</procedure></A>(READONLY c: ARRAY OF Constraint)
    RAISES {Equiv.Forbidden} =
  VAR con: Constraint; BEGIN
    FOR i := FIRST(c) TO LAST(c) DO
      con := c[i];
      CASE con.type OF &lt;* NOWARN *&gt;
      | ConType.Equal =&gt;
          EtpLogConstraint(0);
          WITH r1 = con.arg[0].root, r2 = con.arg[1].root DO
            IF r1 # r2 THEN EVAL r1.union(r2) END
          END
      | ConType.Cons =&gt;
          EtpLogConstraint(1);
          VAR pair := List3(PairOp, con.arg[1], con.arg[2]); BEGIN
            pair.type := Type.Pair;
            EVAL (con.arg[0].root).union(pair)
          END
      | ConType.Plus =&gt;
          EtpLogConstraint(2);
          VAR plus := List3(PlusOp, con.arg[1], con.arg[2]); BEGIN
            SetFuncType(plus, Type.Num);
            EVAL (con.arg[0].root).union(plus)
          END
      | ConType.Times =&gt;
          EtpLogConstraint(3);
          VAR times := List3(TimesOp, con.arg[1], con.arg[2]); BEGIN
            SetFuncType(times, Type.Num);
            EVAL (con.arg[0].root).union(times)
          END
      | ConType.Atan =&gt;
          EtpLogConstraint(6);
          VAR atan := List3(AtanOp, con.arg[1], con.arg[2]); BEGIN
            SetFuncType(atan, Type.Num);
            EVAL (con.arg[0].root).union(atan)
          END
      | ConType.Sin =&gt;
          EtpLogConstraint(7);
          VAR sin := List2(SinOp, con.arg[1]); BEGIN
            SetFuncType(sin, Type.Num);
            EVAL (con.arg[0].root).union(sin)
          END
      | ConType.Cos =&gt;
          EtpLogConstraint(8);
          VAR cos := List2(CosOp, con.arg[1]); BEGIN
            SetFuncType(cos, Type.Num);
            EVAL (con.arg[0].root).union(cos)
          END
      | ConType.Exp =&gt;
          EtpLogConstraint(9);
          VAR exp := List2(ExpOp, con.arg[1]); BEGIN
            SetFuncType(exp, Type.Num);
            EVAL (con.arg[0].root).union(exp)
          END
      | ConType.Real =&gt; EtpLogConstraint(4); SetType(con.arg[0], Type.Num)
      | ConType.Text =&gt; EtpLogConstraint(5); SetType(con.arg[0], Type.Text)
      END
    END;
  END ConstructECs;

PROCEDURE <A NAME="SetFuncType"><procedure>SetFuncType</procedure></A>(v: Var; t: Type) RAISES {Equiv.Forbidden} =
</PRE><BLOCKQUOTE><EM> Sets the type of <CODE>v</CODE> to <CODE>t</CODE>. Verifies that the type of each argument in
   <CODE>v.cdr</CODE> is at most <CODE>t</CODE> in the flat partial order with <CODE>Type.Any</CODE> as bottom,
   and promotes the type of each argument with type <CODE>Type.Any</CODE> to <CODE>t</CODE>.
<P>
   Requires <CODE>v</CODE> to be a root. Raises <CODE>Equiv.Forbidden</CODE> if some argument in
   <CODE>v.cdr</CODE> has a type that is not at most <CODE>t</CODE>. </EM></BLOCKQUOTE><PRE>
  BEGIN
    &lt;* ASSERT v.root = v *&gt;
    v.type := t;
    VAR curr: Var := v.cdr; BEGIN
      WHILE curr # NIL DO
        SetType(curr.car, t);
        curr := curr.cdr
      END
    END
  END SetFuncType;

PROCEDURE <A NAME="SetType"><procedure>SetType</procedure></A>(v: Var; t: Type) RAISES {Equiv.Forbidden} =
</PRE><BLOCKQUOTE><EM> Sets the type of the class containing <CODE>v</CODE> to <CODE>t</CODE>, or raises
   <CODE>Equiv.Forbidden</CODE> if the current type of <CODE>v</CODE>'s class is different from <CODE>t</CODE>
   and greater than bottom. </EM></BLOCKQUOTE><PRE>
  VAR rt: Var := v.root; BEGIN
    IF rt.type = Type.Any THEN
      rt.type := t
    ELSIF rt.type # t THEN
      RAISE Equiv.Forbidden
    END;
  END SetType;

PROCEDURE <A NAME="List3"><procedure>List3</procedure></A>(x, y, z: Egraph.T): Var =
  BEGIN
    RETURN NewPair(car := x, cdr := NewPair(car := y,
      cdr := NewPair(car := z, cdr := NIL)))
  END List3;

PROCEDURE <A NAME="List2"><procedure>List2</procedure></A>(x, y: Egraph.T): Var =
  BEGIN
    RETURN NewPair(car := x, cdr := NewPair(car := y, cdr := NIL))
  END List2;

PROCEDURE <A NAME="Merge"><procedure>Merge</procedure></A>(x: EC; y: Equiv.T): Equiv.T RAISES {Equiv.Forbidden} =
</PRE><BLOCKQUOTE><EM> By the precondition on <CODE>Equiv.T.union()</CODE>, <CODE>x</CODE> and <CODE>y</CODE> must be the roots of
   their equivalence classes. They will also be of type <CODE>JunoSolve.Var</CODE>.
   Let <CODE>rx</CODE> and <CODE>ry</CODE> be the results of narrowing <CODE>x</CODE> and <CODE>y</CODE>, respectively, to
   the type <CODE>JunoSolve.Var</CODE>. Let <CODE>root</CODE> be the root of their resulting combined
   equivalence class. Then <CODE>Merge</CODE> either raises the exception or establishes
   the following post-conditions:
<P>
   Q1: <CODE>root.type</CODE> = the meet of <CODE>rx.type</CODE> and <CODE>ry.type</CODE> in the flat partial
       order. If the meet is undefined, raise <CODE>Equiv.Forbidden</CODE>.
<P>
   Q2: <CODE>root.known = rx.known OR ry.known</CODE>. <CODE>root.val = rx.val</CODE> if <CODE>rx.known</CODE>,
       and <CODE>root.val = ry.val</CODE> if <CODE>ry.known</CODE>. If both are known, but their
       values are different, raise <CODE>Equiv.Forbidden</CODE>. If <CODE>NOT root.known</CODE>,
       then <CODE>root.val</CODE> is <CODE>rx.val</CODE> or <CODE>ry.val</CODE> for whichever of those two
       values is non-NIL, or either if both are non-NIL.
<P>
   Q3: <CODE>root.cons</CODE> is selected from <CODE>rx.cons</CODE> and <CODE>ry.cons</CODE> so that it is
       <CODE>NIL</CODE> only if both of them are. If both are non-NIL, then the values
       <CODE>rx.cons</CODE> and <CODE>ry.cons</CODE> are added to the global unify list.
<P>
   Q4: If <CODE>root.cons # NIL</CODE> and <CODE>root.val</CODE> is a pair, then the car and cdr of
       <CODE>root.val</CODE> have been propagated to the arguments of the cons. If
       <CODE>root.known</CODE> and <CODE>root.cons # NIL</CODE>, raise <CODE>Equiv.Forbidden</CODE> if the
       known value is not a pair. </EM></BLOCKQUOTE><PRE>
  &lt;* LL &gt;= { mu } *&gt;
  VAR
    rx: Var := x; ry: Var := y;
    propagateKnowns := rx.known # ry.known AND
                       (rx.cons = NIL) = rx.known AND
                       (ry.cons = NIL) = ry.known;
    propagateHints  := (rx.val # NIL) # (ry.val # NIL) AND
                       (rx.cons = NIL) = (rx.val # NIL) AND
                       (ry.cons = NIL) = (ry.val # NIL);
    (* Work is necessary to establish Q4 iff &quot;propagateKnowns OR
       propagateHints&quot;. *)
  BEGIN
    &lt;* ASSERT rx # ry *&gt;
    VAR root: Var := Egraph.T.union(rx, ry); nonroot: Var; BEGIN
      IF root = rx THEN nonroot := ry ELSE nonroot := rx END;
      (* Establish Q1 *)
      IF root.type = Type.Any THEN
        root.type := nonroot.type
      ELSIF nonroot.type # Type.Any AND root.type # nonroot.type THEN
        RAISE Equiv.Forbidden
      END;
      (* Establish Q2 *)
      IF nonroot.known THEN
        IF NOT root.known THEN
          root.known := TRUE; root.val := nonroot.val
        ELSIF NOT RTVal.Equal(rx.val, ry.val) THEN
          RAISE Equiv.Forbidden
        END;
      ELSIF NOT root.known THEN
        IF nonroot.val # NIL THEN root.val := nonroot.val END
      END;
      (* Establish Q3 *)
      IF nonroot.cons # NIL THEN
        IF root.cons = NIL THEN
          root.cons := nonroot.cons;
        ELSE
          unifyList.addhi(rx.cons);
          unifyList.addhi(ry.cons)
        END
      END;
      (* Establish Q4 *)
      IF propagateHints OR propagateKnowns THEN
        TYPECASE root.val OF
        | RTVal.Pair (p) =&gt;
            Fix(root.cons.cdr.car,     root.known, p.car);
            Fix(root.cons.cdr.cdr.car, root.known, p.cdr)
        ELSE
            IF root.known THEN RAISE Equiv.Forbidden END
        END;
      END;
      RETURN root
    END;
  END Merge;

PROCEDURE <A NAME="Fix"><procedure>Fix</procedure></A>(v: Var; known: BOOLEAN; val: RTVal.T) RAISES {Equiv.Forbidden} =
  VAR t := New(known, val); BEGIN EVAL (v.root).union(t) END Fix;

PROCEDURE <A NAME="UnifyClose"><procedure>UnifyClose</procedure></A>() RAISES {Equiv.Forbidden} =
  BEGIN
    WHILE unifyList.size() # 0 DO
      VAR
        y: EC := unifyList.remhi();
        x: EC := unifyList.remhi();
      BEGIN
        &lt;* ASSERT x.car = PairOp AND y.car = PairOp *&gt;
        x := x.cdr; y := y.cdr;
        WHILE x # NIL DO
          VAR xa := x.car.root; ya := y.car.root; BEGIN
            IF xa # ya THEN EVAL xa.union(ya) END
          END;
          x := x.cdr; y := y.cdr
        END;
        &lt;* ASSERT y = NIL *&gt;
      END
    END
  END UnifyClose;

PROCEDURE <A NAME="NumericSolve"><procedure>NumericSolve</procedure></A>(READONLY c: ARRAY OF Constraint) RAISES { Unsolvable } =
</PRE><BLOCKQUOTE><EM> By a {\em variable}, we mean a <CODE>Var</CODE> that is the root of its equivalence
   class and appears in a numeric constraint in <CODE>c</CODE>. We say a variable <CODE>v</CODE> is
   {\em labeled} if <CODE>v.index # NoIndex</CODE> and <CODE>numeric_vars[v.index] = v</CODE>. We
   say a variable <CODE>v</CODE> {\em occurs} in a constraint <CODE>con</CODE> if it is unlabeled
   and <CODE>con</CODE> contains a <CODE>Var</CODE> whose root is <CODE>v</CODE>.
<P>
   Q1: For each constraint <CODE>con</CODE>, <CODE>con.hintCnt</CODE> is the number of distinct
       unlabeled variables occurring in <CODE>con</CODE>, and <CODE>con.mask</CODE> is <CODE>2_b2b1b0</CODE>,
       where <CODE>bi</CODE> == ``<CODE>con.arg[i]</CODE> is unlabeled''.
<P>
   Q2: There are two linked lists of constraints whose heads are <CODE>ghostReady</CODE>
       and <CODE>trueReady</CODE>. Constraints on both lists have <CODE>hintCnt &lt;= 1</CODE>, and
       have valid <CODE>nls_con</CODE> and <CODE>unknown</CODE> fields set by Functional().
       Functional constraints with <CODE>hintCnt</CODE> equal to 1 are on the
       <CODE>ghostReady</CODE> queue, while non-functional constraints with <CODE>hintCnt</CODE>
       equal to 1 are on the <CODE>trueReady</CODE> queue. All constraints with <CODE>hintCnt</CODE>
       equal to 0 appear on one or the other of the two queues.
<P>
   Q3: For any variable <CODE>v</CODE>, <CODE>v.uses</CODE> is a list that contains an entry <CODE>ul</CODE>
       for each constraint in which <CODE>v</CODE> occurs. The value <CODE>ul.c</CODE> is
       the constraint, and <CODE>ul.mask</CODE> is <CODE>2_b2b1b0</CODE> where <CODE>bi</CODE> == <CODE>(ul.c.arg[i]
       = v)</CODE>.
<P>
   Q4: The hint for an unlabeled variable is either <CODE>NIL</CODE> or a numeric value.
<P>
   Q5: The variable <CODE>var_cnt</CODE> is the number of non-constant variables. The
       variable <CODE>numeric_cnt</CODE> is the number of numeric constraints.
<P>
   Q6: Every constant has been labeled by a number between <CODE>next_const + 1</CODE>
       and <CODE>LAST(numeric_vars^)</CODE>.
<P>
   Q7: The variable <CODE>hvl</CODE> is the list of hinted unlabeled variables (possibly
       containing some variables which have become labeled), and <CODE>uvl</CODE> is the
       list of unhinted, unlabeled variables (also possible containing some
       variables which have become labeled).
<P>
   Q8: numeric_cnt &lt;= NUMBER(numeric_con).
<P>
   Q9: All knowns (constants) are labeled, and all unknowns are unlabeled.
<P>
   R1: The variable <CODE>lo</CODE> is the number of labeled ``true'' variables, each
       of which has been labeled with a number between <CODE>0</CODE> and <CODE>lo - 1</CODE>.
<P>
   R2: The expression <CODE>var_cnt - hi</CODE> is the number of labeled ghost
       variables, each of which has been labeled by a number between <CODE>hi</CODE>
       and <CODE>var_cnt - 1</CODE>.
<P>
   R3: The variable <CODE>c_lo</CODE> is the number of ghost constraints; these
       constraints are stored in the first <CODE>c_lo</CODE> entries of <CODE>numeric_con</CODE>.
       The expression <CODE>numeric_cnt - c_hi</CODE> is the number of true constraints;
       these constraints are stored in the last entries of <CODE>numeric_con</CODE>.
<P>
<PRE>
      DURING VARIABLE PROCESSING:
      
                  numeric_vars[]           __
                     ________	      |..|
                    |        |	      |..| = unused
                    |  True  |	      |__|
                    |  Vars  |
                    |________|
              lo -&gt; |........|
                    |........|    numeric_con[]
              hi -&gt; |________|    _____________
                    |        |   |             |
                    |  Ghost |   |    Ghost    |
                    |  Vars  |   | Constraints |
                    |________|   |_____________|
         var_cnt -&gt; |........|   |.............| &lt;- c_lo
                    |........|   |.............|
      next_const -&gt; |________|   |_____________| &lt;- c_hi
                    |        |   |             |
                    | Consts |   |     True    |
                    |________|   | Constraints |
      			     |_____________|
                                 |.............| &lt;- numeric_cnt
                                 |.............|
      			     |_____________|
      
      AFTER VARIABLE PROCESSING:
      
                  numeric_vars[] numeric_vals[]
                     ________       ________
                    |        |     |        |
                    |  True  |     |  True  |
                    |  Vars  |     |  Vals  |
                    |        |     |        |    numeric_con[]
              hi -&gt; |________|     |________|    _____________
              lo -&gt; |        |     |        |   |             |
                    |        |     |        |   |    Ghost    |
                    |        |     |        |   | Constraints |
                    |  Ghost |     |  Ghost |   |             |
                    |  Vars  |     |  Vals  |   |             |
                    |________|     |________|   |_____________| &lt;- c_hi
         var_cnt -&gt; |........|     |........|   |             | &lt;- c_lo
                    |........|     |........|   |             |
      next_const -&gt; |________|     |________|   |     True    |
                    |        |     |        |   | Constraints |
                    | Consts |     | Consts |   |_____________|
                    |________|     |________|   |.............| &lt;- numeric_cnt
            		                    |.............|
                                                |_____________|
</EM></BLOCKQUOTE><PRE>
  VAR
    lo, hi, c_lo, c_hi: CARDINAL;
    var_cnt: CARDINAL := 0;		 (* total # of variables *)
    numeric_cnt: CARDINAL := 0;		 (* total # of numeric constraints *)
    next_const := LAST(numeric_vars^);	 (* index of next known value *)
    ghostReady, trueReady: Constraint := NIL;
    hvl, uvl: Var := NIL;

  PROCEDURE NumericArgCnt(c: Constraint): CARDINAL =
  (* Return the number of variables in constraint &quot;c&quot; if it is a numeric
     constraint; 0 otherwise. *)
    BEGIN
      CASE c.type OF
	ConType.Plus, ConType.Times, ConType.Atan =&gt; RETURN 3
      | ConType.Sin, ConType.Cos, ConType.Exp =&gt; RETURN 2
      ELSE RETURN 0
      END
    END NumericArgCnt;

  PROCEDURE AddConToQueue(con: Constraint) =
    BEGIN
      IF Functional(con)
        THEN con.next := ghostReady; ghostReady := con
        ELSE con.next := trueReady; trueReady := con
      END
    END AddConToQueue;

  PROCEDURE UpdateUses(l: UseList) =
    BEGIN
      WHILE l # NIL DO
        DEC(l.c.mask, l.mask);
        DEC(l.c.hintCnt);
        IF l.c.hintCnt = 1 THEN AddConToQueue(l.c) END;
        l := l.next
      END
    END UpdateUses;

  (* NumericSolve *)
  BEGIN
    FOR i := FIRST(c) TO LAST(c) DO
      VAR con := c[i]; cnt := NumericArgCnt(con); BEGIN
        IF cnt &gt; 0 THEN
          INC(numeric_cnt);
          con.hintCnt := 0;
          con.mask := 0;
          FOR j := 0 TO cnt - 1 DO
            VAR v: Var := con.arg[j].root; BEGIN
              IF NOT v.known THEN	 (* &quot;v&quot; is an unknown *)
                IF v.uses = NIL THEN	 (* first time seeing &quot;v&quot; *)
                  INC(var_cnt);
                  (* disregard any non-numeric hint *)
                  IF v.val # NIL AND NOT ISTYPE(v.val, RTVal.Number) THEN
                    v.val := NIL
                  END;
                  IF v.val = NIL
                    THEN v.next := uvl; uvl := v (* unhinted *)
                    ELSE v.next := hvl; hvl := v (*  hinted  *)
                  END
                END;
                (* add &quot;con&quot; to &quot;v.uses&quot; if necessary *)
                IF v.uses = NIL OR v.uses.c # con THEN
                  v.uses := NewUse(con, next := v.uses);
                  INC(con.hintCnt)
                END;
                (* update the variable and constraint masks *)
                VAR bit := Word.LeftShift(1, j); BEGIN
                  v.uses.mask := Word.Or(v.uses.mask, bit);
                  con.mask := Word.Or(con.mask, bit)
                END
              ELSIF v.index = NoIndex THEN
                numeric_vars[next_const] := v;
                v.index := next_const;
                DEC(next_const)
              END
            END
          END; (* FOR *)
          (* Add to &quot;ghostReady&quot; or &quot;trueReady&quot; queue if necessary *)
          IF con.hintCnt &lt;= 1 THEN AddConToQueue(con) END
        END
      END
    END;
    (* Q1 - Q7, Q9 *)
    IF numeric_cnt = 0 THEN RETURN END;

    IF numeric_cnt &gt; NUMBER(numeric_con^) THEN
      numeric_con := NEW(REF ARRAY OF NonLinearSolve.Constraint,
        MAX(numeric_cnt, 2 * NUMBER(numeric_con^)))
    END;
    (* Q1 - Q8, Q9 *)

    lo := 0;
    hi := var_cnt;
    c_lo := 0;
    c_hi := numeric_cnt;
    LOOP
      (* Q1 - Q8, R1 - R3 *)
      IF ghostReady # NIL THEN
       	VAR con := ghostReady; BEGIN
          &lt;* ASSERT con.hintCnt &lt;= 1 *&gt;
          ghostReady := ghostReady.next;
          IF con.hintCnt = 0 THEN
            (* add &quot;con&quot; to the &quot;trueReady&quot; queue *)
            con.next := trueReady;
            trueReady := con
          ELSE
            (* label &quot;con.unknown&quot; as a ghost variable *)
            DEC(hi);
            numeric_vars[hi] := con.unknown;
            con.unknown.index := hi;
            UpdateUses(con.unknown.uses);
            (* finish constructing &quot;con.nls_con&quot; *)
            con.nls_con.arg[0] := hi;
            (* make &quot;con.nls_con&quot; a ghost constraint *)
            numeric_con[c_lo] := con.nls_con;
            INC(c_lo)
          END
        END
      ELSIF hvl # NIL THEN
        IF hvl.index = NoIndex THEN
          (* make &quot;hvl&quot; a true variable *)
          numeric_vars[lo] := hvl;
          hvl.index := lo;
          INC(lo);
          UpdateUses(hvl.uses)
        END;
        hvl := hvl.next
      ELSIF trueReady # NIL THEN
        VAR con := trueReady; BEGIN
          &lt;* ASSERT con.hintCnt &lt;= 1 *&gt;
          trueReady := trueReady.next;
          IF con.hintCnt = 1 THEN
            (* make &quot;con.unknown&quot; a true variable *)
            numeric_vars[lo] := con.unknown;
            con.unknown.index := lo;
            INC(lo);
            UpdateUses(con.unknown.uses)
          END;
          (* finish constructing &quot;con.nls_con&quot; from &quot;con.nls_con_mask&quot; *)
          VAR i := 0; BEGIN
            WHILE con.nls_con_mask &gt; 0 DO
              IF Word.And(con.nls_con_mask, 2_1) = 2_1 THEN
                con.nls_con.arg[i] := con.unknown.index
              END;
              INC(i);
              con.nls_con_mask := Word.RightShift(con.nls_con_mask, 1)
            END
          END;
          (* make &quot;con.nls_con&quot; a true constraint *)
          DEC(c_hi);
          numeric_con[c_hi] := con.nls_con
        END
      ELSIF uvl # NIL THEN
        IF uvl.index = NoIndex THEN
          (* make &quot;uvl&quot; a true variable *)
          numeric_vars[lo] := uvl;
          uvl.index := lo;
          INC(lo);
          UpdateUses(uvl.uses)
        END;
        uvl := uvl.next
      ELSE EXIT
      END
    END;

    (* Solve *)
    InitVals(numeric_vals, numeric_vars, lo, next_const);
    WITH constraints = SUBARRAY(numeric_con^, 0, numeric_cnt) DO
      IF NOT NonLinearSolve.P(lo, var_cnt, numeric_vals^, constraints)
        THEN RAISE Unsolvable
      END
    END;
    NonLinearSolve.Dispose()
  END NumericSolve;

PROCEDURE <A NAME="InitVals"><procedure>InitVals</procedure></A>(VAR numeric_vals: REF ARRAY OF RTVal.Real;
                   numeric_vars: Vars;
                   lo, next_const: INTEGER) =
  (* Fill in &quot;numeric_vals&quot; from &quot;numeric_vars&quot; *)
  BEGIN
    IF NUMBER(numeric_vals^) &lt; NUMBER(numeric_vars^) THEN
      numeric_vals := NEW(REF ARRAY OF RTVal.Real, NUMBER(numeric_vars^))
    END;
    (* fill in ``true'' variable values *)
    FOR i := 0 TO lo - 1 DO
      TYPECASE numeric_vars[i].val OF
      | RTVal.Null =&gt; numeric_vals[i] := 0.0
      | RTVal.Number (r) =&gt; numeric_vals[i] := r.val
      ELSE numeric_vals[i] := 0.0
      END
    END;
    (* fill in constant (known) values *)
    FOR i := next_const + 1 TO LAST(numeric_vars^) DO
      TYPECASE numeric_vars[i].val OF &lt;* NOWARN *&gt;
      | RTVal.Number (r) =&gt; numeric_vals[i] := r.val
      END
    END
  END InitVals;

CONST NoArg = FIRST(INTEGER);

PROCEDURE <A NAME="Functional"><procedure>Functional</procedure></A>(con: Constraint): BOOLEAN =
</PRE><BLOCKQUOTE><EM> Requires <CODE>con.hintCnt &lt;= 1</CODE>, i.e., that there is at most one unknown in the
   constraint.
<P>
   If <CODE>con.mask</CODE> # 0 and it indicates that <CODE>con</CODE> is functional in the single
   unknown variable, then set <CODE>con.unknown</CODE> to that unknown variable, set
   <CODE>con.nls_con</CODE> to a constraint that solves it, and return TRUE.
<P>
   Otherwise, set <CODE>con.nls_con</CODE> to a <CODE>NonLinearSolve.Constraint</CODE> clone of
   <CODE>con</CODE>, set <CODE>con.unknown</CODE> to NIL, and return FALSE.
<P>
   In either case, the arguments in <CODE>con.nls_con</CODE> corresponding to the unknown
   are unset. </EM></BLOCKQUOTE><PRE>
  CONST LoBit = ARRAY [1..7] OF [0..2]{0, 1, 0, 2, 0, 1, 0}; BEGIN
    &lt;* ASSERT con.hintCnt &lt;= 1 *&gt;
    IF con.mask = 0
      THEN con.unknown := NIL
      ELSE con.unknown := con.arg[LoBit[con.mask]].root
    END;
    CASE con.type OF &lt;* NOWARN *&gt;
      ConType.Plus =&gt;
        CASE con.mask OF &lt;* NOWARN *&gt;
          2_001 =&gt;
            con.nls_con := SetArgs(NonLinearSolve.NewPlus(), con, 1, 2);
            con.nls_con_mask := 2_001
        | 2_010 =&gt;
            con.nls_con := SetArgs(NonLinearSolve.NewMinus(), con, 0, 2);
            con.nls_con_mask := 2_001
        | 2_100 =&gt;
            con.nls_con := SetArgs(NonLinearSolve.NewMinus(), con, 0, 1);
            con.nls_con_mask := 2_001
        | 2_110 =&gt;
            con.nls_con := SetArgs(NonLinearSolve.NewHalve(), con, 0);
            con.nls_con_mask := 2_001
        | 2_000, 2_101, 2_011, 2_111 =&gt;
            con.nls_con := SetArgs(NonLinearSolve.NewPlus(), con, 1, 2, 0);
            con.nls_con_mask := con.mask;
            RETURN FALSE
        END
    | ConType.Times =&gt;
        IF con.mask = 2_001 THEN
          con.nls_con := SetArgs(NonLinearSolve.NewTimes(), con, 1, 2);
          con.nls_con_mask := 2_001
        ELSE
          con.nls_con := SetArgs(NonLinearSolve.NewTimes(), con, 1, 2, 0);
          con.nls_con_mask := con.mask;
          RETURN FALSE
        END
    | ConType.Atan =&gt;
        CASE con.mask OF &lt;* NOWARN *&gt;
        | 2_001 =&gt;
            con.nls_con := SetArgs(NonLinearSolve.NewAtan(), con, 1, 2);
            con.nls_con_mask := 2_001
        | 2_010 =&gt;
            con.nls_con := SetArgs(NonLinearSolve.NewMultTan(), con, 2, 0);
            con.nls_con_mask := 2_001
        | 2_000, 2_100, 2_011, 2_101, 2_110, 2_111 =&gt;
            con.nls_con := SetArgs(NonLinearSolve.NewAtan(), con, 1, 2, 0);
            con.nls_con_mask := con.mask;
            RETURN FALSE
        END
    | ConType.Sin =&gt;
        IF con.mask = 2_01 THEN
          con.nls_con := SetArgs(NonLinearSolve.NewSin(), con, 1);
          con.nls_con_mask := 2_01
        ELSE
          con.nls_con := SetArgs(NonLinearSolve.NewSin(), con, 1, NoArg, 0);
          con.nls_con_mask := con.mask;
          RETURN FALSE
        END
    | ConType.Cos =&gt;
        IF con.mask = 2_01 THEN
          con.nls_con := SetArgs(NonLinearSolve.NewCos(), con, 1);
          con.nls_con_mask := 2_01
        ELSE
          con.nls_con := SetArgs(NonLinearSolve.NewCos(), con, 1, NoArg, 0);
          con.nls_con_mask := con.mask;
          RETURN FALSE
        END
    | ConType.Exp =&gt;
        IF con.mask = 2_01 THEN
          con.nls_con := SetArgs(NonLinearSolve.NewExp(), con, 1);
          con.nls_con_mask := 2_01
        ELSE
          con.nls_con := SetArgs(NonLinearSolve.NewExp(), con, 1, NoArg, 0);
          con.nls_con_mask := con.mask;
          RETURN FALSE
        END
    END;
    RETURN TRUE
  END Functional;

PROCEDURE <A NAME="SetArgs"><procedure>SetArgs</procedure></A>(
    nlc: NonLinearSolve.Constraint;
    con: Constraint;
    a1: CARDINAL;
    a2, a0: INTEGER := NoArg)
  : NonLinearSolve.Constraint =
  BEGIN
    nlc.arg[1] := NARROW(con.arg[a1].root, Var).index;
    IF a2 # NoArg THEN nlc.arg[2] := NARROW(con.arg[a2].root, Var).index END;
    IF a0 # NoArg THEN nlc.arg[0] := NARROW(con.arg[a0].root, Var).index END;
    RETURN nlc
  END SetArgs;

PROCEDURE <A NAME="ConstructSoln"><procedure>ConstructSoln</procedure></A>(READONLY c: ARRAY OF Constraint) RAISES {Unsolvable} =
</PRE><BLOCKQUOTE><EM> Set the <CODE>val</CODE> field of each unknown in <CODE>c</CODE> to the value of its root. </EM></BLOCKQUOTE><PRE>
  BEGIN
    FOR i := FIRST(c) TO LAST(c) DO
      VAR argCnt: CARDINAL; BEGIN
        CASE c[i].type OF &lt;* NOWARN *&gt;
          ConType.Cons, ConType.Plus, ConType.Times, ConType.Atan =&gt;
            argCnt := 3
        | ConType.Sin, ConType.Cos, ConType.Exp, ConType.Equal =&gt;
            argCnt := 2
        | ConType.Real, ConType.Text =&gt;
            argCnt := 1
        END;
        FOR j := 0 TO argCnt - 1 DO
          VAR arg := c[i].arg[j]; BEGIN
            IF NOT arg.known THEN arg.val := Value(arg.root) END
          END
        END
      END
    END
  END ConstructSoln;

PROCEDURE <A NAME="Value"><procedure>Value</procedure></A>(v: Var): RTVal.T RAISES {Unsolvable} =
</PRE><BLOCKQUOTE><EM> Return the value of <CODE>v</CODE>, which must be the root of its equivalence class. </EM></BLOCKQUOTE><PRE>
  BEGIN
    &lt;* ASSERT v = v.root *&gt;
    IF NOT v.known THEN
      IF v.cons # NIL THEN
        IF v.marked THEN
          RAISE Unsolvable
        ELSE
          v.marked := TRUE;
          v.val := RTVal.FromPair(
            car := Value(v.cons.cdr.car.root),
            cdr := Value(v.cons.cdr.cdr.car.root))
        END
      ELSIF v.index # NoIndex THEN
        v.val := RTVal.FromReal(numeric_vals[v.index])
      ELSE
        (* &quot;v&quot; is an unknown var that is not equivalent to a cons nor involved
	   in any numeric constraints. It may not have a type, or it may have
           a non-trivial type due to a REAL or TEXT constraint. Assign &quot;v&quot; a
           valid value for its type if it does not already have one. *)
        CASE v.type OF &lt;* NOWARN *&gt;
	| Type.Any =&gt;
            IF v.val = NIL THEN v.val := RTVal.nil END
        | Type.Num =&gt;
            IF v.val = NIL OR NOT ISTYPE(v.val, RTVal.Number) THEN
              v.val := zero
            END
        | Type.Text =&gt;
            IF v.val = NIL OR NOT ISTYPE(v.val, RTVal.Text) THEN
              v.val := emptyString
            END
        END
      END;
      v.known := TRUE;
    END;
    RETURN v.val
  END Value;

BEGIN
END JunoSolve.
</PRE>
</inModule>
<PRE>























</PRE>
</BODY>
</HTML>
