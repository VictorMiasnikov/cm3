<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: juno-machine/src/Equiv.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>juno-machine/src/Equiv.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright (C) 1992, Digital Equipment Corporation                         
 All rights reserved.                                                      
 See the file COPYRIGHT for a full description.                            
                                                                           
 Last modified on Mon Oct 31 09:34:02 PST 1994 by heydon                   

<P><PRE>MODULE <module><implements><A HREF="Equiv.i3.html">Equiv</A></implements></module>;
</PRE> This implementation of disjoint sets uses the quick-find algorithm,
   in which all trees have depth at most 1. Hence, the Find operation
   takes constant time, but the Union operation takes time
   proportional to the size of the smaller set being merged. 

<P><PRE>REVEAL
  <A NAME="T">T</A> = Public BRANDED &quot;EquivQuickFind.T&quot; OBJECT
    size: CARDINAL;
    link: T;
  OVERRIDES
    init  := Init;
    union := Union;
  END;

PROCEDURE <A NAME="Init"><procedure>Init</procedure></A>(self: T): T =
  BEGIN
    self.root := self;
    self.size := 1;
    self.link := self;
    RETURN self
  END Init;

PROCEDURE <A NAME="Union"><procedure>Union</procedure></A>(x: T; y: T): T =
  BEGIN
    &lt;* ASSERT x.root = x AND y.root = y *&gt;
    IF x = y THEN RETURN x END;
    IF x.size &lt; y.size THEN
      VAR t := x; BEGIN x := y; y := t; END
    END;
    VAR p := y; BEGIN
      REPEAT
        p.root := x;
        p := p.link
      UNTIL p = y
    END;
    VAR t := y.link; BEGIN y.link := x.link; x.link := t END;
    INC(x.size, y.size);
    RETURN x
  END Union;

BEGIN
END Equiv.
</PRE>
</inModule>
<PRE>























</PRE>
</BODY>
</HTML>
