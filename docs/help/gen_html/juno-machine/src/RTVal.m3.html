<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: juno-machine/src/RTVal.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>juno-machine/src/RTVal.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright (C) 1992, Digital Equipment Corporation                         
 All rights reserved.                                                      
 See the file COPYRIGHT for a full description.                            
                                                                           
 Last modified on Wed May 10 19:22:29 PDT 1995 by heydon                   
      modified on Sun Jun  5 15:47:38 PDT 1994 by gnelson                  

<P><PRE>MODULE <module><implements><A HREF="RTVal.i3.html">RTVal</A></implements></module>;

IMPORT <A HREF="#x1">JunoValue</A>, <A HREF="InUseRec.i3.html">InUseRec</A>, <A HREF="../derived/InUseRecSeq.i3.html">InUseRecSeq</A>;
IMPORT <A HREF="../../m3core/src/text/Text.i3.html">Text</A> AS TextIntf;  (* to avoid name clash with &quot;RTVal.Text&quot; type *)

IMPORT <A HREF="../../libm3/src/rw/Wr.i3.html">Wr</A>, <A HREF="../../libm3/src/fmtlex/Fmt.i3.html">Fmt</A>, <A HREF="../../libm3/src/rw/Stdio.i3.html">Stdio</A>, <A HREF="../../m3core/src/thread/Common/Thread.i3.html">Thread</A>;
&lt;* FATAL Wr.Failure, Thread.Alerted *&gt;
VAR debug := FALSE;

REVEAL
  <A NAME="Number">Number</A> = NumberPublic BRANDED &quot;RTVal.Number&quot; OBJECT link: Number END;
  <A NAME="Text">Text</A> = TextPublic BRANDED &quot;RTVal.Text&quot; OBJECT link: Text END;
  <A NAME="Pair">Pair</A> = PairPublic BRANDED &quot;RTVal.Pair&quot; OBJECT link: Pair END;

VAR
  markStack := NEW(InUseRecSeq.T).init();
  numAvail, numInUse: Number := NIL;
  textAvail, textInUse: Text := NIL;
  pairAvail, pairInUse: Pair := NIL;

PROCEDURE <A NAME="FromReal"><procedure>FromReal</procedure></A>(r: Real): Number =
  VAR res := numAvail; BEGIN
    IF res = NIL
      THEN res := NEW(Number)
      ELSE numAvail := numAvail.link
    END;
    res.val := r;
    res.link := numInUse;
    numInUse := res;
    RETURN res
  END FromReal;

PROCEDURE <A NAME="FromInt"><procedure>FromInt</procedure></A>(i: INTEGER): Number =
  VAR res := numAvail; BEGIN
    IF res = NIL
      THEN res := NEW(Number)
      ELSE numAvail := numAvail.link
    END;
    res.val := FLOAT(i, Real);
    res.link := numInUse;
    numInUse := res;
    RETURN res
  END FromInt;

PROCEDURE <A NAME="FromText"><procedure>FromText</procedure></A>(txt: TEXT): Text =
  VAR res := textAvail; BEGIN
    &lt;* ASSERT txt # NIL *&gt;
    IF res = NIL
      THEN res := NEW(Text)
      ELSE textAvail := textAvail.link
    END;
    res.val := txt;
    res.link := textInUse;
    textInUse := res;
    RETURN res
  END FromText;

PROCEDURE <A NAME="FromPair"><procedure>FromPair</procedure></A>(car, cdr: T): Pair =
  VAR res := pairAvail; BEGIN
    &lt;* ASSERT car # NIL AND cdr # NIL *&gt;
    IF res = NIL
      THEN res := NEW(Pair)
      ELSE pairAvail := pairAvail.link
    END;
    res.car := car;
    res.cdr := cdr;
    res.link := pairInUse;
    pairInUse := res;
    RETURN res
  END FromPair;

PROCEDURE <A NAME="FromJV"><procedure>FromJV</procedure></A>(jv: JunoValue.T): T =
  BEGIN
    TYPECASE jv OF &lt;*NOWARN*&gt;
      NULL =&gt; RETURN NIL
    | JunoValue.Null =&gt; RETURN nil
    | REF Real (r) =&gt; RETURN FromReal(r^)
    | TEXT (t) =&gt; RETURN FromText(t)
    | REF JunoValue.Pair (r) =&gt; RETURN FromJVPair(r)
    END
  END FromJV;

PROCEDURE <A NAME="FromJVPair"><procedure>FromJVPair</procedure></A>(pr: REF JunoValue.Pair): Pair =
</PRE><BLOCKQUOTE><EM> Equivalent to <CODE>RETURN FromPair(FromJV(pr.car), FromJV(pr.cdr))</CODE>, but
   uses fewer stack frames in the case that <CODE>pr</CODE> is a long list.
<P>
   Note: The calls to <CODE>FromPair</CODE> belowonly need to pass a valid first
   argument. They pass the same second argument only because <CODE>FromPair</CODE>'s
   arguments must be non-NIL. The bogus <CODE>cdr</CODE> value gets overwritten on the
   next iteration, or after the loop. </EM></BLOCKQUOTE><PRE>
  VAR
    car: T := FromJV(pr.car);
    res: Pair := FromPair(car, car);
    curr: Pair := res;
  BEGIN
    LOOP
      TYPECASE pr.cdr OF
        NULL =&gt; EXIT
      | REF JunoValue.Pair (newPr) =&gt;
          car := FromJV(newPr.car);
          curr.cdr := FromPair(car, car);
          pr := newPr;
          curr := curr.cdr
      ELSE EXIT
      END
    END;
    curr.cdr := FromJV(pr.cdr);
    RETURN res
  END FromJVPair;

PROCEDURE <A NAME="ToJV"><procedure>ToJV</procedure></A>(v: T): JunoValue.T =
  BEGIN
    TYPECASE v OF &lt;* NOWARN *&gt;
      NULL =&gt; RETURN NIL
    | Null =&gt; RETURN JunoValue.Nil
    | Number (r) =&gt; RETURN JunoValue.RefReal(r.val)
    | Text (t) =&gt; RETURN t.val
    | Pair (p) =&gt; RETURN ToJVPair(p)
    END
  END ToJV;

PROCEDURE <A NAME="ToJVPair"><procedure>ToJVPair</procedure></A>(pr: Pair): REF JunoValue.Pair =
</PRE><BLOCKQUOTE><EM> Equivalent to:
<PRE>
      
         RETURN NEW(REF JunoValue.Pair,
           car := ToJV(pr.car), cdr := ToJV(pr.cdr))
      
   </PRE>
but uses fewer stack frames in the case that <CODE>pr</CODE> is a long list. </EM></BLOCKQUOTE><PRE>
  VAR
    res := NEW(REF JunoValue.Pair,
      car := ToJV(pr.car), cdr := NIL);
    curr := res;
  BEGIN
    LOOP
      TYPECASE pr.cdr OF
        NULL =&gt; EXIT
      | Pair (newPr) =&gt;
          curr.cdr := NEW(REF JunoValue.Pair,
            car := ToJV(newPr.car), cdr := NIL);
          pr := newPr;
          curr := curr.cdr
      ELSE EXIT
      END
    END;
    curr.cdr := ToJV(pr.cdr);
    RETURN res
  END ToJVPair;

PROCEDURE <A NAME="Equal"><procedure>Equal</procedure></A>(v, w: T): BOOLEAN =
  BEGIN
    IF v = NIL OR w = NIL THEN RETURN FALSE END;
    TYPECASE v OF &lt;*NOWARN*&gt;
      Null =&gt; RETURN w = nil
    | Number (vv) =&gt;
        TYPECASE w OF Number (ww) =&gt;
          RETURN vv.val = ww.val
        ELSE RETURN FALSE
        END
    | Text (vv) =&gt;
        TYPECASE w OF Text (ww) =&gt;
          RETURN TextIntf.Equal(vv.val, ww.val)
        ELSE RETURN FALSE
        END
    | Pair (vv) =&gt;
        TYPECASE w OF Pair (ww) =&gt;
          RETURN EqualPair(vv, ww)
        ELSE RETURN FALSE
        END
    END
  END Equal;

PROCEDURE <A NAME="EqualPair"><procedure>EqualPair</procedure></A>(p1: Pair; p2: Pair): BOOLEAN =
</PRE><BLOCKQUOTE><EM> Equivalent to <CODE>RETURN Equal(p1.car, p2.car) AND Equal(p1.cdr, p2.cdr)</CODE>, but
   uses fewer stack frames when <CODE>p1</CODE> and <CODE>p2</CODE> are long lists. </EM></BLOCKQUOTE><PRE>
  BEGIN
    LOOP
      IF NOT Equal(p1.car, p2.car) THEN RETURN FALSE END;
      TYPECASE p1.cdr OF
        NULL =&gt; RETURN FALSE
      | Pair (newP1) =&gt;
          TYPECASE p2.cdr OF
            NULL =&gt; RETURN FALSE
          | Pair (newP2) =&gt; p1 := newP1; p2 := newP2
          ELSE RETURN FALSE
          END
      ELSE EXIT
      END
    END;
    RETURN Equal(p1.cdr, p2.cdr)
  END EqualPair;

PROCEDURE <A NAME="Mark"><procedure>Mark</procedure></A>() =
  VAR r := InUseRec.T{numInUse, textInUse, pairInUse}; BEGIN
    markStack.addhi(r);
    numInUse := NIL;
    textInUse := NIL;
    pairInUse := NIL
  END Mark;

PROCEDURE <A NAME="Dispose"><procedure>Dispose</procedure></A>() =
  VAR deletedAny := FALSE; BEGIN
    IF debug THEN
      Wr.PutText(Stdio.stderr, &quot;RTVal.Dispose:\n&quot;);
      Wr.Flush(Stdio.stderr)
    END;
    DisposeNum(deletedAny);
    DisposeText(deletedAny);
    DisposePair(deletedAny);
    IF markStack.size() &gt; 0 THEN
      VAR r := markStack.remhi(); BEGIN
    	numInUse := r.numInUse;
    	textInUse := r.textInUse;
    	pairInUse := r.pairInUse
      END
    END;
    IF debug THEN
      IF NOT deletedAny THEN
        Wr.PutText(Stdio.stderr, &quot;  Nothing deleted\n&quot;)
      END;
      Wr.PutChar(Stdio.stderr, '\n');
      Wr.Flush(Stdio.stderr)
    END
  END Dispose;

PROCEDURE <A NAME="DisposeNum"><procedure>DisposeNum</procedure></A>(VAR deletedAny: BOOLEAN) =
  VAR l := numInUse; cnt := 1; BEGIN
    IF l = NIL THEN RETURN END;
    WHILE l.link # NIL DO l := l.link; INC(cnt) END;
    l.link := numAvail;
    numAvail := numInUse;
    numInUse := NIL;
    IF debug THEN
      deletedAny := TRUE;
      Wr.PutText(Stdio.stderr, Fmt.Pad(Fmt.Int(cnt), 7));
      Wr.PutText(Stdio.stderr, &quot; number(s)\n&quot;); Wr.Flush(Stdio.stderr)
    END
  END DisposeNum;

PROCEDURE <A NAME="DisposeText"><procedure>DisposeText</procedure></A>(VAR deletedAny: BOOLEAN) =
  VAR l := textInUse; cnt := 1; BEGIN
    IF l = NIL THEN RETURN END;
    WHILE l.link # NIL DO l := l.link; INC(cnt) END;
    l.link := textAvail;
    textAvail := textInUse;
    textInUse := NIL;
    IF debug THEN
      deletedAny := TRUE;
      Wr.PutText(Stdio.stderr, Fmt.Pad(Fmt.Int(cnt), 7));
      Wr.PutText(Stdio.stderr, &quot; text(s)\n&quot;); Wr.Flush(Stdio.stderr)
    END
  END DisposeText;

PROCEDURE <A NAME="DisposePair"><procedure>DisposePair</procedure></A>(VAR deletedAny: BOOLEAN) =
  VAR l := pairInUse; cnt := 1; BEGIN
    IF l = NIL THEN RETURN END;
    WHILE l.link # NIL DO l := l.link; INC(cnt) END;
    l.link := pairAvail;
    pairAvail := pairInUse;
    pairInUse := NIL;
    IF debug THEN
      deletedAny := TRUE;
      Wr.PutText(Stdio.stderr, Fmt.Pad(Fmt.Int(cnt), 7));
      Wr.PutText(Stdio.stderr, &quot; pair(s)\n&quot;); Wr.Flush(Stdio.stderr)
    END
  END DisposePair;

BEGIN
  nil := NEW(Null)
END RTVal.
</PRE>
</inModule>
<HR>
<A NAME="x1">interface JunoValue is in:
</A><UL>
<LI><A HREF="JunoValue.i3.html#0TOP0">juno-machine/src/JunoValue.i3</A>
<LI><A HREF="../../pkl-fonts/src/JunoValue.i3.html#0TOP0">pkl-fonts/src/JunoValue.i3</A>
</UL>
<P>
<PRE>























</PRE>
</BODY>
</HTML>
