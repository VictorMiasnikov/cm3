<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: juno-machine/src/RedundantLSolve.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>juno-machine/src/RedundantLSolve.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright (C) 1992, Digital Equipment Corporation          
 All rights reserved.                                       
 See the file COPYRIGHT for a full description.             
                                                            
 Last modified on Fri Feb  4 10:40:47 PST 2000 by gnelson   
      modified on Thu Apr 10 09:47:22 PDT 1997 by heydon    
      modified on Thu Nov  3 17:56:57 PST 1994 by isard     

<P><PRE>MODULE <module><implements><A HREF="RedundantLSolve.i3.html">RedundantLSolve</A></implements></module>;

FROM <A HREF="#x1">JunoValue</A> IMPORT Real, Sqrt;
</PRE><BLOCKQUOTE><EM> IMPORT RowOp; </EM></BLOCKQUOTE><PRE>
IMPORT <A HREF="../../libm3/src/rw/IO.i3.html">IO</A>;
IMPORT <A HREF="../../libm3/src/rw/Wr.i3.html">Wr</A>, <A HREF="../../libm3/src/fmtlex/Fmt.i3.html">Fmt</A>, <A HREF="../../m3core/src/text/Text.i3.html">Text</A>;
FROM <A HREF="../../m3core/src/thread/Common/Thread.i3.html">Thread</A> IMPORT Alerted;
&lt;* FATAL Wr.Failure, Alerted *&gt;

VAR debug := 0;
</PRE><BLOCKQUOTE><EM> debug &gt;= 1 =&gt; show initial and scaled matrices
   debug &gt;= 2 =&gt; show row-echelon matrix
   debug &gt;= 3 =&gt; show column permutation, row-echelon matrix, original &amp;
                 orthonormal basis, and minimal solution
</EM></BLOCKQUOTE><PRE>

VAR UseGramSchmidt := TRUE;
VAR UseCompletePivoting := TRUE;

PROCEDURE <A NAME="SetGramSchmidt"><procedure>SetGramSchmidt</procedure></A>(flag: BOOLEAN) =
  BEGIN UseGramSchmidt := flag END SetGramSchmidt;

TYPE
  T = Real;
  CardArray = REF ARRAY OF CARDINAL;

CONST
  InitRows = 50;
  InitCols = 50;

VAR
  Indent := 9;
  Prec := 3;
  FieldWidth := Prec + 8;

VAR
  c1 := 0.66;
  c2 := 0.5;
</PRE> The constants <CODE>c1</CODE> and <CODE>c2</CODE> are used in the Veach Heuristic. 

<P><PRE>PROCEDURE <A NAME="LogT"><procedure>LogT</procedure></A>(t: TEXT) =
  BEGIN IO.Put(t, logWr) END LogT;

PROCEDURE <A NAME="EtpLogSolveRow"><procedure>EtpLogSolveRow</procedure></A>(&lt;*UNUSED*&gt; ops: INTEGER) =
  BEGIN END EtpLogSolveRow;

PROCEDURE <A NAME="ShowMatrix"><procedure>ShowMatrix</procedure></A>(m, n: CARDINAL; READONLY a: Matrix; READONLY t: TEXT) =
  BEGIN
    Wr.PutText(logWr, &quot;  &quot; &amp; t &amp; &quot;:\n&quot;);
    FOR i := 0 TO m - 1 DO
      ShowVector(n, a[i]);
    END;
    Wr.Flush(logWr)
  END ShowMatrix;

PROCEDURE <A NAME="ShowVector"><procedure>ShowVector</procedure></A>(n: CARDINAL; READONLY v: Vector) =
  BEGIN
    Wr.PutText(logWr, Fmt.Pad(&quot;&quot;, Indent));
    FOR j := 0 TO n - 1 DO
      Wr.PutText(logWr,
        Fmt.Pad(Fmt.Real(v[j], Fmt.Style.Sci, prec := Prec), FieldWidth))
    END;
    Wr.PutChar(logWr, '\n');
  END ShowVector;

VAR
  p := NEW(REF Vector, InitCols);     (* solution vector (not permuted) *)
  temp := NEW(REF Vector, InitCols);  (* temporary vector for &quot;SwapRows&quot; *)
  basis := NEW(REF Matrix, InitCols, InitCols);
  colPerm := NEW(CardArray, InitCols);
  rowMax := NEW(CardArray, InitRows);

  (* &quot;colPerm[c]&quot; (0 &lt;= c &lt; n) is the index in &quot;x&quot; of the unknown represented
     by column &quot;c&quot; of matrix &quot;a&quot;.

     &quot;rowMax[i]&quot; (0 &lt;= i &lt; m) is the index of the column of row &quot;i&quot; of &quot;A&quot;
     with largest absolute value (note that &quot;A&quot; does not include column &quot;n&quot; of
     the matrix &quot;a&quot;). *)

PROCEDURE <A NAME="P"><procedure>P</procedure></A>(
    m, n: CARDINAL;
    VAR (*INOUT*) a: Matrix;
    VAR (*OUT*) x: Vector) =
  (* Matrix &quot;A&quot; is stored in &quot;a[0..m-1, 0..n-1]&quot;.
     Vector &quot;b&quot; is stored in &quot;a[0..m-1, n]&quot;.

     IMPLEMENTATION: The current implementation reduces &quot;A&quot; to a row-echelon
     form in which all of the pivot elements are along &quot;A&quot;'s diagonal. At each
     stage, it chooses the element with largest magnitude in the remaining
     sub-matrix and swaps rows and columns as necessary to make that element
     the next pivot. Once the matrix has been reduced to this row-echelon
     form, it may be under-constrained. In that case, the implementation
     chooses for the solution the point on the solution space with smallest L2
     norm (i.e., the point closest to the origin in Euclidean space).
  *)

  VAR rc: INTEGER := 0;
  (* We only consider pivots along the diagonal of &quot;A&quot;, so the current row and
     the current column are always the same value, called &quot;rc&quot;. *)

  PROCEDURE InitColPerm() =
  (* Make sure &quot;NUMBER(colPerm^) &gt;= n&quot;, and initialize &quot;colPerm[i] = i&quot;
     for &quot;i &lt;= 0 &lt; n&quot;. *)
    BEGIN
      (* make sure &quot;colPerm&quot; is large enough *)
      IF NUMBER(colPerm^) &lt; n THEN
	colPerm := NEW(CardArray, MAX(n, 2 * NUMBER(colPerm^)))
      END;
      (* initialize to identity permutation *)
      FOR i := 0 TO n - 1 DO
        colPerm[i] := i
      END
    END InitColPerm;

  PROCEDURE InitRowMax() =
  (* Initialize &quot;rowMax&quot; so it satisfies its invariant.  Also,
     scale the matrix so that the uniform norm of each row
     is unity. *)
  BEGIN
    (* make sure &quot;rowMax&quot; is large enough *)
    IF NUMBER(rowMax^) &lt; m THEN
      rowMax := NEW(CardArray, MAX(m, 2 * NUMBER(rowMax^)))
    END;
    (* initialize maximum index for each row *)
    FOR i := 0 TO m - 1 DO
      WITH currRow = a[i] DO
        VAR maxVal: T := ABS(currRow[0]); maxCol := 0; BEGIN
          FOR j := 1 TO n - 1 DO
            VAR abs := ABS(currRow[j]); BEGIN
              IF abs &gt; maxVal THEN
                maxVal := abs;
                maxCol := j
              END
            END
          END;
          rowMax[i] := maxCol;
          IF maxVal # 0.0 THEN
            (* scale the row so that its uniform norm is unity. *)
            FOR i := 0 TO n DO
              currRow[i] := currRow[i] / maxVal
            END
          END
        END
      END
    END
  END InitRowMax;

  PROCEDURE MaxEntry(r: CARDINAL): CARDINAL =
  (* Return the index &quot;x&quot; of &quot;rowMax&quot; that maximizes the value
     &quot;ABS(a[x, rowMax[x]])&quot; for &quot;r &lt;= x &lt; m&quot;. Requires &quot;r &lt; m&quot;. *)
    VAR maxVal: T := ABS(a[r, rowMax[r]]); maxR := r; BEGIN
      FOR i := r + 1 TO m - 1 DO
        VAR val := ABS(a[i, rowMax[i]]); BEGIN
          IF val &gt; maxVal THEN
            maxR := i;
            maxVal := val
          END
        END
      END;
      RETURN maxR
    END MaxEntry;

  PROCEDURE MaxEntry2(r: CARDINAL): CARDINAL =
  (* Return the index &quot;x&quot; of &quot;rowMax&quot; that maximizes the value
     &quot;ABS(a[x,r)&quot; for &quot;x: r &lt;= x &lt; m&quot;. Requires &quot;r &lt; m&quot;. *)
    VAR maxVal: T := ABS(a[r, r]); maxR := r; BEGIN
      FOR i := r + 1 TO m - 1 DO
        VAR val := ABS(a[i, r]); BEGIN
          IF val &gt; maxVal THEN
            maxR := i;
            maxVal := val
          END
        END
      END;
      RETURN maxR
    END MaxEntry2;

  PROCEDURE SwapCols(c1, c2: CARDINAL) =
  (* Swap columns &quot;c1&quot; and &quot;c2&quot; of the matrix &quot;a&quot;, adjusting &quot;colPerm&quot; and
     &quot;rowMax&quot; to reflect the swap. *)
    BEGIN
      (* swap column values *)
      FOR i := 0 TO m - 1 DO
        WITH currRow = a[i], v1 = currRow[c1], v2 = currRow[c2] DO
          VAR t := v1; BEGIN v1 := v2; v2 := t END
        END;
        WITH currMax = rowMax[i] DO
          IF currMax = c1 THEN currMax := c2
          ELSIF currMax = c2 THEN currMax := c1
          END
        END
      END;
      (* reflect swap in &quot;colPerm&quot; *)
      WITH v1 = colPerm[c1], v2 = colPerm[c2] DO
        VAR t := v1; BEGIN v1 := v2; v2 := t END
      END
    END SwapCols;

  PROCEDURE SwapRows(r1, r2, c: CARDINAL) =
  (* Swap rows &quot;r1&quot; and &quot;r2&quot; of the matrix &quot;a&quot; from column &quot;c&quot; through
     column &quot;n&quot;, adjusting &quot;rowMax&quot; to reflect the swap. *)
    BEGIN
      (* swap rows *)
      WITH row1 = a[r1], row2 = a[r2], cnt = (n - c) + 1 DO
        SUBARRAY(temp^, c, cnt) := SUBARRAY(row1,  c, cnt);
        SUBARRAY(row1,  c, cnt) := SUBARRAY(row2,  c, cnt);
        SUBARRAY(row2,  c, cnt) := SUBARRAY(temp^, c, cnt)
      END;
      (* swap &quot;rowMax&quot; entries *)
      VAR t := rowMax[r1]; BEGIN
        rowMax[r1] := rowMax[r2]; rowMax[r2] := t
      END
    END SwapRows;

  PROCEDURE Pivot(r, c: CARDINAL) =
  (* Pivots matrix &quot;a&quot; about location &quot;(r, c)&quot;. This sets all entries in
     column &quot;c&quot; of &quot;a&quot; below row &quot;r&quot; to &quot;0.0&quot; without changing the set of
     solutions to the equations represented by &quot;a&quot;. It also maintain the
     invariant on &quot;rowMax&quot;. *)
    VAR pivot, factor: T; BEGIN
      WITH pivotRow = a[r] DO
        pivot := pivotRow[c];
      	FOR i := r + 1 TO m - 1 DO
          WITH currRow = a[i] DO
      	    (* adjust row &quot;i&quot; *)
      	    IF currRow[c] # 0.0 THEN
      	      factor := currRow[c] / pivot;
      	      currRow[c] := 0.0;
      	      EtpLogSolveRow(n-c);
      	      VAR maxCol := c; maxAbs := 0.0; BEGIN
      	        FOR j := c+1 TO n-1 DO
                  WITH currRowJ = currRow[j] DO
                    currRowJ := currRowJ - (factor * pivotRow[j]);
                    WITH abs = ABS(currRowJ) DO
                      IF abs &gt; maxAbs THEN
                        maxAbs := abs;
                        maxCol := j
                      END
                    END
                  END
      	        END;
      	        rowMax[i] := maxCol
      	      END;
      	      currRow[n] := currRow[n] - (factor * pivotRow[n])
      	    END
          END
      	END
      END
    END Pivot;

  PROCEDURE SolveRow(
      r: CARDINAL;
      c: INTEGER;
      READONLY v: Vector;
      homog: BOOLEAN): T =
  (* Let &quot;a&quot; be matrix in row-echelon form. The entry &quot;a[r, c]&quot; is assumed to
     be a pivot element of &quot;a&quot;. &quot;SolveRow&quot; returns the value for &quot;v[c]&quot; using
     the previously computed values of &quot;v[c+1..n-1]&quot;. The value returned for
     &quot;v[c]&quot; satisfies:

|      (+ i: c &lt;= i &lt; n: a[r, i] * v[i]) = x

     where &quot;x = 0&quot; if &quot;homog&quot; is &quot;TRUE&quot; and &quot;x = a[r, n]&quot; if &quot;homog&quot; is
     &quot;FALSE&quot;. *)
    VAR sum: T; BEGIN
      WITH rowR = a[r] DO
        (* incorporate known values to the right of &quot;c&quot; *)
        IF homog
          THEN sum := 0.0
          ELSE sum := rowR[n]
        END;
        FOR i := c + 1 TO n - 1 DO
          sum := sum - (rowR[i] * v[i])
        END;
        RETURN sum / rowR[c]
      END
    END SolveRow;

  PROCEDURE BackProp(rc: CARDINAL; VAR (*INOUT*) res: Vector; homog := FALSE) =
  (* Sets the values of the solution vector entries &quot;res[0..rc-1]&quot; so as to
     satisfy &quot;A * res = b&quot;. Assumes that the values of &quot;res[rc..n-1]&quot; are
     valid. If &quot;homog&quot; is &quot;TRUE&quot;, then solves the system &quot;A * res = 0&quot;. *)
    BEGIN
      FOR i := rc - 1 TO 0 BY -1 DO
        res[i] := SolveRow(i, i, res, homog);
      END
    END BackProp;

  PROCEDURE MakeBases(dim: CARDINAL) =
  (* Forms &quot;dim&quot; dimensional basis vectors in &quot;basis[0..dim-1]&quot;. *)
    VAR ndim := n - dim; BEGIN
      (* Zero last &quot;dim&quot; entries of each basis vector *)
      FOR i := 0 TO dim - 1 DO
        WITH currRow = basis[i] DO
          FOR j := ndim TO n - 1 DO
            currRow[j] := 0.0
          END
        END
      END;
      FOR i := 0 TO dim - 1 DO
        basis[i, rc + i] := 1.0;	      (* form i'th basis vector *)
        BackProp(rc, basis[i], TRUE);
      END
    END MakeBases;

  PROCEDURE GramSchmidt(dim: CARDINAL; VAR (*INOUT*) b: Matrix) =
  (* Convert the &quot;dim&quot; x &quot;n&quot; matrix of &quot;dim&quot; basis vectors &quot;b&quot; into an
     orthonormal basis using the Gram-Schmidt algorithm. The basis vectors are
     stored in &quot;b[0..dim-1]&quot;; they are assumed to span a space of dimension
     &quot;dim&quot;. *)
    BEGIN
      FOR i := 0 TO dim - 1 DO
        WITH rowI = b[i] DO
          (* orthogonalize &quot;b[i]&quot;; &quot;b[0..i-1]&quot; are orthonormal *)
          FOR j := 0 TO i - 1 DO
            WITH rowJ = b[j] DO
              VAR dot := Dot(rowI, rowJ); BEGIN
            	FOR k := 0 TO n - 1 DO
        	  rowI[k] := rowI[k] - (dot * rowJ[k])
            	END
              END
            END
          END;
          (* normalize &quot;b[i]&quot; *)
          VAR len := L2Norm(rowI); BEGIN
            &lt;* ASSERT len &gt; 0.0 *&gt;
            FOR k := 0 TO n - 1 DO
              rowI[k] := rowI[k] / len
            END
          END
        END
      END
    END GramSchmidt;

  PROCEDURE L2Norm(READONLY v: Vector): T =
    VAR sum := Dot(v, v); BEGIN
      RETURN Sqrt(sum)
    END L2Norm;

  PROCEDURE Dot(READONLY v1, v2: Vector): T =
    VAR res := 0.0; BEGIN
      FOR i := 0 TO n - 1 DO
        res := res + (v1[i] * v2[i])
      END;
      RETURN res
    END Dot;

  PROCEDURE VeachHeuristic(): CARDINAL =
  (* Use Eric Veach's heuristic to compute and return how many rows to use.
     On entry, &quot;c1&quot; is the fraction of the L1 norm of the residual
     vector that must be accounted for, and &quot;c2&quot; is the additional
     shrinkage that will be allowed in the pivot elements of rows
     that will be added on for free (relative to the pivot element
     of the row that is required in order to get &quot;c1&quot; of the L1 norm
     of the residual. The procedure returns the number of rows to
     ignore. *)
   VAR
     residnorm := 0.0;
     goal: REAL;
     numToUse: CARDINAL;
     pivtotal := 0.0;
   CONST
     Epsilon = 1.0E-6;
   BEGIN
     FOR j := 0 TO m - 1 DO residnorm := residnorm + ABS(a[j, n]) END;
     IF residnorm = 0.0 THEN RETURN 0 END;
     goal := residnorm * c1;
     (* use at least enough rows to reduce the residual norm by &quot;goal&quot;. *)
     residnorm := 0.0;
     numToUse := 0;
     WHILE residnorm &lt; goal AND numToUse &lt; MIN(n,m) AND ABS(a[numToUse,numToUse]) &gt; Epsilon DO
       residnorm := residnorm + ABS(a[numToUse, n]);
       pivtotal := pivtotal + ABS(a[numToUse, numToUse]);
       INC(numToUse)
     END;
     (* Now the sum of the residuals in the first &quot;numToUse&quot; rows is
        at least &quot;goal&quot;; or else &quot;numToUse=m&quot;.  Use any additional rows
        whose pivots are not too small; that is, not smaller than &quot;c2&quot; times
        the average magnitude of one of the pivots in use: *)
     IF numToUse &gt; 0 THEN
       VAR limit := c2 * (pivtotal / FLOAT(numToUse)); BEGIN
         WHILE numToUse &lt; MIN(m,n) AND ABS(a[numToUse, numToUse]) &gt;= limit DO
           INC(numToUse)
         END
       END
     END;
     RETURN numToUse;
   END VeachHeuristic;

  (* PROCEDURE P *)
  BEGIN
    (* Check for buggy call *)
    IF NUMBER(a) &lt; m OR (m &gt; 0 AND NUMBER(a[0]) &lt; n + 1) OR NUMBER(x) &lt; n THEN
      &lt;* ASSERT FALSE *&gt;
    END;
    (* make sure &quot;temp&quot; is large enough for &quot;SwapRows&quot; *)
    IF NUMBER(temp^) &lt; n + 1 THEN
      temp := NEW(REF Vector, MAX(n + 1, 2 * NUMBER(temp^)))
    END;
    IF debug &gt;= 1 THEN ShowMatrix(m, n + 1, a, &quot;Matrix before scaling&quot;) END;

    (* initialize bookkeeping arrays *)
    InitRowMax();
    IF debug &gt;= 1 THEN ShowMatrix(m, n+1, a, &quot;Matrix after scaling&quot;) END;
    InitColPerm();

    (* Put Matrix A in Row-Echelon form *)
    VAR
      maxRow, maxCol: CARDINAL;
    CONST
      Epsilon = 1.0E-6;
      (* Pivots must have magnitude at least &quot;Epsilon&quot;. *)
    BEGIN
      WHILE rc &lt; MIN(m, n) DO
        IF UseCompletePivoting THEN
          maxRow := MaxEntry(rc);
          maxCol := rowMax[maxRow]
        ELSE
          maxRow := MaxEntry2(rc);
          maxCol := rc
        END;
        IF ABS(a[maxRow, maxCol]) &gt; Epsilon THEN
          IF rc # maxCol THEN SwapCols(rc, maxCol) END;
          IF rc # maxRow THEN SwapRows(rc, maxRow, rc) END;
          Pivot(rc, rc);
        END;
        INC(rc)
      END
    END;
    IF debug &gt;= 2 THEN
      ShowMatrix(m, n+1, a, &quot;After pivoting&quot;)
    END;
    IF debug &gt;= 3 THEN
      Wr.PutText(logWr, &quot;  Column Permutation:\n&quot;);
      Wr.PutText(logWr, Fmt.Pad(&quot;&quot;, Indent));
      FOR i := 0 TO n - 1 DO
        VAR c := Text.FromChar(VAL(colPerm[i] + ORD('a'), CHAR)); BEGIN
          Wr.PutText(logWr, Fmt.Pad(c, FieldWidth))
        END
      END;
      Wr.PutChar(logWr, '\n');
      ShowMatrix(m, n + 1, a, &quot;Row-Echelon Matrix&quot;);
    END;
    rc := VeachHeuristic();
    IF debug &gt;= 2 THEN
      LogT(&quot;number of rows to use: &quot;);
      IO.PutInt(rc, logWr);
      LogT(&quot;\n&quot;)
    END;
    IF rc = 0 THEN
      FOR i := 0 TO n - 1 DO x[i] := 0.0 END
    END;
    (* Back-propagate solution values *)
    IF n &gt; NUMBER(p^) THEN
      p := NEW(REF Vector, MAX(n, 2 * NUMBER(p^)))
    END;
    FOR i := rc TO n - 1 DO p[i] := 0.0 END;
    BackProp(rc, p^);
    (* Check if system is under-constrained *)
    IF rc &lt; n AND UseGramSchmidt THEN
      (* Use Gram-Schmidt to find the solution with smallest L2 norm. *)
      IF n &gt; NUMBER(basis^) THEN
        VAR new_size := MAX(n, 2 * NUMBER(basis^)); BEGIN
          basis := NEW(REF Matrix, new_size, new_size)
        END
      END;
      VAR dim := n - rc; BEGIN
        (* compute the basis vectors *)
        MakeBases(dim);
        IF debug &gt;= 3 THEN
          Wr.PutText(logWr, &quot;  Particular Solution:\n&quot;);
          ShowVector(n, p^);
          ShowMatrix(dim, n, basis^, &quot;Original Basis&quot;)
        END;
        (* make the basis orthonormal *)
        GramSchmidt(dim, basis^);
        IF debug &gt;= 3 THEN
          ShowMatrix(dim, n, basis^, &quot;Orthonormal Basis&quot;)
        END;
        (* compute the minimal solution in &quot;p&quot; *)
        FOR i := 0 TO dim - 1 DO
          WITH basisI = basis[i] DO
            VAR alpha := Dot(p^, basisI); BEGIN
              FOR k := 0 TO n - 1 DO
          	p[k] := p[k] - (alpha * basisI[k])
              END
            END
          END
        END;
        IF debug &gt;= 3 THEN
          Wr.PutText(logWr, &quot;  Minimal Solution:\n&quot;);
          ShowVector(n, p^)
        END
      END
    END;
    (* Permute solution &quot;p&quot; into result &quot;x&quot; *)
    FOR i := 0 TO n - 1 DO
      x[colPerm[i]] := p[i]
    END
  END P;

BEGIN END RedundantLSolve.
</PRE>
</inModule>
<HR>
<A NAME="x1">interface JunoValue is in:
</A><UL>
<LI><A HREF="JunoValue.i3.html#0TOP0">juno-machine/src/JunoValue.i3</A>
<LI><A HREF="../../pkl-fonts/src/JunoValue.i3.html#0TOP0">pkl-fonts/src/JunoValue.i3</A>
</UL>
<P>
<PRE>























</PRE>
</BODY>
</HTML>
