<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: deckscape/src/WorkspaceVBT.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>deckscape/src/WorkspaceVBT.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright 1996 Digital Equipment Corporation.              
 Distributed only by permission.                            
                                                            
 Last modified on Mon Aug 19 22:14:04 PDT 1996 by mhb       

<P><PRE>MODULE <module><implements><A HREF="WorkspaceVBT.i3.html">WorkspaceVBT</A></implements></module>;

IMPORT <A HREF="../../formsvbt/src/FormsVBT.i3.html">FormsVBT</A>, <A HREF="../derived/MyBundle.i3.html">MyBundle</A>, <A HREF="../../vbtkit/src/vbtkitutils/Rsrc.i3.html">Rsrc</A>, <A HREF="../../ui/src/vbt/Trestle.i3.html">Trestle</A>, <A HREF="FreeDocVBT.i3.html">FreeDocVBT</A>, <A HREF="DocVBT.i3.html">DocVBT</A>, <A HREF="../../ui/src/vbt/VBT.i3.html">VBT</A>, <A HREF="DeckVBT.i3.html">DeckVBT</A>,
       <A HREF="../../libm3/src/geometry/Point.i3.html">Point</A>, <A HREF="../../libm3/src/geometry/Rect.i3.html">Rect</A>, <A HREF="../../ui/src/split/Split.i3.html">Split</A>, <A HREF="../../ui/src/split/ZSplit.i3.html">ZSplit</A>, <A HREF="../../libm3/src/fmtlex/Fmt.i3.html">Fmt</A>, <A HREF="../../m3core/src/text/Text.i3.html">Text</A>, <A HREF="../../ui/src/vbt/PaintOp.i3.html">PaintOp</A>, <A HREF="../../vbtkit/src/lego/SourceVBT.i3.html">SourceVBT</A>, <A HREF="../../libm3/derived/RefList.i3.html">RefList</A>,
       <A HREF="WSObjectVBT.i3.html">WSObjectVBT</A>, <A HREF="../../ui/src/vbt/Pixmap.i3.html">Pixmap</A>, <A HREF="../../vbtkit/src/lego/PixmapVBT.i3.html">PixmapVBT</A>, <A HREF="../../vbtkit/src/lego/Image.i3.html">Image</A>, <A HREF="../../libm3/src/rw/Rd.i3.html">Rd</A>, <A HREF="../../ui/src/vbt/VBTClass.i3.html">VBTClass</A>, <A HREF="../../ui/src/vbt/VBTRep.i3.html">VBTRep</A>, <A HREF="../../webvbt/src/WebVBT.i3.html">WebVBT</A>,
       <A HREF="../../m3core/src/runtime/common/RTCollector.i3.html">RTCollector</A>, <A HREF="../../m3core/src/runtime/common/RTutils.i3.html">RTutils</A>, <A HREF="../../m3core/src/thread/Common/Thread.i3.html">Thread</A>, <A HREF="../../webvbt/src/URLCache.i3.html">URLCache</A>, <A HREF="Options.i3.html">Options</A>;

&lt;* FATAL ANY *&gt;

REVEAL
  <A NAME="T">T</A> = Public BRANDED OBJECT
    deckCt := 0;
  OVERRIDES
    realize := Realize;
  END;

TYPE
  MButton = FormsVBT.T OBJECT
    deck: DeckVBT.T;
  END;

VAR
  mappedPixmap: Pixmap.T;
  unmappedPixmap: Pixmap.T;
  path: Rsrc.Path;

CONST
 DeckMenuVBox = ARRAY BOOLEAN OF TEXT {&quot;deckMenuVBox&quot;, &quot;permanentDeckMenuVBox&quot;};

PROCEDURE <A NAME="New"><procedure>New</procedure></A> (): T =
  VAR s := NEW(T);
  BEGIN
    EVAL FormsVBT.T.initFromRsrc(s, &quot;Workspace.fv&quot;, path);
    FormsVBT.Attach(
      s, &quot;newDeckButton&quot;, NEW(FormsVBT.Closure, apply := NewDeck));
    WITH searchSearch = NEW(FormsVBT.Closure, apply := SearchSearch) DO
      FormsVBT.Attach(s, &quot;searchSearchButton&quot;, searchSearch);
      FormsVBT.Attach(s, &quot;searchTypein&quot;, searchSearch);
    END;
    FormsVBT.Attach(
      s, &quot;flushCache&quot;, NEW(FormsVBT.Closure, apply := FlushCache));
    FormsVBT.Attach(
      s, &quot;caching&quot;, NEW(FormsVBT.Closure, apply := ToggleCaching));
    FormsVBT.Attach(
      s, &quot;zippers&quot;, NEW(FormsVBT.Closure, apply := ToggleZippers));
    FormsVBT.Attach(
      s, &quot;fgImages&quot;, NEW(FormsVBT.Closure, apply := ToggleFgImages));
    FormsVBT.Attach(s, &quot;debug&quot;, NEW(FormsVBT.Closure, apply := Debug));
    FormsVBT.Attach(s, &quot;quitButton&quot;, NEW(FormsVBT.Closure, apply := Quit));
    VAR
      target := FormsVBT.GetVBT(s, &quot;target&quot;);
      bg     := FormsVBT.GetColorProperty(s, &quot;target&quot;, &quot;BgColor&quot;);
      fg     := FormsVBT.GetColorProperty(s, &quot;target&quot;, &quot;Color&quot;);
      bgOp := PaintOp.FromRGB(bg.r, bg.g, bg.b, PaintOp.Mode.Accurate,
                              -1.0, PaintOp.BW.UseBg);
      fgOp := PaintOp.FromRGB(fg.r, fg.g, fg.b, PaintOp.Mode.Accurate,
                              -1.0, PaintOp.BW.UseFg);
      swapOp := PaintOp.SwapPair(bgOp, fgOp);
      op     := PaintOp.Pair(PaintOp.Transparent, swapOp);
    BEGIN
      SourceVBT.BeTarget(target, SourceVBT.NewSwapTarget(op));
    END;
    SetCaching(s);
    SetZippers(s);
    SetFgImages(s);

    s.hotlist := DeckVBT.New(HotListDeck, permanent := TRUE);
    AddDeck(s, s.hotlist);
    Iconize(s.hotlist);

    WITH doc  = DocVBT.NewFromURL(),
         deck = DeckVBT.New(HomeDeck) DO
      DeckVBT.AddDoc(deck, doc);
      AddDeck(s, deck);
    END;

    WITH webvbt = NEW(WebVBT.T).init(),
         text   = Rsrc.Get(&quot;Help.html&quot;, path) DO
      webvbt.fromText(text);
      FormsVBT.PutGeneric(s, &quot;helpText&quot;, webvbt)
    END;

    RETURN s;
  END New;

PROCEDURE <A NAME="Realize"><procedure>Realize</procedure></A> (ws: T; type, name: TEXT) : VBT.T
  RAISES {FormsVBT.Error} =
  BEGIN
    IF Text.Equal (name, &quot;target&quot;) THEN
      RETURN NEW (Target, ws := ws)
    ELSE
      RETURN FormsVBT.T.realize (ws, type, name)
    END;
  END Realize;

PROCEDURE <A NAME="GetTarget"><procedure>GetTarget</procedure></A> (ws: T): Target =
  BEGIN
    RETURN FormsVBT.GetVBT(ws, &quot;target&quot;)
  END GetTarget;

PROCEDURE <A NAME="GetDecks"><procedure>GetDecks</procedure></A> (s: T): RefList.T =
  VAR list: RefList.T;
    p  := FormsVBT.GetVBT(s, &quot;zSplit&quot;);
    ch := Split.Succ(p, NIL);
  BEGIN
    WHILE ch # NIL DO
      IF ISTYPE(ch, DeckVBT.T) THEN list := RefList.Cons(ch, list) END;
      ch := Split.Succ(p, ch)
    END;
    RETURN list
  END GetDecks;

PROCEDURE <A NAME="AddDeck"><procedure>AddDeck</procedure></A> (s: T; deck: DeckVBT.T; READONLY where := Rect.Empty) =
  VAR
    zSplit := FormsVBT.GetVBT(s, &quot;zSplit&quot;);
    menuVBox := FormsVBT.GetVBT(
                  s, DeckMenuVBox[DeckVBT.GetPermanent(deck)]);
    mButton     := NEW(MButton, deck := deck);
    bannerColor := DeckVBT.GetBannerColor(deck);
  BEGIN
    INC(s.deckCt);
    ZSplit.Insert(zSplit, deck, PositionWindow(where));
    deck.setWorkspace(s);
    EVAL FormsVBT.T.initFromRsrc(mButton, &quot;MButton.fv&quot;, path, TRUE);
    FormsVBT.PutText(mButton, &quot;text&quot;, DeckVBT.GetTitle(deck));
    FormsVBT.PutColorProperty(mButton, &quot;text&quot;, &quot;BgColor&quot;, bannerColor);
    FormsVBT.PutColorProperty(mButton, &quot;icon&quot;, &quot;BgColor&quot;, bannerColor);
    FormsVBT.Attach(
      mButton, &quot;button&quot;, NEW(FormsVBT.Closure, apply := Raise));
    Split.Insert(menuVBox, Split.Pred(menuVBox, NIL), mButton);
  END AddDeck;

PROCEDURE <A NAME="RemDeck"><procedure>RemDeck</procedure></A> (s: T; deck: DeckVBT.T) =
  BEGIN
    WITH mButton = GetMButton(s, deck) DO
      mButton.deck := NIL;
      Split.Delete(mButton.parent, mButton);
      VBT.Discard(mButton);
    END;
    Split.Delete(deck.parent, deck);
    (* need to fork the call to VBT.Discard, because RemDeck is
       called by a callback in a menu, located in the deck. *)
    EVAL Thread.Fork(NEW(DiscardVBTClosure, vbt := deck));
  END RemDeck;

TYPE
  DiscardVBTClosure = Thread.Closure OBJECT
                        vbt: VBT.T;
                      OVERRIDES
                        apply := DoDiscardVBT;
                      END;

PROCEDURE <A NAME="DoDiscardVBT"><procedure>DoDiscardVBT</procedure></A> (cl: DiscardVBTClosure): REFANY =
  BEGIN
    LOCK VBT.mu DO VBT.Discard(cl.vbt) END;
    RETURN NIL;
  END DoDiscardVBT;

PROCEDURE <A NAME="RenamedDeck"><procedure>RenamedDeck</procedure></A> (s: T; deck: DeckVBT.T) =
  VAR mButton := GetMButton(s, deck);
  BEGIN
    FormsVBT.PutText(mButton, &quot;text&quot;, DeckVBT.GetTitle(deck))
  END RenamedDeck;

PROCEDURE <A NAME="RenamedFreeDoc"><procedure>RenamedFreeDoc</procedure></A> (&lt;* UNUSED *&gt; s      : T;
                          &lt;* UNUSED *&gt; freeDoc: FreeDocVBT.T) =
  BEGIN
  END RenamedFreeDoc;

PROCEDURE <A NAME="GetMButton"><procedure>GetMButton</procedure></A> (s: T; deck: DeckVBT.T): MButton =
  VAR
    menuVBox := FormsVBT.GetVBT(s, DeckMenuVBox[DeckVBT.GetPermanent(deck)]);
    ch: MButton := NIL;
  BEGIN
    ch := Split.Succ(menuVBox, NIL);
    WHILE ch # NIL DO
      IF ch.deck = deck THEN RETURN ch END;
      ch := Split.Succ(menuVBox, ch);
    END;
    &lt;* ASSERT FALSE *&gt;
  END GetMButton;

PROCEDURE <A NAME="AddFreeDoc"><procedure>AddFreeDoc</procedure></A> (         s      : T;
                               freeDoc: FreeDocVBT.T;
                      READONLY where                   := Rect.Empty) =
  VAR zSplit := FormsVBT.GetVBT(s, &quot;zSplit&quot;);
  BEGIN
    ZSplit.Insert(zSplit, freeDoc, PositionWindow(where));
    freeDoc.setWorkspace(s);
  END AddFreeDoc;

PROCEDURE <A NAME="RemFreeDoc"><procedure>RemFreeDoc</procedure></A> (&lt;* UNUSED *&gt; s: T; freeDoc: FreeDocVBT.T) =
  BEGIN
    Split.Delete(VBT.Parent(freeDoc), freeDoc);
  END RemFreeDoc;

VAR
  mu := NEW(MUTEX);
  last: Point.T := Point.Origin;

CONST
  DX = 30; DY = 60;
  MAXX = 165; MAXY = 330;

CONST
  DefaultWidth = 600;
  DefaultHeight = 500;

PROCEDURE <A NAME="PositionWindow"><procedure>PositionWindow</procedure></A> (READONLY r: Rect.T): Rect.T =
  BEGIN
    IF Rect.IsEmpty(r) THEN
      LOCK mu DO
        last.h := last.h + DX;
        IF last.h &gt; MAXX THEN last.h := DX + (last.h MOD MAXX) END;
        last.v := last.v + DY;
        IF last.v &gt; MAXY THEN last.v := DY + (last.v MOD MAXY) END;
        RETURN Rect.FromCorner(last, DefaultWidth, DefaultHeight)
      END;
    ELSE RETURN r END
  END PositionWindow;

PROCEDURE <A NAME="NewDeck"><procedure>NewDeck</procedure></A>(&lt;*UNUSED*&gt; cl:FormsVBT.Closure; fv: FormsVBT.T;
                  &lt;*UNUSED*&gt; name: TEXT; &lt;*UNUSED*&gt; time: VBT.TimeStamp) =
VAR
  s: T := fv;
  deck := DeckVBT.New(&quot;Deck &quot; &amp; Fmt.Int(s.deckCt));
  doc := DocVBT.NewFromURL();
BEGIN
  DeckVBT.AddDoc(deck, doc);
  AddDeck(s, deck);
END NewDeck;

PROCEDURE <A NAME="Iconize"><procedure>Iconize</procedure></A> (ch: WSObjectVBT.T) =
  BEGIN
    IF ZSplit.IsMapped (ch) THEN
      ZSplit.Unmap(ch);
      TYPECASE ch OF
      | DeckVBT.T (deck) =&gt;
          VAR ws := deck.getWorkspace();
              mb := GetMButton (ws, deck);
              v := FormsVBT.GetVBT (mb, &quot;icon&quot;);
          BEGIN
            PixmapVBT.Put (v, unmappedPixmap);
          END
      ELSE
      END
    END
  END Iconize;

PROCEDURE <A NAME="Deiconize"><procedure>Deiconize</procedure></A> (ch: WSObjectVBT.T) =
  BEGIN
    IF NOT ZSplit.IsMapped (ch) THEN
      ZSplit.Map (ch);
      TYPECASE ch OF
      | DeckVBT.T (deck) =&gt;
          VAR ws := deck.getWorkspace();
              mb := GetMButton (ws, deck);
              v := FormsVBT.GetVBT (mb, &quot;icon&quot;);
          BEGIN
            PixmapVBT.Put (v, mappedPixmap);
          END
      ELSE
      END
    END
  END Deiconize;

PROCEDURE <A NAME="SearchSearch"><procedure>SearchSearch</procedure></A>(&lt;*UNUSED*&gt; cl:FormsVBT.Closure; fv: FormsVBT.T;
                       &lt;*UNUSED*&gt; name: TEXT; &lt;*UNUSED*&gt; time: VBT.TimeStamp) =
VAR
  s: T := fv;
  term := FormsVBT.GetText(s, &quot;searchTypein&quot;);
  newDeck := DeckVBT.New(&quot;Search Results&quot;);
  deckList, docList: RefList.T;
  doc: DocVBT.T;
BEGIN
  FormsVBT.PopDown(s, &quot;searchDialog&quot;);
  deckList := GetDecks (s);
  AddDeck(s, newDeck);
  WHILE deckList # NIL DO
    docList := DeckVBT.DocList (deckList.head, includeFreeDocs := TRUE);
    WHILE docList # NIL DO
      doc := docList.head;
      IF DocVBT.Search (doc, term) THEN
       DeckVBT.AddDoc(newDeck, DocVBT.Copy(doc));
      END;
      docList := docList.tail;
    END;
    deckList := deckList.tail;
  END;
END SearchSearch;

PROCEDURE <A NAME="FlushCache"><procedure>FlushCache</procedure></A> (&lt;*UNUSED*&gt; cl  : FormsVBT.Closure;
                      &lt;*UNUSED*&gt; fv  : FormsVBT.T;
                      &lt;*UNUSED*&gt; name: TEXT;
                      &lt;*UNUSED*&gt; time: VBT.TimeStamp     ) =
  BEGIN
    URLCache.Flush();
    URLCache.FlushHTML();
  END FlushCache;

CONST
  DisableCachingText = &quot;Disable Caching&quot;;
  EnableCachingText = &quot;Enable Caching&quot;;

PROCEDURE <A NAME="ToggleCaching"><procedure>ToggleCaching</procedure></A> (&lt;*UNUSED*&gt; cl  : FormsVBT.Closure;
                              fv  : FormsVBT.T;
                   &lt;*UNUSED*&gt; name: TEXT;
                   &lt;*UNUSED*&gt; time: VBT.TimeStamp     ) =
  BEGIN
    Options.caching := NOT Options.caching;
    SetCaching(fv);
  END ToggleCaching;

PROCEDURE <A NAME="SetCaching"><procedure>SetCaching</procedure></A> (s: T) =
  BEGIN
    IF Options.caching THEN
      FormsVBT.PutText(s, &quot;cachingText&quot;, DisableCachingText);
      URLCache.Enable();
      URLCache.EnableHTML();
    ELSE
      FormsVBT.PutText(s, &quot;cachingText&quot;, EnableCachingText);
      URLCache.Disable();
      URLCache.DisableHTML();
    END
  END SetCaching;

CONST
  DisableZippersText = &quot;Disable Zippers&quot;;
  EnableZippersText  = &quot;Enable Zippers&quot;;

PROCEDURE <A NAME="ToggleZippers"><procedure>ToggleZippers</procedure></A> (&lt;*UNUSED*&gt; cl  : FormsVBT.Closure;
                              fv  : FormsVBT.T;
                   &lt;*UNUSED*&gt; name: TEXT;
                   &lt;*UNUSED*&gt; time: VBT.TimeStamp     ) =
  BEGIN
    Options.zippers := NOT Options.zippers;
    SetZippers(fv);
  END ToggleZippers;

PROCEDURE <A NAME="SetZippers"><procedure>SetZippers</procedure></A> (s: T) =
  BEGIN
    IF Options.zippers THEN
      FormsVBT.PutText(s, &quot;zippersText&quot;, DisableZippersText);
    ELSE
      FormsVBT.PutText(s, &quot;zippersText&quot;, EnableZippersText);
    END
  END SetZippers;

CONST
  DisableFgImagesText = &quot;Enable background images&quot;;
  EnableFgImagesText  = &quot;Disable background images&quot;;

PROCEDURE <A NAME="ToggleFgImages"><procedure>ToggleFgImages</procedure></A> (&lt;*UNUSED*&gt; cl  : FormsVBT.Closure;
                               fv  : FormsVBT.T;
                    &lt;*UNUSED*&gt; name: TEXT;
                    &lt;*UNUSED*&gt; time: VBT.TimeStamp     ) =
  BEGIN
    Options.fgImages := NOT Options.fgImages;
    SetFgImages(fv)
  END ToggleFgImages;

PROCEDURE <A NAME="SetFgImages"><procedure>SetFgImages</procedure></A> (s: T) =
  BEGIN
    IF Options.fgImages THEN
      FormsVBT.PutText(s, &quot;fgImagesText&quot;, DisableFgImagesText)
    ELSE
      FormsVBT.PutText(s, &quot;fgImagesText&quot;, EnableFgImagesText)
    END
  END SetFgImages;

PROCEDURE <A NAME="Debug"><procedure>Debug</procedure></A> (&lt;*UNUSED*&gt; cl  : FormsVBT.Closure;
                 &lt;*UNUSED*&gt; fv  : FormsVBT.T;
                 &lt;*UNUSED*&gt; name: TEXT;
                 &lt;*UNUSED*&gt; time: VBT.TimeStamp     ) =
  BEGIN
    RTCollector.Collect();
    RTutils.Heap(
      TRUE, presentation := RTutils.HeapPresentation.ByByteCount);
  END Debug;

PROCEDURE <A NAME="Quit"><procedure>Quit</procedure></A> (&lt;*UNUSED*&gt; cl  : FormsVBT.Closure;
                           fv  : FormsVBT.T;
                &lt;*UNUSED*&gt; name: TEXT;
                &lt;*UNUSED*&gt; time: VBT.TimeStamp     ) =
  BEGIN
    Trestle.Delete(fv);
  END Quit;

PROCEDURE <A NAME="Raise"><procedure>Raise</procedure></A>(&lt;*UNUSED*&gt; cl: FormsVBT.Closure; fv: FormsVBT.T;
                &lt;*UNUSED*&gt; name: TEXT; &lt;*UNUSED*&gt; time: VBT.TimeStamp) =
VAR
  s: MButton := fv;
  deck := s.deck;
BEGIN
  DeckVBT.Deiconize (deck);
  ZSplit.Lift(deck);
END Raise;

PROCEDURE <A NAME="GetPixmap"><procedure>GetPixmap</procedure></A> (name: TEXT): Pixmap.T =
  VAR
    rd := Rsrc.Open(name, path);
    image := Image.FromRd(rd);
  BEGIN
    Rd.Close(rd);
    RETURN Image.Scaled(image)
  END GetPixmap;

BEGIN
  path := Rsrc.BuildPath(&quot;$DeckScapePATH&quot;, MyBundle.Get());
  unmappedPixmap := GetPixmap (&quot;unmapped.pbm&quot;);
  mappedPixmap := GetPixmap (&quot;mapped.pbm&quot;);
END WorkspaceVBT.
</PRE>
</inModule>
<PRE>























</PRE>
</BODY>
</HTML>
