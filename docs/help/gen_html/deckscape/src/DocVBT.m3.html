<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: deckscape/src/DocVBT.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>deckscape/src/DocVBT.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright 1996 Digital Equipment Corporation.              
 Distributed only by permission.                            
                                                            
 Last modified on Mon Aug 19 22:13:45 PDT 1996 by mhb       

<P><PRE>&lt;* PRAGMA LL *&gt;

MODULE <module><implements><A HREF="DocVBT.i3.html">DocVBT</A></implements></module>;

IMPORT <A HREF="../../libm3/src/params/Env.i3.html">Env</A>, <A HREF="../../libm3/src/fmtlex/Fmt.i3.html">Fmt</A>, <A HREF="../../formsvbt/src/FormsVBT.i3.html">FormsVBT</A>, <A HREF="../../vbtkit/src/vbtkitutils/Rsrc.i3.html">Rsrc</A>, <A HREF="../derived/MyBundle.i3.html">MyBundle</A>, <A HREF="WSObjectVBT.i3.html">WSObjectVBT</A>, <A HREF="../../ui/src/vbt/VBT.i3.html">VBT</A>, <A HREF="../../web/src/Web.i3.html">Web</A>, <A HREF="../../webvbt/src/WebVBT.i3.html">WebVBT</A>,
       <A HREF="../../m3core/src/thread/Common/Thread.i3.html">Thread</A>, <A HREF="DeckVBT.i3.html">DeckVBT</A>, <A HREF="WorkspaceVBT.i3.html">WorkspaceVBT</A>, <A HREF="../../formsvbt/src/FVTypes.i3.html">FVTypes</A>, <A HREF="../../vbtkit/src/lego/SourceVBT.i3.html">SourceVBT</A>, <A HREF="../../m3core/src/text/Text.i3.html">Text</A>, <A HREF="../../libm3/derived/TextList.i3.html">TextList</A>,
       <A HREF="FreeDocVBT.i3.html">FreeDocVBT</A>, <A HREF="../../vbtkit/src/vbtkitutils/AnyEvent.i3.html">AnyEvent</A>, <A HREF="../../libm3/src/geometry/Point.i3.html">Point</A>, <A HREF="../../libm3/src/geometry/Rect.i3.html">Rect</A>, <A HREF="Options.i3.html">Options</A>;

&lt;* FATAL ANY *&gt;

TYPE
  MyWebVBT = WebVBT.T OBJECT
    doc: T;
    fork: BOOLEAN;
    mu: MUTEX;
    cond: Thread.Condition;
    done: BOOLEAN;
  OVERRIDES
    hotlink := HotLink;
    ready := Ready;
  END;

REVEAL <A NAME="T">T</A> = FormsVBT.T BRANDED OBJECT
    owner: WSObjectVBT.T := NIL;
    webvbt: MyWebVBT;
  OVERRIDES
    realize := Realize;
  END;

TYPE
  Source = FVTypes.FVSource OBJECT
    doc: T;
  OVERRIDES
    hit := Hit;
  END;

PROCEDURE <A NAME="Realize"><procedure>Realize</procedure></A> (doc: T; type, name: TEXT) : VBT.T
  RAISES {FormsVBT.Error} =
  BEGIN
    IF Text.Equal (name, &quot;source&quot;) THEN
      RETURN NEW (Source, doc := doc)
    ELSE
      RETURN FormsVBT.T.realize (doc, type, name)
    END;
  END Realize;

PROCEDURE <A NAME="NewDoc"><procedure>NewDoc</procedure></A> (): T =
  VAR
    doc      := NEW(T);
    path     := Rsrc.BuildPath(&quot;$DeckScapePATH&quot;, MyBundle.Get());
    delete   := NEW(FormsVBT.Closure, apply := Delete);
    hotlist  := NEW(FormsVBT.Closure, apply := Hotlist);
    reparent := NEW(FormsVBT.Closure, apply := Reparent);
  BEGIN
    EVAL FormsVBT.T.initFromRsrc(doc, &quot;Doc.fv&quot;, path, TRUE);
    FormsVBT.Attach(doc, &quot;killButton&quot;, delete);
    FormsVBT.Attach(doc, &quot;hotlistButton&quot;, hotlist);
    FormsVBT.Attach(doc, &quot;source&quot;, reparent);
    doc.webvbt := NEW(MyWebVBT, doc:=doc).init();
    FormsVBT.PutGeneric(doc, &quot;gen&quot;, doc.webvbt);
    RETURN doc;
  END NewDoc;

PROCEDURE <A NAME="NewFromPage"><procedure>NewFromPage</procedure></A> (page: Web.Page; base: TEXT): T =
  VAR
    doc    := NewDoc();
    webvbt := doc.webvbt;
  BEGIN
    FormsVBT.PutText(doc, &quot;docName&quot;, base);
    webvbt.fork := TRUE;
    webvbt.fromText(
      style := GetCurrentStyle(), zippers := GetCurrentZippers(),
      contents := page.contents, contentType := page.header.contentType,
      contentSubType := page.header.contentSubType, url := base);
    RETURN doc;
  END NewFromPage;

PROCEDURE <A NAME="NewFromURL"><procedure>NewFromURL</procedure></A> (url: TEXT := NIL; reload := FALSE; fork := TRUE): T =
  VAR
    doc    := NewDoc();
    webvbt := doc.webvbt;
  BEGIN
    IF url = NIL THEN url := DefaultHomeURL END;
    FormsVBT.PutText(doc, &quot;docName&quot;, url);
    webvbt.fork := fork;
    IF fork THEN
      webvbt.fetch(url, reload := reload, style := GetCurrentStyle(),
                   zippers := GetCurrentZippers());
    ELSE
      webvbt.mu := NEW(MUTEX);
      webvbt.cond := NEW(Thread.Condition);
      webvbt.done := FALSE;
      LOCK webvbt.mu DO
        webvbt.fetch(url, reload := reload, style := GetCurrentStyle(),
                     zippers := GetCurrentZippers());
        WHILE NOT webvbt.done DO Thread.Wait(webvbt.mu, webvbt.cond) END
      END
    END;
    RETURN doc;
  END NewFromURL;
</PRE> Bug: GetCurrent... procedures need to lock VBT.mu; they cannot be NewFromURL is
   called sometime with VBT.mu lock (e.g., WorkspaceVBT.NewDeck) and
   sometime without (e.g., DoExpand). 

<P><PRE>PROCEDURE <A NAME="GetCurrentZippers"><procedure>GetCurrentZippers</procedure></A> (): BOOLEAN =
  BEGIN
    RETURN Options.zippers
  END GetCurrentZippers;

PROCEDURE <A NAME="GetCurrentStyle"><procedure>GetCurrentStyle</procedure></A> (): WebVBT.Style =
  BEGIN
    IF Options.fgImages THEN
      RETURN WebVBT.Style.Normal
    ELSE
      RETURN WebVBT.Style.Background
    END
  END GetCurrentStyle;

PROCEDURE <A NAME="Ready"><procedure>Ready</procedure></A> (w: MyWebVBT; ct: CARDINAL) =
  BEGIN
    IF NOT w.fork AND ct = 0 THEN
      LOCK w.mu DO w.done := TRUE; Thread.Broadcast(w.cond); END
    END
  END Ready;

PROCEDURE <A NAME="Copy"><procedure>Copy</procedure></A> (doc: T): T =
  BEGIN
    RETURN NewFromPage(doc.webvbt.page, doc.webvbt.url);
  END Copy;

PROCEDURE <A NAME="SetOwner"><procedure>SetOwner</procedure></A> (doc: T; owner: VBT.T) =
  BEGIN
    doc.owner := owner;
  END SetOwner;

PROCEDURE <A NAME="GetOwner"><procedure>GetOwner</procedure></A> (doc: T): VBT.T =
  BEGIN
    RETURN doc.owner
  END GetOwner;

PROCEDURE <A NAME="GetPage"><procedure>GetPage</procedure></A> (doc: T): Web.Page =
  BEGIN
    RETURN doc.webvbt.page;
  END GetPage;

PROCEDURE <A NAME="GetTitle"><procedure>GetTitle</procedure></A> (doc: T): TEXT =
  BEGIN
    TYPECASE doc.webvbt.page OF
    | NULL =&gt; RETURN &quot;fetching...&quot;
    | WebVBT.HTMLPage (p) =&gt; RETURN p.html.title
    | WebVBT.ImagePage =&gt; RETURN &quot;&lt;image&gt;&quot;
    ELSE
      RETURN &quot;&lt;????&gt;&quot;
    END;
  END GetTitle;

PROCEDURE <A NAME="Hit"><procedure>Hit</procedure></A> (                      s     : Source;
                                     target: VBT.T;
               &lt;* UNUSED *&gt; READONLY cd    : VBT.PositionRec):
  BOOLEAN =
  VAR owner := s.doc.owner;
  BEGIN
    IF ISTYPE(owner, DeckVBT.T) THEN
      (* a DocVBT inside a DeckVBT can go into the Workspace
         and into a DeckVBT other than its owner *)
      IF ISTYPE(target, WorkspaceVBT.Target) THEN
        RETURN TRUE
      ELSE
        RETURN target # DeckVBT.GetTarget (owner)
      END
    ELSE
      (* a DocVBT inside a FreeDocVBT can go into any DeckVBT,
         but not into the Workspace *)
      RETURN ISTYPE (target, DeckVBT.Target)
    END
  END Hit;

PROCEDURE <A NAME="Reparent"><procedure>Reparent</procedure></A> (&lt;*UNUSED*&gt; cl  : FormsVBT.Closure;
                               fv  : FormsVBT.T;
                    &lt;*UNUSED*&gt; name: TEXT;
                    &lt;*UNUSED*&gt; time: VBT.TimeStamp     ) =
  VAR
    doc    := NARROW(fv, T);
    source := NARROW(FormsVBT.GetVBT(fv, &quot;source&quot;), Source);
    target := SourceVBT.GetTarget(source);
    owner  := source.doc.owner;
    newDeck   : DeckVBT.T;
    newFreeDoc: FreeDocVBT.T;
  BEGIN
    TYPECASE owner OF
    | DeckVBT.T (deck) =&gt;
        (* a DocVBT in a DeckVBT; the target is either a
           Workspace or a DeckVBT *)
        IF ISTYPE(target, WorkspaceVBT.Target) THEN
          DeckVBT.RemDoc(deck, doc);
          newFreeDoc :=
            FreeDocVBT.New(DeckVBT.GetTitle(deck) &amp; &quot; [DOC]&quot;);
          VAR dom := VBT.Domain(deck);
              nw := LocateMouse(fv);
              hor := Rect.HorSize(dom);
              ver := Rect.VerSize(dom);
              r := Rect.FromEdges (nw.h, nw.h+hor, nw.v, nw.v+ver);
          BEGIN
            WorkspaceVBT.AddFreeDoc(deck.getWorkspace(), newFreeDoc, r);
          END;
          DeckVBT.AddFreeDoc(deck, newFreeDoc);
          FreeDocVBT.AddDoc(newFreeDoc, doc);
        ELSE
          DeckVBT.RemDoc(deck, doc);
          newDeck := NARROW(target, DeckVBT.Target).deck;
          DeckVBT.AddDoc(newDeck, doc);
        END
    | FreeDocVBT.T (freeDoc) =&gt;
        (* a DocVBT in a FreeDocVBT; the target must be a
           DeckVBT *)
        FreeDocVBT.RemDoc(freeDoc, doc);
        newDeck := NARROW(target, DeckVBT.Target).deck;
        DeckVBT.AddDoc(newDeck, doc);
    ELSE                         &lt;* ASSERT FALSE *&gt;
    END;
  END Reparent;

PROCEDURE <A NAME="LocateMouse"><procedure>LocateMouse</procedure></A> (fv: FormsVBT.T): Point.T =
  BEGIN
    TYPECASE FormsVBT.GetTheEvent(fv) OF
    | AnyEvent.Mouse (m) =&gt;
        WITH cp = m.mouse.cp DO
          IF NOT cp.offScreen THEN RETURN cp.pt END
        END
    ELSE
    END;
    RETURN Point.Origin
  END LocateMouse;

PROCEDURE <A NAME="Delete"><procedure>Delete</procedure></A> (&lt;*UNUSED*&gt; cl  : FormsVBT.Closure;
                             fv  : FormsVBT.T;
                  &lt;*UNUSED*&gt; name: TEXT;
                  &lt;*UNUSED*&gt; time: VBT.TimeStamp     ) =
  VAR doc: T := fv;
  BEGIN
    doc.owner.remDoc(doc);
  END Delete;

PROCEDURE <A NAME="Hotlist"><procedure>Hotlist</procedure></A> (&lt;*UNUSED*&gt; cl  : FormsVBT.Closure;
                              fv  : FormsVBT.T;
                   &lt;*UNUSED*&gt; name: TEXT;
                   &lt;*UNUSED*&gt; time: VBT.TimeStamp     ) =
  VAR doc: T := fv;
       ws: WorkspaceVBT.T := doc.owner.getWorkspace();
  BEGIN
    DeckVBT.AddDoc(ws.hotlist, Copy(doc));
  END Hotlist;

PROCEDURE <A NAME="Search"><procedure>Search</procedure></A> (doc: T; text: TEXT): BOOLEAN =
  BEGIN
    RETURN doc.webvbt.search(text)
  END Search;

PROCEDURE <A NAME="Reload"><procedure>Reload</procedure></A> (doc: T): T =
  BEGIN
    RETURN NewFromURL(doc.webvbt.url, reload := TRUE)
  END Reload;

TYPE
  Link = REF RECORD
    label, url: TEXT;
    next: Link;
  END;

TYPE
  ExpandClosure = Thread.Closure OBJECT
                    (* READONLY by threads: *)
                    doc     : T;
                    deck    : DeckVBT.T;
                    numLinks: INTEGER;
                    (* protected by mu: *)
                    mu   : MUTEX;
                    links: TextList.T;  (* NIL'd once processed *)
                  OVERRIDES
                    apply := DoExpand;
                  END;

PROCEDURE <A NAME="Expand"><procedure>Expand</procedure></A> (doc: T): VBT.T =
  VAR
    links := doc.webvbt.getLinks();
    l     := links;
    ct    := 0;
    mu    := NEW(MUTEX);
  BEGIN
    WHILE l # NIL DO INC(ct); l := l.tail END;
    WITH deck = DeckVBT.New(&quot;Expanding &quot; &amp; Fmt.Int(ct) &amp; &quot; links&quot;) DO
      LOCK mu DO
        FOR th := 1 TO NumberOfExpansionThreads DO
          EVAL Thread.Fork(NEW(ExpandClosure, doc := doc, deck := deck,
                               numLinks := ct, mu := mu, links := links));
        END
      END;
      RETURN deck
    END
  END Expand;

PROCEDURE <A NAME="DoExpand"><procedure>DoExpand</procedure></A> (cl: ExpandClosure): REFANY =
  VAR
    doc   := cl.doc;
    deck  := cl.deck;
    links := cl.links;
  VAR
    l     : TextList.T;
    url   : TEXT;
    newURL: TEXT;
    newDoc: T;
  BEGIN
    LOOP
      LOCK cl.mu DO
        l := links;
        WHILE l # NIL AND l.head = NIL DO l := l.tail END;
        IF l = NIL THEN EXIT END;
        url := l.head;
        l.head := NIL;
      END;
      newURL := Web.AbsoluteURL(url, doc.webvbt.url);
      newDoc := NewFromURL(newURL, fork := FALSE);
      LOCK VBT.mu DO DeckVBT.AddDoc(deck, newDoc, FALSE) END
    END;
    LOCK VBT.mu DO DeckVBT.SetTitle(deck, &quot;Expanded&quot;); END;
    RETURN NIL;
  END DoExpand;

PROCEDURE <A NAME="HotLink"><procedure>HotLink</procedure></A> (w: MyWebVBT; url: TEXT; READONLY cd: VBT.MouseRec) =
  VAR
    expandedURL    := Web.AbsoluteURL(url, w.url);
    newDoc     : T;
  BEGIN
    IF VBT.Modifier.Control IN cd.modifiers THEN
      VAR
        deck    := WhichDeck(w.doc);
        docList := DeckVBT.DocList(deck, includeFreeDocs := FALSE);
      BEGIN
        WHILE docList # NIL DO
          VAR doc: T := docList.head;
          BEGIN
            IF Text.Equal(expandedURL, doc.webvbt.url) THEN
              DeckVBT.SetTopDoc(deck, DeckVBT.IndexOfDoc(deck, doc));
              RETURN
            END
          END;
          docList := docList.tail
        END
      END
    END;
    newDoc := NewFromURL(expandedURL);
    w.doc.owner.addDoc(newDoc);
  END HotLink;

PROCEDURE <A NAME="WhichDeck"><procedure>WhichDeck</procedure></A> (doc: T): DeckVBT.T =
  VAR owner := doc.owner;
  BEGIN
    IF ISTYPE (owner, DeckVBT.T) THEN
      RETURN owner
    ELSIF ISTYPE (owner, FreeDocVBT.T) THEN
      RETURN FreeDocVBT.GetDeck (owner)
    ELSE &lt;* ASSERT FALSE *&gt;
    END
  END WhichDeck;

BEGIN
  DefaultHomeURL := Env.Get(&quot;WWW_HOME&quot;);
  IF DefaultHomeURL = NIL OR Text.Empty(DefaultHomeURL) THEN
      DefaultHomeURL := &quot;http://www.research.digital.com/&quot;
  END;
END DocVBT.
</PRE>
</inModule>
<PRE>























</PRE>
</BODY>
</HTML>
