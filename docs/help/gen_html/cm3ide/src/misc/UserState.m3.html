<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: cm3ide/src/misc/UserState.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>cm3ide/src/misc/UserState.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright 1996, Critical Mass, Inc.  All rights reserved. 

<P> This module maintains a per-user, persistent table of key-value
   pairs. 

<P><PRE>MODULE <module><implements><A HREF="UserState.i3.html">UserState</A></implements></module>;

IMPORT <A HREF="../../../libm3/derived/IntArraySort.i3.html">IntArraySort</A>, <A HREF="../../../libm3/derived/IntRefTbl.i3.html">IntRefTbl</A>, <A HREF="../../../libm3/src/rw/FileWr.i3.html">FileWr</A>, <A HREF="../../../libm3/src/fmtlex/Fmt.i3.html">Fmt</A>, <A HREF="../../../libm3/src/os/Common/FS.i3.html">FS</A>, <A HREF="#x1">M3Scanner</A>;
IMPORT <A HREF="#x2">OS</A>, <A HREF="../../../libm3/src/os/Common/OSError.i3.html">OSError</A>, <A HREF="../../../libm3/src/os/Common/Pathname.i3.html">Pathname</A>, <A HREF="../../../libm3/src/os/Common/Process.i3.html">Process</A>, <A HREF="../../../m3core/src/text/Text.i3.html">Text</A>, <A HREF="../../../m3core/src/thread/Common/Thread.i3.html">Thread</A>, <A HREF="../../../m3core/src/word/Word.i3.html">Word</A>, <A HREF="../../../libm3/src/rw/Wr.i3.html">Wr</A>;
IMPORT <A HREF="#x3">Buf</A>, <A HREF="#x4">ErrLog</A>, <A HREF="#x5">ID</A>, <A HREF="../utils/LexMisc.i3.html">LexMisc</A>;

CONST
  Escape = '\134'; (* back slash *)

VAR
  mu      : MUTEX;
  changed : Thread.Condition;
  tbl     : IntRefTbl.T;
  version : INTEGER := 0;
  save_dir: TEXT := NIL;
  dirty   : BOOLEAN := FALSE;
  fname   := ARRAY [0..1] OF TEXT { &quot;CM3_IDE.cfg0&quot;, &quot;CM3_IDE.cfg1&quot; };

PROCEDURE <A NAME="Get"><procedure>Get</procedure></A> (key_nm: TEXT): TEXT =
  VAR key := ID.Add (key_nm);  ref: REFANY;
  BEGIN
    LOCK mu DO
      IF NOT tbl.get (key, ref) THEN ref := NIL; END;
    END;
    RETURN ref;
  END Get;

PROCEDURE <A NAME="Put"><procedure>Put</procedure></A> (key_nm, value: TEXT) =
  VAR key := ID.Add (key_nm);  ref: REFANY;
  BEGIN
    IF (value = NIL) THEN value := &quot;&quot;; END;
    LOCK mu DO
      IF tbl.get (key, ref) THEN
        IF NOT Text.Equal (ref, value) THEN
          EVAL tbl.put (key, value);
          dirty := TRUE;
          Thread.Signal (changed);
        END;
      ELSE
        EVAL tbl.put (key, value);
        dirty := TRUE;
        Thread.Signal (changed);
      END;
    END;
  END Put;
</PRE>-------------------------------------------------------------- updating ---

<P><PRE>TYPE UpdateClosure = Thread.Closure OBJECT OVERRIDES apply := DoUpdates; END;

PROCEDURE <A NAME="DoUpdates"><procedure>DoUpdates</procedure></A> (&lt;*UNUSED*&gt; cl: UpdateClosure):  REFANY =
  VAR x: [0..1];
  BEGIN
    LOOP
      LOCK mu DO
        WHILE NOT dirty DO Thread.Wait (mu, changed); END;
        x := Word.And (version+1, 1);
        IF DumpTable (fname [x]) THEN RemoveTable (fname [1-x]); END;
        dirty := FALSE;
      END;
      Thread.Pause (10.0d0);  (* give everybody a break... *)
    END;
  END DoUpdates;

PROCEDURE <A NAME="DumpState"><procedure>DumpState</procedure></A> () =
  (* called hastily during process shutdown... *)
  BEGIN
    IF dirty AND (save_dir # NIL) THEN
      INC (version);
      EVAL DumpTable (fname [Word.And (version, 1)]);
    END;
  END DumpState;

PROCEDURE <A NAME="DumpTable"><procedure>DumpTable</procedure></A> (fn: TEXT): BOOLEAN =
  VAR
    key: INTEGER;
    ref: REFANY;
    wr: Wr.T;
    iter: IntRefTbl.Iterator;
    t1, t2: TEXT;
    keys := NEW (REF ARRAY OF INTEGER, tbl.size ());

  PROCEDURE CmpKey (a, b: INTEGER): [-1 .. +1] =
    BEGIN
      IF    (a = b)        THEN RETURN  0;
      ELSIF (a = ID.NoID)  THEN RETURN +1;
      ELSIF (b = ID.NoID)  THEN RETURN -1;
      ELSIF ID.IsLT (a, b) THEN RETURN -1;
      ELSE                      RETURN +1;
      END;
    END CmpKey;

  BEGIN
    iter := tbl.iterate ();
    FOR i := FIRST (keys^) TO LAST (keys^) DO
      IF NOT iter.next (key, ref) THEN key := ID.NoID; END;
      keys[i] := key;
    END;
    IntArraySort.Sort (keys^, CmpKey);
    TRY
      wr := FileWr.Open (fn);
      TRY
        FOR i := FIRST (keys^) TO LAST (keys^) DO
          key := keys[i];
          IF (key # ID.NoID) AND tbl.get (key, ref) THEN
            t1 := ID.ToText (key);
            t2 := ref;
            IF t2 = NIL THEN t2 := &quot;&quot;; END;
            DumpText (wr, t1);
            IF Text.Length (t1) + Text.Length (t2) &gt; 70 THEN
              Wr.PutText (wr, Wr.EOL);
              Wr.PutText (wr, &quot;  &quot;);
            END;
            Wr.PutText (wr, &quot;  &quot;);
            DumpText (wr, t2);
            Wr.PutText (wr, Wr.EOL);
            Wr.PutText (wr, Wr.EOL);
          END;
        END;
        Wr.PutText (wr, Fmt.Int (version));
        Wr.PutText (wr, &quot; (* sequence number *)&quot;);
        Wr.PutText (wr, Wr.EOL);
      FINALLY
        Wr.Close (wr);
      END;
      INC (version);
    EXCEPT
    | Thread.Alerted =&gt;
        ErrLog.Msg (&quot;interrupted while updating user state file: &quot;, fn);
        RETURN FALSE;
    | OSError.E, Wr.Failure (ec) =&gt;
        ErrLog.Msg (&quot;trouble updating user state file: &quot;, fn, OS.Err (ec));
        RETURN FALSE;
    END;
    RETURN TRUE;
  END DumpTable;

PROCEDURE <A NAME="DumpText"><procedure>DumpText</procedure></A> (wr: Wr.T;  txt: TEXT)
  RAISES {Wr.Failure, Thread.Alerted} =
  VAR x0, x1, x2: INTEGER;  c: CHAR;
  BEGIN
    Wr.PutChar (wr, '\&quot;');
    FOR i := 0 TO Text.Length (txt) - 1 DO
      c := Text.GetChar (txt, i);
      IF    (c = '\n')   THEN    Wr.PutChar (wr, Escape); Wr.PutChar (wr, 'n');
      ELSIF (c = '\r')   THEN    Wr.PutChar (wr, Escape); Wr.PutChar (wr, 'r');
      ELSIF (c = '\t')   THEN    Wr.PutChar (wr, Escape); Wr.PutChar (wr, 't');
      ELSIF (c = '\f')   THEN    Wr.PutChar (wr, Escape); Wr.PutChar (wr, 'f');
      ELSIF (c = '\'')   THEN    Wr.PutChar (wr, Escape); Wr.PutChar (wr, '\'');
      ELSIF (c = '\&quot;')   THEN    Wr.PutChar (wr, Escape); Wr.PutChar (wr, '\&quot;');
      ELSIF (c = Escape) THEN    Wr.PutChar (wr, Escape); Wr.PutChar (wr, Escape);
      ELSIF (' ' &lt;= c) AND (c &lt;= '}') THEN    Wr.PutChar (wr, c);
      ELSE
        x2 :=                  Word.And (ORD (c), 8_007)     + ORD ('0');
        x1 := Word.RightShift (Word.And (ORD (c), 8_070), 3) + ORD ('0');
        x0 := Word.RightShift (Word.And (ORD (c), 8_700), 6) + ORD ('0');
        Wr.PutChar (wr, Escape);
        Wr.PutChar (wr, VAL (x0, CHAR));
        Wr.PutChar (wr, VAL (x1, CHAR));
        Wr.PutChar (wr, VAL (x2, CHAR));
      END;
    END;
    Wr.PutChar (wr, '\&quot;');
  END DumpText;

PROCEDURE <A NAME="RemoveTable"><procedure>RemoveTable</procedure></A> (fn: TEXT) =
  BEGIN
    TRY
      FS.DeleteFile (fn);
    EXCEPT OSError.E =&gt;
      (* darn *)
    END;
  END RemoveTable;
</PRE>----------------------------------------------------------- initialization ---

<P><PRE>PROCEDURE <A NAME="Init"><procedure>Init</procedure></A> (dir: TEXT) =
  VAR
    buf0, buf1: Buf.T;
    tbl0, tbl1: IntRefTbl.T;
    v0,   v1  : INTEGER;
  BEGIN
    mu := NEW (MUTEX);
    changed := NEW (Thread.Condition);

    save_dir := dir;
    IF (dir = NIL) THEN
      tbl := DefaultTable ();
      RETURN;
    END;

    fname[0] := Pathname.Join (dir, fname[0], NIL);
    fname[1] := Pathname.Join (dir, fname[1], NIL);

    buf0 := Buf.FromFile (fname[0], pad := 1);
    buf1 := Buf.FromFile (fname[1], pad := 1);

    IF (buf0 = NIL) AND (buf1 = NIL) THEN
      tbl := DefaultTable ();
    ELSIF (buf0 = NIL) THEN
      ErrLog.Msg (&quot;Recovering user state from &quot;, fname[1]);
      tbl := ReadBuf (buf1, version);
    ELSIF (buf1 = NIL) THEN
      ErrLog.Msg (&quot;Recovering user state from &quot;, fname[0]);
      tbl := ReadBuf (buf0, version);
    ELSE
      tbl0 := ReadBuf (buf0, v0);
      tbl1 := ReadBuf (buf1, v1);
      IF (v0 &gt; v1) THEN
        ErrLog.Msg (&quot;Recovering user state from &quot;, fname[0]);
        version := v0;
        tbl := tbl0;
      ELSIF (v1 &gt;= 0) THEN
        ErrLog.Msg (&quot;Recovering user state from &quot;, fname[1]);
        version := v1;
        tbl := tbl1;
      ELSE
        tbl := DefaultTable ();
      END;
    END;

    (* start the lazy update thread &amp; cleanup routine *)
    EVAL Thread.Fork (NEW (UpdateClosure));
    Process.RegisterExitor (DumpState);
  END Init;
</PRE>--------------------------------------------------------------- reading ---

<P><PRE>PROCEDURE <A NAME="ReadBuf"><procedure>ReadBuf</procedure></A> (buf: Buf.T;  VAR(*OUT*) vers: INTEGER): IntRefTbl.T =
  VAR
    tbl := NewTable ();
    cnt := 0;
    key, txt : TEXT;
    cursor: INTEGER;
    lex := NEW (M3Scanner.Default).initFromBuf (buf, skip_comments := TRUE,
                                                split_pragmas := FALSE);
  BEGIN
    vers := FIRST (INTEGER);
    LOOP
      CASE lex.token OF
      | M3Scanner.TK_EOF, M3Scanner.TK_Error =&gt;
          EXIT;
      | M3Scanner.TK_Text_const =&gt;
          txt := LexMisc.ReadString (SUBARRAY (buf^, lex.offset+1, lex.length-2));
          IF (cnt = 0) THEN
            key := txt;
            INC (cnt);
          ELSIF (cnt = 1) THEN
            EVAL tbl.put (ID.Add (key), txt);
            cnt := 0;
          END;
      | M3Scanner.TK_Card_const =&gt;
          cursor := 0;
          vers := LexMisc.ReadInt (SUBARRAY (buf^, lex.offset, lex.length), cursor);
      ELSE (* ignore the bogus tokens *)
      END;
      lex.next ();
    END;
    RETURN tbl;
  END ReadBuf;

PROCEDURE <A NAME="DefaultTable"><procedure>DefaultTable</procedure></A> (): IntRefTbl.T =
  BEGIN
    ErrLog.Msg (&quot;Using default configuration.&quot;);
    RETURN NewTable ();
  END DefaultTable;

PROCEDURE <A NAME="NewTable"><procedure>NewTable</procedure></A> (): IntRefTbl.T =
  BEGIN
    RETURN NEW (IntRefTbl.Default).init ();
  END NewTable;
</PRE>--------------------------------------------------------------- writing ---

<P><PRE>BEGIN
END UserState.
</PRE>
</inModule>
<HR>
<A NAME="x1">interface M3Scanner is in:
</A><UL>
<LI><A HREF="../../../m3scan/src/M3Scanner.i3.html#0TOP0">m3scan/src/M3Scanner.i3</A>
<LI><A HREF="../../../m3scanner/src/M3Scanner.i3.html#0TOP0">m3scanner/src/M3Scanner.i3</A>
</UL>
<P>
<HR>
<A NAME="x2">interface OS is in:
</A><UL>
<LI><A HREF="../utils/OS.i3.html#0TOP0">cm3ide/src/utils/OS.i3</A>
<LI><A HREF="../../../fix_nl/src/OS.i3.html#0TOP0">fix_nl/src/OS.i3</A>
<LI><A HREF="../../../m3browser/src/OS.i3.html#0TOP0">m3browser/src/OS.i3</A>
</UL>
<P>
<HR>
<A NAME="x3">interface Buf is in:
</A><UL>
<LI><A HREF="../utils/Buf.i3.html#0TOP0">cm3ide/src/utils/Buf.i3</A>
<LI><A HREF="../../../libbuf/src/Buf.i3.html#0TOP0">libbuf/src/Buf.i3</A>
</UL>
<P>
<HR>
<A NAME="x4">interface ErrLog is in:
</A><UL>
<LI><A HREF="../utils/ErrLog.i3.html#0TOP0">cm3ide/src/utils/ErrLog.i3</A>
<LI><A HREF="../../../m3markup/src/ErrLog.i3.html#0TOP0">m3markup/src/ErrLog.i3</A>
</UL>
<P>
<HR>
<A NAME="x5">interface ID is in:
</A><UL>
<LI><A HREF="../utils/ID.i3.html#0TOP0">cm3ide/src/utils/ID.i3</A>
<LI><A HREF="../../../m3markup/src/ID.i3.html#0TOP0">m3markup/src/ID.i3</A>
</UL>
<P>
<PRE>























</PRE>
</BODY>
</HTML>
