<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: cm3ide/src/misc/LineWr.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>cm3ide/src/misc/LineWr.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright 1996, Critical Mass, Inc.   All rights reserved. 

<P><PRE>MODULE <module><implements><A HREF="LineWr.i3.html">LineWr</A></implements></module>;

IMPORT <A HREF="../../../libm3/src/rw/WrClass.i3.html">WrClass</A>, <A HREF="../../../m3core/src/text/Text.i3.html">Text</A>, <A HREF="../../../m3core/src/thread/Common/Thread.i3.html">Thread</A>, <A HREF="../../../libm3/src/rw/Wr.i3.html">Wr</A>;

REVEAL
  <A NAME="T">T</A> = Public BRANDED OBJECT
        user_proc : CallBack   := NIL;
        user_ref  : REFANY     := NIL;
        overflow  : TEXT       := NIL;
        line_len  : INTEGER    := 0;
        line      : ARRAY [0..255] OF CHAR;
      OVERRIDES
        seek   := Seek;
        flush  := Flush;
        close  := Close;
        length := Length;
        init   := Init;
      END;

PROCEDURE <A NAME="Init"><procedure>Init</procedure></A> (wr: T;  proc: CallBack;  ref: REFANY): T =
  BEGIN
    WrClass.Lock(wr);
    TRY
      (* LineWr fields *)
      wr.user_proc := proc;
      wr.user_ref  := ref;
      wr.overflow  := NIL;
      wr.line_len  := 0;

      (* generic Wr fields *)
      IF (wr.buff = NIL) THEN  wr.buff := NEW (REF ARRAY OF CHAR, 1); END;
      wr.st        := 0;
      wr.cur       := 0;
      wr.lo        := 0;
      wr.hi        := 1;
      wr.closed    := FALSE;
      wr.seekable  := FALSE;
      wr.buffered  := FALSE;
    FINALLY
      WrClass.Unlock(wr);
    END;
    RETURN wr;
  END Init;

PROCEDURE <A NAME="New"><procedure>New</procedure></A> (proc: CallBack;  ref: REFANY): T =
  BEGIN
    RETURN NEW (T).init (proc, ref);
  END New;

PROCEDURE <A NAME="Clear"><procedure>Clear</procedure></A> (wr: T) RAISES {} =
  BEGIN
    IF wr.line_len &gt; 0 THEN
      TRY
        FlushLine (wr);
      EXCEPT Wr.Failure, Thread.Alerted =&gt;
        (* ignore *)
      END;
    END;
  END Clear;

PROCEDURE <A NAME="Length"><procedure>Length</procedure></A> (wr: T): CARDINAL RAISES {} =
  BEGIN
    RETURN wr.cur;
  END Length;

PROCEDURE <A NAME="Seek"><procedure>Seek</procedure></A>(wr: T; n: CARDINAL)
  RAISES {Wr.Failure, Thread.Alerted} =
  BEGIN
    &lt;*ASSERT wr.cur = wr.hi *&gt;
    AddChar (wr, wr.buff[0]);
    wr.cur := n;
    wr.lo  := n;
    wr.hi  := n+1;
  END Seek;

PROCEDURE <A NAME="Flush"><procedure>Flush</procedure></A>(wr: T)
  RAISES {Wr.Failure, Thread.Alerted} =
  BEGIN
    IF wr.cur &gt; wr.lo THEN
      AddChar (wr, wr.buff[0]);
      INC (wr.lo);
      INC (wr.hi);
    END;
  END Flush;

PROCEDURE <A NAME="Close"><procedure>Close</procedure></A> (wr: T)
  RAISES {Wr.Failure, Thread.Alerted} =
  BEGIN
    IF wr.line_len &gt; 0 THEN  FlushLine (wr);  END;
    wr.buff      := NIL;
    wr.user_proc := NIL;
    wr.user_ref  := NIL;
    wr.line_len  := 0;
    wr.overflow  := NIL;
  END Close;

PROCEDURE <A NAME="AddChar"><procedure>AddChar</procedure></A> (wr: T;  c: CHAR)
  RAISES {Wr.Failure, Thread.Alerted} =
  BEGIN
    IF (c = '\r') THEN
      (* ignore *)
    ELSIF (c = '\n') THEN
      FlushLine (wr);
    ELSE (* regular character, add it to the current line *)
      IF (wr.line_len &gt; LAST (wr.line)) THEN
        IF (wr.overflow = NIL) THEN wr.overflow := &quot;&quot;; END;
        wr.overflow := wr.overflow &amp; Text.FromChars (wr.line);
        wr.line_len := 0;
      END;
      wr.line [wr.line_len] := c;
      INC (wr.line_len);
    END;
  END AddChar;

PROCEDURE <A NAME="FlushLine"><procedure>FlushLine</procedure></A> (wr: T)
  RAISES {Wr.Failure, Thread.Alerted} =
  VAR txt := Text.FromChars (SUBARRAY (wr.line, 0, wr.line_len));
  BEGIN
    IF wr.overflow # NIL THEN txt := wr.overflow &amp; txt; END;
    wr.user_proc (wr.user_ref, txt);
    wr.line_len := 0;
    wr.overflow := NIL;
  END FlushLine;

BEGIN
END LineWr.
</PRE>
</inModule>
<PRE>























</PRE>
</BODY>
</HTML>
