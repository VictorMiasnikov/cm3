<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: cm3ide/src/nodes/Decl.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>cm3ide/src/nodes/Decl.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright 1996 Critical Mass, Inc. All rights reserved.    

<P><PRE>MODULE <module><implements><A HREF="#x1">Decl</A></implements></module>;

IMPORT <A HREF="../../../libm3/derived/IntList.i3.html">IntList</A>, <A HREF="../../../m3core/src/text/Text.i3.html">Text</A>, <A HREF="../../../m3core/src/thread/Common/Thread.i3.html">Thread</A>, <A HREF="../../../libm3/src/rw/Wr.i3.html">Wr</A>;
IMPORT <A HREF="../misc/BrowserDB.i3.html">BrowserDB</A>, <A HREF="#x2">Buf</A>, <A HREF="#x3">ErrLog</A>, <A HREF="#x4">ID</A>, <A HREF="#x5">M3MarkUp</A>, <A HREF="../markup/M3Sym.i3.html">M3Sym</A>, <A HREF="Node.i3.html">Node</A>, <A HREF="Pkg.i3.html">Pkg</A>;
IMPORT <A HREF="../utils/RegExpr.i3.html">RegExpr</A>, <A HREF="#x6">Source</A>, (**Type,**) <A HREF="#x7">Wx</A>;

REVEAL
  <A NAME="T">T</A> = Node.Named_T BRANDED &quot;Decl.T&quot; OBJECT
    kind      : Node.Class;
  OVERRIDES
    class     := Class;
    filename  := FileName;
    iterate   := Iterate;
    next      := Next;
    gen_page  := GenPage;
  END;

PROCEDURE <A NAME="Class"><procedure>Class</procedure></A> (t: T): Node.Class =
  BEGIN
    RETURN t.kind;
  END Class;

PROCEDURE <A NAME="FileName"><procedure>FileName</procedure></A> (&lt;*UNUSED*&gt; t: T): TEXT =
  BEGIN
    RETURN NIL; (* assume we're being concatenated with the parent source *)
    (** RETURN t.parent.filename (); **)
  END FileName;

PROCEDURE <A NAME="Iterate"><procedure>Iterate</procedure></A> (t: T;  VAR s: Node.IteratorState) =
  (* declaration nodes are fixed-points =&gt; they only return self. *)
  BEGIN
    s.d := t;
  END Iterate;

PROCEDURE <A NAME="Next"><procedure>Next</procedure></A> (t: T;  VAR s: Node.IteratorState): BOOLEAN =
  BEGIN
    IF (s.d # NIL) THEN
      s.d := NIL;
      s.match := NIL;
      IF t.match (s.pattern) THEN s.match := t; END;
      RETURN (s.match # NIL);
    ELSE
      RETURN FALSE;
    END;
    (***
    s.match := s.d;  s.d := NIL;
    RETURN (s.match # NIL);
    ***)
  END Next;

PROCEDURE <A NAME="GenPage"><procedure>GenPage</procedure></A> (t: T;  wx: Wx.T;  action: ID.T;  data: Node.FormData)
  RAISES {Wr.Failure, Thread.Alerted} =
  BEGIN
    Source.EmitPage (t.parent, wx, action, data, ID.ToText (t.name));
  END GenPage;

TYPE
  Scan = M3Sym.CallBack OBJECT
    results     : Node.Set;
    parent      : Source.T;
    parent_pkg  : Pkg.T;
    pattern     : RegExpr.T;
    find_one    : BOOLEAN;
    cur_file    : Source.T;
    unit_name   : ID.T;
    path        : TEXT;
    buf         : Buf.T;
    others      : OtherSym;
    second_try  : BOOLEAN;
    all_exports : BOOLEAN;
    is_interface: BOOLEAN;
    class_match : ARRAY Node.Class OF BOOLEAN;
  OVERRIDES
    note_sym := NoteSym;
  END;

  OtherSym = REF RECORD
    next : OtherSym;
    name : ID.T;
    kind : Source.Kind;
  END;

TYPE
  SKind = M3Sym.Kind;

CONST
  IgnoreSet = M3Sym.KindSet {
    SKind.TypeUse, SKind.ExceptUse, SKind.ProcUse, SKind.MiscUse,
    SKind.Keyword, SKind.BuiltinOp, SKind.BuiltinType, SKind.BuiltinConst
  };

CONST
  ProcClass = ARRAY BOOLEAN(*is_interface*) OF Node.Class {
                Node.Class.Proc, Node.Class.ProcDecl };

PROCEDURE <A NAME="FindNodes"><procedure>FindNodes</procedure></A> (parent: Source.T;  pattern: RegExpr.T): Node.Set =
  VAR s := NEW (Scan);  tag: TEXT;
  BEGIN
    s.parent       := parent;
    s.parent_pkg   := Pkg.Home (parent);
    s.pattern      := pattern;
    s.find_one     := RegExpr.SimpleString (pattern) # NIL;
    s.others       := NIL;
    s.all_exports  := FALSE;
    s.is_interface := FALSE;

    FOR c := FIRST (s.class_match) TO LAST (s.class_match) DO
      tag := Node.ClassTag[c];
      IF (tag # NIL) AND RegExpr.Match (s.pattern, tag)
        THEN  s.class_match [c] := TRUE;  s.find_one := FALSE;
        ELSE  s.class_match [c] := FALSE;
      END;
    END;

    IF RegExpr.Match (s.pattern, M3MarkUp.Intf_to_Impl_Mark) THEN
      FindExporters (s);
    END;

    IF RegExpr.Match (s.pattern, M3MarkUp.Impl_to_Intf_Mark) THEN
      s.all_exports := TRUE;
    END;

    ScanFile (s);

    RETURN s.results;
  END FindNodes;

PROCEDURE <A NAME="FindExporters"><procedure>FindExporters</procedure></A> (s: Scan) =
  VAR ids: IntList.T;  ref: REFANY;  nm: TEXT;
  BEGIN
    IF BrowserDB.db.exporters.get (s.parent.name, ref) THEN
      ids := ref;
      WHILE (ids # NIL) DO
        EVAL FindSource (s, ids.head, Source.Kind.Other (** M3 **));
        ids := ids.tail;
      END;
    END;
    IF (s.parent.kind = Source.Kind.IG) THEN
      (* we'll pretend that generic interfaces are &quot;exported&quot; by the
         corresponding generic modules -- usually they are. *)
      nm := ID.ToText (s.parent.name);
      nm := Text.Sub (nm, 0, Text.Length (nm) - Source.ExtLen[Source.Kind.IG]);
      EVAL FindSource (s, ID.Add (nm), Source.Kind.MG);
    END;
  END FindExporters;

PROCEDURE <A NAME="ScanFile"><procedure>ScanFile</procedure></A> (s: Scan) =
  VAR n_intfs, start: INTEGER;  intfs: Node.Array;
  BEGIN
    s.second_try := FALSE;
    s.cur_file := s.parent;
    s.path := Node.FullPath (s.parent);
    s.buf := Buf.FromFile (s.path, pad := 1);
    M3Sym.Scan (s.buf, s, IgnoreSet);

    IF ((NOT s.find_one) OR (s.results.cnt &lt;= 0))
      AND (NOT s.all_exports) THEN
      (* we didn't find what we're looking for, try the exported
         interfaces for a matching symbol *)
      s.second_try := TRUE;
      start := s.results.cnt;
      WHILE (s.others # NIL) DO
        EVAL FindSource (s, s.others.name, s.others.kind);
        s.others := s.others.next;
      END;

      (* steal the results (== exported interfaces &amp; used generics) *)
      n_intfs := s.results.cnt - start;
      intfs := NEW (Node.Array, n_intfs);
      FOR i := 0 TO n_intfs-1 DO
        WITH z = s.results.elts[i+start] DO
          intfs[i] := z;  z := NIL;
        END;
      END;
      s.results.cnt := start;

      (* finally, scan each of these interfaces *)
      FOR i := 0 TO n_intfs - 1 DO
        s.cur_file := intfs[i];
        s.path := Node.FullPath (s.cur_file);
        s.buf := Buf.FromFile (s.path, pad := 1);
        M3Sym.Scan (s.buf, s, IgnoreSet);
      END;
    END;
  END ScanFile;

PROCEDURE <A NAME="NoteSym"><procedure>NoteSym</procedure></A> (s: Scan;  READONLY sym: M3Sym.Id;
                   kind: SKind;  intf: TEXT): BOOLEAN =
  BEGIN
    CASE kind OF
    | SKind.IntfName =&gt;   s.unit_name := SymID (s, sym);
                          s.is_interface := TRUE;
                          RETURN MatchSource (s, sym, Source.Kind.M3);

    | SKind.ImplName =&gt;   s.unit_name := SymID (s, sym);
                          s.is_interface := FALSE;
                          RETURN MatchSource (s, sym, Source.Kind.I3);

    | SKind.GIntfName =&gt;  s.unit_name := SymID (s, sym);
                          s.is_interface := TRUE;
                          RETURN MatchSource (s, sym, Source.Kind.MG);

    | SKind.GImplName =&gt;  s.unit_name := SymID (s, sym);
                          s.is_interface := FALSE;
                          RETURN MatchSource (s, sym, Source.Kind.IG);

    | SKind.GFormal =&gt;    RETURN MatchLocal  (s, sym, Node.Class.GFormal);
    | SKind.GActual =&gt;    RETURN MatchSource (s, sym, Source.Kind.I3);
    | SKind.Import =&gt;     RETURN MatchSource (s, sym, Source.Kind.I3);
    | SKind.FromImport =&gt; RETURN MatchSource (s, sym, Source.Kind.I3);
    | SKind.SymImport =&gt;  RETURN MatchImport (s, sym, intf);
    | SKind.ImportXX =&gt;   RETURN MatchSource (s, sym, Source.Kind.I3);
    | SKind.ImportAs =&gt;   RETURN MatchRename (s, sym, intf);
    | SKind.ConstDecl =&gt;  RETURN MatchLocal (s, sym, Node.Class.Const);
    | SKind.VarDecl =&gt;    RETURN MatchLocal (s, sym, Node.Class.Var);
    | SKind.ExceptDecl =&gt; RETURN MatchLocal (s, sym, Node.Class.Except);
    | SKind.ProcDecl =&gt;   RETURN MatchLocal (s, sym, ProcClass [s.is_interface]);
    | SKind.TypeDecl =&gt;   (** RETURN MatchType (s, sym); **)
                          RETURN MatchLocal (s, sym, Node.Class.TypeDecl);

    | SKind.GIntfUse =&gt;
        IF MatchSource (s, sym, Source.Kind.IG) THEN RETURN TRUE; END;
        Remember (s, sym, Source.Kind.IG);

    | SKind.GImplUse =&gt;
        IF MatchSource (s, sym, Source.Kind.MG) THEN RETURN TRUE; END;
        Remember (s, sym, Source.Kind.MG);

    | SKind.Export =&gt;
        IF (s.all_exports) THEN
          (* keep any export *)
          EVAL FindSource (s, SymID (s, sym), Source.Kind.I3);
        ELSE
          IF MatchSource (s, sym, Source.Kind.I3) THEN RETURN TRUE; END;
          IF NOT s.second_try THEN  Remember (s, sym, Source.Kind.I3); END;
        END;

    | SKind.TypeUse, SKind.ExceptUse, SKind.ProcUse, SKind.MiscUse,
      SKind.Keyword, SKind.BuiltinOp, SKind.BuiltinType, SKind.BuiltinConst =&gt;
        &lt;*ASSERT FALSE*&gt;
    END;
    RETURN FALSE;
  END NoteSym;

PROCEDURE <A NAME="Remember"><procedure>Remember</procedure></A> (s: Scan;  READONLY sym: M3Sym.Id;  kind: Source.Kind) =
  (* remember this source file in case we need to scan it too...  *)
  BEGIN
    s.others := NEW (OtherSym, next := s.others,
                     name := SymID (s, sym), kind := kind);
  END Remember;

PROCEDURE <A NAME="MatchLocal"><procedure>MatchLocal</procedure></A> (s: Scan;  READONLY sym: M3Sym.Id;
                      kind: Node.Class): BOOLEAN =
  VAR id: ID.T;
  BEGIN
    IF Match (s, sym, kind, id) THEN
      Node.Append (s.results, NEW (T, parent := s.cur_file, name := id,
                                   kind := kind));
      RETURN s.find_one;
    END;
    RETURN FALSE;
  END MatchLocal;
</PRE>*******
PROCEDURE MatchType (s: Scan;  READONLY sym: M3Sym.Id): BOOLEAN =
  VAR id: ID.T;  cnt: INTEGER;
  BEGIN
    IF Match (s, sym, Node.Class.TypeDecl, id) THEN
      cnt := FindType (s, id);
      RETURN s.find_one AND (cnt &gt; 0);
    END;
    RETURN FALSE;
  END MatchType;
<P>
PROCEDURE FindType (s: Scan;  id: ID.T): INTEGER =
  VAR
    name : ID.T    := ID.Add (ID.ToText (s.unit_name) &amp; <CODE>.</CODE> &amp; ID.ToText (id));
    home : ID.T    := s.cur_file.name;
    cnt  : INTEGER := 0;
    ref  : REFANY;
    nd   : Node.List;
    tipe : Type.T;
  BEGIN
    IF BrowserDB.db.type_names.get (name, ref) THEN
      nd := NARROW (ref, Node.List);
      WHILE (nd # NIL) DO
        tipe := nd.head;  nd := nd.tail;
        WHILE (tipe # NIL) DO
          IF (tipe.name = name) AND (tipe.home = home) THEN
            Node.Append (s.results, tipe);
            INC (cnt);
          END;
          tipe := tipe.alias;
        END;
      END;
    END;
    <P>
    IF (cnt = 0) THEN
      (* we didn't find a matching type node =&gt; just point to the declaration 
      <PRE>Node.Append (s.results, NEW (T, parent := s.cur_file, name := id,
                                   kind := Node.Class.Type));
      INC (cnt);
    END;

    RETURN cnt;
  END FindType;
***********)

PROCEDURE <A NAME="MatchSource"><procedure>MatchSource</procedure></A> (s: Scan;  READONLY sym: M3Sym.Id;
                       kind: Source.Kind): BOOLEAN =
  VAR id: ID.T;
  BEGIN
    IF Match (s, sym, Source.NodeClass[kind], id)
      AND (FindSource (s, id, kind) &gt; 0) THEN
      RETURN s.find_one;
    END;
    RETURN FALSE;
  END MatchSource;

PROCEDURE <A NAME="MatchImport"><procedure>MatchImport</procedure></A> (s: Scan;  READONLY sym: M3Sym.Id;  intf: TEXT): BOOLEAN =
  (* FROM &lt;intf&gt; IMPORT &lt;sym&gt; *)
  VAR
    id: ID.T;
    iter: Node.IteratorState;
    intf_cnt : INTEGER;
    match_cnt := 0;
    interface : Node.T;
    start, stop: INTEGER;
  BEGIN
    IF Match (s, sym, Node.Class.Unknown, id) THEN

      TRY
        iter.pattern := RegExpr.Compile (ID.ToText (id));
      EXCEPT RegExpr.Error (msg) =&gt;
        ErrLog.Msg (&quot;Bad regular expression:  Decl.MatchImport (\&quot;&quot;,
                    ID.ToText (id), &quot;\&quot;) =&gt; &quot;, msg);
      END;

      intf_cnt := FindSource (s, ID.Add (intf), Source.Kind.I3);
      start := s.results.cnt - intf_cnt;
      stop := s.results.cnt - 1;

      FOR xx := start TO stop DO
        interface := s.results.elts[xx];
        IF (interface # NIL) THEN
          interface.iterate (iter);
          WHILE interface.next (iter) DO
            IF iter.match # NIL THEN
              Node.Append (s.results, iter.match);
              INC (match_cnt);
            END;
          END;
        END;
      END;

      (* remove the intermediate result nodes *)
      FOR xx := start TO stop DO
        DEC (s.results.cnt);
        s.results.elts[xx] := s.results.elts[s.results.cnt];
      END;

      RETURN s.find_one AND (match_cnt &gt; 0);
    END;
    RETURN FALSE;
  END MatchImport;

PROCEDURE <A NAME="MatchRename"><procedure>MatchRename</procedure></A> (s: Scan;  READONLY sym: M3Sym.Id;  intf: TEXT): BOOLEAN =
  (* IMPORT &lt;intf&gt; AS &lt;sym&gt; *)
  VAR id: ID.T;
  BEGIN
    IF Match (s, sym, Node.Class.Interface, id)
      AND (FindSource (s, ID.Add (intf), Source.Kind.I3) &gt; 0) THEN
      RETURN s.find_one;
    END;
    RETURN FALSE;
  END MatchRename;

PROCEDURE <A NAME="Match"><procedure>Match</procedure></A> (s: Scan;  READONLY sym: M3Sym.Id;
                 class: Node.Class;  VAR(*OUT*)nm: ID.T): BOOLEAN =
  BEGIN
    WITH id = SUBARRAY (s.buf^, sym.start, sym.len) DO
      IF s.class_match[class] OR RegExpr.MatchSub (s.pattern, id)
        THEN nm := ID.FromStr (id);  RETURN TRUE;
        ELSE nm := ID.NoID;          RETURN FALSE;
      END;
    END;
  END Match;

PROCEDURE <A NAME="SymID"><procedure>SymID</procedure></A> (s: Scan;  READONLY sym: M3Sym.Id): ID.T =
  BEGIN
    RETURN ID.FromStr (SUBARRAY (s.buf^, sym.start, sym.len));
  END SymID;

PROCEDURE <A NAME="FindSource"><procedure>FindSource</procedure></A> (s: Scan;  id: ID.T;  kind: Source.Kind): INTEGER =
  VAR
    nm := SourceName (id, kind);
    nd: Node.List;
    start := s.results.cnt;
    quality := 0;
    n: Node.T;
    qual: INTEGER;
    ref: REFANY;
  BEGIN
    IF BrowserDB.db.units.get (nm, ref) THEN
      nd := NARROW (ref, Node.List);
      WHILE (nd # NIL) DO
        n := nd.head;  nd := nd.tail;
        qual := MatchQuality (s, n, kind);
        IF qual &gt; quality THEN
          (* discard any matches found so far *)
          s.results.cnt := start;
          quality := qual;
          Node.Append  (s.results, n);
        ELSIF (qual = quality) THEN
          Node.Append (s.results, n);
        END;
      END;
    END;
    RETURN s.results.cnt - start; (* # of matches *)
  END FindSource;

PROCEDURE <A NAME="SourceName"><procedure>SourceName</procedure></A> (id: ID.T;  kind: Source.Kind): ID.T =
  VAR ext := Source.Ext [kind];  nm := id;
  BEGIN
    IF ext # NIL THEN  nm := ID.Add (ID.ToText (id) &amp; ext);  END;
    RETURN nm;
  END SourceName;

PROCEDURE <A NAME="MatchQuality"><procedure>MatchQuality</procedure></A> (s: Scan;  n: Node.Named_T;  kind: Source.Kind): INTEGER =
  (* Find the best match:  same package &gt; same root &gt; same kind &gt; any source *)
  VAR pkg: Pkg.T;
  BEGIN
    TYPECASE n OF
    | Source.T (src) =&gt;
        IF (src.kind # kind) AND (kind # Source.Kind.Other) THEN  RETURN -1; END;
        pkg := Pkg.Home (n);
        IF (pkg # NIL) AND (s.parent_pkg = pkg) THEN
          RETURN 2;
        ELSIF (s.parent_pkg # NIL) AND (pkg # NIL)
          AND (s.parent_pkg.parent = pkg.parent) THEN
          RETURN 1;
        ELSE
          RETURN 0;
        END;
    ELSE
        RETURN -1;
    END;
  END MatchQuality;

PROCEDURE <A NAME="Init"><procedure>Init</procedure></A> () =
  BEGIN
  END Init;

BEGIN
END Decl.
</PRE>
</inModule>
<HR>
<A NAME="x1">interface Decl is in:
</A><UL>
<LI><A HREF="Decl.i3.html#0TOP0">cm3ide/src/nodes/Decl.i3</A>
<LI><A HREF="../../../m3front/src/values/Decl.i3.html#0TOP0">m3front/src/values/Decl.i3</A>
</UL>
<P>
<HR>
<A NAME="x2">interface Buf is in:
</A><UL>
<LI><A HREF="../utils/Buf.i3.html#0TOP0">cm3ide/src/utils/Buf.i3</A>
<LI><A HREF="../../../libbuf/src/Buf.i3.html#0TOP0">libbuf/src/Buf.i3</A>
</UL>
<P>
<HR>
<A NAME="x3">interface ErrLog is in:
</A><UL>
<LI><A HREF="../utils/ErrLog.i3.html#0TOP0">cm3ide/src/utils/ErrLog.i3</A>
<LI><A HREF="../../../m3markup/src/ErrLog.i3.html#0TOP0">m3markup/src/ErrLog.i3</A>
</UL>
<P>
<HR>
<A NAME="x4">interface ID is in:
</A><UL>
<LI><A HREF="../utils/ID.i3.html#0TOP0">cm3ide/src/utils/ID.i3</A>
<LI><A HREF="../../../m3markup/src/ID.i3.html#0TOP0">m3markup/src/ID.i3</A>
</UL>
<P>
<HR>
<A NAME="x5">interface M3MarkUp is in:
</A><UL>
<LI><A HREF="../markup/M3MarkUp.i3.html#0TOP0">cm3ide/src/markup/M3MarkUp.i3</A>
<LI><A HREF="../../../m3markup/src/M3MarkUp.i3.html#0TOP0">m3markup/src/M3MarkUp.i3</A>
<LI><A HREF="../../../m3tohtml/src/M3MarkUp.i3.html#0TOP0">m3tohtml/src/M3MarkUp.i3</A>
</UL>
<P>
<HR>
<A NAME="x6">interface Source is in:
</A><UL>
<LI><A HREF="Source.i3.html#0TOP0">cm3ide/src/nodes/Source.i3</A>
<LI><A HREF="../../../juno-app/src/Source.i3.html#0TOP0">juno-app/src/Source.i3</A>
</UL>
<P>
<HR>
<A NAME="x7">interface Wx is in:
</A><UL>
<LI><A HREF="../utils/Wx.i3.html#0TOP0">cm3ide/src/utils/Wx.i3</A>
<LI><A HREF="../../../libbuf/src/Wx.i3.html#0TOP0">libbuf/src/Wx.i3</A>
</UL>
<P>
<PRE>























</PRE>
</BODY>
</HTML>
