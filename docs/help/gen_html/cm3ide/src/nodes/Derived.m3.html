<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: cm3ide/src/nodes/Derived.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>cm3ide/src/nodes/Derived.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright 1996 Critical Mass, Inc. All rights reserved.    

<P><PRE>MODULE <module><implements><A HREF="Derived.i3.html">Derived</A></implements></module>;

IMPORT <A HREF="../../../libm3/src/params/Env.i3.html">Env</A>, <A HREF="../../../libm3/src/os/Common/Pathname.i3.html">Pathname</A>, <A HREF="../../../m3core/src/text/Text.i3.html">Text</A>, <A HREF="../../../m3core/src/thread/Common/Thread.i3.html">Thread</A>, <A HREF="../../../libm3/src/rw/Wr.i3.html">Wr</A>;
IMPORT <A HREF="../misc/BrowserDB.i3.html">BrowserDB</A>, <A HREF="#x1">Builder</A>, <A HREF="ClassDir.i3.html">ClassDir</A>, <A HREF="../misc/Default.i3.html">Default</A>, <A HREF="Dir.i3.html">Dir</A>, <A HREF="#x2">ErrLog</A>, <A HREF="#x3">ID</A>;
IMPORT <A HREF="Node.i3.html">Node</A>, <A HREF="#x4">HTML</A>, <A HREF="#x5">OS</A>, <A HREF="Pkg.i3.html">Pkg</A>, <A HREF="../utils/RegExpr.i3.html">RegExpr</A>, <A HREF="#x6">Source</A>, <A HREF="../misc/UserState.i3.html">UserState</A>, <A HREF="#x7">Wx</A>;

REVEAL
  <A NAME="T">T</A> = Tx BRANDED &quot;Derived.T&quot; OBJECT
  OVERRIDES
    class      := Class;
    printname  := PrintName;
    match      := Match;
    iterate    := Iterate;
    next       := Next;
    gen_page   := GenPage;
  END;

VAR
  PreChop  : ARRAY BOOLEAN OF CARDINAL;
  PostChop : ARRAY BOOLEAN OF CARDINAL;
  viewID   := ID.Add (&quot;view&quot;);
  runID    := ID.Add (&quot;run&quot;);

PROCEDURE <A NAME="Class"><procedure>Class</procedure></A> (t: T): Node.Class =
  TYPE  NC  = Node.Class;
  CONST Map = ARRAY BOOLEAN OF NC { NC.Library, NC.Program };
  BEGIN
    RETURN Map [t.is_pgm];
  END Class;

PROCEDURE <A NAME="PrintName"><procedure>PrintName</procedure></A> (t: T): TEXT =
  VAR
    nm   := ID.ToText (t.name);
    pre  := PreChop [t.is_pgm];
    post := PostChop [t.is_pgm];
  BEGIN
    RETURN Text.Sub (nm, pre, Text.Length (nm) - pre - post);
  END PrintName;

PROCEDURE <A NAME="Match"><procedure>Match</procedure></A> (t: T;  re: RegExpr.T): BOOLEAN =
  VAR
    nm   := ID.ToText (t.name);
    pre  := PreChop [t.is_pgm];
    post := PostChop [t.is_pgm];
  BEGIN
    RETURN RegExpr.Match (re, nm)
        OR RegExpr.MatchSubstring (re, nm, pre, post);
  END Match;

PROCEDURE <A NAME="Iterate"><procedure>Iterate</procedure></A> (t: T;  VAR s: Node.IteratorState) =
  VAR x := GenContents (t);
  BEGIN
    s.a := 0;
    s.b := x.cnt;
    s.d := x.elts;
  END Iterate;

PROCEDURE <A NAME="Next"><procedure>Next</procedure></A> (&lt;*UNUSED*&gt; t: T;  VAR s: Node.IteratorState): BOOLEAN =
  VAR n: Node.T;  elts: Node.Array := s.d;
  BEGIN
    WHILE (s.a &lt; s.b) DO
      n := elts [s.a];  INC (s.a);
      IF n.match (s.pattern) THEN
        s.match := n;  RETURN TRUE;
      END;
    END;
    RETURN FALSE;
  END Next;

PROCEDURE <A NAME="GenPage"><procedure>GenPage</procedure></A> (t: T;  wx: Wx.T;  action: ID.T;  data: Node.FormData)
  RAISES {Wr.Failure, Thread.Alerted} =
  BEGIN
    IF (action = runID)
      THEN GenRun (t, wx, data);
      ELSE GenView (t, wx, action, data);
    END;
  END GenPage;

PROCEDURE <A NAME="PgmPath"><procedure>PgmPath</procedure></A> (t: T): TEXT =
  VAR file, path: TEXT;
  BEGIN
    IF NOT t.is_pgm THEN RETURN NIL END;
    file := Node.FullPath (t);
    IF OS.IsExecutable (file) THEN RETURN file; END;
    (* but, public programs are usually shipped to the BIN directory! *)
    path := OS.FindExecutable (Pathname.Last (file));
    IF (path # NIL) THEN RETURN path; END;
    (* but, if it's not there, give up. *)
    RETURN file;
  END PgmPath;

PROCEDURE <A NAME="GenView"><procedure>GenView</procedure></A> (t: T;  wx: Wx.T;  action: ID.T;  data: Node.FormData)
  RAISES {Wr.Failure, Thread.Alerted} =
  VAR
    path := Node.FullPath (t);
    x    : Node.Set;
  BEGIN
    GenHeader (t, path, wx);
    IF (action = viewID) THEN
      Pkg.GenActionButtons (t, wx);
      x := GenContents (t);
      HTML.GenChoices (x, wx);
    ELSE
      HTML.NoAction (action, wx);
    END;
    HTML.NoData (data, wx);
    HTML.End (wx);
  END GenView;

PROCEDURE <A NAME="GenRun"><procedure>GenRun</procedure></A> (t: T;  wx: Wx.T;  data: Node.FormData)
  RAISES {Wr.Failure, Thread.Alerted} =
  VAR
    path   := Node.FullPath (t);
    cmdkey := &quot;RUNCMD|&quot; &amp; path;
    dirkey := &quot;RUNDIR|&quot; &amp; path;
    cmd    := UserState.Get (cmdkey);
    dir    := UserState.Get (dirkey);
  BEGIN
    (* process the incoming form data *)
    WHILE (data # NIL) DO
      IF Text.Equal (data.field, &quot;run-cmd&quot;) THEN
        cmd := data.value;
      ELSIF Text.Equal (data.field, &quot;run-dir&quot;) THEN
        dir := data.value;
      ELSE
        wx.put (&quot;&lt;STRONG&gt;Unrecognized field: &quot;, data.field, &quot;&lt;/STRONG&gt;&lt;BR&gt;\n&quot;);
      END;
      data := data.next;
    END;

    (* try to ensure there's some sort of reasonable values *)
    IF (cmd = NIL) THEN cmd := PgmPath (t); END;
    IF (dir = NIL) THEN dir := Env.Get (&quot;HOME&quot;); END;
    IF (dir = NIL) THEN dir := Node.FullPath (t.parent); END;

    (* and record them for posterity *)
    UserState.Put (cmdkey, cmd);
    UserState.Put (dirkey, dir);

    GenHeader (t, path, wx);
    Builder.Run (Pkg.Home (t), cmd, dir, wx);
    HTML.End (wx);
  END GenRun;

PROCEDURE <A NAME="GenHeader"><procedure>GenHeader</procedure></A> (t: T;  path: TEXT;  wx: Wx.T)
  RAISES {Wr.Failure, Thread.Alerted} =
  BEGIN
    HTML.Begin (t, wx);
    Pkg.GenFileNote (path, wx, is_dir := FALSE);
    Pkg.GenBuildNote (t, wx);
    wx.put (&quot;\n&quot;);
  END GenHeader;

PROCEDURE <A NAME="FixName"><procedure>FixName</procedure></A> (t: T) =
  VAR nm := ID.ToText (t.name);
  BEGIN
    CASE ORD (t.is_pgm) + 2 * ORD (Default.on_unix) OF
    | 0 =&gt;  t.name := ID.Add (nm &amp; &quot;.lib&quot;);       (* Win32 library:  foo.lib  *)
    | 1 =&gt;  t.name := ID.Add (nm &amp; &quot;.exe&quot;);       (* Win32 program:  foo.exe  *)
    | 2 =&gt;  t.name := ID.Add (&quot;lib&quot; &amp; nm &amp; &quot;.a&quot;); (* Unix library:   libfoo.a *)
    ELSE    (*skip*)                              (* Unix program:   foo      *)
    END;
  END FixName;

PROCEDURE <A NAME="Init"><procedure>Init</procedure></A> () =
  BEGIN
    IF (Default.on_unix) THEN
      (* assume Unix naming conventions *)
      PreChop [FALSE] := 3;  PostChop [FALSE] := 2;  (* Unix library:   libfoo.a *)
      PreChop [TRUE]  := 0;  PostChop [TRUE]  := 0;  (* Unix program:   foo      *)
    ELSE
      (* assume Win32 naming conventions *)
      PreChop [FALSE] := 0;  PostChop [FALSE] := 4;  (* Win32 library:  foo.lib  *)
      PreChop [TRUE]  := 0;  PostChop [TRUE]  := 4;  (* Win32 program:  foo.exe  *)
    END;
  END Init;

PROCEDURE <A NAME="GenContents"><procedure>GenContents</procedure></A> (t: T): Node.Set =
  VAR x: Node.Set;  c: Node.Class;  pkg: Pkg.T;  path : NamePath;  n: Node.T;
  BEGIN
    (* scan and build the node class virtual directories *)
    FOR k := FIRST (t.seen) TO LAST (t.seen) DO
      IF (t.seen[k]) THEN
        c := Source.NodeClass [k];
        IF (Node.ClassID[c] # ID.NoID) THEN
          Node.Append (x, NEW (ClassDir.T, name := Node.ClassID [c],
                               parent := t, kind := c));
        END;
      END;
    END;

    pkg := Pkg.Home (t);
    FOR i := 0 TO t.n_elts-1 DO
      WITH z = t.contents [i] DO
        path.len := 0;
        AddArcs (path, z.loc.subdir);
        path.arcs [path.len] := z.file;  INC (path.len);
        n := FindSource (z.loc.pkg, path, pkg);
        IF (n # NIL) THEN
          Node.Append (x, n);
        ELSE
          ErrLog.Msg (&quot;Unable to locate source: &quot;, PathToText (z.loc.pkg, path),
                      &quot; for &quot;, Node.FullPath (t));
        END;
      END;
    END;

    RETURN x;
  END GenContents;

TYPE
  NamePath = RECORD
    len  : INTEGER;
    arcs : ARRAY [0..19] OF ID.T;
  END;

PROCEDURE <A NAME="AddArcs"><procedure>AddArcs</procedure></A> (VAR path: NamePath;  x: ID.T) =
  CONST BackSlash = '\134';
  VAR
    txt := ID.ToText (x);
    len := Text.Length (txt);
    s0, s1: INTEGER;
    buf: ARRAY [0..99] OF CHAR;
  BEGIN
    &lt;*ASSERT len &lt;= NUMBER (buf) *&gt;
    Text.SetChars (buf, txt);
    s0 := 0;
    WHILE (s0 &lt; len) DO
      s1 := s0;
      WHILE (s1 &lt; len) AND (buf[s1] # '/') AND (buf[s1] # BackSlash) DO
        INC (s1);
      END;
      IF (s0 &lt; s1) THEN
        path.arcs [path.len] := ID.FromStr (SUBARRAY (buf, s0, s1 - s0));
        INC (path.len);
      END;
      s0 := s1 + 1;
    END;
  END AddArcs;

PROCEDURE <A NAME="FindSource"><procedure>FindSource</procedure></A> (pkg_nm: ID.T;  READONLY path: NamePath;
                      hint: Pkg.T): Node.T =
  VAR n: Node.T;  pkgs: Node.List;  ref: REFANY;
  BEGIN
    (* try the hint *)
    IF (hint # NIL) AND (hint.name = pkg_nm) THEN
      n := FindFile (path, hint);
      IF (n # NIL) THEN RETURN n; END;
    END;

    (* search all the packages with the same name *)
    IF NOT BrowserDB.db.packages.get (pkg_nm, ref) THEN RETURN NIL; END;
    pkgs := ref;
    WHILE (pkgs # NIL) DO
      n := FindFile (path, pkgs.head);
      IF (n # NIL) THEN RETURN n; END;
      pkgs := pkgs.tail;
    END;

    (* failed... *)
    RETURN NIL;
  END FindSource;

PROCEDURE <A NAME="FindFile"><procedure>FindFile</procedure></A> (READONLY path: NamePath;  dir: Dir.T): Node.T =
  VAR n: Node.Named_T;  nm: ID.T;
  BEGIN
    (* walk through the subdirectories *)
    FOR i := 0 TO path.len-1 DO
      (* find the next subdirectory *)
      nm := path.arcs [i];
      n  := dir.contents;
      LOOP
        IF (n = NIL) THEN RETURN NIL; END;
        IF (n.name = nm) THEN
          IF (i = path.len-1) THEN RETURN n; END;
          IF NOT ISTYPE (n, Dir.T) THEN RETURN NIL; END;
          dir := n; EXIT;
        END;
        n := n.sibling;
      END;
    END;

    (* failed... *)
    RETURN NIL;
  END FindFile;

PROCEDURE <A NAME="PathToText"><procedure>PathToText</procedure></A> (pkg: ID.T;  READONLY path: NamePath): TEXT =
  VAR p := ID.ToText (pkg);
  BEGIN
    FOR i := 0 TO path.len - 1 DO
      p := HTML.MakeURL (p, ID.ToText (path.arcs[i]));
    END;
    RETURN p;
  END PathToText;

BEGIN
END Derived.
</PRE>
</inModule>
<HR>
<A NAME="x1">interface Builder is in:
</A><UL>
<LI><A HREF="../../../cm3/src/Builder.i3.html#0TOP0">cm3/src/Builder.i3</A>
<LI><A HREF="../misc/Builder.i3.html#0TOP0">cm3ide/src/misc/Builder.i3</A>
<LI><A HREF="../../../metasyn/src/Builder.i3.html#0TOP0">metasyn/src/Builder.i3</A>
</UL>
<P>
<HR>
<A NAME="x2">interface ErrLog is in:
</A><UL>
<LI><A HREF="../utils/ErrLog.i3.html#0TOP0">cm3ide/src/utils/ErrLog.i3</A>
<LI><A HREF="../../../m3markup/src/ErrLog.i3.html#0TOP0">m3markup/src/ErrLog.i3</A>
</UL>
<P>
<HR>
<A NAME="x3">interface ID is in:
</A><UL>
<LI><A HREF="../utils/ID.i3.html#0TOP0">cm3ide/src/utils/ID.i3</A>
<LI><A HREF="../../../m3markup/src/ID.i3.html#0TOP0">m3markup/src/ID.i3</A>
</UL>
<P>
<HR>
<A NAME="x4">interface HTML is in:
</A><UL>
<LI><A HREF="../misc/HTML.i3.html#0TOP0">cm3ide/src/misc/HTML.i3</A>
<LI><A HREF="../../../webvbt/src/HTML.i3.html#0TOP0">webvbt/src/HTML.i3</A>
</UL>
<P>
<HR>
<A NAME="x5">interface OS is in:
</A><UL>
<LI><A HREF="../utils/OS.i3.html#0TOP0">cm3ide/src/utils/OS.i3</A>
<LI><A HREF="../../../fix_nl/src/OS.i3.html#0TOP0">fix_nl/src/OS.i3</A>
<LI><A HREF="../../../m3browser/src/OS.i3.html#0TOP0">m3browser/src/OS.i3</A>
</UL>
<P>
<HR>
<A NAME="x6">interface Source is in:
</A><UL>
<LI><A HREF="Source.i3.html#0TOP0">cm3ide/src/nodes/Source.i3</A>
<LI><A HREF="../../../juno-app/src/Source.i3.html#0TOP0">juno-app/src/Source.i3</A>
</UL>
<P>
<HR>
<A NAME="x7">interface Wx is in:
</A><UL>
<LI><A HREF="../utils/Wx.i3.html#0TOP0">cm3ide/src/utils/Wx.i3</A>
<LI><A HREF="../../../libbuf/src/Wx.i3.html#0TOP0">libbuf/src/Wx.i3</A>
</UL>
<P>
<PRE>























</PRE>
</BODY>
</HTML>
