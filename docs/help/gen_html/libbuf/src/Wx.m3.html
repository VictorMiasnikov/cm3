<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: libbuf/src/Wx.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>libbuf/src/Wx.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright (C) 1994, Digital Equipment Corporation           
 All rights reserved.                                        
 See the file COPYRIGHT for a full description.              
                                                             
 Last modified on Thu Dec  8 09:51:33 PST 1994 by kalsow     

<P><PRE>MODULE <module><implements><A HREF="#x1">Wx</A></implements></module>;

IMPORT <A HREF="../../m3core/src/convert/Convert.i3.html">Convert</A>, <A HREF="../../m3core/src/text/Text.i3.html">Text</A>, <A HREF="../../m3core/src/text/Text8.i3.html">Text8</A>;
IMPORT <A HREF="../../m3core/src/thread/Common/Thread.i3.html">Thread</A>, <A HREF="../../libm3/src/rw/Wr.i3.html">Wr</A>;

CONST
  ChunkSize = (2 * 1024) - (3 * BYTESIZE(INTEGER));
  (* leave some slop for the 'next' pointer &amp; the allocator overhead *)

TYPE
  Chunk = REF RECORD
            next : Chunk := NIL;
            buf  : ARRAY [0..ChunkSize - 1] OF CHAR;
          END;

REVEAL
  <A NAME="T">T</A> = BRANDED &quot;Wx.T&quot; REF RECORD
        nFull : INTEGER;
        next  : INTEGER;
        head  : Chunk;
        tail  : Chunk;
      END;
</PRE>----------------------------------------------------------------------
<PRE>PROCEDURE <A NAME="New"><procedure>New</procedure></A> (): T =
  VAR t := NEW (T);
  BEGIN
    t.nFull := 0;
    t.next  := 0;
    t.head  := NEW (Chunk);
    t.tail  := t.head;
    RETURN t;
  END New;
</PRE>----------------------------------------------------------------------
<PRE>PROCEDURE <A NAME="PutChar"><procedure>PutChar</procedure></A> (t: T;  ch: CHAR) =
  BEGIN
    IF (t.next &gt;= ChunkSize) THEN
      Expand (t)
    END;
    t.tail.buf[t.next] := ch;
    INC (t.next);
  END PutChar;
</PRE>----------------------------------------------------------------------
<PRE>PROCEDURE <A NAME="PutStr"><procedure>PutStr</procedure></A> (t: T;  READONLY x: ARRAY OF CHAR) =
  VAR
    next := 0;
    len  := NUMBER (x);
    n : INTEGER;
  BEGIN
    IF (len &lt; ChunkSize - t.next) THEN
      SUBARRAY (t.tail.buf, t.next, len) := x;
      INC (t.next, len);
    ELSE
      WHILE (len &gt; 0) DO
        n := MIN (len, ChunkSize - t.next);
        SUBARRAY (t.tail.buf, t.next, n) := SUBARRAY (x, next, n);
        DEC (len, n);
        INC (next, n);
        INC (t.next, n);
        IF (len &gt; 0) THEN Expand (t) END;
      END;
    END;
  END PutStr;
</PRE>----------------------------------------------------------------------
<PRE>PROCEDURE <A NAME="PutChars"><procedure>PutChars</procedure></A> (t: T;  READONLY x: TEXT) =
  VAR
    next := 0;
    len  := Text.Length (x);
    n : INTEGER;
  BEGIN
    IF (len &lt; ChunkSize - t.next) THEN
      FOR i := 0 TO len - 1 DO
        t.tail.buf[t.next + i] := Text.GetChar(x, i);
      END;
      INC (t.next, len);
    ELSE
      WHILE (len &gt; 0) DO
        n := MIN (len, ChunkSize - t.next);
        FOR i := 0 TO n - 1 DO
          t.tail.buf[t.next + i] := Text.GetChar(x, next + i);
        END;
        DEC (len, n);
        INC (next, n);
        INC (t.next, n);
        IF (len &gt; 0) THEN Expand (t) END;
      END;
    END;
  END PutChars;
</PRE>----------------------------------------------------------------------
<PRE>PROCEDURE <A NAME="PutText"><procedure>PutText</procedure></A>(t : T; a, b, c, d, e : TEXT := NIL) =
  BEGIN
    IF (a # NIL) THEN PutChars(t, a);
     IF (b # NIL) THEN PutChars(t, b);
      IF (c # NIL) THEN PutChars(t, c);
       IF (d # NIL) THEN PutChars(t, d);
        IF (e # NIL) THEN PutChars(t, e);
    END END END END END
  END PutText;
</PRE>----------------------------------------------------------------------
<PRE>CONST digits = ARRAY [0..9] OF CHAR {'0','1','2','3','4','5','6','7','8','9'};

CONST digits_100A = ARRAY [0..99] OF CHAR {
  '0', '1', '2', '3', '4', '5', '6', '7', '8', '9',
  '0', '1', '2', '3', '4', '5', '6', '7', '8', '9',
  '0', '1', '2', '3', '4', '5', '6', '7', '8', '9',
  '0', '1', '2', '3', '4', '5', '6', '7', '8', '9',
  '0', '1', '2', '3', '4', '5', '6', '7', '8', '9',
  '0', '1', '2', '3', '4', '5', '6', '7', '8', '9',
  '0', '1', '2', '3', '4', '5', '6', '7', '8', '9',
  '0', '1', '2', '3', '4', '5', '6', '7', '8', '9',
  '0', '1', '2', '3', '4', '5', '6', '7', '8', '9',
  '0', '1', '2', '3', '4', '5', '6', '7', '8', '9'
  };

CONST digits_100B = ARRAY [0..99] OF CHAR {
  '0', '0', '0', '0', '0', '0', '0', '0', '0', '0',
  '1', '1', '1', '1', '1', '1', '1', '1', '1', '1',
  '2', '2', '2', '2', '2', '2', '2', '2', '2', '2',
  '3', '3', '3', '3', '3', '3', '3', '3', '3', '3',
  '4', '4', '4', '4', '4', '4', '4', '4', '4', '4',
  '5', '5', '5', '5', '5', '5', '5', '5', '5', '5',
  '6', '6', '6', '6', '6', '6', '6', '6', '6', '6',
  '7', '7', '7', '7', '7', '7', '7', '7', '7', '7',
  '8', '8', '8', '8', '8', '8', '8', '8', '8', '8',
  '9', '9', '9', '9', '9', '9', '9', '9', '9', '9'
  };
</PRE>----------------------------------------------------------------------
<PRE>PROCEDURE <A NAME="PutInt"><procedure>PutInt</procedure></A> (t: T;  i: INTEGER) =
  &lt;*FATAL Convert.Failed*&gt;
  VAR len: INTEGER;  buf: ARRAY [0..BITSIZE(INTEGER) + 3] OF CHAR;
  BEGIN
    IF (0 &lt;= i) THEN
      IF (i &lt; 10) THEN
        PutChar (t, digits[i]);
        RETURN;
      ELSIF (i &lt; 100) THEN
        PutChar (t, digits_100B[i]);
        PutChar (t, digits_100A[i]);
        RETURN;
      ELSIF (i &lt; 1000) THEN
        PutChar (t, digits[i DIV 100]);
        PutChar (t, digits[(i DIV 10) MOD 10]);
        PutChar (t, digits[i MOD 10]);
        RETURN;
      END;
    END;
    len := Convert.FromInt (buf, i, 10);
    PutStr (t, SUBARRAY (buf, 0, len));
  END PutInt;
</PRE>----------------------------------------------------------------------
<PRE>PROCEDURE <A NAME="GetLength"><procedure>GetLength</procedure></A> (t: T): INTEGER =
  BEGIN
    RETURN t.nFull * ChunkSize + t.next;
  END GetLength;
</PRE>----------------------------------------------------------------------
<PRE>PROCEDURE <A NAME="ToText"><procedure>ToText</procedure></A> (t: T): TEXT =
  VAR txt := Text8.Create(t.nFull * ChunkSize + t.next);
      c := t.head;   n := 0;
  BEGIN
    FOR i := 1 TO t.nFull DO
      SUBARRAY (txt.contents^, n, ChunkSize) := c.buf;
      c := c.next;
      INC (n, ChunkSize);
    END;
    IF (t.next # 0) THEN
      SUBARRAY (txt.contents^, n, t.next) := SUBARRAY (c.buf, 0, t.next);
    END;
    txt.contents^ [LAST (txt.contents^)] := '\000';
    Reset (t);
    RETURN txt;
  END ToText;
</PRE>----------------------------------------------------------------------
<PRE>PROCEDURE <A NAME="ToWr"><procedure>ToWr</procedure></A> (t: T; wr : Wr.T) RAISES {Wr.Failure, Thread.Alerted} =
  VAR c := t.head;
      n := 0;
  BEGIN
    FOR i := 1 TO t.nFull DO
      Wr.PutString(wr, c.buf);
      c := c.next;
      INC(n, ChunkSize);
    END;
    IF (t.next # 0) THEN
      Wr.PutString(wr, SUBARRAY(c.buf, 0, t.next));
    END;
  END ToWr;
</PRE>----------------------------------------------------------------------
<PRE>PROCEDURE <A NAME="Reset"><procedure>Reset</procedure></A> (t: T) =
  BEGIN
    (* NOTE: we're not freeing the allocated chunks... *)
    t.tail  := t.head;
    t.nFull := 0;
    t.next  := 0;
  END Reset;
</PRE>------------------------------------ internal ----------------------------

<P><PRE>PROCEDURE <A NAME="Expand"><procedure>Expand</procedure></A> (t: T) =
  BEGIN
    &lt;* ASSERT t.next = ChunkSize *&gt;
    IF (t.tail.next = NIL) THEN t.tail.next := NEW (Chunk); END;
    t.tail := t.tail.next;
    t.next := 0;
    INC (t.nFull);
  END Expand;

BEGIN
END Wx.
</PRE>
</inModule>
<HR>
<A NAME="x1">interface Wx is in:
</A><UL>
<LI><A HREF="../../cm3ide/src/utils/Wx.i3.html#0TOP0">cm3ide/src/utils/Wx.i3</A>
<LI><A HREF="Wx.i3.html#0TOP0">libbuf/src/Wx.i3</A>
</UL>
<P>
<PRE>























</PRE>
</BODY>
</HTML>
