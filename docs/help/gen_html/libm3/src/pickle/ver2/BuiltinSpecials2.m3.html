<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: libm3/src/pickle/ver2/BuiltinSpecials2.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>libm3/src/pickle/ver2/BuiltinSpecials2.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright 1996-2000, Critical Mass, Inc.  All rights reserved. 
 See file COPYRIGHT-CMASS for details. 

<P><PRE>UNSAFE MODULE <module><implements><A HREF="BuiltinSpecials2.i3.html">BuiltinSpecials2</A></implements></module>;

IMPORT <A HREF="../../atom/Atom.i3.html">Atom</A>, <A HREF="Pickle2.i3.html">Pickle2</A> AS Pickle, <A HREF="PickleStubs.i3.html">PickleStubs</A>, <A HREF="../../rw/Rd.i3.html">Rd</A>, <A HREF="../../../derived/RefList.i3.html">RefList</A>;
IMPORT <A HREF="../../../../m3core/src/text/Text.i3.html">Text</A>, <A HREF="../../../../m3core/src/text/Text8.i3.html">Text8</A>, <A HREF="../../../../m3core/src/text/Text16.i3.html">Text16</A>, <A HREF="../../../../m3core/src/text/TextLiteral.i3.html">TextLiteral</A>, <A HREF="../../../../m3core/src/text/Text8CString.i3.html">Text8CString</A>, <A HREF="../../../../m3core/src/text/TextClass.i3.html">TextClass</A>, <A HREF="../../../../m3core/src/thread/Common/Thread.i3.html">Thread</A>, <A HREF="../../rw/Wr.i3.html">Wr</A>;

FROM <A HREF="Pickle2.i3.html">Pickle2</A> IMPORT Error, RefID, Special, Reader, Writer;

VAR init_done := FALSE;

PROCEDURE <A NAME="Register"><procedure>Register</procedure></A> () =
  BEGIN
    IF init_done THEN RETURN; END;
    Pickle.RegisterSpecial (NEW (Special, sc := TYPECODE (TextLiteral.T),
                                 write := TextPklWrite, read  := TextPklRead));
    Pickle.RegisterSpecial (NEW (Special, sc := TYPECODE (Text8CString.T),
                                 write := TextPklWrite, read  := TextPklRead));
    Pickle.RegisterSpecial (NEW (Special, sc := TYPECODE (Atom.T),
                                 write := AtomPklWrite, read  := AtomPklRead));
    Pickle.RegisterSpecial (NEW (Special, sc := TYPECODE (RefList.T),
                                 write := ListPklWrite, read  := ListPklRead));
    init_done := TRUE;
  END Register;
</PRE>-------------------------------------------------------- TextLiteral.T ---

<P><PRE>PROCEDURE <A NAME="TextPklWrite"><procedure>TextPklWrite</procedure></A> (&lt;*UNUSED*&gt; sp: Special;  r: REFANY;  pwr: Writer)
  RAISES {Error, Wr.Failure, Thread.Alerted} =
  TYPE CPtr  = UNTRACED REF ARRAY [0..TextLiteral.MaxBytes] OF CHAR;
  TYPE WCPtr = UNTRACED REF ARRAY [0..TextLiteral.MaxBytes DIV 2] OF WIDECHAR;
  VAR txt := LOOPHOLE (r, TEXT);  cp: CPtr;  wcp: WCPtr;  info: TextClass.Info;
  BEGIN
    TYPECASE (txt) OF
    | TextLiteral.T  =&gt; (* ok *)
    | Text8CString.T =&gt; (* ok *)
    ELSE
      RAISE Error (&quot;cannot pickle subtypes of TextLiteral.T or Text8CString.T&quot;);
    END;
    txt.get_info (info);
    IF info.wide THEN
      PickleStubs.OutInteger (pwr, -info.length);
      wcp := LOOPHOLE (info.start, WCPtr);
      PickleStubs.OutWideChars (pwr, SUBARRAY (wcp^, 0, info.length));
    ELSE
      PickleStubs.OutInteger (pwr, info.length);
      cp := LOOPHOLE (info.start, CPtr);
      PickleStubs.OutChars (pwr, SUBARRAY (cp^, 0, info.length));
    END;
  END TextPklWrite;

PROCEDURE <A NAME="TextPklRead"><procedure>TextPklRead</procedure></A> (&lt;*UNUSED*&gt; sp: Special;  prd: Reader;
                        &lt;*UNUSED*&gt; id: RefID): REFANY
  RAISES {Error, Rd.Failure, Thread.Alerted} =
  VAR len := PickleStubs.InInteger (prd);
  BEGIN
    IF (len &gt;= 0)
      THEN RETURN Text8Read (prd, len);
      ELSE RETURN Text16Read (prd, -len);
    END;
  END TextPklRead;

PROCEDURE <A NAME="Text8Read"><procedure>Text8Read</procedure></A> (prd: Reader;  len: INTEGER): TEXT
  RAISES {Error, Rd.Failure, Thread.Alerted} =
  VAR
    txt : Text8.T;
    buf : ARRAY [0..63] OF CHAR;
  BEGIN
    IF (len &lt;= NUMBER (buf)) THEN
      PickleStubs.InChars (prd, SUBARRAY (buf, 0, len));
      RETURN Text.FromChars (SUBARRAY (buf, 0, len));
    ELSE
      txt := Text8.Create (len);
      PickleStubs.InChars (prd, SUBARRAY (txt.contents^, 0, len));
      RETURN txt;
    END;
  END Text8Read;

PROCEDURE <A NAME="Text16Read"><procedure>Text16Read</procedure></A> (prd: Reader;  len: INTEGER): TEXT
  RAISES {Error, Rd.Failure, Thread.Alerted} =
  VAR
    txt : Text16.T;
    buf : ARRAY [0..63] OF WIDECHAR;
  BEGIN
    IF (len &lt;= NUMBER (buf)) THEN
      PickleStubs.InWideChars (prd, SUBARRAY (buf, 0, len));
      RETURN Text.FromWideChars (SUBARRAY (buf, 0, len));
    ELSE
      txt := Text16.Create (len);
      PickleStubs.InWideChars (prd, SUBARRAY (txt.contents^, 0, len));
      RETURN txt;
    END;
  END Text16Read;
</PRE>--------------------------------------------------------------- Atom.T ---

<P><PRE>PROCEDURE <A NAME="AtomPklWrite"><procedure>AtomPklWrite</procedure></A> (&lt;*UNUSED*&gt; sp: Special;  r: REFANY;  pwr: Writer)
  RAISES {Error, Wr.Failure, Thread.Alerted } =
  BEGIN
    pwr.write (Atom.ToText (r));
  END AtomPklWrite;

PROCEDURE <A NAME="AtomPklRead"><procedure>AtomPklRead</procedure></A> (&lt;*UNUSED*&gt; sp: Special;  prd: Reader;
                       &lt;*UNUSED*&gt; id: RefID): REFANY
  RAISES { Error, Rd.EndOfFile, Rd.Failure, Thread.Alerted } =
  BEGIN
    RETURN Atom.FromText (prd.read ());
  END AtomPklRead;
</PRE>------------------------------------------------------------ RefList.T ---
 Iterative List Special to prevent small stacks from 
   overflowing on RefList.Ts of length &gt;~ 25. 

<P><PRE>PROCEDURE <A NAME="ListPklWrite"><procedure>ListPklWrite</procedure></A> (sp: Special;  r: REFANY;  pwr: Writer)
  RAISES { Error, Wr.Failure, Thread.Alerted } =
  VAR
    l: RefList.T := r;
    len := RefList.Length (l);
  BEGIN
    IF PutSubtypeMark (r, TYPECODE (RefList.T), pwr) THEN
      (* we don't know how to marshal subtypes of RefList.T *)
      Special.write (sp, r, pwr);
    ELSE
      PickleStubs.OutCardinal (pwr, len);
      FOR i := 1 TO len DO
        pwr.write (l.head);
        l := l.tail;
      END;
      &lt;* ASSERT l = NIL *&gt;
    END;
  END ListPklWrite;

PROCEDURE <A NAME="ListPklRead"><procedure>ListPklRead</procedure></A> (sp: Special;  prd: Reader;  id: RefID) : REFANY
  RAISES { Error, Rd.EndOfFile, Rd.Failure, Thread.Alerted } =
  VAR
    len: CARDINAL;
    res, tail: RefList.T;
  BEGIN
    IF GetSubtypeMark (prd) THEN
      (* the writer encountered a subtype of RefList.T *)
      res := Special.read (sp, prd, id);
    ELSE
      len := PickleStubs.InCardinal (prd);
      res := NEW (RefList.T);
      tail := res;
      FOR i := 1 TO len - 1 DO
        tail.head := prd.read ();
        tail.tail := NEW (RefList.T);
        tail := tail.tail;
      END;
      tail.head := prd.read ();
      tail.tail := NIL;
    END;
    RETURN res;
  END ListPklRead;
</PRE>------------------------------------------------------------- internal ---

<P><PRE>PROCEDURE <A NAME="PutSubtypeMark"><procedure>PutSubtypeMark</procedure></A> (r: REFANY;  tcode: INTEGER;  pwr: Writer): BOOLEAN
  RAISES {Wr.Failure, Thread.Alerted} =
  VAR isSubtype := TYPECODE (r) # tcode;
  BEGIN
    Wr.PutChar (pwr.wr, VAL (ORD (isSubtype), CHAR));
    RETURN isSubtype;
  END PutSubtypeMark;

PROCEDURE <A NAME="GetSubtypeMark"><procedure>GetSubtypeMark</procedure></A> (prd: Reader): BOOLEAN
  RAISES {Rd.EndOfFile, Rd.Failure, Thread.Alerted} =
  CONST TrueMark = VAL (ORD (TRUE), CHAR);
  BEGIN
    RETURN Rd.GetChar (prd.rd) = TrueMark;
  END GetSubtypeMark;

BEGIN
END BuiltinSpecials2.
</PRE>
</inModule>
<PRE>























</PRE>
</BODY>
</HTML>
