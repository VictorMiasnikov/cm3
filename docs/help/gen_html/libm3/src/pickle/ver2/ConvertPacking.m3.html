<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: libm3/src/pickle/ver2/ConvertPacking.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>libm3/src/pickle/ver2/ConvertPacking.m3</H2></A><HR>
<inModule>
<PRE></PRE><P>
 * For information about this program, contact Blair MacIntyre            
 * (bm@cs.columbia.edu) or Steven Feiner (feiner@cs.columbia.edu)         
 * at the Computer Science Dept., Columbia University,                    
 * 1214 Amsterdam Ave. Mailstop 0401, New York, NY, 10027.                
 *                                                                        
 * Copyright (C) 1995, 1996 by The Trustees of Columbia University in the 
 * City of New York.  Blair MacIntyre, Computer Science Department.       
 

<P><PRE>UNSAFE MODULE <module><implements><A HREF="ConvertPacking.i3.html">ConvertPacking</A></implements></module>;

IMPORT <A HREF="../../../../m3core/src/runtime/common/RTTipe.i3.html">RTTipe</A>, <A HREF="../../../../m3core/src/runtime/common/RTPacking.i3.html">RTPacking</A>, <A HREF="PklAction.i3.html">PklAction</A>, <A HREF="../../../derived/PklActionSeq.i3.html">PklActionSeq</A>, <A HREF="../../../../m3core/src/thread/Common/Thread.i3.html">Thread</A>, <A HREF="../../rw/Wr.i3.html">Wr</A>, <A HREF="../../rw/Rd.i3.html">Rd</A>, <A HREF="../../uid/Common/Swap.i3.html">Swap</A>, <A HREF="../../../../m3core/src/word/Word.i3.html">Word</A>,
       <A HREF="../../rw/IO.i3.html">IO</A>, <A HREF="../../fmtlex/Fmt.i3.html">Fmt</A>, <A HREF="../../../derived/PackingTbl.i3.html">PackingTbl</A>, <A HREF="PackingTypeCode.i3.html">PackingTypeCode</A>;

VAR packingCache: PackingTbl.T := NEW(PackingTbl.Default).init();

REVEAL <A NAME="ReadVisitor">ReadVisitor</A> = RVPublic BRANDED &quot;Packing Read Visitor 1.0&quot; OBJECT END;
REVEAL <A NAME="WriteVisitor">WriteVisitor</A> = WVPublic BRANDED &quot;Packing Write Visitor 1.0&quot; OBJECT END;

REVEAL <A NAME="T">T</A> = Public BRANDED &quot;ConvertPacking 1.0&quot; OBJECT
      prog: PklActionSeq.T;
      wordKind: Kind;
      longKind: Kind;
      fromOffset: INTEGER := 0;
      toOffset: INTEGER := 0;
      fromSize: INTEGER := 0;
      toSize: INTEGER := 0;
      fromTipe: RTTipe.T;
      toTipe : RTTipe.T;
      from: RTPacking.T;
      to: RTPacking.T;
      nDim, fromEltPack, toEltPack: INTEGER := 0;

      writer: T;
    METHODS
      extractSwap (x: Word.T; i, n: CARDINAL;
                   size: INTEGER): Word.T := ExtractSwap;

      buildOne(fromTipe: RTTipe.T; toTipe: RTTipe.T) RAISES {Error} :=
          BuildOne;

      addCopy(length: INTEGER) := AddCopy;
      addCopy32to64(length: INTEGER; signed: BOOLEAN) := AddCopy32to64;
      (* ^PRE: length MOD 32 = 0 *)
      addCopy64to32(length: INTEGER; signed: BOOLEAN) := AddCopy64to32;
      (* ^PRE: length MOD 64 = 0 *)

      addPackedSwapFirstField(fieldsize: INTEGER) :=
          AddPackedSwapFirstField;
      addPackedSwapNextField(fieldsize: INTEGER; offset: INTEGER) :=
          AddPackedSwapNextField;
      addPackedSwapArray(length, numElts, fieldsize, wordsize:
          INTEGER) := AddPackedSwapArray;

      addSkipFrom(length: INTEGER) := AddSkipFrom;
      addSkipTo(length: INTEGER) := AddSkipTo;
      addSkipOrCopy(length: INTEGER) := AddSkipOrCopy;
      addSkip(fromDiff, toDiff: INTEGER) := AddSkip;
      addSwap16(length: INTEGER) := AddSwap16;
      (* ^PRE: length MOD 16 = 0 *)
      addSwap32(length: INTEGER) := AddSwap32;
      (* ^PRE: length MOD 32 = 0 *)
      addSwap64(length: INTEGER) := AddSwap64;
      (* ^PRE: length MOD 64 = 0 *)
      addSwap32to64(length: INTEGER; signed: BOOLEAN) := AddSwap32to64;
      (* ^PRE: length MOD 32 = 0 *)
      addSwap64to32(length: INTEGER; signed: BOOLEAN) := AddSwap64to32;
      (* ^PRE: length MOD 64 = 0 *)
      addRef(type: RefType) := AddRef;
      addDone() := AddDone;

      buildSuper(typecode: INTEGER;
                 VAR fromSize, fromAlign, toSize, toAlign: INTEGER)
        RAISES {Error} := BuildSuper;
      buildFields(fromField: RTTipe.Field; fromSize: INTEGER;
                  toField: RTTipe.Field; toSize: INTEGER)
        RAISES {Error} := BuildFields;

      appendProg(other: T) RAISES {Error} := AppendProg;
    OVERRIDES
      init := Init;
      convertRead := Convert;
      write := Write;
      print := Print;
      printProgram := PrintProgram;
      getDim := GetDim;
    END;

CONST SignExt32 = ARRAY [0..1] OF Swap.Int32 {0, -1};

TYPE
  Int8 = BITS 8 FOR [-16_80 .. 16_7F];
</PRE> Based on Word.Extract, but extracts from a word of size bytes with
   the opposite endian to this machine.
   Take n bits from x, with bit i as the least significant bit, and return them
   as the least significant n bits of a word whose other bits are 0. A checked
   runtime error if n + i &gt; Word.Size. 
<PRE>PROCEDURE <A NAME="ExtractSwap"><procedure>ExtractSwap</procedure></A> (self: T; x: Word.T; i, n: CARDINAL;
                       size: INTEGER): Word.T =
  VAR xb := LOOPHOLE(x, ARRAY [0..BYTESIZE(Word.T)-1] OF Int8);
      to: Word.T := 0;
      bit: INTEGER := 0;
  BEGIN
    (* March through the bytes from the input word backward, since
       that gives us the bytes in forward order from the other
       endian. Start with the byte containing the first part of the
       field to be extracted. *)
</PRE><BLOCKQUOTE><EM><P>
    WITH last = size - 1 - (i DIV 8) DO
      i := i MOD 8;
<P>
      FOR b := last TO FIRST(xb) BY -1 DO
</EM></BLOCKQUOTE><PRE>
    WITH first = i DIV 8 DO
      i := i MOD 8;

      IF NOT self.from.little_endian THEN
        bit := n;
      END;

      FOR b := first TO size-1 DO
        (* If we want some data from this *)
        WITH len = MIN(n, 8 - i) DO
          (* Get the appropriate part of the byte *)
          IF self.from.little_endian THEN
            to := Word.Insert(to, Word.Extract(xb[b], i, len), bit, len);
            INC(bit, len);
          ELSE
            DEC(bit, len);
            to := Word.Insert(to, Word.Extract(xb[b], 8-i-len, len), bit, len);
          END;
          (* set i to 0 after the first pass. *)
          i := 0;

          (* keep track of how much we have left to get *)
          DEC(n, len);
          IF n = 0 THEN
            RETURN to;
          END;
        END;
      END;
    END;
    RETURN to;
  END ExtractSwap;

CONST MAXLEN = 65536;
TYPE  BigBuf = ARRAY [0..MAXLEN-1] OF CHAR;
TYPE  BufPtr = UNTRACED REF BigBuf;

PROCEDURE <A NAME="ReadData"><procedure>ReadData</procedure></A>(v: ReadVisitor;  dest: ADDRESS; len: INTEGER)
  RAISES {Rd.EndOfFile, Rd.Failure, Thread.Alerted} =
  BEGIN
    WHILE len &gt;= MAXLEN DO
      v.readData(LOOPHOLE(dest, BufPtr)^);
      INC(dest, MAXLEN);  DEC(len, MAXLEN);
    END;
    IF len &gt; 0 THEN
      v.readData (SUBARRAY(LOOPHOLE (dest, BufPtr)^, 0, len));
    END;
  END ReadData;

TYPE Int32Rec = BITS 32 FOR RECORD v : Swap.Int32 END;
</PRE><BLOCKQUOTE><EM> We need v to be inside a record.  Otherwise, the language would allow
   a compiler to actually allocate more than the BITS 32 for a value of
   type Swap.Int32.
</EM></BLOCKQUOTE><PRE>
VAR Int32RecVar : Int32Rec;
&lt;*UNUSED*&gt;
VAR CheckInt32 : [ 0 .. 0 ] := ADR(Int32RecVar) - ADR(Int32RecVar.v);

PROCEDURE <A NAME="Convert"><procedure>Convert</procedure></A>(self: T; dest: ADDRESS; v: ReadVisitor;
                  number: INTEGER := 1): ADDRESS RAISES
          {Error, Rd.EndOfFile, Rd.Failure, Thread.Alerted} =
  VAR t: ARRAY [0..7] OF CHAR;
      repetition: INTEGER := 1;

  BEGIN
    IF self.prog.size() = 2 THEN
      (* We have a one step program (the step plus the DONE step), so
         lets just do it in one shot. *)
      repetition := number;
      number := 1;
    END;
    FOR n := 1 TO number DO
      FOR i := 0 TO self.prog.size() - 1 DO
        WITH elem = self.prog.get(i),
             length = elem.length * repetition DO
          CASE elem.kind OF
          | PklAction.Kind.Copy =&gt;
            ReadData(v, dest, length);
            INC(dest, length);
          | PklAction.Kind.SwapPacked =&gt;
            WITH nelem = NARROW(elem, PklAction.SwapPacked) DO
              FOR i := 1 TO length DO
                VAR from: Word.T := 0;
                    to: Word.T := 0;
                    bit: INTEGER := 0;
                    wb := LOOPHOLE(ADR(from), UNTRACED REF
                                   ARRAY [0..BYTESIZE(Word.T)-1] OF CHAR);
                BEGIN
                  v.readData(SUBARRAY(wb^, 0, nelem.size));

                  (** IO.Put(&quot;Extracting from: &quot; &amp;
                    Fmt.Pad(Fmt.Unsigned(from, 2), nelem.size * 8, '0') &amp; &quot;\n&quot;); **)

                  IF self.from.little_endian THEN
                    FOR j := FIRST(nelem.field^) TO LAST(nelem.field^) DO
                      WITH extract = self.extractSwap(from, bit,
                                                      nelem.field[j],
                                                      nelem.size) DO
                        (** IO.Put(&quot; Extracted field: &quot; &amp;
                          Fmt.Pad(Fmt.Unsigned(extract, 2),
                                  nelem.field[j], '0') &amp; &quot;\n&quot;); **)

                        to := Word.Insert(to, extract,
                                          Word.Size-bit-nelem.field[j],
                                          nelem.field[j]);

                        (** IO.Put(&quot; Into field: &quot; &amp;
                          Fmt.Pad(Fmt.Unsigned(to, 2), Word.Size, '0') &amp; &quot;\n&quot;); **)

                        INC(bit, nelem.field[j]);
                      END;
                    END;
                  ELSE
                    FOR j := FIRST(nelem.field^) TO LAST(nelem.field^) DO
                      WITH extract = self.extractSwap(from, bit,
                                                      nelem.field[j],
                                                      nelem.size) DO
                        (** IO.Put(&quot; Extracted field: &quot; &amp;
                          Fmt.Pad(Fmt.Unsigned(extract, 2),
                                  nelem.field[j], '0') &amp; &quot;\n&quot;); **)

                        to := Word.Insert(to, extract,
                                          bit, nelem.field[j]);

                        (** IO.Put(&quot; Into field: &quot; &amp;
                          Fmt.Pad(Fmt.Unsigned(to, 2), nelem.size * 8, '0') &amp; &quot;\n&quot;); **)

                        INC(bit, nelem.field[j]);
                      END;
                    END;
                  END;
                  (* Copy the bytes *)
                  SUBARRAY(LOOPHOLE(dest, BufPtr)^, 0, nelem.size) :=
                    SUBARRAY(LOOPHOLE(ADR(to), BufPtr)^, 0, nelem.size);
                END;
                INC(dest, nelem.size);
              END;
            END;
          | PklAction.Kind.SkipFrom =&gt;
            v.skipData(length);
          | PklAction.Kind.SkipTo =&gt;
            INC(dest, length);
          | PklAction.Kind.Skip =&gt;
            v.skipData(length);
            INC(dest, length);
          | PklAction.Kind.Swap16 =&gt;
            ReadData(v, dest, length*2);
            FOR i := 1 TO length DO
              WITH int16 = LOOPHOLE(dest, UNTRACED REF Swap.Int16) DO
                int16^ := Swap.Swap2(int16^);
              END;
              INC(dest, 2);
            END;
          | PklAction.Kind.Swap32 =&gt;
            ReadData(v, dest, length*4);
            FOR i := 1 TO length DO
              WITH int32 = LOOPHOLE(dest, UNTRACED REF Int32Rec)^.v DO
                int32 := Swap.Swap4(int32);
              END;
              INC(dest, 4);
            END;
          | PklAction.Kind.Swap64 =&gt;
            ReadData(v, dest, length*8);
            FOR i := 1 TO length DO
              WITH int64 = LOOPHOLE(dest, UNTRACED REF Swap.Int64On32) DO
                int64^ := Swap.Swap8(int64^);
              END;
              INC(dest, 8);
            END;
          | PklAction.Kind.Copy32to64, PklAction.Kind.Swap32to64 =&gt;
            WITH nelem = NARROW(elem, PklAction.Copy32to64) DO
              FOR i := 1 TO length DO
                v.readData(SUBARRAY(t, 0, 4));
                WITH int32 = LOOPHOLE(ADR(t[0]), UNTRACED REF Int32Rec)^.v,
                     int64 = LOOPHOLE(dest, UNTRACED REF Swap.Int64On32) DO
                  (* First, put it in a 64 bit word appropriate to the
                     sending machine. *)
                  IF self.from.little_endian THEN
                    int64.a := int32;
                    IF nelem.signed THEN
                      int64.b := SignExt32[Word.Extract(ORD(t[3]), 7, 1)];
                    ELSE
                      int64.b := 0;
                    END;
                  ELSE
                    int64.b := int32;
                    IF nelem.signed THEN
                      int64.a := SignExt32[Word.Extract(ORD(t[0]), 7, 1)];
                    ELSE
                      int64.a := 0;
                    END;
                  END;
                  (* Now, swap it if need be. *)
                  IF elem.kind = PklAction.Kind.Swap32to64 THEN
                    int64^ := Swap.Swap8(int64^);
                  END;
                END;
                INC(dest, 8);
              END;
            END;
          | PklAction.Kind.Copy64to32, PklAction.Kind.Swap64to32 =&gt;
            WITH nelem = NARROW(elem, PklAction.Copy32to64) DO
              FOR i := 1 TO length DO
                v.readData(t);
                WITH int64 = LOOPHOLE(ADR(t[0]), UNTRACED REF Swap.Int64On32)^,
                     int32 = LOOPHOLE(dest, UNTRACED REF Int32Rec)^.v DO
                  (* First, get the lower 32 bits as perceived on the the
                     sending machine. *)
                  IF self.from.little_endian THEN
                    int32 := int64.a;
                    IF int64.b # 0 AND (NOT nelem.signed OR int64.b # -1) THEN
                      RAISE Error(&quot;Data value too big.&quot;);
                    END;
                  ELSE
                    int32 := int64.b;
                    IF int64.a # 0 AND (NOT nelem.signed OR int64.a # -1) THEN
                      RAISE Error(&quot;Data value too big.&quot;);
                    END;
                  END;

                  (* Now, swap it if need be. *)
                  IF elem.kind = PklAction.Kind.Swap64to32 THEN
                    int32 := Swap.Swap4(int32);
                  END;
                END;
                INC(dest, 4);
              END;
            END;
          | PklAction.Kind.ReadRef =&gt;
            WITH nelem = NARROW(elem, PklAction.Ref) DO
              FOR i := 1 TO length DO
                WITH ref = LOOPHOLE(dest, UNTRACED REF REFANY) DO
                  ref^ := v.readRef(nelem.refType);
                END;
                INC(dest, self.to.word_size DIV 8);
              END;
            END;
          | PklAction.Kind.Done =&gt;
          END;
        END;
      END;
    END;
    RETURN dest;
  END Convert;

PROCEDURE <A NAME="WriteData"><procedure>WriteData</procedure></A>(v: WriteVisitor;  src: ADDRESS;  len: INTEGER)
  RAISES {Wr.Failure, Thread.Alerted} =
  BEGIN
    WHILE len &gt;= MAXLEN DO
      v.writeData(LOOPHOLE(src, BufPtr)^);
      INC(src, MAXLEN);  DEC(len, MAXLEN);
    END;
    IF (len &gt; 0) THEN
      v.writeData(SUBARRAY(LOOPHOLE(src, BufPtr)^, 0, len));
    END;
  END WriteData;

PROCEDURE <A NAME="Write"><procedure>Write</procedure></A>(self: T; src: ADDRESS; v: WriteVisitor;
                number: INTEGER := 1): ADDRESS RAISES
          {Error, Wr.Failure, Thread.Alerted} =
  VAR repetition: INTEGER := 1;

  BEGIN
    IF self.writer # NIL THEN
      RETURN self.writer.write(src, v, number);
    END;

    IF self.prog.size() = 2 THEN
      (* We have a one step program (the step plus the DONE step), so
         lets just do it in one shot. *)
      repetition := number;
      number := 1;
    END;
    FOR n := 1 TO number DO
      FOR i := 0 TO self.prog.size() - 1 DO
        WITH elem = self.prog.get(i),
             length = elem.length * repetition DO
          CASE elem.kind OF
          | PklAction.Kind.Copy =&gt;
            WriteData (v, src, length);
            INC(src, length);

          | PklAction.Kind.Skip =&gt;
            v.skipData(length);
            INC(src, length);

          | PklAction.Kind.ReadRef =&gt;
            WITH nelem = NARROW(elem, PklAction.Ref) DO
              FOR i := 1 TO length DO
                WITH ref = LOOPHOLE(src, UNTRACED REF REFANY) DO
                  v.writeRef(nelem.refType, ref^);
                END;
                INC(src, self.to.word_size DIV 8);
              END;
            END;

          | PklAction.Kind.Done =&gt;
          | PklAction.Kind.SwapPacked =&gt;
            RAISE Error(&quot;PklAction.Kind.SwapPacked called during write?&quot;);
          | PklAction.Kind.SkipFrom =&gt;
            RAISE Error(&quot;PklAction.Kind.SkipFrom called during write?&quot;);
          | PklAction.Kind.SkipTo =&gt;
            RAISE Error(&quot;PklAction.Kind.SkipTo called during write?&quot;);
          | PklAction.Kind.Swap16 =&gt;
            RAISE Error(&quot;PklAction.Kind.Swap16 called during write?&quot;);
          | PklAction.Kind.Swap32 =&gt;
            RAISE Error(&quot;PklAction.Kind.Swap32 called during write?&quot;);
          | PklAction.Kind.Swap64 =&gt;
            RAISE Error(&quot;PklAction.Kind.Swap64 called during write?&quot;);
          | PklAction.Kind.Copy32to64 =&gt;
            RAISE Error(&quot;PklAction.Kind.Copy32to64 called during write?&quot;);
          | PklAction.Kind.Swap32to64 =&gt;
            RAISE Error(&quot;PklAction.Kind.Swap32to64 called during write?&quot;);
          | PklAction.Kind.Copy64to32 =&gt;
            RAISE Error(&quot;PklAction.Kind.Copy64to32 called during write?&quot;);
          | PklAction.Kind.Swap64to32 =&gt;
            RAISE Error(&quot;PklAction.Kind.Swap64to32 called during write?&quot;);

          END;
        END;
      END;
    END;
    RETURN src;
  END Write;

PROCEDURE <A NAME="AppendProg"><procedure>AppendProg</procedure></A>(self: T; other: T) RAISES {Error} =
  VAR nelem: PklAction.T;
  BEGIN
    FOR i := 0 TO other.prog.size() - 1 DO
      WITH elem = other.prog.get(i) DO
        CASE elem.kind OF
        | PklAction.Kind.Copy =&gt;
          nelem := NEW(PklAction.T, kind := elem.kind, length := elem.length);
          INC(self.fromOffset, elem.length*8);
          INC(self.toOffset, elem.length*8);
        | PklAction.Kind.SwapPacked =&gt;
          WITH t = NARROW(elem, PklAction.SwapPacked),
               nt = NEW(PklAction.SwapPacked,
                        kind := t.kind, length := t.length, size := t.size,
                        field := NEW(REF ARRAY OF CARDINAL,
                                     NUMBER(t.field^))) DO
            FOR i := FIRST(nt.field^) TO LAST(nt.field^) DO
              nt.field[i] := t.field[i];
            END;
            nelem := nt;
            INC(self.fromOffset, elem.length*8*t.size);
            INC(self.toOffset, elem.length*8*t.size);
          END;
        | PklAction.Kind.SkipFrom =&gt;
          nelem := NEW(PklAction.T, kind := elem.kind, length := elem.length);
          INC(self.fromOffset, elem.length*8);
        | PklAction.Kind.SkipTo =&gt;
          nelem := NEW(PklAction.T, kind := elem.kind, length := elem.length);
          INC(self.toOffset, elem.length*8);
        | PklAction.Kind.Skip =&gt;
          nelem := NEW(PklAction.T, kind := elem.kind, length := elem.length);
          INC(self.fromOffset, elem.length*8);
          INC(self.toOffset, elem.length*8);
        | PklAction.Kind.Swap16 =&gt;
          nelem := NEW(PklAction.T, kind := elem.kind, length := elem.length);
          INC(self.fromOffset, elem.length*16);
          INC(self.toOffset, elem.length*16);
        | PklAction.Kind.Swap32 =&gt;
          nelem := NEW(PklAction.T, kind := elem.kind, length := elem.length);
          INC(self.fromOffset, elem.length*32);
          INC(self.toOffset, elem.length*32);
        | PklAction.Kind.Swap64 =&gt;
          nelem := NEW(PklAction.T, kind := elem.kind, length := elem.length);
          INC(self.fromOffset, elem.length*64);
          INC(self.toOffset, elem.length*64);
        | PklAction.Kind.Copy32to64, PklAction.Kind.Swap32to64 =&gt;
          WITH t = NARROW(elem, PklAction.Copy32to64) DO
            nelem := NEW(PklAction.Copy32to64, kind := t.kind,
                         length := t.length, signed := t.signed);
          END;
          INC(self.fromOffset, elem.length*32);
          INC(self.toOffset, elem.length*64);
        | PklAction.Kind.Copy64to32, PklAction.Kind.Swap64to32 =&gt;
          WITH t = NARROW(elem, PklAction.Copy32to64) DO
            nelem := NEW(PklAction.Copy32to64, kind := t.kind,
                         length := t.length, signed := t.signed);
          END;
          INC(self.fromOffset, elem.length*64);
          INC(self.toOffset, elem.length*32);
        | PklAction.Kind.ReadRef =&gt;
          WITH t = NARROW(elem, PklAction.Ref) DO
            nelem := NEW(PklAction.Ref, kind := t.kind, length := t.length,
                         refType := t.refType);
          END;
          INC(self.fromOffset, elem.length*self.from.word_size);
          INC(self.toOffset, elem.length*self.to.word_size);
        | PklAction.Kind.Done =&gt;
          RETURN;
        END;
        self.prog.addhi(nelem);
      END;
    END;
    RAISE Error(&quot;Invalid conversion program.&quot;);
  END AppendProg;

PROCEDURE <A NAME="GetHiKind"><procedure>GetHiKind</procedure></A>(prog: PklActionSeq.T; kind: PklAction.Kind;
                    VAR elem: PklAction.T) : BOOLEAN =
  BEGIN
    IF prog.size() &gt; 0 THEN
      elem := prog.gethi();
      IF elem.kind = kind THEN
        RETURN TRUE;
      END;
    END;
    RETURN FALSE;
  END GetHiKind;

PROCEDURE <A NAME="AddCopy"><procedure>AddCopy</procedure></A>(self: T; length: INTEGER) =
  VAR elem: PklAction.T;
  BEGIN
    INC(self.fromOffset, length);
    INC(self.toOffset, length);
    IF GetHiKind(self.prog, PklAction.Kind.Copy, elem) THEN
      INC(elem.length, length DIV 8);
      RETURN;
    END;
    self.prog.addhi(NEW(PklAction.T, kind := PklAction.Kind.Copy,
                        length := length DIV 8));
  END AddCopy;

PROCEDURE <A NAME="AddPackedSwapFirstField"><procedure>AddPackedSwapFirstField</procedure></A>(self: T; fieldsize: INTEGER) =
  BEGIN
    (* We'll copy the minimum number of whole bytes. *)
    WITH length = RoundUp(fieldsize, 8) DO
      INC(self.fromOffset, length);
      INC(self.toOffset, length);
      WITH elem = NEW(PklAction.SwapPacked, kind := PklAction.Kind.SwapPacked,
                      length := 1, size := length DIV 8,
                      field := NEW(REF ARRAY OF CARDINAL, 1)) DO
        elem.field[0] := fieldsize;
        self.prog.addhi(elem);
      END;
    END;
  END AddPackedSwapFirstField;

PROCEDURE <A NAME="AddPackedSwapNextField"><procedure>AddPackedSwapNextField</procedure></A>(self: T; fieldsize: INTEGER;
                                 offset: INTEGER) =
  VAR elem: PklAction.T;
      total: CARDINAL;
  BEGIN
    WITH ret = GetHiKind(self.prog, PklAction.Kind.SwapPacked, elem) DO
      (* The last entry _must_ be one of these. *)
      &lt;* ASSERT ret *&gt;
      WITH nelem = NARROW(elem, PklAction.SwapPacked) DO
        total := 0;
        FOR i := FIRST(nelem.field^) TO LAST(nelem.field^) DO
          INC(total, nelem.field[i]);
        END;
        (* They should be packed, so our offset should equal the
           total thus far.  We can't check it, but we can at least
           check the bits line up in the current byte. *)
        &lt;* ASSERT (total MOD 8) = (offset MOD 8) *&gt;

        INC(total, fieldsize);

        (* The max total size of a packed set of fields is the word
           size!  This is guaranteed because a packed field cannot
           span a word boundary, and we only call this function when a
           packed field starts within a byte.  If a packed field
           starts on a byte boundary, we start a new set of packed
           fields. *)
        &lt;* ASSERT total &lt;= self.from.word_size *&gt;

        (* Make sure the size in nelem is correct, and make sure the
           the fromOffset and toOffsets are updated to include any new
           bytes *)
        total := RoundUp(total, 8);
        WITH bytes = (total DIV 8) - nelem.size DO
          IF bytes &gt; 0 THEN
            INC(self.fromOffset, bytes*8);
            INC(self.toOffset, bytes*8);
            INC(nelem.size, bytes);
          END;
        END;

        WITH new_field = NEW(REF ARRAY OF CARDINAL,
                             NUMBER(nelem.field^) + 1) DO
          SUBARRAY(new_field^, 0, NUMBER(nelem.field^)) := nelem.field^;
          nelem.field := new_field;
        END;
        nelem.field[LAST(nelem.field^)] := fieldsize;
      END;
    END;
  END AddPackedSwapNextField;

PROCEDURE <A NAME="AddPackedSwapArray"><procedure>AddPackedSwapArray</procedure></A>(self: T; length: INTEGER;
                             numElts: INTEGER; fieldsize: INTEGER;
                             wordsize: INTEGER) =
  VAR count := wordsize DIV fieldsize;
  BEGIN
    (* We will build a packing for at most &quot;count&quot; fields.  Packed
       arrays must not have elements that span word boundaries, so
       if &quot;numElts &gt; count&quot; then &quot;count * fieldsize = wordsize&quot; *)
    &lt;* ASSERT (numElts &lt;= count) OR (count * fieldsize = wordsize) *&gt;

    INC(self.fromOffset, length);
    INC(self.toOffset, length);

    WITH len       = numElts DIV count,
         size      = wordsize DIV 8,
         extraCnt  = numElts MOD count,
         extraSize = (length MOD wordsize) DIV 8 DO

      (* First, we add an element to handle the bulk of the array
         element swapping when the array spans more than one word.
         It takes care of the first len*count elements which occupy
         the first len words. *)
      IF len &gt; 0 THEN
        WITH nelem = NEW(PklAction.SwapPacked,
                         kind := PklAction.Kind.SwapPacked,
                         length := len, size := size,
                         field := NEW(REF ARRAY OF CARDINAL, count)) DO
          FOR i := FIRST(nelem.field^) TO LAST(nelem.field^) DO
            nelem.field[i] := fieldsize;
          END;
          self.prog.addhi(nelem);
        END;
      END;

      (* Next, if the number of array elements does not fit exactly in
         full words, add one more element to the end which handles the
         extra fields.  *)
      IF extraSize &gt; 0 THEN
        WITH nelem = NEW(PklAction.SwapPacked,
                         kind := PklAction.Kind.SwapPacked,
                         length := 1, size := extraSize,
                         field := NEW(REF ARRAY OF CARDINAL, extraCnt)) DO
          FOR i := FIRST(nelem.field^) TO LAST(nelem.field^) DO
            nelem.field[i] := fieldsize;
          END;
          self.prog.addhi(nelem);
        END;
      END;
    END;
  END AddPackedSwapArray;

PROCEDURE <A NAME="AddCopy32to64"><procedure>AddCopy32to64</procedure></A>(self: T; length: INTEGER; signed: BOOLEAN) =
</PRE><BLOCKQUOTE><EM> PRE: length MOD 32 = 0 </EM></BLOCKQUOTE><PRE>
  VAR elem: PklAction.T;
  BEGIN
    INC(self.fromOffset, length);
    INC(self.toOffset, length*2);
    IF GetHiKind(self.prog, PklAction.Kind.Copy32to64, elem) THEN
      WITH nelem = NARROW(elem, PklAction.Copy32to64) DO
        IF nelem.signed = signed THEN
          INC(elem.length, length DIV 32);
          RETURN;
        END;
      END;
    END;
    self.prog.addhi(NEW(PklAction.Copy32to64, kind := PklAction.Kind.Copy32to64,
                        length := length DIV 32, signed := signed));
  END AddCopy32to64;

PROCEDURE <A NAME="AddCopy64to32"><procedure>AddCopy64to32</procedure></A>(self: T; length: INTEGER; signed: BOOLEAN) =
</PRE><BLOCKQUOTE><EM> PRE: length MOD 64 = 0 </EM></BLOCKQUOTE><PRE>
  VAR elem: PklAction.T;
  BEGIN
    INC(self.fromOffset, length);
    &lt;* ASSERT length MOD 2 = 0 *&gt;
    INC(self.toOffset, length DIV 2);
    IF GetHiKind(self.prog, PklAction.Kind.Copy64to32, elem) THEN
      WITH nelem = NARROW(elem, PklAction.Copy32to64) DO
        IF nelem.signed = signed THEN
          INC(elem.length, length DIV 64);
          RETURN;
        END;
      END;
    END;
    self.prog.addhi(NEW(PklAction.T, kind := PklAction.Kind.Copy64to32,
                        length := length DIV 64));
  END AddCopy64to32;

PROCEDURE <A NAME="AddSkipFrom"><procedure>AddSkipFrom</procedure></A>(self: T; length: INTEGER) =
  VAR elem: PklAction.T;
  BEGIN
    INC(self.fromOffset, length);
    IF GetHiKind(self.prog, PklAction.Kind.SkipFrom, elem) THEN
      INC(elem.length, length DIV 8);
      RETURN;
    END;
    self.prog.addhi(NEW(PklAction.T, kind := PklAction.Kind.SkipFrom,
                        length := length DIV 8));
  END AddSkipFrom;

PROCEDURE <A NAME="AddSkipTo"><procedure>AddSkipTo</procedure></A>(self: T; length: INTEGER) =
  VAR elem: PklAction.T;
  BEGIN
    INC(self.toOffset, length);
    IF GetHiKind(self.prog, PklAction.Kind.SkipTo, elem) THEN
      INC(elem.length, length DIV 8);
      RETURN;
    END;
    self.prog.addhi(NEW(PklAction.T, kind := PklAction.Kind.SkipTo,
                        length := length DIV 8));
  END AddSkipTo;

PROCEDURE <A NAME="AddSkipOrCopy"><procedure>AddSkipOrCopy</procedure></A>(self: T; length: INTEGER) =
  VAR elem: PklAction.T;
  BEGIN
    INC(self.fromOffset, length);
    INC(self.toOffset, length);
    IF GetHiKind(self.prog, PklAction.Kind.Copy, elem) THEN
      INC(elem.length, length DIV 8);
      RETURN;
    ELSIF GetHiKind(self.prog, PklAction.Kind.Skip, elem) THEN
      INC(elem.length, length DIV 8);
      RETURN;
    END;
    self.prog.addhi(NEW(PklAction.T, kind := PklAction.Kind.Skip,
                        length := length DIV 8));
  END AddSkipOrCopy;

PROCEDURE <A NAME="AddSkip"><procedure>AddSkip</procedure></A>(self: T; fromDiff, toDiff: INTEGER) =
  BEGIN
    IF fromDiff = toDiff THEN
      IF fromDiff &gt; 0 THEN
        self.addSkipOrCopy(fromDiff);
      END;
    ELSE
      WITH bothPad = MIN(toDiff, fromDiff) DO
        IF bothPad &gt; 0 THEN
          self.addSkipOrCopy(bothPad);
        END;
        DEC(fromDiff, bothPad);
        DEC(toDiff, bothPad);
        IF fromDiff &gt; 0 THEN
          self.addSkipFrom(fromDiff);
        END;
        IF toDiff &gt; 0 THEN
          self.addSkipTo(toDiff);
        END;
      END;
    END;
  END AddSkip;

PROCEDURE <A NAME="AddSwap16"><procedure>AddSwap16</procedure></A>(self: T; length: INTEGER) =
</PRE><BLOCKQUOTE><EM> PRE: length MOD 16 = 0 </EM></BLOCKQUOTE><PRE>
  VAR elem: PklAction.T;
  BEGIN
    INC(self.fromOffset, length);
    INC(self.toOffset, length);
    IF GetHiKind(self.prog, PklAction.Kind.Swap16, elem) THEN
      INC(elem.length, length DIV 16);
      RETURN;
    END;
    self.prog.addhi(NEW(PklAction.T, kind := PklAction.Kind.Swap16,
                        length := length DIV 16));
  END AddSwap16;

PROCEDURE <A NAME="AddSwap32"><procedure>AddSwap32</procedure></A>(self: T; length: INTEGER) =
</PRE><BLOCKQUOTE><EM> PRE: length MOD 32 = 0 </EM></BLOCKQUOTE><PRE>
  VAR elem: PklAction.T;
  BEGIN
    INC(self.fromOffset, length);
    INC(self.toOffset, length);
    IF GetHiKind(self.prog, PklAction.Kind.Swap32, elem) THEN
      INC(elem.length, length DIV 32);
      RETURN;
    END;
    self.prog.addhi(NEW(PklAction.T, kind := PklAction.Kind.Swap32,
                        length := length DIV 32));
  END AddSwap32;

PROCEDURE <A NAME="AddSwap64"><procedure>AddSwap64</procedure></A>(self: T; length: INTEGER) =
</PRE><BLOCKQUOTE><EM> PRE: length MOD 64 = 0 </EM></BLOCKQUOTE><PRE>
  VAR elem: PklAction.T;
  BEGIN
    INC(self.fromOffset, length);
    INC(self.toOffset, length);
    IF GetHiKind(self.prog, PklAction.Kind.Swap64, elem) THEN
      INC(elem.length, length DIV 64);
      RETURN;
    END;
    self.prog.addhi(NEW(PklAction.T, kind := PklAction.Kind.Swap64,
                        length := length DIV 64));
  END AddSwap64;

PROCEDURE <A NAME="AddSwap32to64"><procedure>AddSwap32to64</procedure></A>(self: T; length: INTEGER; signed: BOOLEAN) =
</PRE><BLOCKQUOTE><EM> PRE: length MOD 32 = 0 </EM></BLOCKQUOTE><PRE>
  VAR elem: PklAction.T;
  BEGIN
    INC(self.fromOffset, length);
    INC(self.toOffset, length*2);
    IF GetHiKind(self.prog, PklAction.Kind.Swap32to64, elem) THEN
      WITH nelem = NARROW(elem, PklAction.Copy32to64) DO
        IF nelem.signed = signed THEN
          INC(elem.length, length DIV 32);
          RETURN;
        END;
      END;
    END;
    self.prog.addhi(NEW(PklAction.Copy32to64, kind := PklAction.Kind.Swap32to64,
                        length := length DIV 32, signed := signed));
  END AddSwap32to64;

PROCEDURE <A NAME="AddSwap64to32"><procedure>AddSwap64to32</procedure></A>(self: T; length: INTEGER; signed: BOOLEAN) =
</PRE><BLOCKQUOTE><EM> PRE: length MOD 64 = 0 </EM></BLOCKQUOTE><PRE>
  VAR elem: PklAction.T;
  BEGIN
    INC(self.fromOffset, length);
    &lt;* ASSERT length MOD 64 = 0 *&gt;
    INC(self.toOffset, length DIV 2);
    IF GetHiKind(self.prog, PklAction.Kind.Swap64to32, elem) THEN
      WITH nelem = NARROW(elem, PklAction.Copy32to64) DO
        IF nelem.signed = signed THEN
          INC(elem.length, length DIV 64);
          RETURN;
        END;
      END;
    END;
    self.prog.addhi(NEW(PklAction.T, kind := PklAction.Kind.Swap64to32,
                        length := length DIV 64));
  END AddSwap64to32;

PROCEDURE <A NAME="AddRef"><procedure>AddRef</procedure></A>(self: T; type: RefType) =
  VAR elem: PklAction.T;
  BEGIN
    INC(self.fromOffset, self.from.word_size);
    INC(self.toOffset, self.to.word_size);
    IF GetHiKind(self.prog, PklAction.Kind.ReadRef, elem) THEN
      WITH nelem = NARROW(elem, PklAction.Ref) DO
        IF nelem.refType = type THEN
          INC(elem.length);
          RETURN;
        END;
      END;
    END;
    self.prog.addhi(NEW(PklAction.Ref, kind := PklAction.Kind.ReadRef,
                        length := 1, refType := type));
  END AddRef;

PROCEDURE <A NAME="AddDone"><procedure>AddDone</procedure></A>(self: T) =
  BEGIN
    self.prog.addhi(NEW(PklAction.T, kind := PklAction.Kind.Done));
  END AddDone;

PROCEDURE <A NAME="New"><procedure>New</procedure></A>(typecode: INTEGER; from: RTPacking.T; to: RTPacking.T;
              VAR (*OUT*) nDim, fromEltPack, toEltPack: INTEGER): T
  RAISES {Error} =
  VAR key := PackingTypeCode.T{from := RTPacking.Encode(from),
                               to := RTPacking.Encode(to),
                               tc := typecode};
      ref : REFANY;
  BEGIN
    (* If we've already building this converter, return it. *)
    IF packingCache.get(key, ref) THEN
      WITH ret = NARROW(ref, T) DO
        ret.getDim(nDim, fromEltPack, toEltPack);
        RETURN ret;
      END;
    END;

    RETURN NEW(T).init(typecode, from, to, nDim, fromEltPack, toEltPack);
  END New;

PROCEDURE <A NAME="GetDim"><procedure>GetDim</procedure></A>(self: T; VAR (*OUT*) nDim, fromEltPack, toEltPack: INTEGER) =
  BEGIN
    nDim := self.nDim;
    fromEltPack := self.fromEltPack;
    toEltPack := self.toEltPack;
  END GetDim;

PROCEDURE <A NAME="RoundUp"><procedure>RoundUp</procedure></A> (size, alignment: INTEGER): INTEGER =
  BEGIN
    IF (alignment = 0) THEN RETURN size;
    ELSE RETURN ((size + alignment - 1) DIV alignment) * alignment END;
  END RoundUp;

PROCEDURE <A NAME="Init"><procedure>Init</procedure></A>(self: T; typecode: INTEGER; from: RTPacking.T;
               to: RTPacking.T;
               VAR (*OUT*) nDim, fromEltPack, toEltPack: INTEGER): T
  RAISES {Error} =
  VAR key := PackingTypeCode.T{from := RTPacking.Encode(from),
                               to := RTPacking.Encode(to),
                               tc := typecode};
      ref : REFANY;
  BEGIN
    (* If we've already building this converter, return it.  We've
       still wasted a NEW() to get here, but better than recomputing
       everything! *)
    IF packingCache.get(key, ref) THEN
      WITH ret = NARROW(ref, T) DO
        ret.getDim(nDim, fromEltPack, toEltPack);
        RETURN ret;
      END;
    END;

    (* Get the fromTipe data structure and check for a few errors. *)
    self.fromTipe := RTTipe.Get(typecode, from);
    IF self.fromTipe = NIL THEN
      RAISE Error(&quot;\&quot;from\&quot; RTTipe.T is NIL.&quot;);
    END;

    self.toTipe := RTTipe.Get(typecode, to);
    IF self.toTipe = NIL THEN
      RAISE Error(&quot;\&quot;to\&quot; RTTipe.T is NIL.&quot;);
    END;

    (* OpenArrays need to be handled outside of the convert routine.  A
       conversion is therefore built for the type of the array element,
       and that conversion should be applied the correct number of
       times when the array dimmensions are known. *)
    TYPECASE self.fromTipe OF
    | RTTipe.OpenArray(oa) =&gt;
      WITH oa2 = NARROW(self.toTipe, RTTipe.OpenArray) DO
        self.nDim := oa.n_dimensions;
        self.fromEltPack := oa.elt_pack;
        self.toEltPack := oa2.elt_pack;
        self.fromTipe := oa.element;
        self.toTipe := oa2.element;
      END;
    ELSE
      self.nDim := 0;
      self.fromEltPack := 0;
      self.toEltPack := 0;
    END;
    nDim := self.nDim;
    fromEltPack := self.fromEltPack;
    toEltPack := self.toEltPack;

    (* We don't know how to do floating point conversions. *)
    IF from.float # to.float THEN
      RAISE Error(&quot;Data format (float) not handled.&quot;);
    END;

    IF from.word_size &lt; 32 OR to.word_size &lt; 32 THEN
      RAISE Error(&quot;Data format (word size) not handled.&quot;);
    END;

    self.from := from;
    self.to := to;
    self.prog := NEW(PklActionSeq.T).init();

    self.wordKind := GetWordKind(from, to);
    self.longKind := GetLongintKind(from, to);

    (* Build the tipe conversion for the top level  *)
    self.buildOne(self.fromTipe, self.toTipe);

    (* Figure out the allocated size. *)
    IF self.nDim &gt; 0 THEN
      (* Open arrays will actually store the data for their elemnent
         here, so we don't adjust that. *)
      self.fromSize := self.fromTipe.size;
      self.toSize := self.toTipe.size;
    ELSE
      (* If this is not an open array, we need to heed what it says in
         in RTType.FixSizes().  A comment points out that
         all the REF types dataSizes are a multiple of &quot;header&quot; words,
         where the size of &quot;header&quot; is the word size of the machine.
         So, we will ensure that any conversion copies a multiple of
         word_size if it is a reftype.  *)

      (* Get the size of the data of the referent.  If it's an object,
         we want the size of the fields.  Otherwise, the size is what we
         want. *)

      self.fromSize := self.fromOffset;
      self.toSize := self.toOffset;
</PRE><BLOCKQUOTE><EM><P>
      TYPECASE self.fromTipe OF
      <PRE>
      RTTipe.Object(o) =&gt;
        </PRE>
WITH o2 = NARROW(self.toTipe, RTTipe.Object) DO
          self.fromSize := o.field_size;
          self.toSize := o2.field_size;
        END;
      ELSE
        self.fromSize := self.fromTipe.size;
        self.toSize := self.toTipe.size;
      END;
</EM></BLOCKQUOTE><PRE>
      WITH newFromSize = RoundUp(self.fromSize, self.from.word_size),
           newToSize = RoundUp(self.toSize, self.to.word_size),
           fromDiff = newFromSize - self.fromSize,
           toDiff = newToSize - self.toSize DO
	&lt;* ASSERT fromDiff &gt;= 0 *&gt;
	&lt;* ASSERT toDiff &gt;= 0 *&gt;

        self.addSkip(fromDiff, toDiff);
        self.fromSize := newFromSize;
        self.toSize := newToSize;
      END;
    END;
    self.addDone();

    (* the write method is only geared towards &quot;converting&quot; between
       exactly the same machine. *)
    IF self.from # self.to THEN
      VAR wnDim, wfromEltPack, wtoEltPack: INTEGER;
      BEGIN
        self.writer := New(typecode, to, to, wnDim,
                           wfromEltPack, wtoEltPack);
      END;
    ELSE
      self.writer := NIL;
    END;

    EVAL packingCache.put(key, self);
</PRE><BLOCKQUOTE><EM><P>
    BasicTipeToText(self.fromTipe);
    IO.Put(<CODE>, total size: </CODE> &amp; Fmt.Int(self.fromSize) &amp; <CODE>\n</CODE>);
    BasicTipeToText(self.toTipe);
    IO.Put(<CODE>, total size: </CODE> &amp; Fmt.Int(self.toSize) &amp; <CODE>\n</CODE>);
</EM></BLOCKQUOTE><PRE>
    RETURN self;
  END Init;

PROCEDURE <A NAME="GetWordKind"><procedure>GetWordKind</procedure></A>(from: RTPacking.T; to: RTPacking.T): Kind =
</PRE><BLOCKQUOTE><EM> The result is good for all ordinal types except LONGINT. </EM></BLOCKQUOTE><PRE>
  BEGIN
    IF from.word_size = to.word_size THEN
      IF from.little_endian = to.little_endian THEN
        RETURN Kind.Copy;
      ELSE
        RETURN Kind.Swap;
      END;
    ELSE
      IF from.little_endian = to.little_endian THEN
        IF from.word_size = 32 THEN
          RETURN Kind.Copy32to64;
        ELSE
          RETURN Kind.Copy64to32;
        END;
      ELSE
        IF from.word_size = 32 THEN
          RETURN Kind.Swap32to64;
        ELSE
          RETURN Kind.Swap64to32;
        END;
      END;
    END;
  END GetWordKind;

PROCEDURE <A NAME="GetLongintKind"><procedure>GetLongintKind</procedure></A>(from: RTPacking.T; to: RTPacking.T): Kind =
</PRE><BLOCKQUOTE><EM> The result is good only for LONGINT. </EM></BLOCKQUOTE><PRE>
  BEGIN
    IF from.long_size = to.long_size THEN
      IF from.little_endian = to.little_endian THEN
        RETURN Kind.Copy;
      ELSE
        RETURN Kind.Swap;
      END;
    ELSE
      IF from.little_endian = to.little_endian THEN
        IF from.long_size = 32 THEN
          RETURN Kind.Copy32to64;
        ELSE
          RETURN Kind.Copy64to32;
        END;
      ELSE
        IF from.long_size = 32 THEN
          RETURN Kind.Swap32to64;
        ELSE
          RETURN Kind.Swap64to32;
        END;
      END;
    END;
  END GetLongintKind;

PROCEDURE <A NAME="BuildSuper"><procedure>BuildSuper</procedure></A>(self: T; typecode: INTEGER;
                     VAR fromSize, fromAlign, toSize, toAlign: INTEGER)
  RAISES {Error} =
  VAR nDim, fromEltPack, toEltPack: INTEGER;
      superConverter := New(typecode, self.from, self.to, nDim,
                            fromEltPack, toEltPack);
      superFromTipe, superToTipe: RTTipe.Object;
  BEGIN
    (* Get the fromTipe data structure and check for a few errors. *)
    (*
    superFromTipe := RTTipe.Get(typecode, self.from);
    IF superFromTipe = NIL THEN
      RAISE Error(&quot;supertype \&quot;from\&quot; RTTipe.T is NIL.&quot;);
    END;

    superToTipe := RTTipe.Get(typecode, self.to);
    IF superToTipe = NIL THEN
      RAISE Error(&quot;supertype \&quot;to\&quot; RTTipe.T is NIL.&quot;);
    END;
    self.buildOne(superFromTipe, superToTipe);
    *)

    (* Add the supertype conversion program to ours. *)
    self.appendProg(superConverter);

    (* Return the proper allocated size. *)
    fromSize := superConverter.fromSize;
    toSize := superConverter.toSize;

    (* Coerce to RTTipe.Objects, which they must be. *)
    superFromTipe := superConverter.fromTipe;
    superToTipe := superConverter.toTipe;
    fromAlign := superFromTipe.field_align;
    toAlign := superToTipe.field_align;
  END BuildSuper;

PROCEDURE <A NAME="BuildOrdinal"><procedure>BuildOrdinal</procedure></A>(self: T; fromTipe: RTTipe.T;
                       toTipe: RTTipe.T; convKind: Kind; signed: BOOLEAN)
          RAISES {Error} =
  BEGIN
    CASE convKind OF
    | Kind.Copy =&gt;
      &lt;* ASSERT fromTipe.size = toTipe.size *&gt;
      self.addCopy(fromTipe.size);
    | Kind.Swap =&gt;
      &lt;* ASSERT fromTipe.size = toTipe.size *&gt;
      CASE fromTipe.size OF
      | 8 =&gt; self.addCopy(8);
      | 16 =&gt; self.addSwap16(16);
      | 32 =&gt; self.addSwap32(32);
      | 64 =&gt; self.addSwap64(64);
      ELSE
        RAISE Error(&quot;Should not get here: 2&quot;);
      END;
    | Kind.Copy32to64 =&gt;
      IF fromTipe.size = toTipe.size THEN
        self.addCopy(fromTipe.size);
      ELSE
        &lt;* ASSERT fromTipe.size = 32 *&gt;
	&lt;* ASSERT toTipe.size = 64 *&gt;
        self.addCopy32to64(32, signed);
      END;
    | Kind.Copy64to32 =&gt;
      IF fromTipe.size = toTipe.size THEN
        self.addCopy(fromTipe.size);
      ELSE
        &lt;* ASSERT fromTipe.size = 64 *&gt;
	&lt;* ASSERT toTipe.size = 32 *&gt;
        self.addCopy64to32(64, signed);
      END;
    | Kind.Swap32to64 =&gt;
      CASE fromTipe.size OF
      | 8 =&gt; &lt;* ASSERT toTipe.size = 8 *&gt; self.addCopy(8);
      | 16 =&gt; &lt;* ASSERT toTipe.size = 16 *&gt; self.addSwap16(16);
      | 32 =&gt;
        IF toTipe.size = 32 THEN
          self.addSwap32(32);
        ELSE
          &lt;* ASSERT toTipe.size = 64 *&gt;
          self.addSwap32to64(32, signed);
        END;
      | 64 =&gt; RAISE Error(&quot;Should not get here: 1&quot;);
      ELSE
        RAISE Error(&quot;Should not get here: 3&quot;);
      END;
    | Kind.Swap64to32 =&gt;
      CASE fromTipe.size OF
      | 8 =&gt; &lt;* ASSERT toTipe.size = 8 *&gt; self.addCopy(8);
      | 16 =&gt; &lt;* ASSERT toTipe.size = 16 *&gt; self.addSwap16(16);
      | 32 =&gt; &lt;* ASSERT toTipe.size = 32 *&gt; self.addSwap32(32);
      | 64 =&gt; &lt;* ASSERT toTipe.size = 32 *&gt; self.addSwap64to32(64, signed);
      ELSE
        RAISE Error(&quot;Should not get here: 4&quot;);
      END;
    END;
  END BuildOrdinal;

PROCEDURE <A NAME="BuildOne"><procedure>BuildOne</procedure></A>(self: T; fromTipe: RTTipe.T;
                   toTipe: RTTipe.T) RAISES {Error} =
  BEGIN
    (* When we start, we assume that the offsets are positioned
       correctly for these tipes in the data objects. *)

    (* Check some obvious conditions. *)
    &lt;* ASSERT fromTipe.kind = toTipe.kind *&gt;

    (* In fact, these next two aren't always correct because of the
       weird way objects are laid out. *)
    (* ASSERT self.fromOffset MOD fromTipe.align = 0 *)
    (* ASSERT self.toOffset MOD toTipe.align = 0 *)

    TYPECASE fromTipe OF
    | RTTipe.Builtin, RTTipe.Enum, RTTipe.Set, RTTipe.Subrange =&gt;
      CASE fromTipe.kind OF
      | RTTipe.Kind.Address =&gt; self.addRef(RefType.UntracedRef);
      | RTTipe.Kind.Proc =&gt; self.addRef(RefType.Proc);
      | RTTipe.Kind.Refany =&gt; self.addRef(RefType.Ref);
      | RTTipe.Kind.Null =&gt;
        (* No need to actually swap, since the only value of type NULL  is NIL,
            and it is always represented as zero.
        *)
        CASE self.wordKind OF
	| Kind.Copy, Kind.Swap =&gt;
	  &lt;* ASSERT fromTipe.size = toTipe.size *&gt;
	  self.addCopy(fromTipe.size);
	| Kind.Copy32to64, Kind.Swap32to64 =&gt;
	  &lt;* ASSERT fromTipe.size = 32 *&gt;
	  &lt;* ASSERT toTipe.size = 64 *&gt;
	  self.addCopy32to64(32, signed := FALSE);
	| Kind.Copy64to32, Kind.Swap64to32 =&gt;
	  &lt;* ASSERT fromTipe.size = 64 *&gt;
	  &lt;* ASSERT toTipe.size = 32 *&gt;
	  self.addCopy64to32(64, signed := FALSE);
	END;
      | RTTipe.Kind.Boolean, RTTipe.Kind.Char, RTTipe.Kind.Enum,
        RTTipe.Kind.Set, RTTipe.Kind.Subrange, RTTipe.Kind.Cardinal =&gt;
        BuildOrdinal(self, fromTipe, toTipe, self.wordKind, signed:=FALSE);
      | RTTipe.Kind.Integer =&gt;
        BuildOrdinal(self, fromTipe, toTipe, self.wordKind, signed:=TRUE);
      | RTTipe.Kind.Longcard =&gt;
        BuildOrdinal(self, fromTipe, toTipe, self.longKind, signed:=FALSE);
      | RTTipe.Kind.Longint =&gt;
        BuildOrdinal(self, fromTipe, toTipe, self.longKind, signed:=TRUE);
      | RTTipe.Kind.Extended, RTTipe.Kind.Longreal =&gt;
        &lt;* ASSERT toTipe.size = 64 *&gt;
        &lt;* ASSERT fromTipe.size = 64 *&gt;
        CASE self.wordKind OF
	| Kind.Copy, Kind.Copy32to64, Kind.Copy64to32 =&gt;
	  self.addCopy(64);
	| Kind.Swap, Kind.Swap32to64, Kind.Swap64to32 =&gt;
	  self.addSwap64(64);
	END;
      | RTTipe.Kind.Real =&gt;
        &lt;* ASSERT toTipe.size = 32 *&gt;
        &lt;* ASSERT fromTipe.size = 32 *&gt;
        CASE self.wordKind OF
	| Kind.Copy, Kind.Copy32to64, Kind.Copy64to32 =&gt;
	  self.addCopy(32);
	| Kind.Swap, Kind.Swap32to64, Kind.Swap64to32 =&gt;
	  self.addSwap32(32);
	END;
      ELSE
        RAISE Error(&quot;Builtin but not builtin.&quot;);
      END;

    | RTTipe.Packed(p) =&gt;
      (* We should only get here if we have a reference to a packed
         element, since packed fields are handled in BuildFields.
         We can ignore the parent type and just create a single entry
         packedswap or a byte copy. *)
      &lt;* ASSERT fromTipe.size = toTipe.size *&gt;
      CASE self.wordKind OF
      | Kind.Copy, Kind.Copy32to64, Kind.Copy64to32 =&gt;
        WITH bytesNeeded = RoundUp(p.size, 8) DO
          self.addCopy(bytesNeeded);
        END;
      | Kind.Swap, Kind.Swap32to64, Kind.Swap64to32 =&gt;
        self.addPackedSwapFirstField(p.size);
      END;

    | RTTipe.Array(a) =&gt;
      WITH a2 = NARROW(toTipe, RTTipe.Array) DO
        IF a.element.kind = RTTipe.Kind.Packed THEN
          IF a.element.size = 8 THEN
            (* An array of packed elements that fit exactly in a byte.  We can
               just copy this on any machine! *)
            self.addCopy(a.size);
          ELSE
            (* On same endian machines, we can just copy these bytes. *)
            CASE self.wordKind OF
            | Kind.Copy, Kind.Copy32to64, Kind.Copy64to32 =&gt;
              self.addCopy(a.size);
            | Kind.Swap =&gt;
              self.addPackedSwapArray(a.size, a.n_elts, a.element.size,
                                      self.from.word_size);
            | Kind.Swap32to64, Kind.Swap64to32 =&gt;
              (* Must pack properly into the smaller 32 bit words *)
              self.addPackedSwapArray(a.size, a.n_elts, a.element.size,
                                      32);
            END;
          END;
        ELSE
          (* Since array elements must fit in totally
             continguous memory, we don't need to check the offsets
             when we return from buildOne *)
          FOR i := 1 TO a.n_elts DO
            self.buildOne(a.element, a2.element);
          END;
        END;
      END;

    | RTTipe.Object(o) =&gt;
      WITH o2 = NARROW(toTipe, RTTipe.Object) DO
        &lt;* ASSERT o.super # NIL *&gt;
        &lt;* ASSERT o2.super # NIL *&gt;
        &lt;* ASSERT o.super.typecode = o2.super.typecode *&gt;
        (* Get the conversion program for the supertype, and append it
           onto the end of ours.  Note that we assume that we are
           operating on the data addresses of the objects, as returned
           by RTHeap.GetDataAdr().  Not the actual object address.
           This address is BYTESIZE(ADDRESS) after the actual start of
           the allocated space. *)

        VAR fromSize, fromAlign, toSize, toAlign: INTEGER;
            fromPad, toPad: INTEGER := 0;
        BEGIN
          IF o.super.typecode = TYPECODE(ROOT) THEN
            fromSize := 0;
            toSize := 0;
            fromAlign := o.align;
            toAlign := o2.align;
          ELSIF o.super.typecode = TYPECODE(UNTRACED ROOT) THEN
            RAISE Error(&quot;UNTRACED ROOT passed to ConvertPacking&quot;);
          ELSE
            self.buildSuper(o.super.typecode, fromSize, fromAlign,
                            toSize, toAlign);
          END;

          (* If the alignment increases from a supertype to a subtype,
             we may have to add some padding in the data fields.  Note
             that we have to take into account the real allocated
             space to figure the current alignment, so add that extra
             BITSIZE(ADDRESS) to the beginning. *)
          WITH fromDiff = o.field_align - fromAlign  DO
            IF fromDiff &gt; 0 THEN
              fromPad := (fromSize + self.from.word_size) MOD o.field_align;
              IF fromPad &gt; 0 THEN
                fromPad := o.field_align - fromPad;
              END;
            END;
          END;
          WITH toDiff = o2.field_align - toAlign DO
            IF toDiff &gt; 0 THEN
              toPad := (toSize + self.to.word_size) MOD o2.field_align;
              IF toPad &gt; 0 THEN
                toPad := o2.field_align - toPad;
              END;
            END;
          END;

          (* If we need to pad them, do it. *)
          self.addSkip(fromPad, toPad);
        END;
        (* Since a conversion program guarantees it reads and writes
           the proper number of bytes, we can now just continue with
           this objects fields. *)

        self.buildFields(o.fields, o.field_size, o2.fields, o2.field_size);
      END;

    | RTTipe.Record(r) =&gt;
      WITH r2 = NARROW(toTipe, RTTipe.Record) DO
        self.buildFields(r.fields, r.size, r2.fields, r2.size);
      END;

    | RTTipe.Ref(r) =&gt;
      IF r.traced THEN
        self.addRef(RefType.Ref);
      ELSE
        self.addRef(RefType.UntracedRef);
      END;
    | RTTipe.OpenArray =&gt; RAISE Error(&quot;OpenArray within BuildOne?&quot;);
    ELSE
    END;
  END BuildOne;

PROCEDURE <A NAME="BuildFields"><procedure>BuildFields</procedure></A>(self: T;
                      fromField: RTTipe.Field; fromSize: INTEGER;
                      toField: RTTipe.Field; toSize: INTEGER)
    RAISES {Error} =
 VAR fromOffset := self.fromOffset;
     toOffset := self.toOffset;
  BEGIN
    WHILE fromField # NIL DO
      &lt;* ASSERT fromField.type.kind = toField.type.kind *&gt;
      (* Check that the last field advanced the data pointers
         correctly. *)
      WITH fromDiff = fromOffset + fromField.offset - self.fromOffset,
           toDiff = toOffset + toField.offset - self.toOffset DO
        &lt;* ASSERT fromDiff &gt; -8 *&gt;
        &lt;* ASSERT toDiff &gt; -8 *&gt;
        IF fromDiff &lt; 0 THEN
          (* A data difference &lt; 0 is ok if this field is packed *)
          &lt;* ASSERT fromField.type.kind = RTTipe.Kind.Packed *&gt;
          &lt;* ASSERT toField.type.kind = RTTipe.Kind.Packed *&gt;
          CASE self.wordKind OF
          | Kind.Copy, Kind.Copy32to64, Kind.Copy64to32 =&gt;
            (* If this field overflows the current packed byte, copy
               the next ones as required. *)
            WITH spaceNeeded = fromField.type.size + fromDiff DO
              IF spaceNeeded &gt; 0 THEN
                WITH bytesNeeded = RoundUp(spaceNeeded, 8) DO
                  self.addCopy(bytesNeeded);
                END;
              END;
            END;
          | Kind.Swap, Kind.Swap32to64, Kind.Swap64to32 =&gt;
            self.addPackedSwapNextField(fromField.type.size,
                                        fromField.offset);
          END;
        ELSE
          (* If we need to add a skip, do it *)
          self.addSkip(fromDiff, toDiff);

          IF fromField.type.kind = RTTipe.Kind.Packed THEN
            (* We can ignore the word size here, size the size is
               explicit in the packing and will be the same on both
               machines, so no conversion is required. *)
            &lt;* ASSERT fromField.type.size = toField.type.size *&gt;
            &lt;* ASSERT toField.type.kind = RTTipe.Kind.Packed *&gt;
            CASE self.wordKind OF
            | Kind.Copy, Kind.Copy32to64, Kind.Copy64to32 =&gt;
              WITH bytesNeeded = RoundUp(fromField.type.size, 8) DO
                self.addCopy(bytesNeeded);
              END;
            | Kind.Swap, Kind.Swap32to64, Kind.Swap64to32 =&gt;
              &lt;* ASSERT fromField.offset MOD 8 = 0 *&gt;
              self.addPackedSwapFirstField(fromField.type.size);
            END;
          ELSIF fromField.type.kind = RTTipe.Kind.Object THEN
            (* Treat objects, when in a field, as references.
               Otherwise we get an infinite loop trying to expand things. *)
            self.addRef(RefType.Ref);
          ELSE
            self.buildOne(fromField.type, toField.type);
          END;
        END;
      END;

      fromField := fromField.next;
      toField := toField.next;
    END;

    (* Now that it's done, check if there is any space after the last
       field that needs to be accounted for. *)
    WITH fromDiff = fromOffset + fromSize - self.fromOffset,
         toDiff = toOffset + toSize - self.toOffset DO
      &lt;* ASSERT fromDiff &gt;= 0 *&gt;
      &lt;* ASSERT toDiff &gt;= 0 *&gt;
      self.addSkip(fromDiff, toDiff);
    END;
  END BuildFields;
</PRE>**************************************************************
 * For printing 
 **************************************************************

<P><PRE>PROCEDURE <A NAME="PrintPacking"><procedure>PrintPacking</procedure></A>(packing: RTPacking.T) =
  BEGIN
    IO.Put(&quot; word_size: &quot; &amp; Fmt.Unsigned(packing.word_size, 10) &amp; &quot;\n&quot;);
    IO.Put(&quot; max_align: &quot; &amp; Fmt.Unsigned(packing.max_align, 10) &amp; &quot;\n&quot;);
    IO.Put(&quot; struct_align: &quot; &amp; Fmt.Unsigned(packing.struct_align, 10) &amp; &quot;\n&quot;);
    CASE packing.float OF
    | RTPacking.FloatKind.IEEE =&gt;
      IO.Put(&quot; float_kind: IEEE\n&quot;);
    | RTPacking.FloatKind.VAX =&gt;
      IO.Put(&quot; float_kind: VAX\n&quot;);
    | RTPacking.FloatKind.other =&gt;
      IO.Put(&quot; float_kind: other\n&quot;);
    END;
    IO.Put(&quot; little_endian: &quot; &amp; Fmt.Bool(packing.little_endian) &amp; &quot;\n&quot;);
  END PrintPacking;

PROCEDURE <A NAME="KindToText"><procedure>KindToText</procedure></A>(kind: RTTipe.Kind) =
  BEGIN
    CASE kind OF
    | RTTipe.Kind.Address =&gt; IO.Put( &quot;Address&quot;);
    | RTTipe.Kind.Array =&gt; IO.Put( &quot;Array&quot;);
    | RTTipe.Kind.Boolean =&gt; IO.Put( &quot;Boolean&quot;);
    | RTTipe.Kind.Cardinal =&gt; IO.Put( &quot;Cardinal&quot;);
    | RTTipe.Kind.Char =&gt; IO.Put( &quot;Char&quot;);
    | RTTipe.Kind.Enum =&gt; IO.Put( &quot;Enum&quot;);
    | RTTipe.Kind.Extended =&gt; IO.Put( &quot;Extended&quot;);
    | RTTipe.Kind.Integer =&gt; IO.Put( &quot;Integer&quot;);
    | RTTipe.Kind.Longcard =&gt; IO.Put( &quot;Longcard&quot;);
    | RTTipe.Kind.Longint =&gt; IO.Put( &quot;Longint&quot;);
    | RTTipe.Kind.Longreal =&gt; IO.Put( &quot;Longreal&quot;);
    | RTTipe.Kind.Null =&gt; IO.Put( &quot;Null&quot;);
    | RTTipe.Kind.Object =&gt; IO.Put( &quot;Object&quot;);
    | RTTipe.Kind.OpenArray =&gt; IO.Put( &quot;OpenArray&quot;);
    | RTTipe.Kind.Packed =&gt; IO.Put( &quot;Packed&quot;);
    | RTTipe.Kind.Proc =&gt; IO.Put( &quot;Proc&quot;);
    | RTTipe.Kind.Real =&gt; IO.Put( &quot;Real&quot;);
    | RTTipe.Kind.Record =&gt; IO.Put( &quot;Record&quot;);
    | RTTipe.Kind.Ref =&gt; IO.Put( &quot;Ref&quot;);
    | RTTipe.Kind.Refany =&gt; IO.Put( &quot;Refany&quot;);
    | RTTipe.Kind.Set =&gt; IO.Put( &quot;Set&quot;);
    | RTTipe.Kind.Subrange =&gt; IO.Put( &quot;Subrange&quot;);
    | RTTipe.Kind.UntracedRef =&gt; IO.Put( &quot;UntracedRef&quot;);
    END;
  END KindToText;

PROCEDURE <A NAME="BasicTipeToText"><procedure>BasicTipeToText</procedure></A>(tipe: RTTipe.T) =
  BEGIN
    IO.Put( &quot;kind: &quot; );
    KindToText(tipe.kind);
    IO.Put( &quot;, size: &quot; &amp; Fmt.Int(tipe.size DIV 8) &amp; &quot;+&quot; &amp;
      Fmt.Int(tipe.size MOD 8) &amp; &quot;, align: &quot; &amp; Fmt.Int(tipe.align));
  END BasicTipeToText;

PROCEDURE <A NAME="FieldsToText"><procedure>FieldsToText</procedure></A>(f: RTTipe.Field; pre: TEXT;
                       packing: RTPacking.T) =
  BEGIN
    IF f = NIL THEN
      IO.Put( pre &amp; &quot;&lt;NO FIELDS&gt;&quot;);
      RETURN;
    END;
    IO.Put(pre &amp; &quot;offset: &quot; &amp; Fmt.Int(f.offset DIV 8) &amp; &quot;+&quot; &amp;
        Fmt.Int(f.offset MOD 8) &amp; &quot;\n&quot;);
    TipeToText(f.type, &quot;  &quot; &amp; pre, packing);
    f := f.next;
    WHILE f # NIL DO
      IO.Put(&quot;\n&quot; &amp; pre &amp; &quot;offset: &quot; &amp; Fmt.Int(f.offset DIV 8) &amp; &quot;+&quot; &amp;
        Fmt.Int(f.offset MOD 8) &amp; &quot;\n&quot;);
      TipeToText(f.type, &quot;  &quot; &amp; pre, packing);
      f := f.next;
    END;
  END FieldsToText;

CONST HexDigits = 2*ADRSIZE(INTEGER);

PROCEDURE <A NAME="DoubleIntToText"><procedure>DoubleIntToText</procedure></A>(Val: RTTipe.DoubleInt):TEXT =
  BEGIN (* Just do it in hex--it's easier. *)
    IF Val.Lo &gt;= 0 AND Val.Hi # 0 OR Val.Lo &lt; 0 AND Val.Hi # -1
    THEN (* Hi is not just a sign extension. *)
      RETURN &quot;16_&quot;
             &amp; Fmt.Pad(Fmt.Unsigned(Val.Hi,16),HexDigits,'0')
             &amp; Fmt.Pad(Fmt.Unsigned(Val.Lo,16),HexDigits,'0');
    ELSE
      RETURN &quot;16_&quot; &amp; Fmt.Pad(Fmt.Unsigned(Val.Lo,16),HexDigits,'0');
    END;
  END DoubleIntToText;

PROCEDURE <A NAME="TipeToText"><procedure>TipeToText</procedure></A>(tipe: RTTipe.T; pre: TEXT; packing: RTPacking.T) =
  BEGIN
    IF tipe = NIL THEN
      IO.Put(pre &amp; &quot;&lt;NIL&gt;&quot;);
      RETURN;
    END;
    IO.Put(pre);
    BasicTipeToText(tipe);

    TYPECASE tipe OF
    | RTTipe.Builtin =&gt;
      IO.Put(&quot; (Builtin)&quot;);
    | RTTipe.Array(a) =&gt;
      IO.Put(&quot; (packed elements &quot; &amp; Fmt.Int(a.elt_pack) &amp; &quot;) [1..&quot; &amp;
             Fmt.Int(a.n_elts) &amp; &quot;] of \n&quot;);
      TipeToText(a.element, pre &amp; &quot;  &quot;, packing);
    | RTTipe.Enum(e) =&gt;
      IO.Put(&quot; of &quot; &amp; Fmt.Int(e.n_elts) &amp; &quot; elements&quot;);
    | RTTipe.Object(o) =&gt;
      IO.Put(&quot;:\n&quot; &amp; pre &amp; &quot;  Super Type = &quot;);
      IF o.super # NIL THEN
        IO.Put(&quot;typecode &quot; &amp; Fmt.Int(o.super.typecode) &amp; &quot;\n&quot;);
        TipeToText(RTTipe.Get(o.super.typecode, packing),
                   pre &amp; &quot;    &quot;, packing);
      ELSE
        IO.Put(&quot; &lt;NIL&gt; type?\n&quot;);
      END;
      IO.Put(&quot;\n&quot; &amp; pre &amp; &quot;  Fields.  size: &quot; &amp;
        Fmt.Int(o.field_size DIV 8) &amp; &quot;+&quot; &amp;
        Fmt.Int(o.field_size MOD 8) &amp; &quot;, align: &quot; &amp;
        Fmt.Int(o.field_align) &amp; &quot;\n&quot;);
      FieldsToText(o.fields, &quot;    &quot; &amp; pre, packing);
    | RTTipe.OpenArray(oa) =&gt;
      IO.Put(&quot; in &quot; &amp; Fmt.Int(oa.n_dimensions) &amp;
        &quot; dimensions, packed &quot; &amp; Fmt.Int(oa.elt_pack) &amp; &quot; of \n&quot;);
      TipeToText(oa.element, pre &amp; &quot;  &quot;, packing);
    | RTTipe.Packed(p) =&gt;
      IO.Put(&quot; in &quot; &amp; Fmt.Int(p.n_bits) &amp; &quot; bits of\n&quot;);
      TipeToText(p.base, &quot;  &quot; &amp; pre, packing);
    | RTTipe.Record(r) =&gt;
      IO.Put(&quot; of:\n&quot;);
      FieldsToText(r.fields, &quot;  &quot; &amp; pre, packing);
    | RTTipe.Ref(r) =&gt;
      IO.Put(&quot; (traced: &quot; &amp; Fmt.Bool(r.traced) &amp; &quot;, uid: &quot; &amp;
             Fmt.Int(r.uid) &amp; &quot;) to &quot;);
      IF r.self # NIL THEN
        IO.Put(&quot;typecode &quot; &amp; Fmt.Int(r.self.typecode) &amp; &quot;\n&quot;);
      ELSE
        IO.Put(&quot;&lt;NIL&gt; type?\n&quot;);
      END;
    | RTTipe.Set(s) =&gt;
      IO.Put(&quot; of &quot; &amp; Fmt.Int(s.n_elts) &amp; &quot; elements&quot;);
    | RTTipe.Subrange(s) =&gt;
      IO.Put(&quot; from &quot; &amp; DoubleIntToText(s.min)
             &amp; &quot; to &quot; &amp; DoubleIntToText(s.max)
            );
    ELSE
    END;
  END TipeToText;

PROCEDURE <A NAME="Print"><procedure>Print</procedure></A>(self: T) =
  BEGIN
    IO.Put(&quot;Converting from packing:\n&quot;);
    PrintPacking(self.from);
    IO.Put(&quot;to packing:\n&quot;);
    PrintPacking(self.to);
    IO.Put(&quot;Converting from RTTipe:\n&quot;);
    TipeToText(self.fromTipe, &quot;  &quot;, self.from);
    IO.Put(&quot;\nto RTTipe:\n&quot;);
    TipeToText(self.toTipe, &quot;  &quot;, self.to);
    self.printProgram();
  END Print;

PROCEDURE <A NAME="PrintProgram"><procedure>PrintProgram</procedure></A>(self: T) =
  VAR fromSize := 0;
      toSize := 0;
  BEGIN
    IO.Put(&quot;\nDoing it in &quot; &amp; Fmt.Int(self.prog.size()) &amp; &quot; step(s):\n&quot;);
    FOR i := 0 TO self.prog.size() - 1 DO
      WITH elem = self.prog.get(i) DO
        CASE elem.kind OF
        | PklAction.Kind.Copy =&gt;
          IO.Put(&quot; Copy &quot; &amp; Fmt.Int(elem.length) &amp; &quot; byte(s).\n&quot;);
          INC(fromSize, elem.length);
          INC(toSize, elem.length);
        | PklAction.Kind.SwapPacked =&gt;
          WITH nelem = NARROW(elem, PklAction.SwapPacked) DO
            IO.Put(&quot; Copy and swap &quot; &amp; Fmt.Int(elem.length) &amp;
              &quot; unit(s) of &quot; &amp; Fmt.Int(nelem.size) &amp;
              &quot; byte(s) with packed bitfields: &quot;);
            FOR i := FIRST(nelem.field^) TO LAST(nelem.field^) DO
              IO.Put(Fmt.Int(nelem.field[i]) &amp; &quot; &quot;);
            END;
          END;
          IO.Put(&quot;\n&quot;);
          INC(fromSize, elem.length);
          INC(toSize, elem.length);
        | PklAction.Kind.SkipFrom =&gt;
          IO.Put(&quot; Skip &quot; &amp; Fmt.Int(elem.length) &amp; &quot; src byte(s).\n&quot;);
          INC(fromSize, elem.length);
        | PklAction.Kind.SkipTo =&gt;
          IO.Put(&quot; Skip &quot; &amp; Fmt.Int(elem.length) &amp; &quot; dst byte(s).\n&quot;);
          INC(toSize, elem.length);
        | PklAction.Kind.Skip =&gt;
          IO.Put(&quot; Skip &quot; &amp; Fmt.Int(elem.length) &amp; &quot; src and dst byte(s).\n&quot;);
          INC(fromSize, elem.length);
          INC(toSize, elem.length);
        | PklAction.Kind.Swap16 =&gt;
          IO.Put(&quot; Swap &quot; &amp; Fmt.Int(elem.length) &amp; &quot; 16-bit word(s).\n&quot;);
          INC(fromSize, elem.length*2);
          INC(toSize, elem.length*2);
        | PklAction.Kind.Swap32 =&gt;
          IO.Put(&quot; Swap &quot; &amp; Fmt.Int(elem.length) &amp; &quot; 32-bit word(s).\n&quot;);
          INC(fromSize, elem.length*4);
          INC(toSize, elem.length*4);
        | PklAction.Kind.Swap64 =&gt;
          IO.Put(&quot; Swap &quot; &amp; Fmt.Int(elem.length) &amp; &quot; 64-bit word(s).\n&quot;);
          INC(fromSize, elem.length*8);
          INC(toSize, elem.length*8);
        | PklAction.Kind.Copy32to64 =&gt;
          IO.Put(&quot; Copy &quot; &amp; Fmt.Int(elem.length) &amp; &quot; 32 to 64-bit &quot;);
          WITH nelem = NARROW(elem, PklAction.Copy32to64) DO
            IF nelem.signed THEN
              IO.Put(&quot;signed &quot;);
            ELSE
              IO.Put(&quot;unsigned &quot;);
            END;
          END;
          IO.Put(&quot;word(s).\n&quot;);
          INC(fromSize, elem.length*4);
          INC(toSize, elem.length*8);
        | PklAction.Kind.Copy64to32 =&gt;
          IO.Put(&quot; Copy &quot; &amp; Fmt.Int(elem.length) &amp; &quot; 64 to 32-bit &quot;);
          WITH nelem = NARROW(elem, PklAction.Copy32to64) DO
            IF nelem.signed THEN
              IO.Put(&quot;signed &quot;);
            ELSE
              IO.Put(&quot;unsigned &quot;);
            END;
          END;
          IO.Put(&quot;word(s).\n&quot;);
          INC(fromSize, elem.length*8);
          INC(toSize, elem.length*4);
        | PklAction.Kind.Swap32to64 =&gt;
          IO.Put(&quot; Copy and Swap &quot; &amp; Fmt.Int(elem.length) &amp; &quot; 32 to 64-bit &quot;);
          WITH nelem = NARROW(elem, PklAction.Copy32to64) DO
            IF nelem.signed THEN
              IO.Put(&quot;signed &quot;);
            ELSE
              IO.Put(&quot;unsigned &quot;);
            END;
          END;
          IO.Put(&quot;word(s).\n&quot;);
          INC(fromSize, elem.length*4);
          INC(toSize, elem.length*8);
        | PklAction.Kind.Swap64to32 =&gt;
          IO.Put(&quot; Copy and Swap &quot; &amp; Fmt.Int(elem.length) &amp; &quot; 64 to 32-bit &quot;);
          WITH nelem = NARROW(elem, PklAction.Copy32to64) DO
            IF nelem.signed THEN
              IO.Put(&quot;signed &quot;);
            ELSE
              IO.Put(&quot;unsigned &quot;);
            END;
          END;
          IO.Put(&quot;word(s).\n&quot;);
          INC(fromSize, elem.length*8);
          INC(toSize, elem.length*4);
        | PklAction.Kind.ReadRef =&gt;
          IO.Put(&quot; Read &quot; &amp; Fmt.Int(elem.length));
          WITH nelem = NARROW(elem, PklAction.Ref) DO
            CASE nelem.refType OF
            | PklAction.RefType.Ref =&gt; IO.Put(&quot; traced&quot;);
            | PklAction.RefType.UntracedRef =&gt; IO.Put(&quot; untraced&quot;);
            | PklAction.RefType.Proc =&gt; IO.Put(&quot; procedure&quot;);
            END;
          END;
          IO.Put(&quot; references(s).\n&quot;);
          INC(fromSize, elem.length*(self.from.word_size DIV 8));
          INC(toSize, elem.length*(self.to.word_size DIV 8));
        | PklAction.Kind.Done =&gt;
          IO.Put(&quot;Copied &quot; &amp; Fmt.Int(fromSize) &amp; &quot; bytes to &quot; &amp;
            Fmt.Int(toSize) &amp; &quot;\n&quot;);
        END;
      END;
    END;
  END PrintProgram;

BEGIN
  (* This will only work on machines with 8 bit chars right now. *)
  &lt;* ASSERT BITSIZE(CHAR) = 8 *&gt;
END ConvertPacking.
</PRE>
</inModule>
<PRE>























</PRE>
</BODY>
</HTML>
