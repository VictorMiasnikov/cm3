<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: libm3/src/os/POSIX/FilePosix.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>libm3/src/os/POSIX/FilePosix.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright (C) 1993, Digital Equipment Corporation           
 All rights reserved.                                        
 See the file COPYRIGHT for a full description.              
 Last modified on Thu Mar 30 14:12:11 PST 1995 by mcjones    
      modified on Sun Jan  8 14:13:46 PST 1995 by kalsow     
      modified on Wed Mar 16 14:33:40 PST 1994 by wobber     
      modified on Mon Feb  8 12:35:46 PST 1993 by mjordan    

<P><PRE>UNSAFE MODULE <module><implements><A HREF="FilePosix.i3.html">FilePosix</A></implements></module>;

IMPORT <A HREF="../../../../m3core/src/C/Common/Cerrno.i3.html">Cerrno</A>, <A HREF="../../../../m3core/src/C/Common/Ctypes.i3.html">Ctypes</A>, <A HREF="../Common/File.i3.html">File</A>, <A HREF="../Common/FS.i3.html">FS</A>, <A HREF="../../../../m3core/src/C/Common/M3toC.i3.html">M3toC</A>, <A HREF="../Common/OSError.i3.html">OSError</A>, <A HREF="OSErrorPosix.i3.html">OSErrorPosix</A>, <A HREF="../Common/Pipe.i3.html">Pipe</A>,
  <A HREF="../Common/RegularFile.i3.html">RegularFile</A>, <A HREF="../../../../m3core/src/thread/Common/SchedulerPosix.i3.html">SchedulerPosix</A>, <A HREF="../Common/Terminal.i3.html">Terminal</A>, <A HREF="#x1">Uerror</A>, <A HREF="#x2">Unix</A>, <A HREF="../../../../m3core/src/unix/Common/Ustat.i3.html">Ustat</A>, <A HREF="#x3">Uuio</A>,
  <A HREF="../../../../m3core/src/word/Word.i3.html">Word</A>, <A HREF="#x4">Utypes</A>, <A HREF="FilePosixC.i3.html">FilePosixC</A>;

REVEAL
  File.<A NAME="T">T</A> = T BRANDED OBJECT OVERRIDES
    close := FileClose;
    status := FileStatus
  END;

TYPE IntermittentFile = File.T BRANDED OBJECT OVERRIDES
    read := IntermittentRead;
    write := IntermittentWrite
  END;

REVEAL
  Pipe.<A NAME="T">T</A> = IntermittentFile BRANDED OBJECT END;
  Terminal.<A NAME="T">T</A> = IntermittentFile BRANDED OBJECT END;
  RegularFile.<A NAME="T">T</A> = RegularFile.Public BRANDED OBJECT OVERRIDES
    read := RegularFileRead;
    write := RegularFileWrite;
    seek := RegularFileSeek;
    flush := RegularFileFlush;
    lock := RegularFileLock;
    unlock := RegularFileUnlock
  END;

PROCEDURE <A NAME="FileTypeFromStatbuf"><procedure>FileTypeFromStatbuf</procedure></A>(READONLY statbuf: Ustat.struct_stat)
  : File.Type =
  BEGIN
    WITH type = Word.And(statbuf.st_mode, Ustat.S_IFMT) DO
      IF type = Ustat.S_IFCHR THEN
        IF IsDevNull(statbuf)
          THEN RETURN RegularFile.FileType
          ELSE RETURN Terminal.FileType
        END;
      ELSIF (type = Ustat.S_IFIFO) OR (type = Ustat.S_IFSOCK) THEN
        RETURN Pipe.FileType;
      ELSIF type = Ustat.S_IFREG THEN
        RETURN RegularFile.FileType
      ELSIF type = Ustat.S_IFDIR THEN
        RETURN FS.DirectoryFileType
      ELSE
        RETURN RegularFile.FileType
      END
    END;
  END FileTypeFromStatbuf;

PROCEDURE <A NAME="New"><procedure>New</procedure></A>(fd: INTEGER; ds: DirectionSet): File.T RAISES {OSError.E} =
  VAR statbuf: Ustat.struct_stat; type: File.Type;
  BEGIN
    IF Ustat.fstat(fd, ADR(statbuf)) # 0 THEN OSErrorPosix.Raise() END;
    type := FileTypeFromStatbuf(statbuf);
    IF type = RegularFile.FileType THEN
      RETURN NEW(RegularFile.T, fd := fd, ds := ds)
    END;
    IF type = Terminal.FileType THEN
      RETURN NEW(Terminal.T, fd := fd, ds := ds)
    END;
    IF type = Pipe.FileType THEN
      RETURN NEW(Pipe.T, fd := fd, ds := ds)
    END;
    IF type = FS.DirectoryFileType THEN
      OSErrorPosix.Raise0(Uerror.EISDIR)
    END;
    (* Other... *)
    RETURN NEW(RegularFile.T, fd := fd, ds := ds)
  END New;

PROCEDURE <A NAME="NewPipe"><procedure>NewPipe</procedure></A>(fd: INTEGER; ds: DirectionSet): Pipe.T =
  BEGIN
    RETURN NEW(Pipe.T, fd := fd, ds := ds)
  END NewPipe;
</PRE>---------------------------File methods------------------------------------

<P><PRE>PROCEDURE <A NAME="FileClose"><procedure>FileClose</procedure></A>(h: File.T) RAISES {OSError.E} =
  BEGIN
    IF Unix.close(h.fd) &lt; 0 THEN OSErrorPosix.Raise() END
  END FileClose;

PROCEDURE <A NAME="FileStatus"><procedure>FileStatus</procedure></A>(h: File.T): File.Status RAISES {OSError.E} =
  VAR statBuf: Ustat.struct_stat;
    status: File.Status;
  BEGIN
    IF Ustat.fstat(h.fd, ADR(statBuf)) &lt; 0 THEN OSErrorPosix.Raise() END;
    TYPECASE h OF
    | RegularFile.T =&gt; status.type := RegularFile.FileType
    | Pipe.T =&gt; status.type := Pipe.FileType
    | Terminal.T =&gt; status.type := Terminal.FileType
    ELSE &lt;* ASSERT FALSE *&gt;
    END;
    status.modificationTime := FLOAT(statBuf.st_mtime, LONGREAL);
    WITH size = statBuf.st_size DO
      IF size &lt; 0L THEN OSErrorPosix.Raise() END;
      status.size := size;
    END;
    RETURN status
  END FileStatus;
</PRE>---------------------------RegularFile methods-----------------------------

<P><PRE>PROCEDURE <A NAME="RegularFileRead"><procedure>RegularFileRead</procedure></A>(
    h: RegularFile.T;
    VAR (*OUT*) b: ARRAY OF File.Byte;
    &lt;*UNUSED*&gt; mayBlock: BOOLEAN := TRUE)
  : INTEGER RAISES {OSError.E} =
  VAR p_b: ADDRESS := ADR(b[0]);
  BEGIN
    IF NOT(Direction.Read IN h.ds) THEN BadDirection(); END;
    WITH bytesRead = Uuio.read(h.fd, p_b, NUMBER(b)) DO
      IF bytesRead &lt; 0 THEN OSErrorPosix.Raise() END;
      RETURN bytesRead
    END
  END RegularFileRead;

PROCEDURE <A NAME="RegularFileWrite"><procedure>RegularFileWrite</procedure></A>(
    h: RegularFile.T;
    READONLY b: ARRAY OF File.Byte)
  RAISES {OSError.E} =
  VAR
    p_b: ADDRESS := ADR(b[0]);
    bytes := NUMBER(b);
    bytesWritten: INTEGER;  BEGIN
    IF NOT(Direction.Write IN h.ds) THEN BadDirection(); END;
    LOOP
      bytesWritten := Uuio.write(h.fd, p_b, bytes);
      IF bytesWritten &lt; 0 THEN OSErrorPosix.Raise() END;
      (* Partial write if media is full, quota exceeded, etc. *)
      IF bytesWritten = bytes THEN EXIT END;
      &lt;* ASSERT bytesWritten &gt; 0 *&gt;
      INC(p_b, bytesWritten);
      DEC(bytes, bytesWritten);
    END
  END RegularFileWrite;

PROCEDURE <A NAME="RegularFileSeek"><procedure>RegularFileSeek</procedure></A>(
    h: RegularFile.T; origin: RegularFile.Origin; offset: INTEGER)
  : INTEGER RAISES {OSError.E} =
  BEGIN
    WITH result = Unix.lseek(h.fd, VAL(offset, Utypes.off_t), ORD(origin)) DO
      IF result &lt; VAL(0, Utypes.off_t) THEN OSErrorPosix.Raise() END;
      RETURN VAL(result, INTEGER)
    END
  END RegularFileSeek;

PROCEDURE <A NAME="RegularFileFlush"><procedure>RegularFileFlush</procedure></A>(h: RegularFile.T) RAISES {OSError.E} =
  BEGIN
    IF Unix.fsync(h.fd) &lt; 0 THEN OSErrorPosix.Raise() END
  END RegularFileFlush;

PROCEDURE <A NAME="RegularFileLock"><procedure>RegularFileLock</procedure></A>(h: RegularFile.T): BOOLEAN RAISES {OSError.E} =
  VAR i: INTEGER;
  BEGIN
    i := FilePosixC.RegularFileLock(h.fd);
    IF i &lt; 0 THEN
      OSErrorPosix.Raise();
    END;
    RETURN (i # 0);
  END RegularFileLock;

PROCEDURE <A NAME="RegularFileUnlock"><procedure>RegularFileUnlock</procedure></A>(h: RegularFile.T) RAISES {OSError.E} =
  VAR i: INTEGER;
  BEGIN
    i := FilePosixC.RegularFileUnlock(h.fd);
    IF i &lt; 0 THEN
      OSErrorPosix.Raise();
    END;
  END RegularFileUnlock;
</PRE>---------------------IntermittentFile methods------------------------------

<P><PRE>PROCEDURE <A NAME="IntermittentRead"><procedure>IntermittentRead</procedure></A>(
    h: IntermittentFile;
    VAR (*OUT*) b: ARRAY OF File.Byte;
    mayBlock := TRUE)
  : INTEGER RAISES {OSError.E} =
  VAR
    status, errno: INTEGER;
    old_mode := Unix.fcntl(h.fd, Unix.F_GETFL, 0);
    new_mode := Word.Or(old_mode, Unix.M3_NONBLOCK);
    p_b: ADDRESS := ADR (b[0]);
  BEGIN
    IF NOT(Direction.Read IN h.ds) THEN BadDirection(); END;

    LOOP
      (* Make the read call non-blocking; we cannot set/reset the mode
         at creation/close time, because this may leave the file in an
         unexpected state in the case of a core dump elsewhere. *)

      IF Unix.fcntl(h.fd, Unix.F_SETFL, new_mode) # 0 THEN
        OSErrorPosix.Raise()
      END;

      status := Uuio.read(h.fd, p_b, NUMBER(b));
      errno := Cerrno.GetErrno();

      IF Unix.fcntl(h.fd, Unix.F_SETFL, old_mode) # 0 THEN
        OSErrorPosix.Raise()
      END;

      IF status &gt;= 0 THEN
        RETURN status
      ELSIF (status = -1)
         AND (errno # Uerror.EWOULDBLOCK)
         AND (errno # Uerror.EAGAIN) THEN
        OSErrorPosix.Raise0(errno)
      ELSIF NOT mayBlock THEN
        RETURN -1
      END;

      EVAL SchedulerPosix.IOWait(h.fd, TRUE)
    END
  END IntermittentRead;

PROCEDURE <A NAME="IntermittentWrite"><procedure>IntermittentWrite</procedure></A>(h: File.T; READONLY b: ARRAY OF File.Byte)
  RAISES {OSError.E} =
  VAR
    status, errno: INTEGER;
    old_mode := Unix.fcntl(h.fd, Unix.F_GETFL, 0);
    new_mode := Word.Or(old_mode, Unix.M3_NONBLOCK);
    p := LOOPHOLE (ADR(b[0]), Ctypes.char_star);
    n: Ctypes.int := NUMBER(b);
  BEGIN
    IF NOT(Direction.Write IN h.ds) THEN BadDirection (); END;

    LOOP
      (* Make the write call non-blocking; we cannot set/reset the mode
         at creation/close time, because this may leave the file in an
         unexpected state in the case of a core dump elsewhere. *)

      IF Unix.fcntl(h.fd, Unix.F_SETFL, new_mode) # 0 THEN
        OSErrorPosix.Raise()
      END;

      status := Uuio.write(h.fd, p, n);
      errno := Cerrno.GetErrno();

      IF Unix.fcntl(h.fd, Unix.F_SETFL, old_mode) # 0 THEN
        OSErrorPosix.Raise()
      END;

      IF status &gt;= 0 THEN
        p := LOOPHOLE(LOOPHOLE(p, INTEGER) + status, Ctypes.char_star);
        n := n - status;
        IF n = 0 THEN EXIT END
      ELSIF (status = -1)
         AND (errno # Uerror.EWOULDBLOCK)
         AND (errno # Uerror.EAGAIN) THEN
        OSErrorPosix.Raise0(errno)
      END;

      EVAL SchedulerPosix.IOWait(h.fd, FALSE)
    END
  END IntermittentWrite;
</PRE>-------------------------Support procedures--------------------------------

<P><PRE>VAR
  null_done := FALSE;
  null_stat: Ustat.struct_stat;
  null_fd: INTEGER;

PROCEDURE <A NAME="IsDevNull"><procedure>IsDevNull</procedure></A>(READONLY statbuf: Ustat.struct_stat): BOOLEAN RAISES {} =
  VAR result: INTEGER;
  BEGIN
    IF NOT null_done THEN
      null_fd := Unix.open(M3toC.FlatTtoS(&quot;/dev/null&quot;), Unix.O_RDONLY, Unix.Mrwrwrw);
      IF null_fd &lt; 0 THEN
        null_done := TRUE;
        RETURN FALSE
      ELSE
        result := Ustat.fstat(null_fd, ADR(null_stat));
        EVAL Unix.close(null_fd);
        IF result # 0 THEN
          null_fd := -1
        END
      END;
      null_done := TRUE;
    END;
    RETURN null_fd &gt;= 0 AND statbuf.st_rdev = null_stat.st_rdev
  END IsDevNull;

EXCEPTION IllegalDirection;

PROCEDURE <A NAME="BadDirection"><procedure>BadDirection</procedure></A> () =
  &lt;*FATAL IllegalDirection*&gt;
  BEGIN
    RAISE IllegalDirection;
  END BadDirection;

BEGIN
END FilePosix.
</PRE>
</inModule>
<HR>
<A NAME="x1">interface Uerror is in:
</A><UL>
<LI><A HREF="../../../../m3core/src/unix/Common/Uerror.i3.html#0TOP0">m3core/src/unix/Common/Uerror.i3</A>
<LI><A HREF="../../../../m3core/src/unix/linux-common/Uerror.i3.html#0TOP0">m3core/src/unix/linux-common/Uerror.i3</A>
</UL>
<P>
<HR>
<A NAME="x2">interface Unix is in:
</A><UL>
<LI><A HREF="../../../../m3core/src/unix/Common/Unix.i3.html#0TOP0">m3core/src/unix/Common/Unix.i3</A>
<LI><A HREF="../../../../m3core/src/unix/linux-common/Unix.i3.html#0TOP0">m3core/src/unix/linux-common/Unix.i3</A>
</UL>
<P>
<HR>
<A NAME="x3">interface Uuio is in:
</A><UL>
<LI><A HREF="../../../../m3core/src/unix/Common/Uuio.i3.html#0TOP0">m3core/src/unix/Common/Uuio.i3</A>
<LI><A HREF="../../../../m3core/src/unix/linux-common/Uuio.i3.html#0TOP0">m3core/src/unix/linux-common/Uuio.i3</A>
</UL>
<P>
<HR>
<A NAME="x4">interface Utypes is in:
</A><UL>
<LI><A HREF="../../../../m3core/src/unix/Common/Utypes.i3.html#0TOP0">m3core/src/unix/Common/Utypes.i3</A>
<LI><A HREF="../../../../m3core/src/unix/linux-common/Utypes.i3.html#0TOP0">m3core/src/unix/linux-common/Utypes.i3</A>
</UL>
<P>
<PRE>























</PRE>
</BODY>
</HTML>
