<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: libm3/src/os/POSIX/ProcessPosixCommon.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>libm3/src/os/POSIX/ProcessPosixCommon.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright (C) 1993, Digital Equipment Corporation           
 All rights reserved.                                        
 See the file COPYRIGHT for a full description.              
 Last modified on Thu Mar 30 14:54:37 PST 1995 by mcjones    
      modified on Wed Jun 22 16:45:37 PDT 1994 by kalsow     
      modified on Mon Feb 22 11:41:21 PST 1993 by mjordan    

<P> common code for Posix and Cygwin 

<P><PRE>UNSAFE MODULE <module>ProcessPosixCommon</module> EXPORTS <A HREF="ProcessPosixCommon.i3.html"><implements>ProcessPosixCommon</A></implements>, <A HREF="../Common/Process.i3.html"><implements>Process</A></implements>;

IMPORT <A HREF="../../atom/Atom.i3.html">Atom</A>, <A HREF="../../../derived/AtomList.i3.html">AtomList</A>, <A HREF="../../../../m3core/src/C/Common/Cerrno.i3.html">Cerrno</A>, <A HREF="../../../../m3core/src/C/Common/Ctypes.i3.html">Ctypes</A>, <A HREF="../../params/Env.i3.html">Env</A>, <A HREF="../Common/File.i3.html">File</A>, <A HREF="FilePosix.i3.html">FilePosix</A>, <A HREF="../../../../m3core/src/C/Common/M3toC.i3.html">M3toC</A>, <A HREF="../Common/OSError.i3.html">OSError</A>,
  <A HREF="OSErrorPosix.i3.html">OSErrorPosix</A>, <A HREF="../Common/Pathname.i3.html">Pathname</A>, <A HREF="../../../../m3core/src/runtime/common/RTLinker.i3.html">RTLinker</A>, <A HREF="../../../../m3core/src/runtime/common/RTProcess.i3.html">RTProcess</A>, <A HREF="../../../../m3core/src/runtime/common/RTSignal.i3.html">RTSignal</A>,
  <A HREF="../../../../m3core/src/thread/Common/Scheduler.i3.html">Scheduler</A>, <A HREF="../../../../m3core/src/text/Text.i3.html">Text</A>, <A HREF="../../../../m3core/src/thread/Common/SchedulerPosix.i3.html">SchedulerPosix</A>, <A HREF="#x1">Unix</A>, <A HREF="#x2">Uerror</A>, <A HREF="#x3">Uexec</A>, <A HREF="#x4">Uprocess</A>, <A HREF="../../../../m3core/src/unix/Common/Ustat.i3.html">Ustat</A>,
  <A HREF="#x5">Utime</A>, <A HREF="#x6">Uugid</A>, <A HREF="../../../../m3core/src/word/Word.i3.html">Word</A>, <A HREF="../Common/Process.i3.html">Process</A>;

CONST
  NoFileDescriptor: INTEGER = -1;
  (* A non-existent file descriptor *)
</PRE> Posix Create just calls this; Cygwin only sometimes. 
<PRE>PROCEDURE <A NAME="Create_ForkExec"><procedure>Create_ForkExec</procedure></A>(
    cmd: Pathname.T;
    READONLY params: ARRAY OF TEXT;
    env: REF ARRAY OF TEXT := NIL;
    wd: Pathname.T := NIL;
    stdin, stdout, stderr: File.T := NIL)
  : Process.T RAISES {OSError.E} =
  VAR
    argx: ArrCStr;
    envx: ArrCStr;
    envp: Ctypes.char_star_star;
    wdstr: Ctypes.char_star;
    oit, nit: Utime.struct_itimerval;
    forkResult, execResult: INTEGER;
    forkErrno, execErrno: Ctypes.int;
    waitStatus: Ctypes.int;
    stdin_fd, stdout_fd, stderr_fd: INTEGER := NoFileDescriptor;
  BEGIN
    VAR path := GetPathToExec(cmd); BEGIN
      (* make sure the result is an absolute pathname if &quot;wd # NIL&quot; *)
      IF wd # NIL AND NOT Text.Empty(wd) AND NOT Pathname.Absolute(path) THEN
        path := Pathname.Join(GetWorkingDirectory(), path, ext := NIL);
        &lt;* ASSERT Pathname.Absolute(path) *&gt;
      END;
      argx := AllocArgs(path, Pathname.Base(cmd), params)
    END;
    IF env # NIL THEN
      envx := AllocEnv(env^);
      envp := ADR(envx[0])
    ELSE
      envx := NIL;
      envp := LOOPHOLE(RTLinker.envp, Ctypes.char_star_star)
    END;
    IF wd # NIL AND NOT Text.Empty(wd) THEN
      wdstr := M3toC.SharedTtoS(wd)
    ELSE
     wdstr := NIL
    END;

    (* grab the file descriptors from inside the traced File.Ts so
       we don't trigger a GC after the fork() call. *)
    stdin_fd  := NoFileDescriptor;
    IF (stdin  # NIL) THEN stdin_fd  := stdin.fd;  END;
    stdout_fd := NoFileDescriptor;
    IF (stdout # NIL) THEN stdout_fd := stdout.fd; END;
    stderr_fd := NoFileDescriptor;
    IF (stderr # NIL) THEN stderr_fd := stderr.fd; END;

    (* Turn off the interval timer (so it won't be running in child). *)
    nit := Utime.struct_itimerval {
             it_interval := Utime.struct_timeval {0, 0},
             it_value    := Utime.struct_timeval {0, 0}};
    IF Utime.setitimer(Utime.ITIMER_VIRTUAL, nit, oit) &lt; 0 THEN
      &lt;* ASSERT FALSE *&gt;
    END;

    (* Disable the scheduler. *)
    Scheduler.DisableSwitching ();

    execResult := 0;
    forkResult := Unix.fork();
    IF forkResult = 0 THEN (* in the child *)
      execResult := ExecChild(argx, envp, wdstr, stdin_fd, stdout_fd,
          stderr_fd);
      (* If ExecChild returns, the execve failed. Let's try to leave
        a note for our parent, in case we're still sharing their
        address space. *)
      execErrno := Cerrno.GetErrno();
      Unix.underscore_exit(99)
    END;

    (* Back in parent. *)

    forkErrno := Cerrno.GetErrno();

    (* Enable scheduler. *)
    Scheduler.EnableSwitching ();

    (* Restore previous virtual timer. *)
    IF Utime.setitimer(Utime.ITIMER_VIRTUAL, oit, nit) &lt; 0 THEN
      &lt;* ASSERT FALSE *&gt;
    END;

    FreeArgs(argx);
    IF envx # NIL THEN FreeEnv(envx) END;
    IF wdstr # NIL THEN M3toC.FreeSharedS(wd, wdstr); END;

    IF forkResult &lt; 0 THEN OSErrorPosix.Raise0(forkErrno) END;

    (* The vfork succeeded.  Did the execve succeed? *)
    IF execResult &lt; 0 THEN
      (* No, clean up child process. *)
      EVAL Uexec.waitpid(forkResult, ADR(waitStatus), 0);
      OSErrorPosix.Raise0(execErrno)
    END;

    RETURN NEW(Process.T, pid := forkResult)
  END Create_ForkExec;

PROCEDURE <A NAME="GetPathToExec"><procedure>GetPathToExec</procedure></A>(pn: Pathname.T): Pathname.T RAISES {OSError.E} =
</PRE><BLOCKQUOTE><EM> Return the filename to execute given <CODE>base</CODE> and the value of the <CODE>PATH</CODE>
   environment variable. Based on Mike Burrows's preexec(). </EM></BLOCKQUOTE><PRE>
  VAR
    path, prog: TEXT;
    start, i, end, result, uid, gid: INTEGER;
    statBuf: Ustat.struct_stat;
    pname: Ctypes.char_star;
  (*CONST*) MaskXXX := Ustat.S_IEXEC + Ustat.S_GEXEC + Ustat.S_OEXEC;
  BEGIN
    IF Text.FindChar(pn, '/') &lt; 0 THEN
      path := Env.Get(&quot;PATH&quot;);
      IF path = NIL THEN path := &quot;:/bin:/usr/bin&quot; END;
      uid := -1; gid := -1;
      start := 0;
      LOOP
        i := Text.FindChar(path, ':', start);
        IF i &lt; 0 THEN end := Text.Length(path) ELSE end := i END;
        prog := Pathname.Join(Text.Sub(path, start, end - start), pn, NIL);
        pname := M3toC.SharedTtoS(prog);
        result := Ustat.stat(pname, ADR(statBuf));
        M3toC.FreeSharedS(prog, pname);
        IF result = 0 AND
          Word.And(statBuf.st_mode, Ustat.S_IFMT) = Ustat.S_IFREG THEN
          statBuf.st_mode := Word.And(statBuf.st_mode, MaskXXX);
          IF statBuf.st_mode # 0 THEN
            IF statBuf.st_mode = MaskXXX THEN RETURN prog END;
            IF uid &lt; 0 THEN uid := Uugid.geteuid() END;
            IF uid = statBuf.st_uid THEN
              statBuf.st_mode := Word.And(statBuf.st_mode, Ustat.S_IEXEC)
            ELSE
              IF gid &lt; 0 THEN gid := Uugid.getegid() END;
              IF gid = statBuf.st_gid THEN
                statBuf.st_mode := Word.And(statBuf.st_mode, Ustat.S_GEXEC)
              ELSE
                statBuf.st_mode := Word.And(statBuf.st_mode, Ustat.S_OEXEC)
              END
            END;
            IF statBuf.st_mode # 0 THEN RETURN prog END
          END;
        END;
        IF i &lt; 0 THEN EXIT END;
        start := i + 1
      END;
      OSErrorPosix.Raise0(Uerror.ENOENT)
    ELSE (* pn contains '/' *)
      pname := M3toC.SharedTtoS(pn);
      IF Ustat.stat(pname, ADR(statBuf)) &lt; 0 THEN
        result := Cerrno.GetErrno();
        M3toC.FreeSharedS(pn, pname);
        OSErrorPosix.Raise0(result)
      END;
      M3toC.FreeSharedS(pn, pname);
    END;
    RETURN pn
  END GetPathToExec;

PROCEDURE <A NAME="AllocArgs"><procedure>AllocArgs</procedure></A>(path, base: TEXT; READONLY args: ARRAY OF TEXT): ArrCStr =
</PRE><BLOCKQUOTE><EM> Return an array of pointers, say <CODE>a</CODE>, with:
<PRE>
         a[0] = path
         a[1] = &quot;sh&quot;
         a[2] = base
         a[3+i] = args[i] for i = 0,...,LAST(args)
         a[n] = NIL for n = NUMBER(args) + 3
</EM></BLOCKQUOTE><PRE>
  VAR argx := NEW(ArrCStr, NUMBER(args) + 4);
  BEGIN
    argx[0] := M3toC.CopyTtoS(path);
    argx[1] := Sh;
    argx[2] := M3toC.CopyTtoS(base);
    FOR i := 0 TO LAST(args) DO argx[3 + i] := M3toC.CopyTtoS(args[i]) END;
    argx[LAST(argx^)] := NIL;
    RETURN argx
  END AllocArgs;

PROCEDURE <A NAME="FreeArgs"><procedure>FreeArgs</procedure></A>(VAR argx: ArrCStr) =
</PRE><BLOCKQUOTE><EM> Free all elements except <CODE>argx[1]</CODE>, which equals <CODE>Sh</CODE>.  Note that
   <CODE>ExecChild</CODE> may swap <CODE>argx[0]</CODE> and <CODE>argx[2]</CODE>. </EM></BLOCKQUOTE><PRE>
  BEGIN
    FOR i := 0 TO LAST(argx^) - 1 DO
      IF i # 1 THEN M3toC.FreeCopiedS(argx[i]) END
    END;
    &lt;* ASSERT argx[LAST(argx^)] = NIL *&gt;
    DISPOSE(argx)
  END FreeArgs;

PROCEDURE <A NAME="AllocEnv"><procedure>AllocEnv</procedure></A>(READONLY env: ARRAY OF TEXT): ArrCStr =
  VAR envx := NEW(ArrCStr, NUMBER(env) + 1);
  BEGIN
    FOR i := 0 TO LAST(env) DO envx[i] := M3toC.CopyTtoS(env[i]) END;
    envx[LAST(envx^)] := NIL;
    RETURN envx
  END AllocEnv;

PROCEDURE <A NAME="FreeEnv"><procedure>FreeEnv</procedure></A>(VAR envx: ArrCStr) =
  BEGIN
    FOR i := 0 TO LAST(envx^) - 1 DO
      M3toC.FreeCopiedS(envx[i])
    END;
    &lt;* ASSERT envx[LAST(envx^)] = NIL *&gt;
    DISPOSE(envx)
  END FreeEnv;

VAR (*CONST*)
  Sh := M3toC.FlatTtoS(&quot;sh&quot;);

PROCEDURE <A NAME="ExecChild"><procedure>ExecChild</procedure></A>(
    argx: ArrCStr; (* see &quot;AllocArgs&quot; for layout *)
    envp: Ctypes.char_star_star;
    wdstr: Ctypes.char_star;
    stdin, stdout, stderr: INTEGER) : INTEGER
  RAISES {} =
</PRE><BLOCKQUOTE><EM> Modify Unix state using <CODE>stdin</CODE>, ..., and invoke execve using
   <CODE>argx</CODE> and <CODE>envp</CODE>.  Do not invoke scheduler, allocator, or
   exceptions.  Return only if a fatal Unix error is encountered, in
   which case Cerrno.GetErrno() is set. </EM></BLOCKQUOTE><PRE>
  VAR res := 0; t: Ctypes.char_star;
  BEGIN
    IF wdstr # NIL THEN
      IF Unix.chdir(wdstr) &lt; 0 THEN RETURN -1; END
    END;
    IF NOT (SetFd(0, stdin) AND SetFd(1, stdout) AND SetFd(2, stderr)) THEN
      RETURN -1;
    END;
    FOR fd := 3 TO Unix.getdtablesize() - 1 DO
      EVAL Unix.close(fd) (* ignore errors *)
    END;
    (* Modula-3 ignores SIGPIPE, but most programs don't expect that: *)
    RTSignal.RestoreHandlers();
    res := Unix.execve((*path*)argx[0], ADR(argx[2]), envp);
    &lt;* ASSERT res &lt; 0 *&gt;
    IF Cerrno.GetErrno() = Uerror.ENOEXEC THEN
      t := argx[0]; argx[0] := argx[2]; argx[2] := t;
      res := Unix.execve(BinSh, ADR(argx[1]), envp);
      &lt;* ASSERT res &lt; 0 *&gt;
    END;
    RETURN res;
  END ExecChild;

PROCEDURE <A NAME="SetFd"><procedure>SetFd</procedure></A>(fd: INTEGER; h: INTEGER(*File.T*)): BOOLEAN =
  (* Make file descriptor &quot;fd&quot; refer to file &quot;h&quot;, or set &quot;fd&quot;'s
     close-on-exec flag if &quot;h=NoFile&quot;.  Return &quot;TRUE&quot; if succesful. *)
  BEGIN
    IF h # NoFileDescriptor THEN
      RETURN NOT Unix.dup2(h, fd) &lt; 0
    ELSIF Unix.fcntl(fd, Unix.F_SETFD, 1) &gt;= 0 THEN
      RETURN TRUE;
    ELSE (* EBADF =&gt; &quot;fd&quot; was already closed, don't panic *)
      RETURN (Cerrno.GetErrno() = Uerror.EBADF);
    END;
  END SetFd;

EXCEPTION WaitAlreadyCalled;

PROCEDURE <A NAME="Wait"><procedure>Wait</procedure></A>(p: T): ExitCode = &lt;* FATAL WaitAlreadyCalled *&gt;
  VAR
    result, status: Ctypes.int;
  BEGIN
    IF NOT p.waitOk THEN RAISE WaitAlreadyCalled END;
    p.waitOk := FALSE;
    result := SchedulerPosix.WaitProcess (p.pid, status);
    &lt;*ASSERT result &gt; 0*&gt;
    Uexec.RepackStatus(status);
    RETURN MIN(LAST(Process.ExitCode), status);
  END Wait;

PROCEDURE <A NAME="Exit"><procedure>Exit</procedure></A>(n: ExitCode) =
  BEGIN
    RTProcess.Exit(n)
  END Exit;

PROCEDURE <A NAME="Crash"><procedure>Crash</procedure></A>(msg: TEXT) =
  BEGIN
    RTProcess.Crash(msg)
  END Crash;

PROCEDURE <A NAME="RegisterExitor"><procedure>RegisterExitor</procedure></A>(p: PROCEDURE()) =
  BEGIN
    RTProcess.RegisterExitor(p)
  END RegisterExitor;

PROCEDURE <A NAME="GetID"><procedure>GetID</procedure></A>(p: T): ID =
  BEGIN
    RETURN p.pid
  END GetID;

PROCEDURE <A NAME="GetMyID"><procedure>GetMyID</procedure></A>(): ID =
  BEGIN
    RETURN Uprocess.getpid()
  END GetMyID;

PROCEDURE <A NAME="GetStandardFileHandles"><procedure>GetStandardFileHandles</procedure></A>(VAR stdin, stdout, stderr: File.T) =
  BEGIN
    stdin := stdin_g; stdout := stdout_g; stderr := stderr_g
  END GetStandardFileHandles;

VAR
  wdCacheMutex := NEW(MUTEX);
  wdCache: Pathname.T := NIL; (* NIL =&gt; unknown *)
</PRE><BLOCKQUOTE><EM> The main purpose for this cache is speeding up FS.Iterate when it
   is called with a relative pathname. </EM></BLOCKQUOTE><PRE>

PROCEDURE <A NAME="GetWorkingDirectory"><procedure>GetWorkingDirectory</procedure></A>(): Pathname.T RAISES {OSError.E} =
  VAR
    buffer: ARRAY [0..Unix.MaxPathLen] OF Ctypes.char;
    rc: Ctypes.char_star;
  BEGIN
    LOCK wdCacheMutex DO
      IF wdCache = NIL THEN
        rc := Unix.getcwd(ADR(buffer[0]), Unix.MaxPathLen+1);
        IF rc = NIL THEN
          RAISE OSError.E(
            NEW(AtomList.T,
              head := Atom.FromText(M3toC.CopyStoT(ADR(buffer[0]))),
              tail := NIL))
        END;
        wdCache := M3toC.CopyStoT(ADR(buffer[0]))
      END;
      RETURN wdCache
    END
  END GetWorkingDirectory;

PROCEDURE <A NAME="SetWorkingDirectory"><procedure>SetWorkingDirectory</procedure></A>(pn: Pathname.T) RAISES {OSError.E} =
  VAR fname := M3toC.SharedTtoS(pn);  err: INTEGER;
  BEGIN
    LOCK wdCacheMutex DO
      IF Unix.chdir(fname) &lt; 0 THEN
        err := Cerrno.GetErrno();
        M3toC.FreeSharedS(pn, fname);
        OSErrorPosix.Raise0(err);
      END;
      wdCache := NIL
    END;
    M3toC.FreeSharedS(pn, fname);
  END SetWorkingDirectory;
</PRE> Initialization 

<P><PRE>PROCEDURE <A NAME="GetFileHandle"><procedure>GetFileHandle</procedure></A>(fd: INTEGER; ds: FilePosix.DirectionSet): File.T =
  VAR f: File.T := NIL;
  BEGIN
    TRY f := FilePosix.New(fd, ds);
    EXCEPT
    | OSError.E =&gt; (* not available *)
    END;
    RETURN f
  END GetFileHandle;

BEGIN
  BinSh := M3toC.FlatTtoS(&quot;/bin/sh&quot;);
  stdin_g := GetFileHandle(0, FilePosix.Read);
  stdout_g := GetFileHandle(1, FilePosix.Write);
  stderr_g := GetFileHandle(2, FilePosix.Write)
END ProcessPosixCommon.
</PRE>
</inModule>
<HR>
<A NAME="x1">interface Unix is in:
</A><UL>
<LI><A HREF="../../../../m3core/src/unix/Common/Unix.i3.html#0TOP0">m3core/src/unix/Common/Unix.i3</A>
<LI><A HREF="../../../../m3core/src/unix/linux-common/Unix.i3.html#0TOP0">m3core/src/unix/linux-common/Unix.i3</A>
</UL>
<P>
<HR>
<A NAME="x2">interface Uerror is in:
</A><UL>
<LI><A HREF="../../../../m3core/src/unix/Common/Uerror.i3.html#0TOP0">m3core/src/unix/Common/Uerror.i3</A>
<LI><A HREF="../../../../m3core/src/unix/linux-common/Uerror.i3.html#0TOP0">m3core/src/unix/linux-common/Uerror.i3</A>
</UL>
<P>
<HR>
<A NAME="x3">interface Uexec is in:
</A><UL>
<LI><A HREF="../../../../m3core/src/unix/Common/Uexec.i3.html#0TOP0">m3core/src/unix/Common/Uexec.i3</A>
<LI><A HREF="../../../../m3core/src/unix/linux-common/Uexec.i3.html#0TOP0">m3core/src/unix/linux-common/Uexec.i3</A>
</UL>
<P>
<HR>
<A NAME="x4">interface Uprocess is in:
</A><UL>
<LI><A HREF="../../../../m3core/src/unix/Common/Uprocess.i3.html#0TOP0">m3core/src/unix/Common/Uprocess.i3</A>
<LI><A HREF="../../../../m3core/src/unix/linux-common/Uprocess.i3.html#0TOP0">m3core/src/unix/linux-common/Uprocess.i3</A>
</UL>
<P>
<HR>
<A NAME="x5">interface Utime is in:
</A><UL>
<LI><A HREF="../../../../m3core/src/unix/Common/Utime.i3.html#0TOP0">m3core/src/unix/Common/Utime.i3</A>
<LI><A HREF="../../../../m3core/src/unix/linux-common/Utime.i3.html#0TOP0">m3core/src/unix/linux-common/Utime.i3</A>
</UL>
<P>
<HR>
<A NAME="x6">interface Uugid is in:
</A><UL>
<LI><A HREF="../../../../m3core/src/unix/Common/Uugid.i3.html#0TOP0">m3core/src/unix/Common/Uugid.i3</A>
<LI><A HREF="../../../../m3core/src/unix/linux-common/Uugid.i3.html#0TOP0">m3core/src/unix/linux-common/Uugid.i3</A>
</UL>
<P>
<PRE>























</PRE>
</BODY>
</HTML>
