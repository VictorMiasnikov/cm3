<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: libm3/src/os/WIN32/OSConfigWin32.m3</TITLE>
</HEAD>
<BODY bgcolor="#eeeeee">
<A NAME="0TOP0">
<H2>libm3/src/os/WIN32/OSConfigWin32.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright 1996-2000, Critical Mass, Inc.  All rights reserved. 
 See file COPYRIGHT-CMASS for details. 

<P><PRE>UNSAFE MODULE <module>OSConfigWin32</module> EXPORTS <A HREF="#x1"><implements>OSConfig</A></implements>;

IMPORT <A HREF="../../params/Env.i3.html">Env</A>, <A HREF="../../fmtlex/Fmt.i3.html">Fmt</A>, <A HREF="../../../../m3core/src/C/Common/M3toC.i3.html">M3toC</A>, <A HREF="../Common/OSError.i3.html">OSError</A>, <A HREF="../Common/Process.i3.html">Process</A>, <A HREF="../../../../m3core/src/text/Text.i3.html">Text</A>, <A HREF="../../../../m3core/src/win32/WinBase.i3.html">WinBase</A>, <A HREF="../../../../m3core/src/win32/WinNT.i3.html">WinNT</A>, <A HREF="#x2">WinSock</A>;

VAR
  host_name  : TEXT := NIL;
  host_arch  : TEXT := NIL;
  os_name    : TEXT := NIL;
  os_version : TEXT := NIL;
  user_name  : TEXT := NIL;
  user_home  : TEXT := NIL;

PROCEDURE <A NAME="HostName"><procedure>HostName</procedure></A> (): TEXT =
  BEGIN
    IF (host_name = NIL) THEN InitHostName (); END;
    RETURN host_name;
  END HostName;

PROCEDURE <A NAME="HostArchitecture"><procedure>HostArchitecture</procedure></A> (): TEXT =
  BEGIN
    IF (host_arch = NIL) THEN InitHostArch (); END;
    RETURN host_arch;
  END HostArchitecture;

PROCEDURE <A NAME="OSName"><procedure>OSName</procedure></A> (): TEXT =
  BEGIN
    IF (os_name = NIL) THEN InitOSName (); END;
    RETURN os_name;
  END OSName;

PROCEDURE <A NAME="OSVersion"><procedure>OSVersion</procedure></A> (): TEXT =
  BEGIN
    IF (os_version = NIL) THEN InitOSName (); END;
    RETURN os_version;
  END OSVersion;

PROCEDURE <A NAME="UserName"><procedure>UserName</procedure></A> (): TEXT =
  BEGIN
    IF user_name = NIL THEN InitUserName (); END;
    RETURN user_name;
  END UserName;

PROCEDURE <A NAME="UserHome"><procedure>UserHome</procedure></A> (): TEXT =
  BEGIN
    IF user_home = NIL THEN InitUserHome (); END;
    RETURN user_home;
  END UserHome;
</PRE>----------------------------------------------------------- internal ---

<P><PRE>PROCEDURE <A NAME="InitHostName"><procedure>InitHostName</procedure></A> () =
  CONST WinSockVersion = 16_0101;       (* App version 1.1 *)
  VAR data: WinSock.WSAData;  name: ARRAY [0..255] OF CHAR;
  BEGIN
    IF host_name # NIL THEN RETURN; END;
    IF WinSock.WSAStartup(WinSockVersion, ADR(data)) = 0 THEN
      IF WinSock.gethostname (ADR (name[0]), BYTESIZE (name)) = 0
        THEN host_name := M3toC.CopyStoT (ADR (name[0]));
        ELSE host_name := &quot;&lt;unknown&gt;&quot;;
      END;
      EVAL WinSock.WSACleanup();
    ELSE
      host_name := &quot;&lt;unknown&gt;&quot;;
    END;
  END InitHostName;

PROCEDURE <A NAME="InitHostArch"><procedure>InitHostArch</procedure></A> () =
  VAR sys: WinBase.SYSTEM_INFO;
  BEGIN
    IF host_arch # NIL THEN RETURN; END;
    WinBase.GetSystemInfo (ADR (sys));
    CASE sys.wProcessorArchitecture OF
    | WinNT.PROCESSOR_ARCHITECTURE_INTEL =&gt;
        CASE sys.wProcessorLevel OF
        | 3 =&gt;  host_arch := &quot;Intel 386&quot;;
        | 4 =&gt;  host_arch := &quot;Intel 486&quot;;
        | 5 =&gt;  host_arch := &quot;Intel Pentium&quot;;
        ELSE    host_arch := &quot;Intel x86&quot;;
        END;
    | WinNT.PROCESSOR_ARCHITECTURE_ALPHA =&gt;
        host_arch := &quot;Alpha &quot; &amp; Fmt.Int (sys.wProcessorLevel);
    | WinNT.PROCESSOR_ARCHITECTURE_MIPS =&gt;
        host_arch := &quot;Mips R4000&quot;;
    | WinNT.PROCESSOR_ARCHITECTURE_PPC =&gt;
        CASE sys.wProcessorLevel OF
        | 1  =&gt;  host_arch := &quot;PowerPC 601&quot;;
        | 3  =&gt;  host_arch := &quot;PowerPC 603&quot;;
        | 4  =&gt;  host_arch := &quot;PowerPC 604&quot;;
        | 6  =&gt;  host_arch := &quot;PowerPC 603+&quot;;
        | 9  =&gt;  host_arch := &quot;PowerPC 604+&quot;;
        | 20 =&gt;  host_arch := &quot;PowerPC 620&quot;;
        ELSE     host_arch := &quot;PowerPC&quot;;
        END;
    ELSE
        host_arch := &quot;&lt;unknown&gt;&quot;;
    END;
  END InitHostArch;

PROCEDURE <A NAME="InitOSName"><procedure>InitOSName</procedure></A> () =
  VAR ver: WinBase.OSVERSIONINFO;
  BEGIN
    IF os_version # NIL THEN RETURN; END;
    ver.dwOSVersionInfoSize := BYTESIZE (ver);
    IF WinBase.GetVersionEx (ADR (ver)) # 0 THEN
      CASE ver.dwPlatformId OF
      | WinBase.VER_PLATFORM_WIN32s        =&gt; os_name := &quot;Windows (with Win32s)&quot;;
      | WinBase.VER_PLATFORM_WIN32_WINDOWS =&gt; os_name := &quot;Windows 95&quot;;
      | WinBase.VER_PLATFORM_WIN32_NT      =&gt; os_name := &quot;Windows NT&quot;;
      ELSE                                    os_name := &quot;Windows&quot;;
      END;
      os_version := Fmt.Int (ver.dwMajorVersion)
                    &amp; &quot;.&quot; &amp; Fmt.Int (ver.dwMinorVersion);
    ELSE
      os_name    := &quot;Windows&quot;;
      os_version := &quot;&quot;;
    END;
  END InitOSName;

PROCEDURE <A NAME="InitUserName"><procedure>InitUserName</procedure></A> () =
  VAR buf: ARRAY [0..127] OF CHAR;  len: INTEGER;
  BEGIN
    IF user_name # NIL THEN RETURN; END;

    user_name := Env.Get (&quot;USER&quot;);
    IF user_name # NIL THEN RETURN; END;

    len := NUMBER (buf);
    IF WinBase.GetUserName (ADR (buf[0]), ADR (len)) # 0 AND (len &gt; 1) THEN
      user_name := Text.FromChars (SUBARRAY (buf, 0, len-1));
    END;
    IF user_name # NIL THEN RETURN; END;

    user_name := &quot;&lt;unknown user&gt;&quot;;
  END InitUserName;

PROCEDURE <A NAME="InitUserHome"><procedure>InitUserHome</procedure></A> () =
  BEGIN
    IF user_home # NIL THEN RETURN; END;

    user_home := Env.Get (&quot;HOME&quot;);
    IF user_home # NIL THEN RETURN; END;

    TRY user_home := Process.GetWorkingDirectory ();
    EXCEPT OSError.E =&gt;
    END;
    IF user_home # NIL THEN RETURN; END;

    user_home := &quot;C:\\&quot;;
  END InitUserHome;

BEGIN
END OSConfigWin32.
</PRE>
</inModule>
<HR>
<A NAME="x1">interface OSConfig is in:
</A><UL>
<LI><A HREF="../POSIX/OSConfig.i3.html#0TOP0">libm3/src/os/POSIX/OSConfig.i3</A>
<LI><A HREF="OSConfig.i3.html#0TOP0">libm3/src/os/WIN32/OSConfig.i3</A>
</UL>
<P>
<HR>
<A NAME="x2">interface WinSock is in:
</A><UL>
<LI><A HREF="../../../../m3core/src/win32/WinSock.i3.html#0TOP0">m3core/src/win32/WinSock.i3</A>
<LI><A HREF="../../../../tcp/src/WIN32/WinSock.i3.html#0TOP0">tcp/src/WIN32/WinSock.i3</A>
</UL>
<P>
<PRE>























</PRE>
</BODY>
</HTML>
