<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: webscape/src/Main.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>webscape/src/Main.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright 1996 Digital Equipment Corporation.              
 Distributed only by permission.                            
                                                            
 Last modified on Tue Aug 27 15:53:33 PDT 1996 by mhb       

<P><PRE>MODULE <module><implements><A HREF="../../m3core/src/main/Main.i3.html">Main</A></implements></module>;

IMPORT <A HREF="../../vbtkit/src/vbtkitutils/AnyEvent.i3.html">AnyEvent</A>, <A HREF="../../libm3/src/params/Env.i3.html">Env</A>, <A HREF="../../libm3/src/fmtlex/Fmt.i3.html">Fmt</A>, <A HREF="../../formsvbt/src/FormsVBT.i3.html">FormsVBT</A>, <A HREF="#x1">HTML</A>, <A HREF="../../libm3/src/rw/Rd.i3.html">Rd</A>, <A HREF="../../libm3/derived/RefSeq.i3.html">RefSeq</A>, <A HREF="../../vbtkit/src/vbtkitutils/Rsrc.i3.html">Rsrc</A>, <A HREF="../../libm3/src/rw/Stdio.i3.html">Stdio</A>, <A HREF="../../m3core/src/thread/Common/Thread.i3.html">Thread</A>,
       <A HREF="../../ui/src/vbt/Trestle.i3.html">Trestle</A>, <A HREF="../../ui/src/vbt/TrestleComm.i3.html">TrestleComm</A>, <A HREF="../derived/UIBundle.i3.html">UIBundle</A>, <A HREF="../../ui/src/vbt/VBT.i3.html">VBT</A>, <A HREF="../../web/src/Web.i3.html">Web</A>, <A HREF="../../webvbt/src/WebVBT.i3.html">WebVBT</A>, <A HREF="../../libm3/src/rw/Wr.i3.html">Wr</A>;

&lt;* FATAL FormsVBT.Error *&gt;
&lt;* FATAL FormsVBT.Unimplemented *&gt;
&lt;* FATAL Thread.Alerted *&gt;
&lt;* FATAL TrestleComm.Failure *&gt;
&lt;* FATAL VBT.Error *&gt;
&lt;* FATAL Wr.Failure *&gt;

TYPE
  Form = FormsVBT.T OBJECT
           w: MyWebVBT;          (* the one currently displayed *)
         END;

  Activity = {Ready, Loading, Reloading, ImageFetching, Aborted};

  MyWebVBT = WebVBT.T OBJECT
               activity: Activity;
               imageCt : CARDINAL;
               fv      : Form;
               toFetch : TEXT;
             OVERRIDES
               ready   := Ready;
               hotlink := Link;
               ismap   := IsMap;
               isindex := IsIndex;
             END;

VAR (* protected by VBT.mu *)
  pages := NEW(RefSeq.T).init(); (* stack of MyWebVBT's *)
  currPage := -1;
</PRE> called with LL=VBT.mu 
<PRE>PROCEDURE <A NAME="Link"><procedure>Link</procedure></A> (w: MyWebVBT; link: TEXT; &lt;*UNUSED*&gt; READONLY cd: VBT.MouseRec) =
  VAR
    fv  := w.fv;
    url := Web.AbsoluteURL(link, w.url);
  BEGIN
    LoadURL(fv, url);
  END Link;
</PRE> called with LL=VBT.mu 
<PRE>PROCEDURE <A NAME="IsMap"><procedure>IsMap</procedure></A> (w: MyWebVBT; absURL: TEXT; &lt;*UNUSED*&gt; READONLY cd: VBT.MouseRec) =
  VAR fv := w.fv;
  BEGIN
    LoadURL(fv, absURL);
  END IsMap;
</PRE> called with LL=VBT.mu 
<PRE>PROCEDURE <A NAME="IsIndex"><procedure>IsIndex</procedure></A> (w: MyWebVBT; absURL: TEXT) =
  VAR fv := w.fv;
  BEGIN
    LoadURL(fv, absURL);
  END IsIndex;
</PRE> called with LL=VBT.mu 
<PRE>PROCEDURE <A NAME="Ready"><procedure>Ready</procedure></A> (w: MyWebVBT; ct: CARDINAL) =
  VAR fv := w.fv;
  BEGIN
    IF ct = 0 THEN
       w.activity := Activity.Ready
    ELSE
       w.activity := Activity.ImageFetching;
       w.imageCt := ct;
    END;
    IF fv.w = w THEN
      (* we still displaying page w *)
      FormsVBT.PutText(fv, &quot;url&quot;, w.url);
      UpdatePageBanner (fv);
    END
  END Ready;

PROCEDURE <A NAME="QuitProc"><procedure>QuitProc</procedure></A> (             fv: FormsVBT.T;
                    &lt;* UNUSED *&gt; e : TEXT;
                    &lt;* UNUSED *&gt; cl: REFANY;
                    &lt;* UNUSED *&gt; t : VBT.TimeStamp) =
  BEGIN
    Trestle.Delete(fv)
  END QuitProc;

PROCEDURE <A NAME="DebugProc"><procedure>DebugProc</procedure></A> (             fv: FormsVBT.T;
                     &lt;* UNUSED *&gt; e : TEXT;
                     &lt;* UNUSED *&gt; cl: REFANY;
                     &lt;* UNUSED *&gt; t : VBT.TimeStamp) =
  CONST
    Separator = &quot;\n\n\n***************************************************************\n\n&quot;;
  VAR w := NARROW(fv, Form).w;
  BEGIN
    Wr.PutText(Stdio.stderr, Separator);
    (*
    ** Wr.PutText(Stdio.stderr, w.page.header);
    ** Wr.PutText(Stdio.stderr, Separator);
    *)
    Wr.PutText(Stdio.stderr, w.page.contents);
    TYPECASE (w.page) OF
    | NULL =&gt;
    | WebVBT.HTMLPage (h) =&gt;
        Wr.PutText(Stdio.stderr, Separator);
        HTML.Dump(h.html, Stdio.stderr);
        Wr.PutText(Stdio.stderr, Separator);
    ELSE
    END;
    Wr.PutText(Stdio.stderr, Separator);
  END DebugProc;

PROCEDURE <A NAME="URLProc"><procedure>URLProc</procedure></A> (             fv: FormsVBT.T;
                   &lt;* UNUSED *&gt; e : TEXT;
                   &lt;* UNUSED *&gt; cl: REFANY;
                   &lt;* UNUSED *&gt; t : VBT.TimeStamp) =
  VAR url := FormsVBT.GetText(fv, &quot;url&quot;);
  BEGIN
    LoadURL(fv, url);
  END URLProc;

PROCEDURE <A NAME="HomeURL"><procedure>HomeURL</procedure></A> (): TEXT =
  VAR url := Env.Get (&quot;WWW_HOME&quot;);
  BEGIN
    IF url = NIL THEN
      url := &quot;http://www.research.digital.com/SRC/webbrowsing/&quot;;
    END;
    RETURN url;
  END HomeURL;

PROCEDURE <A NAME="HomeProc"><procedure>HomeProc</procedure></A> (            fv: FormsVBT.T;
                   &lt;* UNUSED *&gt; e : TEXT;
                   &lt;* UNUSED *&gt; cl: REFANY;
                   &lt;* UNUSED *&gt; t : VBT.TimeStamp) =
  BEGIN
    LoadURL(fv, HomeURL ());
  END HomeProc;

PROCEDURE <A NAME="BackProc"><procedure>BackProc</procedure></A> (            fv: FormsVBT.T;
                   &lt;* UNUSED *&gt; e : TEXT;
                   &lt;* UNUSED *&gt; cl: REFANY;
                   &lt;* UNUSED *&gt; t : VBT.TimeStamp) =
  BEGIN
    DEC(currPage);
    ShowPage (fv, pages.get(currPage));
    UpdatePageBanner (fv);
  END BackProc;

PROCEDURE <A NAME="ForwardProc"><procedure>ForwardProc</procedure></A> (         fv: FormsVBT.T;
                   &lt;* UNUSED *&gt; e : TEXT;
                   &lt;* UNUSED *&gt; cl: REFANY;
                   &lt;* UNUSED *&gt; t : VBT.TimeStamp) =
  BEGIN
    INC(currPage);
    ShowPage (fv, pages.get(currPage));
    UpdatePageBanner (fv);
  END ForwardProc;

PROCEDURE <A NAME="ReloadProc"><procedure>ReloadProc</procedure></A> (             fv: FormsVBT.T;
                      &lt;* UNUSED *&gt; e : TEXT;
                      &lt;* UNUSED *&gt; cl: REFANY;
                      &lt;* UNUSED *&gt; t : VBT.TimeStamp) =
  BEGIN
    ReloadURL(fv)
  END ReloadProc;

PROCEDURE <A NAME="StopProc"><procedure>StopProc</procedure></A> (             fv: FormsVBT.T;
                    &lt;* UNUSED *&gt; e : TEXT;
                    &lt;* UNUSED *&gt; cl: REFANY;
                    &lt;* UNUSED *&gt; t : VBT.TimeStamp) =
  VAR w := NARROW(FormsVBT.GetGeneric(fv, &quot;contents&quot;), MyWebVBT);
  BEGIN
    w.activity := Activity.Aborted;
    w.stop();
    UpdatePageBanner(fv);
  END StopProc;

PROCEDURE <A NAME="SelectedStyle"><procedure>SelectedStyle</procedure></A> (fv: Form): WebVBT.Style =
  BEGIN
    IF FormsVBT.IsSelected(fv, &quot;displayUgly&quot;) THEN
      RETURN WebVBT.Style.Ugly
    ELSIF FormsVBT.IsSelected(fv, &quot;displayNoImages&quot;) THEN
      RETURN WebVBT.Style.NoImages
    ELSIF FormsVBT.IsSelected(fv, &quot;displayImages&quot;) THEN
      RETURN WebVBT.Style.Normal
    ELSE
      RETURN WebVBT.Style.Background
    END;
  END SelectedStyle;

PROCEDURE <A NAME="ZipperStyle"><procedure>ZipperStyle</procedure></A> (fv: Form): BOOLEAN =
  BEGIN
    RETURN FormsVBT.GetBoolean(fv, &quot;useZippers&quot;);
  END ZipperStyle;

PROCEDURE <A NAME="ReloadURL"><procedure>ReloadURL</procedure></A> (fv: Form) =
  VAR w := NARROW(FormsVBT.GetGeneric(fv, &quot;contents&quot;), MyWebVBT);
  BEGIN
    w.activity := Activity.Reloading;
    IF w.url # NIL THEN w.toFetch := w.url END;
    w.fetch(w.toFetch, style := SelectedStyle(fv), zippers := ZipperStyle(fv), reload := TRUE);
    UpdatePageBanner(fv)
  END ReloadURL;

PROCEDURE <A NAME="LoadURL"><procedure>LoadURL</procedure></A> (fv: Form; url: TEXT) =
  VAR w: MyWebVBT;
  BEGIN
    w := NEW(MyWebVBT, fv:=fv, url:=url).init();
    INC(currPage);
    FOR i := currPage TO pages.size() - 1 DO EVAL pages.remhi() END;
    pages.addhi(w);
    w.activity := Activity.Loading;
    w.toFetch := url;
    w.fetch(w.toFetch, style := SelectedStyle(fv), zippers := ZipperStyle(fv));
    ShowPage(fv, w);
    UpdatePageBanner(fv);
  END LoadURL;

PROCEDURE <A NAME="ShowPage"><procedure>ShowPage</procedure></A> (fv: Form; w: MyWebVBT) =
  VAR url: TEXT;
  BEGIN
    fv.w := w;
    IF w.url = NIL THEN url := w.toFetch ELSE url := w.url END;
    FormsVBT.PutText(fv, &quot;url&quot;, url);
    FormsVBT.PutGeneric(fv, &quot;contents&quot;, w);
    FormsVBT.PutText(fv, &quot;pageCounts&quot;, Fmt.Int(currPage+1) &amp; &quot;/&quot; &amp; Fmt.Int(pages.size()));
    IF currPage = 0 THEN FormsVBT.MakeDormant (fv, &quot;back&quot;)
    ELSE FormsVBT.MakeActive (fv, &quot;back&quot;) END;
    IF currPage = pages.size()-1 THEN FormsVBT.MakeDormant (fv, &quot;forward&quot;)
    ELSE FormsVBT.MakeActive (fv, &quot;forward&quot;) END;
  END ShowPage;

PROCEDURE <A NAME="UpdatePageBanner"><procedure>UpdatePageBanner</procedure></A> (fv: Form) =
  VAR color, bgColor, title: TEXT; w:=fv.w;
  PROCEDURE GetTitle(): TEXT =
    BEGIN
      TYPECASE w.page OF
      | WebVBT.HTMLPage(page) =&gt; RETURN page.html.title
      ELSE RETURN &quot;&lt;Untitled&gt;&quot;
      END
    END GetTitle;
  BEGIN
    CASE w.activity OF
    | Activity.Loading =&gt;
        bgColor := &quot;VeryLightRed&quot;;
        color := &quot;Black&quot;;
        title := &quot;Fetching &quot; &amp; w.toFetch &amp; &quot; ...&quot;;
        FormsVBT.MakeActive(fv, &quot;stop&quot;);
        FormsVBT.MakeDormant(fv, &quot;reload&quot;);
        FormsVBT.MakeDormant(fv, &quot;url&quot;);
    | Activity.ImageFetching =&gt;
        bgColor := &quot;VeryLightBlue&quot;;
        color := &quot;Black&quot;;
        title := &quot;Fetching images; &quot; &amp; Fmt.Int(w.imageCt) &amp; &quot; remaining...&quot;;
        FormsVBT.MakeActive(fv, &quot;stop&quot;);
        FormsVBT.MakeDormant(fv, &quot;reload&quot;);
        FormsVBT.MakeDormant(fv, &quot;url&quot;);
    | Activity.Reloading =&gt;
        bgColor := &quot;VeryLightRed&quot;;
        color := &quot;Black&quot;;
        title := &quot;Reloading &quot; &amp; w.toFetch &amp; &quot; ...&quot;;
        FormsVBT.MakeActive(fv, &quot;stop&quot;);
        FormsVBT.MakeDormant(fv, &quot;reload&quot;);
        FormsVBT.MakeDormant(fv, &quot;url&quot;);
    | Activity.Aborted =&gt;
        IF w.page = NIL THEN
          bgColor := &quot;DarkRed&quot;;
          color := &quot;White&quot;;
          title := &quot;Fetching of &quot; &amp; w.toFetch &amp; &quot; interrupted by user&quot;;
        ELSE
          bgColor := &quot;DarkRed&quot;;
          color := &quot;White&quot;;
          title := &quot;Reloading \&quot;&quot; &amp; GetTitle() &amp; &quot;\&quot; interrupted by user&quot;;
        END;
        FormsVBT.MakeDormant(fv, &quot;stop&quot;);
        FormsVBT.MakeActive(fv, &quot;reload&quot;);
        FormsVBT.MakeActive(fv, &quot;url&quot;);
    | Activity.Ready =&gt;
        bgColor := &quot;White&quot;;
        color := &quot;Black&quot;;
        title := GetTitle();
        FormsVBT.MakeDormant(fv, &quot;stop&quot;);
        FormsVBT.MakeActive(fv, &quot;reload&quot;);
        FormsVBT.MakeActive(fv, &quot;url&quot;);
    END;
    FormsVBT.PutTextProperty (fv, &quot;title&quot;, &quot;BgColor&quot;, bgColor);
    FormsVBT.PutTextProperty (fv, &quot;title&quot;, &quot;Color&quot;, color);
    FormsVBT.PutText(fv, &quot;title&quot;, title);
  END UpdatePageBanner;

PROCEDURE <A NAME="OpenProc"><procedure>OpenProc</procedure></A> (             fv: FormsVBT.T;
                    &lt;* UNUSED *&gt; e : TEXT;
                    &lt;* UNUSED *&gt; cl: REFANY;
                                 t : VBT.TimeStamp) =
  VAR event := FormsVBT.GetTheEvent(fv);
  BEGIN
    TYPECASE event OF
    | AnyEvent.Mouse (m) =&gt;
        IF VBT.Modifier.MouseM IN m.mouse.modifiers THEN
          TYPECASE VBT.Read(fv, VBT.Source, t).toRef() OF
          | NULL =&gt;
          | TEXT (txt) =&gt; LoadURL(fv, txt); RETURN;
          ELSE
          END;
        END;
    ELSE
    END;
    FormsVBT.TakeFocus(fv, &quot;openurl&quot;, t, TRUE);
    FormsVBT.PopUp(fv, &quot;OpenDlg&quot;, TRUE, t);
  END OpenProc;

PROCEDURE <A NAME="OpenURLProc"><procedure>OpenURLProc</procedure></A> (             fv: FormsVBT.T;
                       &lt;* UNUSED *&gt; e : TEXT;
                       &lt;* UNUSED *&gt; cl: REFANY;
                       &lt;* UNUSED *&gt; t : VBT.TimeStamp) =
  VAR url := FormsVBT.GetText(fv, &quot;openurl&quot;);
  BEGIN
    LoadURL(fv, url)
  END OpenURLProc;

PROCEDURE <A NAME="OpenClearProc"><procedure>OpenClearProc</procedure></A> (             fv: FormsVBT.T;
                         &lt;* UNUSED *&gt; e : TEXT;
                         &lt;* UNUSED *&gt; cl: REFANY;
                         &lt;* UNUSED *&gt; t : VBT.TimeStamp) =
  BEGIN
    FormsVBT.PutText(fv, &quot;openurl&quot;, &quot;&quot;)
  END OpenClearProc;

PROCEDURE <A NAME="OpenPasteProc"><procedure>OpenPasteProc</procedure></A> (             fv: FormsVBT.T;
                         &lt;* UNUSED *&gt; e : TEXT;
                         &lt;* UNUSED *&gt; cl: REFANY;
                                      t : VBT.TimeStamp) =
  BEGIN
    TYPECASE VBT.Read(fv, VBT.Source, t).toRef() OF
    | NULL =&gt;
    | TEXT (txt) =&gt;
        FormsVBT.PutText(fv, &quot;openurl&quot;, txt);
        FormsVBT.TakeFocus(fv, &quot;openurl&quot;, t, FALSE);
    ELSE
    END;
  END OpenPasteProc;

PROCEDURE <A NAME="NewForm"><procedure>NewForm</procedure></A> (): Form =
  &lt;* FATAL Rd.Failure, Rsrc.NotFound *&gt;
  VAR
    fv := NEW(Form).initFromRsrc(
            &quot;ui.fv&quot;, Rsrc.BuildPath(&quot;$BrowserPATH&quot;, UIBundle.Get()));
  BEGIN
    FormsVBT.AttachProc(fv, &quot;url&quot;, URLProc);
    FormsVBT.AttachProc(fv, &quot;back&quot;, BackProc);
    FormsVBT.AttachProc(fv, &quot;forward&quot;, ForwardProc);
    FormsVBT.AttachProc(fv, &quot;home&quot;, HomeProc);
    FormsVBT.AttachProc(fv, &quot;reload&quot;, ReloadProc);
    FormsVBT.AttachProc(fv, &quot;stop&quot;, StopProc);
    FormsVBT.AttachProc(fv, &quot;quit&quot;, QuitProc);
    FormsVBT.AttachProc(fv, &quot;displayStyle&quot;, ReloadProc);
    FormsVBT.AttachProc(fv, &quot;useZippers&quot;, ReloadProc);
    FormsVBT.AttachProc(fv, &quot;debug&quot;, DebugProc);

    FormsVBT.AttachProc(fv, &quot;open&quot;, OpenProc);
    FormsVBT.AttachProc(fv, &quot;openurl&quot;, OpenURLProc);
    FormsVBT.AttachProc(fv, &quot;openopen&quot;, OpenURLProc);
    FormsVBT.AttachProc(fv, &quot;openclear&quot;, OpenClearProc);
    FormsVBT.AttachProc(fv, &quot;openpaste&quot;, OpenPasteProc);

    LOCK VBT.mu DO
      LoadURL(fv, HomeURL ())
    END;
    RETURN fv
  END NewForm;

BEGIN
  WITH z = NewForm() DO
    Trestle.Install(z);
    Trestle.AwaitDelete(z);
  END
END Main.
</PRE>
</inModule>
<HR>
<A NAME="x1">interface HTML is in:
</A><UL>
<LI><A HREF="../../cm3ide/src/misc/HTML.i3.html#0TOP0">cm3ide/src/misc/HTML.i3</A>
<LI><A HREF="../../webvbt/src/HTML.i3.html#0TOP0">webvbt/src/HTML.i3</A>
</UL>
<P>
<PRE>























</PRE>
</BODY>
</HTML>
