<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: obliqparse/src/ObFrame.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>obliqparse/src/ObFrame.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright 1991 Digital Equipment Corporation.               
 Distributed only by permission.                             
<PRE>MODULE <module><implements><A HREF="ObFrame.i3.html">ObFrame</A></implements></module>;
IMPORT <A HREF="../../obliqrt/src/ObErr.i3.html">ObErr</A>, <A HREF="../../synloc/src/SynWr.i3.html">SynWr</A>, <A HREF="../../synex/src/SynScan.i3.html">SynScan</A>, <A HREF="../../libm3/src/rw/Rd.i3.html">Rd</A>, <A HREF="../../libm3/src/rw/TextRd.i3.html">TextRd</A>, <A HREF="../../libm3/src/fmtlex/Lex.i3.html">Lex</A>, <A HREF="../../libm3/src/rw/FileRd.i3.html">FileRd</A>, <A HREF="../../m3core/src/text/Text.i3.html">Text</A>, <A HREF="../../libm3/src/os/Common/OSError.i3.html">OSError</A>,
       <A HREF="../../libm3/src/os/Common/Pathname.i3.html">Pathname</A>, <A HREF="../../obliqrt/src/ObLib.i3.html">ObLib</A>, <A HREF="../../obliqrt/src/ObValue.i3.html">ObValue</A>, <A HREF="../../synloc/src/SynLocation.i3.html">SynLocation</A>, <A HREF="../../obliqrt/src/ObEval.i3.html">ObEval</A>, <A HREF="../../m3core/src/thread/Common/Thread.i3.html">Thread</A>,
       <A HREF="../../obliqrt/src/ObCommand.i3.html">ObCommand</A>, <A HREF="../../libm3/src/fmtlex/Fmt.i3.html">Fmt</A>, <A HREF="../../m3core/src/fingerprint/Fingerprint.i3.html">Fingerprint</A>, <A HREF="../../libm3/src/pickle/ver2/Pickle2.i3.html">Pickle2</A> AS Pickle, <A HREF="../../libm3/src/rw/Wr.i3.html">Wr</A>,
       <A HREF="../../libm3/src/pickle/ver2/PickleStubs.i3.html">PickleStubs</A>, <A HREF="../../netobj/src/netobjrt/NetObj.i3.html">NetObj</A>, <A HREF="../../libm3/derived/TextList.i3.html">TextList</A>;
IMPORT <A HREF="../../libm3/src/params/Env.i3.html">Env</A> AS ProcessEnv;
IMPORT <A HREF="../../obliqrt/src/ObPathSep.i3.html">ObPathSep</A>;

PROCEDURE <A NAME="FmtSearchPath"><procedure>FmtSearchPath</procedure></A>(searchPath: SearchPath): TEXT  =
  BEGIN
    IF searchPath=NIL THEN RETURN &quot;&quot;;
    ELSIF searchPath.rest=NIL THEN RETURN searchPath.first;
    ELSE RETURN
      searchPath.first &amp;
        Text.FromChar(SearchPathSeparator) &amp;
        FmtSearchPath(searchPath.rest);
    END;
  END FmtSearchPath;

PROCEDURE <A NAME="LexSearchPath"><procedure>LexSearchPath</procedure></A>(rd: TextRd.T): SearchPath =
  VAR item, junk: TEXT; rest: SearchPath;
  BEGIN
    TRY
      IF Rd.EOF(rd) (* NOWARN *) THEN RETURN NIL
      ELSE
        junk :=
            Lex.Scan(rd, (* NOWARN *)
                     Lex.Blanks + SET OF CHAR{SearchPathSeparator}); (*NOWARN*)
        item :=
            Lex.Scan(rd, (* NOWARN *)
                     Lex.NonBlanks - SET OF CHAR{SearchPathSeparator});(*NOWARN*)
        IF Text.Empty(junk) AND Text.Empty(item) THEN RETURN NIL END;
        rest := LexSearchPath(rd);
        IF Text.Empty(item) THEN RETURN rest;
        ELSIF NOT Pathname.Valid(item) THEN RETURN rest;
        ELSE RETURN NEW(SearchPath, first:=item, rest:=rest);
        END;
      END;
    EXCEPT
    | Rd.Failure, Thread.Alerted =&gt; RETURN NIL;
    END;
  END LexSearchPath;

PROCEDURE <A NAME="PostFile"><procedure>PostFile</procedure></A>(sc: SynScan.T; filename: Pathname.T): BOOLEAN =
  VAR rd: Rd.T;
  BEGIN
    TRY
      rd:= FileRd.Open(filename);
      SynWr.Text(SynWr.out, &quot;Loading '&quot; &amp; filename &amp; &quot;'\n&quot;);
      SynScan.PushInput(sc, Pathname.Last(filename), rd, TRUE, TRUE);
      RETURN TRUE;
    EXCEPT OSError.E =&gt; RETURN FALSE
    END;
  END PostFile;

PROCEDURE <A NAME="LoadFile"><procedure>LoadFile</procedure></A>(sc: SynScan.T; filename: Pathname.T;
  complain: BOOLEAN:=TRUE) =
  VAR scan: SearchPath;
  BEGIN
    IF Pathname.Valid(filename) THEN
      IF Pathname.Absolute(filename) THEN
        IF PostFile(sc, filename) THEN RETURN END;
      ELSE
        scan := searchPath;
        WHILE scan # NIL DO
          IF PostFile(sc,
               Pathname.Join(scan.first, filename, NIL))
          THEN RETURN
          END;
          scan := scan.rest;
        END;
      END;
    END;
    IF complain THEN
      SynScan.ErrorMsg(sc, &quot;Could not open file '&quot; &amp; filename
        &amp; &quot;' along path '&quot; &amp; FmtSearchPath(searchPath) &amp; &quot;'&quot;);
    END;
  END LoadFile;

PROCEDURE <A NAME="ModuleFrame"><procedure>ModuleFrame</procedure></A>(sc: SynScan.T; name, for: TEXT;
  imports: NameList; env: Env) RAISES {ObErr.Fail} =
</PRE><BLOCKQUOTE><EM> Push scanner inputs so it will first load the imports first
   to last, then establish a frame for this module, and then
   finish reading this module. The last PushInput is executed first. </EM></BLOCKQUOTE><PRE>
  BEGIN
    SynScan.PushInput(sc, &quot;&lt;none&gt;&quot;,
      TextRd.New(&quot;establish &quot; &amp; name &amp; &quot; for &quot; &amp; for &amp; &quot;;\n&quot;),
      TRUE, TRUE);
    LoadImports(sc, imports, env);
  END ModuleFrame;

PROCEDURE <A NAME="ModuleEnd"><procedure>ModuleEnd</procedure></A>(sc: SynScan.T; ideList: NameList) =
  VAR qual := &quot;qualify&quot;;
      first := TRUE;
  BEGIN
    IF ideList # NIL THEN
      qual := qual &amp; &quot; exporting&quot;;
    END;
    WHILE ideList # NIL DO
      IF first THEN
        first := FALSE;
        qual := qual &amp; &quot; &quot; &amp; ideList.first;
      ELSE
        qual := qual &amp; &quot;, &quot; &amp; ideList.first;
      END;
      ideList := ideList.rest;
    END;
    SynScan.PushInput(sc, &quot;&lt;none&gt;&quot;, TextRd.New(qual &amp; &quot;;\n&quot;), TRUE, TRUE);
  END ModuleEnd;

PROCEDURE <A NAME="LoadImports"><procedure>LoadImports</procedure></A>(sc: SynScan.T; imports: NameList; env: Env)
  RAISES {ObErr.Fail} =
</PRE><BLOCKQUOTE><EM> last to first, so the scanner will see them first to last </EM></BLOCKQUOTE><PRE>
  BEGIN
    IF imports#NIL THEN
      LoadImports(sc, imports.rest, env);
      ImportFrame(sc, imports.first, env);
    END;
  END LoadImports;

PROCEDURE <A NAME="ImportFrame"><procedure>ImportFrame</procedure></A>(sc: SynScan.T; name: TEXT; env: Env) =
  VAR scan: Env;
  BEGIN
    scan:=FindFrame(name, env);
    IF scan=NIL THEN LoadFile(sc, name &amp; &quot;.obl&quot;);
    ELSIF SynScan.TopLevel(sc) THEN
      SynWr.Text(SynWr.out, &quot;(Frame '&quot; &amp; name &amp;
        &quot;' already exists and has not been reloaded)\n&quot;);
    END;
  END ImportFrame;

PROCEDURE <A NAME="ModAndLib"><procedure>ModAndLib</procedure></A>(name, for: TEXT): TEXT =
  BEGIN
    IF Text.Equal(name, for) THEN RETURN &quot;'&quot; &amp; name &amp; &quot;'&quot;
    ELSE RETURN &quot;'&quot; &amp; name &amp; &quot;' for '&quot; &amp; for &amp; &quot;'&quot; END;
  END ModAndLib;

PROCEDURE <A NAME="EstablishFrame"><procedure>EstablishFrame</procedure></A>(name, for: TEXT; env: Env): Env
    RAISES {ObErr.Fail} =
  VAR moduleExists, frameExists: BOOLEAN;
  BEGIN
    SynWr.Text(SynWr.out, &quot;Establishing &quot; &amp; ModAndLib(name,for) &amp; &quot;\n&quot;);
    moduleExists := ObLib.Lookup(name, env.libEnv)#NIL;
    frameExists := FindFrame(name, env)#NIL;
    IF frameExists THEN
      RETURN SaveFrame(name, for, DeleteFrame(name, env));
    ELSIF moduleExists THEN
      ObErr.Fault(SynWr.out,
        &quot;Module name conflicts with existing library: '&quot; &amp; name &amp; &quot;_'&quot;);
      &lt;*ASSERT FALSE*&gt;
    ELSE
      RETURN SaveFrame(name, for, env);
    END;
  END EstablishFrame;

PROCEDURE <A NAME="SaveFrame"><procedure>SaveFrame</procedure></A>(name, for: TEXT; env: Env): Env
    RAISES {ObErr.Fail} =
  VAR scan: Env;
  BEGIN
    scan:=FindFrame(name, env);
    IF scan#NIL THEN
      ObErr.Fault(SynWr.out, &quot;Frame already exists: '&quot; &amp; name &amp; &quot;'&quot;);
      RETURN env;
    END;
    IF NOT Text.Empty(name) THEN
      SynWr.Text(SynWr.out, &quot;(Created frame &quot; &amp; ModAndLib(name,for) &amp; &quot;)\n&quot;);
    END;
    RETURN
      NEW(Env,
        frameName := name,
        forName := for,
        libEnv := env.libEnv,
        scopeEnv := env.scopeEnv,
        checkEnv := env.checkEnv,
        valueEnv := env.valueEnv,
        nextFrame := env);
  END SaveFrame;

PROCEDURE <A NAME="DeleteFrame"><procedure>DeleteFrame</procedure></A>(name: TEXT; env: Env): Env =
  VAR scan: Env;
  BEGIN
    scan:=FindFrame(name, env);
    IF scan=NIL THEN
      RETURN env;
    ELSE
      LOOP
        SynWr.Text(SynWr.out,
          &quot;(Deleted frame &quot; &amp; ModAndLib(env.frameName,env.forName) &amp; &quot;)\n&quot;);
	IF env=scan THEN EXIT END;
	env:=env.nextFrame;
      END;
      RETURN scan.nextFrame;
    END;
  END DeleteFrame;

PROCEDURE <A NAME="FindFrame"><procedure>FindFrame</procedure></A>(name: TEXT; env: Env): Env =
  VAR scan: Env;
  BEGIN
    scan:=env;
    LOOP
      IF scan=NIL THEN EXIT END;
      IF Text.Equal(scan.frameName, name) THEN EXIT END;
      scan := scan.nextFrame;
    END;
    RETURN scan;
  END FindFrame;

TYPE
  FrameLib =
    ObLib.T OBJECT
      OVERRIDES
        Eval := FrameLibEval;
      END;

PROCEDURE <A NAME="InNameList"><procedure>InNameList</procedure></A>(name: TEXT; list: NameList): BOOLEAN =
  BEGIN
    WHILE list # NIL DO
      IF Text.Equal(name, list.first) THEN RETURN TRUE END;
      list := list.rest;
    END;
    RETURN FALSE;
  END InNameList;

PROCEDURE <A NAME="QualifyFrame"><procedure>QualifyFrame</procedure></A>(env: Env; ideList: NameList): Env =
  VAR scanValueEnv: ObValue.Env;
      frameCount, frameSize: INTEGER; opCodes: REF ObLib.OpCodes;
      library: ObLib.T; newLibEnv: ObLib.Env; newEnv: Env;
      seen, tail: TextList.T := NIL;
  BEGIN
    IF Text.Empty(env.frameName) THEN RETURN env END;
    scanValueEnv := env.valueEnv;
    frameSize := 0;
    frameCount := 0;
    LOOP
      IF scanValueEnv=env.nextFrame.valueEnv THEN EXIT END;
      IF ideList = NIL OR InNameList(scanValueEnv.name.text, ideList) THEN
        IF NOT TextList.Member(seen, scanValueEnv.name.text) THEN
          (* want a list of the elements in the same order *)
          IF seen = NIL THEN
            tail := TextList.List1(scanValueEnv.name.text);
            seen := tail;
          ELSE
            tail.tail := TextList.List1(scanValueEnv.name.text);
            tail := tail.tail;
          END;
          INC(frameSize);
        END;
      END;
      INC(frameCount);
      scanValueEnv:=scanValueEnv.rest;
    END;
    opCodes := NEW(REF ObLib.OpCodes, frameSize);
    scanValueEnv := env.valueEnv;
    frameSize := 0;
    FOR i:=0 TO frameCount-1 DO
      IF seen # NIL AND Text.Equal(scanValueEnv.name.text, seen.head) THEN
        seen := seen.tail;
        opCodes[frameSize] :=
            NEW(FrameOpCode, name:=scanValueEnv.name.text,
                arity := -2, fixity := ObLib.OpFixity.Qualified,
                val := NARROW(scanValueEnv, ObValue.LocalEnv).val);
        INC(frameSize);
      END;
      scanValueEnv:=scanValueEnv.rest;
    END;
    library := NEW(FrameLib, name:=env.forName, opCodes:=opCodes);
    newLibEnv := ObLib.Extend(library, env.libEnv);
    newEnv :=
      NEW(Env,
          frameName := env.frameName,
          forName := env.forName,
          libEnv := newLibEnv,
          scopeEnv := env.nextFrame.scopeEnv,
          checkEnv := env.nextFrame.checkEnv,
          valueEnv := env.nextFrame.valueEnv,
          nextFrame := env.nextFrame);
    SynWr.Text(SynWr.out,
      &quot;(Closed frame &quot; &amp; ModAndLib(env.frameName,env.forName) &amp; &quot;)\n&quot;);
    RETURN newEnv;
  END QualifyFrame;

PROCEDURE <A NAME="FrameLibEval"><procedure>FrameLibEval</procedure></A>(self: FrameLib; opCode: ObLib.OpCode;
                       arity: ObLib.OpArity; READONLY args: ObValue.ArgArray;
                       &lt;*UNUSED*&gt;temp: BOOLEAN; loc: SynLocation.T)
  : ObValue.Val RAISES {ObValue.Error, ObValue.Exception} =
  VAR frameOpCode: FrameOpCode;
  BEGIN
    frameOpCode := NARROW(opCode, FrameOpCode);
    IF arity = -1 THEN
      RETURN frameOpCode.val;
    ELSIF arity &gt; NUMBER(args) THEN
      ObValue.RaiseError(&quot;Too many arguments&quot;, loc);
      &lt;*ASSERT FALSE*&gt;
    ELSE
      TYPECASE frameOpCode.val OF
      | ObValue.ValFun(fun) =&gt;
        RETURN ObEval.Call(fun, SUBARRAY(args, 0, arity), loc);
      ELSE
         ObValue.RaiseError(&quot;Not expecting argument list for: &quot; &amp;
          self.name &amp; &quot;_&quot; &amp; opCode.name, loc);
         &lt;*ASSERT FALSE*&gt;
      END;
    END;
  END FrameLibEval;

TYPE
  HelpCommand = ObCommand.T OBJECT
    short, long: TEXT;
  END;

PROCEDURE <A NAME="AddHelpFrame"><procedure>AddHelpFrame</procedure></A>(name, sort, short, long: TEXT;
                          &lt;*UNUSED*&gt;env: Env) =
  (* add a help file for this module *)
  BEGIN
    ObCommand.Register(ObLib.helpCommandSet,
                       NEW(HelpCommand, name:=name,
                           sortingName:= sort,
                           short := short, long := long,
                           Exec:= Help));
  END AddHelpFrame;

PROCEDURE <A NAME="Help"><procedure>Help</procedure></A> (comm: ObCommand.T; arg : TEXT; &lt;* UNUSED *&gt; data : REFANY) =
  VAR self := NARROW(comm, HelpCommand);
  BEGIN
    IF Text.Equal (arg, &quot;!&quot;) THEN
      SynWr.Text (SynWr.out,
                  &quot;  &quot; &amp; Fmt.Pad (self.name, 18, ' ', Fmt.Align.Left) &amp;
                  &quot;(&quot; &amp; self.short &amp; &quot;)\n&quot;);
    ELSIF Text.Equal (arg, &quot;?&quot;) THEN
      SynWr.Text (SynWr.out, self.long);
      SynWr.NewLine (SynWr.out);
    ELSE
      SynWr.Text(SynWr.out, &quot;Command &quot; &amp; self.name &amp; &quot;: bad argument: &quot; &amp; arg);
      SynWr.NewLine (SynWr.out);
    END;
  END Help;

PROCEDURE <A NAME="Setup"><procedure>Setup</procedure></A>()  =
  VAR envPath: TEXT;
  BEGIN
    envPath := ProcessEnv.Get(&quot;OBLIQPATH&quot;);
    IF envPath=NIL THEN
      searchPath :=
        NEW(SearchPath,
            first:=Pathname.Current,
            rest:= NIL);
    ELSE
      searchPath := LexSearchPath(TextRd.New(envPath));
    END;
  END Setup;

TYPE
  ObFrameSpecial = Pickle.Special BRANDED OBJECT
                       OVERRIDES
                         write := WriteLib;
                         read := ReadLib;
                       END;

TYPE
  LocalHandle = OpCodeHandle OBJECT
                       OVERRIDES
                         getOpCodes := GetOpCodes;
                       END;

VAR
  handle := NEW(LocalHandle);
</PRE> Implementation note:  I think this is guaranteed to work.  The only
   worry is if the library can disappear via garbage collection
   between the time the pickle is written and the time GetOpCodes is
   called.  I don't think this can happen.  Since all netobj calls are
   synchronous, the higher level caller of the pickler will write all
   the info and then wait for the return value from the remote call.
   The remote process reads all the info, which includes calling
   ReadLib below and potentially calling GetOpCodes, before the remote
   procedure is executed and the return value written. 

<P><PRE>PROCEDURE <A NAME="GetOpCodes"><procedure>GetOpCodes</procedure></A> (&lt;*UNUSED*&gt;self: LocalHandle;
                      ts: Fingerprint.T): REF ObLib.OpCodes=
  VAR lib := ObLib.LookupFP(ts);
  BEGIN
    IF lib = NIL THEN RETURN NIL END;
    RETURN lib.opCodes;
  END GetOpCodes;

PROCEDURE <A NAME="WriteLib"><procedure>WriteLib</procedure></A> (&lt;*UNUSED*&gt;ts: ObFrameSpecial;
                    ref: REFANY; out: Pickle.Writer)
  RAISES {Pickle.Error, Wr.Failure, Thread.Alerted} =
  VAR o := NARROW(ref, FrameLib);
  BEGIN
    ObLib.CheckFP(o);
    PickleStubs.OutBytes(out, o.ts.byte);
    PickleStubs.OutText(out, o.name);
    PickleStubs.OutRef(out, handle);
  END WriteLib;

PROCEDURE <A NAME="ReadLib"><procedure>ReadLib</procedure></A> (&lt;*UNUSED*&gt;ts: ObFrameSpecial;
                   in: Pickle.Reader;
                   id: Pickle.RefID):REFANY
  RAISES {Pickle.Error, Rd.EndOfFile, Rd.Failure, Thread.Alerted} =
  VAR fp: Fingerprint.T;
      rHandle: OpCodeHandle;
      lib: FrameLib := NIL;
      name: TEXT;
  BEGIN
    PickleStubs.InBytes(in, fp.byte);
    name := PickleStubs.InText(in);
    rHandle := PickleStubs.InRef(in);

    lib := ObLib.LookupFP(fp);
    IF lib = NIL THEN
      TRY
        lib := NEW(FrameLib, name:=name, opCodes:= NIL, ts:=fp);
        in.noteRef(lib, id);
        lib.opCodes := rHandle.getOpCodes(fp);
        (* check again, using the full object *)
        lib := ObLib.LookupFP(fp, lib);
      EXCEPT NetObj.Error =&gt; END;
    ELSE
      in.noteRef(lib, id);
    END;
    RETURN lib;
  END ReadLib;

BEGIN
  Pickle.RegisterSpecial(NEW(ObFrameSpecial, sc := TYPECODE(FrameLib)));
  SearchPathSeparator := ObPathSep.SearchPathSeparator;
END ObFrame.
</PRE>
</inModule>
<PRE>























</PRE>
</BODY>
</HTML>
