<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: kext/src/ExtBody.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>kext/src/ExtBody.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright (c) 2000 California Institute of Technology 
 All rights reserved. See the file COPYRIGHT for a full description. 
 $Id: ExtBody.m3.html,v 1.3 2010-04-29 17:18:48 wagner Exp $ 

<P><PRE>MODULE <module><implements><A HREF="ExtBody.i3.html">ExtBody</A></implements></module>;
IMPORT <A HREF="../../ktoklib/src/FileRdErr.i3.html">FileRdErr</A>;
IMPORT <A HREF="../../libm3/src/fmtlex/Scan.i3.html">Scan</A>;
IMPORT <A HREF="ExtSection.i3.html">ExtSection</A>;
IMPORT <A HREF="Override.i3.html">Override</A>;
IMPORT <A HREF="../../ktoklib/src/BracedCode.i3.html">BracedCode</A>;
IMPORT <A HREF="../../ktoklib/src/CharRange.i3.html">CharRange</A>;
IMPORT <A HREF="LoadSpec.i3.html">LoadSpec</A>;
IMPORT <A HREF="../../cit_util/src/TextSubs.i3.html">TextSubs</A>;
IMPORT <A HREF="../../cit_common/src/TextReader.i3.html">TextReader</A>;
IMPORT <A HREF="../../ktoklib/src/Pragma.i3.html">Pragma</A>;
IMPORT <A HREF="../../ktoklib/src/PragmaRead.i3.html">PragmaRead</A>;
IMPORT <A HREF="../../m3core/src/text/Text.i3.html">Text</A>;
IMPORT <A HREF="../../libm3/derived/TextTextTbl.i3.html">TextTextTbl</A>;
IMPORT <A HREF="../../libm3/src/rw/Wr.i3.html">Wr</A>, <A HREF="../../libm3/src/rw/TextWr.i3.html">TextWr</A>;
IMPORT <A HREF="../../libm3/src/rw/Rd.i3.html">Rd</A>, <A HREF="../../libm3/src/rw/TextRd.i3.html">TextRd</A>;
IMPORT <A HREF="../../m3core/src/thread/Common/Thread.i3.html">Thread</A>;
IMPORT <A HREF="../../ktoklib/src/Import.i3.html">Import</A>;
IMPORT <A HREF="../../m3core/src/float/IEEE-default/FloatMode.i3.html">FloatMode</A>, <A HREF="../../libm3/src/fmtlex/Lex.i3.html">Lex</A>;
IMPORT <A HREF="../../libm3/src/fmtlex/Fmt.i3.html">Fmt</A>;
IMPORT <A HREF="InitGen.i3.html">InitGen</A>;
</PRE><BLOCKQUOTE><EM> IMPORT Term; </EM></BLOCKQUOTE><PRE>

&lt;* FATAL Rd.EndOfFile, Rd.Failure, Wr.Failure, Thread.Alerted *&gt;
&lt;* FATAL FloatMode.Trap, Lex.Error *&gt;

TYPE
  Self = OBJECT
    secondPass: TextSubs.T;
    type, proc: Override.T;
    pragSubs: TextTextTbl.T;
    spec: LoadSpec.Info;
    typeExt := &quot;&quot;;
  END;

  SelfPragma = Pragma.T OBJECT
    self: Self;
    key: TEXT;
  END;

PROCEDURE <A NAME="PrintAlloc"><procedure>PrintAlloc</procedure></A>(self: Self; body: TEXT; VAR cur: INTEGER; wr: Wr.T) =
  VAR
    rd := TextRd.New(body);
    start, len: INTEGER;
    name: TEXT;
  BEGIN
    (* hopefully only lexers use this *)
    Rd.Seek(rd, cur);
    WHILE Rd.GetChar(rd) IN CharRange.AlphaNum DO END; Rd.UnGetChar(rd);
    WHILE Rd.GetChar(rd) IN CharRange.WhiteSpace DO END; Rd.UnGetChar(rd);
    start := Rd.Index(rd);
    TRY
      WHILE Rd.GetChar(rd) IN CharRange.AlphaNum DO END; Rd.UnGetChar(rd);
    EXCEPT
      Rd.EndOfFile =&gt;
    END;
    len := Rd.Index(rd) - start;
    name := Text.Sub(body, start, len);
    EVAL self.spec.allocTypes.put(name, NIL);
    Wr.PutText(wr, &quot;VAR result:&quot; &amp; name &amp; &quot;:=NewPT(self.allocate_&quot; &amp; name &amp;
      &quot;,TYPECODE(&quot; &amp; name &amp; &quot;));BEGIN BEGIN\n    &quot;);
    Wr.PutText(wr, FixTheBody(self, BracedCode.GetAhead(rd), 0, NIL));
    Wr.PutText(wr, &quot;\n   END;RETURN result;END&quot;);
    cur := Rd.Index(rd);
  END PrintAlloc;

PROCEDURE <A NAME="FixTheBody"><procedure>FixTheBody</procedure></A>(self: Self; body: TEXT; argCount: INTEGER;rd:Rd.T): TEXT =
  VAR
    argUsed := NEW(REF ARRAY OF BOOLEAN, argCount+1);
    cur, last, save: INTEGER := 0;
    c: CHAR;
    wr := TextWr.New();
    pre: TEXT := &quot;&quot;;
  PROCEDURE DoVal() =
    BEGIN
      IF cur = Text.Length(body) OR Text.GetChar(body, cur) # '.' THEN
        Wr.PutText(wr, &quot;.val&quot;);
      END;
    END DoVal;
  BEGIN
    FOR i := 1 TO LAST(argUsed^) DO argUsed[i] := FALSE; END;
    WHILE cur # Text.Length(body) DO
      cur := Text.FindChar(body, '$', last);
      IF cur = -1 THEN
        cur := Text.Length(body);
      END;
      Wr.PutText(wr, Text.Sub(body, last, cur-last));
      IF cur &lt; Text.Length(body) THEN
        INC(cur);
        IF cur = Text.Length(body) THEN
          c := ' ';
        ELSE
          c := Text.GetChar(body, cur);
        END;
        CASE c OF
        | '1'..'9' =&gt;
          save := cur;
          Wr.PutChar(wr, 'n');
          WHILE cur &lt; Text.Length(body) AND
            Text.GetChar(body, cur) IN CharRange.Digit DO
            Wr.PutChar(wr, Text.GetChar(body, cur));
            INC(cur);
          END;
          save := Scan.Int(Text.Sub(body, save, cur-save));
          IF save &gt; argCount THEN
            FileRdErr.E(rd, &quot;parameter out of range: $&quot; &amp; Fmt.Int(save));
          END;
          argUsed[save] := TRUE;
          DoVal();
        | 'R' =&gt;
          PrintAlloc(self, body, cur, wr);
        | '$' =&gt;
          INC(cur);
          Wr.PutText(wr, &quot;result&quot;);
          DoVal();
        ELSE
          Wr.PutText(wr, &quot;self.getText()&quot;);
        END;
        last := cur;
      END;
    END;
    FOR i := 1 TO argCount DO
      IF NOT argUsed[i] THEN
        pre := pre &amp; &quot;EVAL n&quot; &amp; Fmt.Int(i) &amp; &quot;;&quot;;
      END;
    END;
    RETURN pre &amp; TextWr.ToText(wr);
  END FixTheBody;

PROCEDURE <A NAME="ProcSubs"><procedure>ProcSubs</procedure></A>(self: Self; form, body: TEXT; argCount: INTEGER;
                   rd: Rd.T): TEXT =
  VAR
    subs := NEW(TextSubs.T).init();
  BEGIN
    (*    Term.WrLn(&quot;ProcSubs: &quot; &amp; form &amp; &quot;/&quot; &amp; body); *)
    subs.add(&quot;\\\n&quot;, &quot;&quot;);
    subs.add(&quot;%body&quot;, FixTheBody(self, body, argCount, rd));
    subs.add(&quot;%yaccName&quot;, self.spec.methMN);
    RETURN subs.apply(form);
  END ProcSubs;

PROCEDURE <A NAME="ParseProc"><procedure>ParseProc</procedure></A>(p: SelfPragma; rd: Rd.T) =
  VAR
    self := p.self;
    name, mn, bodyform := &quot;&quot;;
    c: CHAR;
    frag: TEXT;
    argCount: INTEGER;
  BEGIN
    WHILE Rd.GetChar(rd) IN CharRange.WhiteSpace DO END;
    Rd.UnGetChar(rd);
    REPEAT
      c := Rd.GetChar(rd);
      IF c IN CharRange.AlphaNum THEN name := name &amp; Text.FromChar(c);END;
    UNTIL NOT c IN CharRange.AlphaNum;
    IF c = '{' THEN Rd.UnGetChar(rd);END;
    frag := BracedCode.GetAhead(rd);
    IF c = ':' THEN
      IF Text.Length(frag) # 0 THEN
        EVAL self.spec.types.get(name, mn);
        self.type.add(name, &quot;  &quot; &amp; name &amp; &quot; = &quot; &amp; mn &amp; &quot;.&quot; &amp; name &amp;
          &quot; BRANDED \&quot;&quot; &amp; self.spec.outMN &amp; &quot;.&quot; &amp; name &amp; &quot;\&quot;&quot; &amp;
          &quot; OBJECT\n    &quot; &amp; frag &amp; &quot;\n  END;\n&quot;, rd);
        frag := InitGen.Get(&quot;\n    result.%name :=%val;&quot;,frag);
        IF self.spec.kind # 'y' THEN
          IF NOT Text.Equal(frag, &quot;&quot;) THEN
            FileRdErr.E(rd,&quot;Token fields cannot be automatically initialized&quot;);
          END;
        END;
        self.secondPass.add(&quot;(*%TYPEINIT%&quot; &amp; name &amp; &quot;%*)&quot;,frag);
      END;
      IF self.spec.kind#'l' THEN self.typeExt := &quot;_&quot; &amp; name;END;
    ELSE
      IF Text.Length(frag) # 0 THEN
        name := name &amp; self.typeExt;
        EVAL self.spec.procs.get(name, bodyform);
        argCount := 0;
        EVAL self.spec.argCount.get(name, argCount);
        self.proc.add(name, ProcSubs(self, bodyform, frag, argCount,rd), rd);
      END;
    END;
  END ParseProc;

PROCEDURE <A NAME="ExtraOver"><procedure>ExtraOver</procedure></A>(self: Self) =
  VAR
    argCount: INTEGER;
    proc, body, type: TEXT;
    iter := self.spec.procs.iterate();
  BEGIN
    IF self.spec.kind = 'y' THEN
      WHILE iter.next(proc, body) DO
        IF NOT self.proc.overridden(proc) THEN
          EVAL self.spec.retType.get(proc, type);
          EVAL self.spec.argCount.get(proc, argCount);
          IF self.type.overridden(type) THEN
            self.proc.add(proc,
                          ProcSubs(self, body,
                                   &quot;(* just allocating the new type *)&quot;,
                                   argCount, NIL), NIL);
          END;
        END;
      END;
    END;
  END ExtraOver;

PROCEDURE <A NAME="ParseSubs"><procedure>ParseSubs</procedure></A>(p: SelfPragma; rd: Rd.T) =
  VAR
    prev: TEXT;
  BEGIN
    EVAL p.self.pragSubs.get(p.key, prev);
    EVAL p.self.pragSubs.put(p.key, prev &amp; BracedCode.Match(rd));
    Rd.UnGetChar(rd);
    (* PragmaRead will eat the '}' *)
  END ParseSubs;

PROCEDURE <A NAME="Parse"><procedure>Parse</procedure></A>(from: Rd.T; READONLY spec: LoadSpec.Info): T =
  VAR
    prag := NEW(PragmaRead.T).init();
    subs := NEW(TextSubs.T).init();
    self := NEW(Self,
                secondPass := NEW(TextSubs.T).init(),
                type := NEW(Override.T).init(spec.types),
                proc := NEW(Override.T).init(spec.procs),
                spec := spec,
                pragSubs := NEW(TextTextTbl.Default).init());
    parseProc := NEW(SelfPragma, self := self, do := ParseProc);
    import := NEW(Import.T).init();
  PROCEDURE SubsPrags() =
    VAR
      pragma := ExtSection.GetText(spec.kind, ExtSection.T.Pragma);
      cur := NEW(TextReader.T).init(pragma).shatter(&quot;\t\n &quot;,&quot;&quot;,TRUE);
      parseSubs: SelfPragma;
    BEGIN
      WHILE cur # NIL DO
        EVAL self.pragSubs.put(cur.head, &quot;&quot;);
        parseSubs := NEW(SelfPragma, self := self,
                         key := cur.head, do := ParseSubs);
        prag.add(parseSubs, cur.head);
        cur := cur.tail;
      END;
    END SubsPrags;
  PROCEDURE PragSubs() =
    VAR
      iter := self.pragSubs.iterate();
      key, value: TEXT;
    BEGIN
      WHILE iter.next(key, value) DO
        subs.add(key, value);
      END;
    END PragSubs;
  PROCEDURE AllocFmt(form: TEXT): TEXT =
    VAR
      subs: TextSubs.T;
      iter := self.spec.allocTypes.iterate();
      name, dummy: TEXT;
      wr := TextWr.New();
    BEGIN
      WHILE iter.next(name, dummy) DO
        subs := NEW(TextSubs.T).init();
       (* subs.add(&quot;%tok&quot;, self.spec.tokMN); *)
        subs.add(&quot;%name&quot;, name);
        Wr.PutText(wr, subs.apply(form));
      END;
      RETURN TextWr.ToText(wr);
    END AllocFmt;
  BEGIN
    prag.add(parseProc, &quot;%proc&quot;);
    prag.add(parseProc, &quot;&quot;);
    SubsPrags();      (* add pragmas that substitute text *)
    prag.apply(from); (* read input *)
    PragSubs();       (* make substitutions collected by SubsPrags *)
    ExtraOver(self);  (* override remaining procs returning ext types *)
    subs.add(&quot;\\\n&quot;,&quot;&quot;);
    subs.add(&quot;%gen&quot;,&quot;(* generated by kext *)&quot;);
    self.type.importRemaining();
    subs.add(&quot;%gnTypes&quot;, self.type.getText());
    subs.add(&quot;%gnProcs&quot;, self.secondPass.apply(self.proc.getText()));
    subs.add(&quot;%ovr&quot;, self.proc.getProcAssignText());
    subs.add(&quot;%tok&quot;, spec.tokMN);
    subs.add(&quot;%name&quot;, spec.outMN);
    subs.add(&quot;%orig&quot;, spec.orig);
    subs.add(&quot;%tokOrig&quot;, spec.tokOrig);
    subs.add(&quot;%tkimp&quot;, subs.apply(ExtSection.Res(&quot;extform.tokimport.i3&quot;)));
    import.addModule(spec.tokMN);
    IF spec.methMN # NIL THEN
      import.addModule(spec.methMN);
      subs.add(&quot;%meth&quot;, spec.methMN);
    END;
    subs.add(&quot;%import&quot;, import.toDeclaration());
   subs.add(&quot;%alloc&quot;,AllocFmt(&quot;    allocate_%name: Allocator := NIL;\n&quot;));
    subs.add(&quot;%purge&quot;,AllocFmt(&quot;\n      + Purge(self.allocate_%name)&quot;));
    RETURN subs;
  END Parse;

BEGIN
END ExtBody.
</PRE>
</inModule>
<PRE>























</PRE>
</BODY>
</HTML>
