<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: events/src/RdWrMutex.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>events/src/RdWrMutex.m3</H2></A><HR>
<inModule>
<PRE></PRE>                            -*- Mode: Modula-3 -*- 
 * 
 * For information about this program, contact Blair MacIntyre            
 * (bm@cs.columbia.edu) or Steven Feiner (feiner@cs.columbia.edu)         
 * at the Computer Science Dept., Columbia University,                    
 * 1214 Amsterdam Ave. Mailstop 0401, New York, NY, 10027.                
 *                                                                        
 * Copyright (C) 1995, 1996 by The Trustees of Columbia University in the 
 * City of New York.  Blair MacIntyre, Computer Science Department.       
 * See file COPYRIGHT-COLUMBIA for details.
 * 
 * Author          : Blair MacIntyre
 * Created On      : Mon May 15 17:26:50 1995
 * Last Modified By: Blair MacIntyre
 * Last Modified On: Mon Aug  4 12:30:01 1997
 * Update Count    : 51
 * 
 * $Source: /opt/cvs/cm3/doc/help/gen_html/events/src/RdWrMutex.m3.html,v $
 * $Date: 2010-04-29 17:18:13 $
 * $Author: wagner $
 * $Revision: 1.5 $
 * 
 * $Log: not supported by cvs2svn $
 * Revision 1.4.2.1  2010-04-15 20:58:51  wagner
 * update generated HTML doc to RC5
 *
 * Revision 1.2  2001/12/02 00:20:38  wagner
 * add copyright notes, fix overrides for cm3, and make everything compile
 *
 * added: events/COPYRIGHT-COLUMBIA
 * added: events/src/COPYRIGHT-COLUMBIA
 * modified: events/src/Event.i3
 * modified: events/src/Event.m3
 * modified: events/src/EventConn.i3
 * modified: events/src/EventConn.m3
 * modified: events/src/EventCounter.i3
 * modified: events/src/EventCounter.m3
 * modified: events/src/EventHandle.i3
 * modified: events/src/EventIO.i3
 * modified: events/src/EventNumber.i3
 * modified: events/src/EventNumber.m3
 * modified: events/src/EventNumberF.i3
 * modified: events/src/EventPort.i3
 * modified: events/src/EventPort.m3
 * modified: events/src/EventProtocol.i3
 * modified: events/src/EventRd.i3
 * modified: events/src/EventRd.m3
 * modified: events/src/EventSpaceID.i3
 * modified: events/src/EventSpaceID.m3
 * modified: events/src/EventStubLib.i3
 * modified: events/src/EventStubLib.m3
 * modified: events/src/EventWireRep.i3
 * modified: events/src/EventWireRep.m3
 * modified: events/src/EventWr.i3
 * modified: events/src/EventWr.m3
 * modified: events/src/EventWrF.i3
 * modified: events/src/HostInfo.i3
 * modified: events/src/HostInfo.m3
 * modified: events/src/RdWrMutex.i3
 * modified: events/src/RdWrMutex.m3
 * modified: events/src/Work.i3
 * modified: events/src/WorkerPool.i3
 * modified: events/src/WorkerPool.m3
 * modified: events/src/Zombie.i3
 * modified: events/src/m3makefile
 * modified: events/src/m3overrides
 *
 * Revision 1.1.1.1  2001/12/02 00:06:45  wagner
 * Blair MacIntyre's events library
 *
 * Revision 1.6  1997/08/04 20:15:14  bm
 * Fixed BRANDs
 *
 * Revision 1.5  1997/01/23 15:26:41  bm
 * Lots of little bug fixes.
 *
 * Revision 1.4  1996/11/21 22:51:32  bm
 * fixed header
 *
 * 
 * HISTORY
 

<P><PRE>MODULE <module><implements><A HREF="RdWrMutex.i3.html">RdWrMutex</A></implements></module>;

IMPORT <A HREF="../../m3core/src/thread/Common/Thread.i3.html">Thread</A>, <A HREF="#x1">ThreadF</A>, <A HREF="../../libm3/derived/IntList.i3.html">IntList</A>, <A HREF="../../listfuncs/derived/IntListFuncs.i3.html">IntListFuncs</A>, <A HREF="../../libm3/src/os/Common/Process.i3.html">Process</A>, <A HREF="../../libm3/src/fmtlex/Fmt.i3.html">Fmt</A>, <A HREF="../../libm3/src/rw/IO.i3.html">IO</A>;
IMPORT <A HREF="../../parseparams/src/ParseParams.i3.html">ParseParams</A>, <A HREF="../../libm3/src/rw/Stdio.i3.html">Stdio</A>;

VAR
  debug: BOOLEAN := FALSE;

REVEAL
  <A NAME="T">T</A> = Public BRANDED &quot;RdWrMutex.T&quot; OBJECT
    count: INTEGER;
    cv: Thread.Condition;
    mu: Thread.Mutex;
    id: INTEGER := -1;
    readers: IntList.T := NIL;
  OVERRIDES
    init := Init;
    acquireRead := AcquireRead;
    acquireWrite := AcquireWrite;
    releaseRead := ReleaseRead;
    releaseWrite := ReleaseWrite;
    wait := Wait;
  END;

PROCEDURE <A NAME="RemoveReader"><procedure>RemoveReader</procedure></A>(self: T) =
  BEGIN
    WITH num = IntList.Length(self.readers) DO
      IF IntListFuncs.DeleteD(self.readers, ThreadF.MyId()) = NIL THEN
        Process.Crash(&quot;RdWrMutex: releaseRead called by non-locker.\n&quot; &amp;
          &quot;Releasing Reader with &quot; &amp; Fmt.Int(num) &amp; &quot; readers resulting in &quot; &amp;
          Fmt.Int(IntList.Length(self.readers)) &amp; &quot; readers.\n&quot;);
      END;
    END;
  END RemoveReader;

PROCEDURE <A NAME="GetReaders"><procedure>GetReaders</procedure></A>(self: T): IntList.T =
   BEGIN
     RETURN IntListFuncs.DeleteAllD(self.readers, ThreadF.MyId());
   END GetReaders;

PROCEDURE <A NAME="Init"><procedure>Init</procedure></A>(self: T): T =
  BEGIN
    self.count := 0;
    self.cv := NEW(Thread.Condition);
    self.mu := NEW(Thread.Mutex);
    RETURN self;
  END Init;

PROCEDURE <A NAME="AcquireRead"><procedure>AcquireRead</procedure></A>(self: T) =
  BEGIN
    LOCK self.mu DO
      IF self.count &lt; 0 THEN
        IF self.id # ThreadF.MyId() THEN
          (* If the lock is held by a writer other than ourself, wait
             for the write lock to be released. *)
          WHILE self.count &lt; 0 DO Thread.Wait(self.mu, self.cv) END;

          (* Now it's ours, so mark it as acquired by another reader *)
          INC(self.count);
        ELSE
          (* Otherwise, it is held by ourself as a writer, so we
             don't change the count and continue on *)
        END;
      ELSE
        (* If the lock is free or held by readers, increment the count of
           readers.  *)
        INC(self.count);
      END;

      (* add to the list of readers. *)
      self.readers := IntList.Cons(ThreadF.MyId(), self.readers);

      IF debug THEN
        IO.Put(&quot;RdWrMutex: AcquireRead succeeded for &quot; &amp;
          Fmt.Int(ThreadF.MyId()) &amp; &quot;, count=&quot; &amp; Fmt.Int(self.count) &amp; &quot;\n&quot;);
      END;
    END;
  END AcquireRead;

PROCEDURE <A NAME="AcquireWrite"><procedure>AcquireWrite</procedure></A>(self: T) =
  VAR
    myReaders: IntList.T := NIL;
  BEGIN
    LOCK self.mu DO
      IF self.count &lt; 0 THEN
        IF self.id = ThreadF.MyId() THEN
          (* We already have the write lock, increment our lock count and
             return. *)
          DEC(self.count);
          IF debug THEN
            IO.Put(&quot;RdWrMutex: AcquireWrite recursively called by &quot; &amp;
              Fmt.Int(ThreadF.MyId()) &amp; &quot;, count=&quot; &amp; Fmt.Int(self.count) &amp;
              &quot;\n&quot;);
          END;
          RETURN;
        END;
      ELSIF self.count &gt; 0 THEN
        (* temporarily &quot;release&quot; our read locks. *)
        WITH num = IntList.Length(self.readers) DO
          myReaders := GetReaders(self);
          &lt;* ASSERT IntList.Length(myReaders) +
                    IntList.Length(self.readers) = num *&gt;
          IF myReaders # NIL THEN
            DEC(self.count, IntList.Length(myReaders));
          END;
        END;
      END;

      (* Someone else has the write lock or a read lock.  Wait. *)
      WHILE self.count # 0 DO Thread.Wait(self.mu, self.cv) END;
      self.count := -1;
      self.id := ThreadF.MyId();
      self.readers := myReaders;
      IF debug THEN
        IO.Put(&quot;RdWrMutex: AcquireWrite succeeded for &quot; &amp;
          Fmt.Int(ThreadF.MyId()) &amp; &quot;, count=&quot; &amp; Fmt.Int(self.count) &amp;
          &quot;, numreaders=&quot; &amp; Fmt.Int(IntList.Length(self.readers)) &amp; &quot;\n&quot;);
      END;
    END;
  END AcquireWrite;

PROCEDURE <A NAME="ReleaseRead"><procedure>ReleaseRead</procedure></A>(self: T) =
  BEGIN
    LOCK self.mu DO
      (* First, remove our entry. *)
      RemoveReader(self);

      (* If the lock is held by readers, decrease the count. *)
      IF self.count &gt; 0 THEN
        DEC(self.count);
      END;

      IF self.count = 0 THEN
        Thread.Broadcast(self.cv)
      END;
      IF debug THEN
        IO.Put(&quot;RdWrMutex: ReleaseRead succeeded for &quot; &amp;
          Fmt.Int(ThreadF.MyId()) &amp; &quot;, count=&quot; &amp; Fmt.Int(self.count) &amp; &quot;\n&quot;);
      END;
    END;
  END ReleaseRead;

PROCEDURE <A NAME="ReleaseWrite"><procedure>ReleaseWrite</procedure></A>(self: T) =
  BEGIN
    LOCK self.mu DO
      (* replace asserts by if statements for now *)
      IF (self.count &gt;= 0) OR (self.id # ThreadF.MyId()) THEN
        Process.Crash(&quot;RdWrMutex: releaseWrite called by non-locker, &quot; &amp;
          &quot;count=&quot; &amp; Fmt.Int(self.count) &amp; &quot; \n&quot;);
      END;

      IF self.count = -1 THEN
        self.id := -1;
        self.count := IntList.Length(self.readers);
        Thread.Broadcast(self.cv);
      ELSE
        INC(self.count);
      END;
      IF debug THEN
        IO.Put(&quot;RdWrMutex: ReleaseWrite succeeded for &quot; &amp;
          Fmt.Int(ThreadF.MyId()) &amp; &quot;, count=&quot; &amp; Fmt.Int(self.count) &amp; &quot;\n&quot;);
      END;
    END;
  END ReleaseWrite;

PROCEDURE <A NAME="Wait"><procedure>Wait</procedure></A>(self: T; mu: Thread.Mutex; cv: Thread.Condition) =
  VAR
    myReaders: IntList.T := NIL;
    writeLockCount: INTEGER := 0;
    myId: INTEGER := -1;
  BEGIN
    LOCK self.mu DO
      IF debug THEN
        IO.Put(&quot;RdWrMutex: Wait going to sleep for &quot; &amp;
          Fmt.Int(ThreadF.MyId()) &amp; &quot;, count=&quot; &amp; Fmt.Int(self.count) &amp;
          &quot;\n&quot;);
      END;

      (* remember our state *)
      IF self.count &lt; 0 THEN
        myId := self.id;
        myReaders := self.readers;
        writeLockCount := self.count;

        self.id := -1;
        self.count := 0;
        self.readers := NIL;
      ELSIF self.count &gt; 0 THEN
        (* temporarily &quot;release&quot; our read locks. *)
        WITH num = IntList.Length(self.readers) DO
          myReaders := GetReaders(self);
          &lt;* ASSERT IntList.Length(myReaders) +
                    IntList.Length(self.readers) = num *&gt;
          IF myReaders # NIL THEN
            DEC(self.count, IntList.Length(myReaders));
          END;
        END;
      END;

      (* Wait *)
      IF self.count = 0 THEN
        Thread.Broadcast(self.cv)
      END;
    END;
    Thread.Wait(mu, cv);
    Thread.Release(mu);
    LOCK self.mu DO
      IF debug THEN
        IO.Put(&quot;RdWrMutex: Wait awoken, attempting to reacquire &quot; &amp;
          &quot;lock for &quot; &amp; Fmt.Int(ThreadF.MyId()) &amp; &quot;\n&quot;);
      END;

      IF writeLockCount &lt; 0 THEN
        (* Now, attempt to reacquire the write lock! *)
        (* While someone else has the write lock or a read lock,  wait. *)
        WHILE self.count # 0 DO Thread.Wait(self.mu, self.cv) END;

        (* Restore state *)
        self.count := writeLockCount;
        self.id := myId;
        self.readers := myReaders;
      ELSIF myReaders # NIL THEN
        (* Now, attempt to reacquire the read lock! *)
        (* While someone else has the write lock,  wait. *)
        WHILE self.count &lt; 0 DO Thread.Wait(self.mu, self.cv) END;

        (* add to the list of readers. *)
        self.readers := IntList.AppendD(self.readers, myReaders);
        INC(self.count, IntList.Length(myReaders));
      END;

      IF debug THEN
        IO.Put(&quot;RdWrMutex: Wait has reacquired write lock for &quot; &amp;
          Fmt.Int(ThreadF.MyId()) &amp; &quot;, count=&quot; &amp; Fmt.Int(self.count) &amp;
          &quot;, numreaders=&quot; &amp; Fmt.Int(IntList.Length(self.readers)) &amp; &quot;\n&quot;);
      END;
    END;
    Thread.Acquire(mu);
  END Wait;

BEGIN
  debug := NEW(ParseParams.T).init(Stdio.stderr).keywordPresent(&quot;@EVdebugrwm&quot;);
END RdWrMutex.
</PRE>
</inModule>
<HR>
<A NAME="x1">interface ThreadF is in:
</A><UL>
<LI><A HREF="../../m3core/src/thread/Common/ThreadF.i3.html#0TOP0">m3core/src/thread/Common/ThreadF.i3</A>
<LI><A HREF="../../m3core/src/thread/PTHREAD/ThreadF.i3.html#0TOP0">m3core/src/thread/PTHREAD/ThreadF.i3</A>
</UL>
<P>
<PRE>























</PRE>
</BODY>
</HTML>
