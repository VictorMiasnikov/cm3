<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: netobjd/src/NMonRegistrySvr.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>netobjd/src/NMonRegistrySvr.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright 1992 Digital Equipment Corporation.               
 Distributed only by permission.                             
 NMonRegistrySvr.m3 
 Last modified on Wed Sep  8 13:37:17 PDT 1993 by wobber 
      modified on Tue Sep 15 11:07:45 PDT 1992 by evers  

<P> An implementation of the <CODE>NetObjMon.Registry</CODE> service. 

<P><PRE>MODULE <module><implements><A HREF="NMonRegistrySvr.i3.html">NMonRegistrySvr</A></implements></module>;

IMPORT <A HREF="../../netobj/src/NetObjMon.i3.html">NetObjMon</A>, <A HREF="../../netobj/src/netobjrt/NetObj.i3.html">NetObj</A>, <A HREF="../../netobj/src/netobjrt/NetObjNotifier.i3.html">NetObjNotifier</A>;

IMPORT <A HREF="../../libm3/derived/TextRefTbl.i3.html">TextRefTbl</A>;

&lt;* PRAGMA LL *&gt;

TYPE
  T = NetObjMon.Registry OBJECT
    mu: MUTEX;
    &lt;* LL &gt;= {mu} *&gt;
    entries: TextRefTbl.T (* of NetObjMon.T *) := NIL;
  OVERRIDES
    register := RegistryRegister;
    list     := RegistryList;
    get      := RegistryGet;
  END;

  Note = NetObjNotifier.NotifierClosure OBJECT
    t: T;
    id: TEXT;
  OVERRIDES
    notify := DeadEntry;
  END;

PROCEDURE <A NAME="New"><procedure>New</procedure></A> (): NetObjMon.Registry =
  BEGIN
    RETURN NEW (T, mu := NEW (MUTEX),
                   entries := NEW(TextRefTbl.Default).init());
  END New;

PROCEDURE <A NAME="DeadEntry"><procedure>DeadEntry</procedure></A>(n: Note; obj: NetObj.T;
                    &lt;*UNUSED*&gt; st: NetObjNotifier.OwnerState) =
  VAR r: REFANY;
  BEGIN
    LOCK n.t.mu DO
      IF n.t.entries.get(n.id, r) AND r = obj THEN
        EVAL n.t.entries.delete(n.id, r);
      END;
    END;
  END DeadEntry;

PROCEDURE <A NAME="RegistryRegister"><procedure>RegistryRegister</procedure></A> (t: T; mon: NetObjMon.T; id: TEXT) =
  BEGIN
    IF mon # NIL THEN
      NetObjNotifier.AddNotifier(mon, NEW(Note, t := t, id := id));
    END;
    LOCK t.mu DO EVAL t.entries.put (id, mon); END;
  END RegistryRegister;

PROCEDURE <A NAME="RegistryList"><procedure>RegistryList</procedure></A> (t: T): REF ARRAY OF TEXT =
  VAR
    res: REF ARRAY OF TEXT;
  BEGIN
    LOCK t.mu DO
      res := NEW (REF ARRAY OF TEXT, t.entries.size());
      VAR it := t.entries.iterate();
          i := 0;
          r: REFANY;
          txt: TEXT;
      BEGIN
        WHILE it.next(txt, r) DO res[i] := txt; INC(i); END;
      END;
    END;
    RETURN res;
  END RegistryList;

PROCEDURE <A NAME="RegistryGet"><procedure>RegistryGet</procedure></A> (t: T; id: TEXT): NetObjMon.T =
  VAR
    res: REFANY;
  BEGIN
    LOCK t.mu DO
      IF t.entries.get (id, res) THEN
      	RETURN res;
      ELSE
      	RETURN NIL;
      END;
    END;
  END RegistryGet;

BEGIN
END NMonRegistrySvr.
</PRE>
</inModule>
<PRE>























</PRE>
</BODY>
</HTML>
