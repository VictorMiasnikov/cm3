<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: tcp/src/POSIX/TCP.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>tcp/src/POSIX/TCP.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright 1992 Digital Equipment Corporation. 
 Distributed only by permission. 
 Created on Sat Jan 11 15:49:00 PST 1992 by gnelson 
 Last modified on Mon Mar 27 17:18:33 PST 1995 by wobber 
      modified on Tue Feb  7 15:48:33 PST 1995 by kalsow 
      modified on Fri Jan  7 13:31:11 PST 1994 by msm    
      modified on Sun Jan 12 16:16:54 PST 1992 by meehan 
      modified on Sat Jan 11 16:55:00 PST 1992 by gnelson 
 $Id: TCP.m3.html,v 1.6 2010-04-29 17:20:11 wagner Exp $ 

<P><PRE>UNSAFE MODULE <module>TCP</module> EXPORTS <A HREF="../common/TCP.i3.html"><implements>TCP</A></implements>, <A HREF="../common/TCPMisc.i3.html"><implements>TCPMisc</A></implements>, <A HREF="../common/TCPSpecial.i3.html"><implements>TCPSpecial</A></implements>;

IMPORT <A HREF="../../../libm3/src/atom/Atom.i3.html">Atom</A>, <A HREF="../../../libm3/derived/AtomList.i3.html">AtomList</A>, <A HREF="../common/IP.i3.html">IP</A>, <A HREF="../../../libm3/src/rw/Rd.i3.html">Rd</A>, <A HREF="../../../libm3/src/rw/Wr.i3.html">Wr</A>, <A HREF="../../../m3core/src/thread/Common/Thread.i3.html">Thread</A>;
IMPORT <A HREF="#x1">Usocket</A>, <A HREF="#x2">Uerror</A>, <A HREF="../../../m3core/src/unix/uin-common/Uin.i3.html">Uin</A>, <A HREF="#x3">Unix</A>, <A HREF="#x4">Uuio</A>, <A HREF="#x5">Utypes</A>,
       <A HREF="../../../m3core/src/thread/Common/SchedulerPosix.i3.html">SchedulerPosix</A>, <A HREF="../../../libm3/src/fmtlex/Fmt.i3.html">Fmt</A>, <A HREF="../../../m3core/src/word/Word.i3.html">Word</A>;
IMPORT <A HREF="../common/ConnFD.i3.html">ConnFD</A>;
IMPORT <A HREF="../../../m3core/src/C/Common/Cerrno.i3.html">Cerrno</A>;
IMPORT <A HREF="TCPHack.i3.html">TCPHack</A>;
IMPORT <A HREF="TCPPosix.i3.html">TCPPosix</A>;
FROM <A HREF="../../../m3core/src/C/Common/Ctypes.i3.html">Ctypes</A> IMPORT char, int;

REVEAL
  <A NAME="Connector">Connector</A> = MUTEX BRANDED &quot;TCP.Connector&quot; OBJECT
    fd: INTEGER;       (*CONST*)
    ep: IP.Endpoint;   (*CONST*)
    closed: BOOLEAN := FALSE;
  END;

REVEAL
  <A NAME="T">T</A> = TCPPosix.Public BRANDED &quot;TCP.T&quot; OBJECT
    ep: IP.Endpoint;
    error: AtomList.T := NIL;
  OVERRIDES
    get := GetBytesFD;
    put := PutBytesFD;
    shutdownIn := ShutdownIn;
    shutdownOut := ShutdownOut;
    close := Close;
  END;

TYPE SockAddrIn = Uin.struct_sockaddr_in;

CONST TCP_NODELAY = 1;

CONST Sin_Zero = ARRAY [0 .. 7] OF char{VAL(0, char), ..};

VAR ClosedErr: AtomList.T;

PROCEDURE <A NAME="NewConnector"><procedure>NewConnector</procedure></A> (ep: IP.Endpoint): Connector RAISES {IP.Error} =
  VAR
    res                := NEW(Connector, ep := ep);
    name  : SockAddrIn;
    status: INTEGER;
    True: int := 1;  (* CONST *)
  BEGIN
    res.fd := Usocket.socket(Usocket.AF_INET, Usocket.SOCK_STREAM, 0 (* TCP*));
    IF res.fd = -1 THEN
      WITH errno = Cerrno.GetErrno() DO
        IF errno = Uerror.EMFILE OR errno = Uerror.ENFILE THEN
          Raise(IP.NoResources);
        ELSE
          RaiseUnexpected();
        END
      END
    END;
    MakeNonBlocking (res.fd);
    EVAL Usocket.setsockopt(res.fd, Usocket.SOL_SOCKET, Usocket.SO_REUSEADDR,
      ADR(True), BYTESIZE(True));
    name.sin_family := Usocket.AF_INET;
    name.sin_port := Uin.htons(ep.port);
    name.sin_addr.s_addr := LOOPHOLE(ep.addr, Utypes.u_int);
    name.sin_zero := Sin_Zero;
    status := Usocket.bind(res.fd, ADR(name), BYTESIZE(SockAddrIn));
    IF status # 0 THEN
      IF Cerrno.GetErrno() = Uerror.EADDRINUSE THEN
        Raise(IP.PortBusy);
      ELSE
        RaiseUnexpected();
      END
    END;
    IF Usocket.listen(res.fd, 8) # 0 THEN RaiseUnexpected(); END;
    RETURN res
  END NewConnector;

PROCEDURE <A NAME="GetEndPoint"><procedure>GetEndPoint</procedure></A>(c: Connector): IP.Endpoint =
  VAR
    namelen  : INTEGER;
    name  : SockAddrIn;
  BEGIN
    IF c.ep.addr = IP.NullAddress THEN
      c.ep.addr := IP.GetHostAddr();
    END;
    IF c.ep.port = IP.NullPort THEN
      namelen := BYTESIZE(SockAddrIn);
      IF Usocket.getsockname(c.fd, ADR(name), ADR(namelen)) # 0 THEN
        Die()
      END;
      c.ep.port := Uin.ntohs(name.sin_port);
    END;
    RETURN c.ep
  END GetEndPoint;

PROCEDURE <A NAME="Connect"><procedure>Connect</procedure></A> (ep: IP.Endpoint): T
    RAISES {IP.Error, Thread.Alerted} =
  VAR
    t := StartConnect(ep);
    ok := FALSE;
  BEGIN
    TRY
      EVAL FinishConnect(t);
      ok := TRUE;
    FINALLY
     IF NOT ok THEN Close(t); END;
    END;
    RETURN t;
  END Connect;

PROCEDURE <A NAME="StartConnect"><procedure>StartConnect</procedure></A>(to: IP.Endpoint;
                       from: IP.Endpoint := IP.NullEndPoint): T
    RAISES {IP.Error} =
  VAR
    fd: INTEGER;
    status: int;
    True: int := 1;  (* CONST *)
    fromName: SockAddrIn;
    ok := FALSE;
  BEGIN
    fd := Usocket.socket(Usocket.AF_INET, Usocket.SOCK_STREAM, 0 (* TCP*));
    IF fd &lt; 0 THEN
      WITH errno = Cerrno.GetErrno() DO
        IF errno = Uerror.EMFILE OR errno = Uerror.ENFILE THEN
          Raise(IP.NoResources);
        ELSE
          RaiseUnexpected();
        END;
      END;
    END;
    InitFD(fd);

    IF from # IP.NullEndPoint THEN  (* Bind to the &quot;from&quot; address. *)
      EVAL Usocket.setsockopt(fd, Usocket.SOL_SOCKET, Usocket.SO_REUSEADDR,
        ADR(True), BYTESIZE(True));
      fromName.sin_family := Usocket.AF_INET;
      fromName.sin_port := Uin.htons(from.port);
      fromName.sin_addr.s_addr := LOOPHOLE(from.addr, Utypes.u_int);
      fromName.sin_zero := Sin_Zero;
      status := Usocket.bind(fd, ADR(fromName), BYTESIZE(SockAddrIn));
      IF status # 0 THEN
        IF Cerrno.GetErrno() = Uerror.EADDRINUSE THEN
          Raise(IP.PortBusy);
        ELSE
          RaiseUnexpected();
        END
      END;
    END;

    TRY
      EVAL CheckConnect(fd, to);
      ok := TRUE;
    FINALLY
      IF NOT ok THEN EVAL Unix.close(fd); END;
    END;
    RETURN NEW(T, fd := fd, ep := to);
  END StartConnect;

PROCEDURE <A NAME="FinishConnect"><procedure>FinishConnect</procedure></A>(t: T; timeout: LONGREAL := -1.0D0): BOOLEAN
    RAISES {IP.Error, Thread.Alerted} =
  BEGIN
    LOOP
      EVAL SchedulerPosix.IOAlertWait(t.fd, FALSE, timeout);
      LOCK t DO
        IF t.error # NIL THEN RAISE IP.Error(t.error); END;
        IF CheckConnect(t.fd, t.ep) THEN EXIT; END;
      END;
      IF timeout &gt;= 0.0D0 THEN RETURN FALSE; END;
    END;
    RETURN TRUE;
  END FinishConnect;

VAR seenBadFBug: BOOLEAN := FALSE;

PROCEDURE <A NAME="CheckConnect"><procedure>CheckConnect</procedure></A>(fd: INTEGER; ep: IP.Endpoint) : BOOLEAN
    RAISES {IP.Error} =
  VAR
    name: SockAddrIn;
    status: INTEGER;
  BEGIN
    name.sin_family := Usocket.AF_INET;
    name.sin_port := Uin.htons(ep.port);
    name.sin_addr.s_addr := LOOPHOLE(ep.addr, Utypes.u_int);
    name.sin_zero := Sin_Zero;
    status := Usocket.connect(fd, ADR(name), BYTESIZE(SockAddrIn));
    IF status = 0 THEN RETURN TRUE; END;
    WITH errno = Cerrno.GetErrno() DO
      IF errno = Uerror.EINVAL THEN
        (* special hack to try to get real errno, hidden due to NBIO bug in connect *)
        EVAL TCPHack.RefetchError(fd);
      ELSIF errno = Uerror.EBADF THEN
        (* we'll try the same for EBADF, which we've seen on Alpha *)
        IF TCPHack.RefetchError(fd) THEN seenBadFBug := TRUE END;
      END;
    END;
    WITH errno = Cerrno.GetErrno() DO
      IF (errno = Uerror.EISCONN) THEN
        RETURN TRUE;
      ELSIF  (errno = Uerror.EADDRNOTAVAIL)
          OR (errno = Uerror.ECONNREFUSED)
          OR (errno = Uerror.EINVAL)
          OR (errno = Uerror.ECONNRESET)
          OR (errno = Uerror.EBADF) THEN
        Raise(Refused);
      ELSIF (errno = Uerror.ETIMEDOUT) THEN
        Raise(Timeout);
      ELSIF  (errno = Uerror.ENETUNREACH)
          OR (errno = Uerror.EHOSTUNREACH)
          OR (errno = Uerror.EHOSTDOWN)
          OR (errno = Uerror.ENETDOWN) THEN
        Raise(IP.Unreachable);
      ELSIF  (errno = Uerror.EWOULDBLOCK)
          OR (errno = Uerror.EAGAIN)
          OR (errno = Uerror.EINPROGRESS)
          OR (errno = Uerror.EALREADY) THEN
      ELSE RaiseUnexpected();
      END;
    END;
    RETURN FALSE;
  END CheckConnect;

PROCEDURE <A NAME="Accept"><procedure>Accept</procedure></A>(c: Connector): T
    RAISES {IP.Error, Thread.Alerted} =
  VAR
    ep: IP.Endpoint;
  BEGIN
    RETURN AcceptFrom(c, ep);
  END Accept;

PROCEDURE <A NAME="CloseConnector"><procedure>CloseConnector</procedure></A>(c: Connector) =
  BEGIN
    LOCK c DO
      IF NOT c.closed THEN
        EVAL Unix.close(c.fd);
        c.closed := TRUE;
      END;
    END;
  END CloseConnector;

PROCEDURE <A NAME="EOF"><procedure>EOF</procedure></A>(t: T) : BOOLEAN =
  VAR status: INTEGER;  charsToRead: int;
  BEGIN
    LOCK t DO
      IF SchedulerPosix.IOWait(t.fd, TRUE, 0.0D0) =
                            SchedulerPosix.WaitResult.Ready THEN
        status := Unix.ioctl(t.fd, Unix.FIONREAD, ADR(charsToRead));
        RETURN (status = 0) AND (charsToRead = 0);
      END;
    END;
    RETURN FALSE;
  END EOF;
</PRE> methods of TCP.T 

<P><PRE>PROCEDURE <A NAME="InitFD"><procedure>InitFD</procedure></A>(fd: CARDINAL) =
  (* We assume that the runtime ignores SIGPIPE signals *)
  VAR
    one: int := 1;
    linger := Usocket.struct_linger{1, 1};
  BEGIN
    EVAL Usocket.setsockopt(fd, Usocket.SOL_SOCKET, Usocket.SO_LINGER,
                            ADR(linger), BYTESIZE(linger));
    EVAL Usocket.setsockopt(
           fd, Uin.IPPROTO_TCP, TCP_NODELAY, ADR(one), BYTESIZE(one));
    MakeNonBlocking (fd);
  END InitFD;

PROCEDURE <A NAME="MakeNonBlocking"><procedure>MakeNonBlocking</procedure></A>(fd: INTEGER) =
  BEGIN
    IF Unix.fcntl(fd, Unix.F_SETFL,
        Word.Or(Unix.fcntl(fd, Unix.F_GETFL, 0), Unix.M3_NONBLOCK)) # 0
    THEN
      Die();
    END;
  END MakeNonBlocking;

PROCEDURE <A NAME="Close"><procedure>Close</procedure></A>(t: T) =
  BEGIN
    LOCK t DO
      IF NOT t.closed THEN
        EVAL Unix.close(t.fd);
        t.closed := TRUE;
        t.error := ClosedErr;
      END;
    END;
  END Close;

PROCEDURE <A NAME="GetBytesFD"><procedure>GetBytesFD</procedure></A>(
    t: T; VAR arr: ARRAY OF CHAR; timeout: LONGREAL) : CARDINAL
    RAISES {Rd.Failure, ConnFD.TimedOut, Thread.Alerted} =
  VAR
    len: INTEGER;
  BEGIN
    LOOP
      LOCK t DO
        IF t.error # NIL THEN RAISE Rd.Failure(t.error); END;
        len := Uuio.read(t.fd, ADR(arr[0]), NUMBER(arr));
      END;
      IF len &gt;= 0 THEN
        RETURN len;
      ELSE
        WITH errno = Cerrno.GetErrno() DO
          IF (errno = Uerror.ECONNRESET) THEN
            RETURN 0;
          ELSIF  (errno = Uerror.EPIPE)
              OR (errno = Uerror.ENETRESET) THEN
            SetError(t,ConnLost);
          ELSIF  (errno = Uerror.ETIMEDOUT) THEN
            SetError(t,Timeout);
          ELSIF  (errno = Uerror.ENETUNREACH)
              OR (errno = Uerror.EHOSTUNREACH)
              OR (errno = Uerror.EHOSTDOWN)
              OR (errno = Uerror.ENETDOWN) THEN
            SetError(t,IP.Unreachable);
          ELSIF  (errno = Uerror.EWOULDBLOCK)
              OR (errno = Uerror.EAGAIN) THEN
            IF timeout = 0.0D0 OR
                   SchedulerPosix.IOAlertWait(t.fd, TRUE, timeout) =
                       SchedulerPosix.WaitResult.Timeout THEN
              RAISE ConnFD.TimedOut;
            END;
          ELSE
            SetError(t,Unexpected);
          END;
        END;
      END;
    END;
  END GetBytesFD;

PROCEDURE <A NAME="PutBytesFD"><procedure>PutBytesFD</procedure></A>(t: T; READONLY arr: ARRAY OF CHAR)
    RAISES {Wr.Failure, Thread.Alerted} =
  VAR pos := 0;
      len: INTEGER;
  BEGIN
    WHILE pos # NUMBER(arr) DO
      LOCK t DO
        IF t.error # NIL THEN RAISE Wr.Failure(t.error); END;
        len := Uuio.write(t.fd, ADR(arr[pos]), NUMBER(arr)-pos);
      END;
      IF len &gt;= 0 THEN
        INC(pos, len)
      ELSE
        WITH errno = Cerrno.GetErrno() DO
          IF     (errno = Uerror.EPIPE)
              OR (errno = Uerror.ECONNRESET)
              OR (errno = Uerror.ENETRESET) THEN
            SetError(t,ConnLost);
          ELSIF (errno = Uerror.ETIMEDOUT) THEN
            SetError(t,Timeout);
          ELSIF  (errno = Uerror.ENETUNREACH)
              OR (errno = Uerror.EHOSTUNREACH)
              OR (errno = Uerror.EHOSTDOWN)
              OR (errno = Uerror.ENETDOWN) THEN
            SetError(t,IP.Unreachable);
          ELSIF  (errno = Uerror.EWOULDBLOCK)
              OR (errno = Uerror.EAGAIN) THEN
            EVAL SchedulerPosix.IOAlertWait(t.fd, FALSE);
            (* IF Thread.TestAlert() THEN RAISE Thread.Alerted END *)
          ELSE
            SetError(t,Unexpected);
          END
        END
      END
    END;
  END PutBytesFD;

VAR lastErrorMu := NEW(MUTEX);
    lastErrors: ARRAY [0..19] OF INTEGER;
    lastErrorPos: CARDINAL := 0;

PROCEDURE <A NAME="SetError"><procedure>SetError</procedure></A>(t: T; atom: Atom.T) =
  BEGIN
    LOCK t DO
      WITH errno = Cerrno.GetErrno() DO
        t.error := AtomList.List2(atom, Atom.FromText(Fmt.Int(errno)));
        LOCK lastErrorMu DO
          lastErrors[lastErrorPos] := errno;
          INC(lastErrorPos);
          IF lastErrorPos &gt;= NUMBER(lastErrors) THEN lastErrorPos := 0; END;
        END;
      END;
    END;
  END SetError;

PROCEDURE <A NAME="ShutdownIn"><procedure>ShutdownIn</procedure></A>(t: T) RAISES {Rd.Failure} =
  BEGIN
    LOCK t DO
      IF t.error # NIL THEN RAISE Rd.Failure(t.error); END;
      EVAL Usocket.shutdown(t.fd, 0);
    END;
  END ShutdownIn;

PROCEDURE <A NAME="ShutdownOut"><procedure>ShutdownOut</procedure></A>(t: T) RAISES {Wr.Failure} =
  BEGIN
    LOCK t DO
      IF t.error # NIL THEN RAISE Wr.Failure(t.error); END;
      EVAL Usocket.shutdown(t.fd, 1);
    END;
  END ShutdownOut;

PROCEDURE <A NAME="Raise"><procedure>Raise</procedure></A>(a: Atom.T) RAISES {IP.Error} =
  BEGIN
    RAISE IP.Error(AtomList.List2(a,
                                  Atom.FromText(Fmt.Int(Cerrno.GetErrno()))));
  END Raise;

PROCEDURE <A NAME="RaiseUnexpected"><procedure>RaiseUnexpected</procedure></A>() RAISES {IP.Error} =
  BEGIN
    Raise(Unexpected);
  END RaiseUnexpected;

 PROCEDURE <A NAME="RaiseNoEC"><procedure>RaiseNoEC</procedure></A>(a: Atom.T) RAISES {IP.Error} =
  BEGIN
    RAISE IP.Error(AtomList.List1(a));
  END RaiseNoEC;

EXCEPTION FatalError;

PROCEDURE <A NAME="Die"><procedure>Die</procedure></A>() RAISES {} =
  &lt;* FATAL FatalError *&gt;
  BEGIN
    RAISE FatalError;
  END Die;
</PRE>***************************************************************************
 TCPMisc procedures. 
***************************************************************************

<P><PRE>PROCEDURE <A NAME="AcceptFrom"><procedure>AcceptFrom</procedure></A>(c: Connector; VAR (*OUT*) peer: IP.Endpoint): T
    RAISES {IP.Error, Thread.Alerted} =
  VAR
    addr                 : SockAddrIn;
    addrSize             : int      := BYTESIZE(addr);
    fd                   : INTEGER;
  BEGIN
    LOOP
      LOCK c DO
        IF c.closed THEN RaiseNoEC(Closed); END;
        fd := Usocket.accept(c.fd, ADR(addr), ADR(addrSize));
      END;
      WITH errno = Cerrno.GetErrno() DO
        IF fd &gt;= 0 THEN
          EXIT
        ELSIF errno = Uerror.EMFILE OR errno = Uerror.ENFILE THEN
          Raise(IP.NoResources);
        ELSIF
          errno = Uerror.EWOULDBLOCK OR errno = Uerror.EAGAIN
          THEN
          EVAL SchedulerPosix.IOAlertWait(c.fd, TRUE);
        ELSE
          RaiseUnexpected();
        END
      END
    END;
    InitFD(fd);
    peer.addr := LOOPHOLE(addr.sin_addr, IP.Address);
    peer.port := Uin.ntohs(addr.sin_port);
    RETURN NEW(T, fd := fd, ep := IP.NullEndPoint);
  END AcceptFrom;

PROCEDURE <A NAME="CoalesceWrites"><procedure>CoalesceWrites</procedure></A>(tcp: T; allow: BOOLEAN)
  RAISES {IP.Error} =
  VAR
    noDelay: int;
  BEGIN
    IF allow THEN noDelay := 0 ELSE noDelay := 1 END;

    LOCK tcp DO
      IF tcp.closed THEN
        RAISE IP.Error(AtomList.List1(Closed));
      END;
      IF Usocket.setsockopt(tcp.fd, Uin.IPPROTO_TCP, TCP_NODELAY,
        ADR(noDelay), BYTESIZE(noDelay)) = -1 THEN
        RaiseUnexpected();
      END;
    END;
  END CoalesceWrites;

PROCEDURE <A NAME="ConnectFrom"><procedure>ConnectFrom</procedure></A>(to, from: IP.Endpoint): T
  RAISES {IP.Error, Thread.Alerted} =
  VAR
    t := StartConnect(to, from);
    ok := FALSE;
  BEGIN
    TRY
      EVAL FinishConnect(t);
      ok := TRUE;
    FINALLY
     IF NOT ok THEN Close(t); END;
    END;
    RETURN t;
  END ConnectFrom;

PROCEDURE <A NAME="GetPeerName"><procedure>GetPeerName</procedure></A>(tcp: T): IP.Endpoint
  RAISES {IP.Error} =
  VAR
    addr: SockAddrIn;
    len: int := BYTESIZE(addr);
    ep: IP.Endpoint;
  BEGIN
    LOCK tcp DO
      IF tcp.closed THEN
        RAISE IP.Error(AtomList.List1(Closed));
      END;
      IF Usocket.getpeername(tcp.fd, ADR(addr), ADR(len)) = -1 THEN
        RaiseUnexpected();
      END;
    END;

    ep.addr := LOOPHOLE(addr.sin_addr, IP.Address);
    ep.port := Uin.ntohs(addr.sin_port);
    RETURN ep;
  END GetPeerName;

PROCEDURE <A NAME="GetSockName"><procedure>GetSockName</procedure></A>(tcp: T): IP.Endpoint
  RAISES {IP.Error} =
  VAR
    addr: SockAddrIn;
    len: int := BYTESIZE(addr);
    ep: IP.Endpoint;
  BEGIN
    LOCK tcp DO
      IF tcp.closed THEN
        RAISE IP.Error(AtomList.List1(Closed));
      END;
      IF Usocket.getsockname(tcp.fd, ADR(addr), ADR(len)) = -1 THEN
        RaiseUnexpected();
      END;
    END;

    ep.addr := LOOPHOLE(addr.sin_addr, IP.Address);
    ep.port := Uin.ntohs(addr.sin_port);
    RETURN ep;
  END GetSockName;

PROCEDURE <A NAME="KeepAlive"><procedure>KeepAlive</procedure></A>(tcp: T; allow: BOOLEAN)
  RAISES {IP.Error} =
  VAR
    keepAlive: int;
  BEGIN
    IF allow THEN keepAlive := 1 ELSE keepAlive := 0 END;

    LOCK tcp DO
      IF tcp.closed THEN
        RAISE IP.Error(AtomList.List1(Closed));
      END;
      IF Usocket.setsockopt(tcp.fd, Usocket.SOL_SOCKET, Usocket.SO_KEEPALIVE,
        ADR(keepAlive), BYTESIZE(keepAlive)) = -1 THEN
        RaiseUnexpected();
      END;
    END;
  END KeepAlive;

PROCEDURE <A NAME="LingerOnClose"><procedure>LingerOnClose</procedure></A>(tcp: T; allow: BOOLEAN)
  RAISES {IP.Error} =
  VAR
    linger: Usocket.struct_linger;
  BEGIN
    IF allow THEN
      linger.l_onoff := 1;
      linger.l_linger := 1;
    ELSE
      linger.l_onoff := 0;
      linger.l_linger := 0;
    END;

    LOCK tcp DO
      IF tcp.closed THEN
        RAISE IP.Error(AtomList.List1(Closed));
      END;
      IF Usocket.setsockopt(tcp.fd, Usocket.SOL_SOCKET, Usocket.SO_LINGER,
        ADR(linger), BYTESIZE(linger)) = -1 THEN
        RaiseUnexpected();
      END;
    END;
  END LingerOnClose;

BEGIN
  Refused := Atom.FromText(&quot;TCP.Refused&quot;);
  Closed := Atom.FromText(&quot;TCP.Closed&quot;);
  Timeout := Atom.FromText(&quot;TCP.Timeout&quot;);
  ConnLost := Atom.FromText(&quot;TCP.ConnLost&quot;);
  Unexpected := Atom.FromText(&quot;TCP.Unexpected&quot;);
  ClosedErr := AtomList.List1(Closed);
END TCP.
</PRE>
</inModule>
<HR>
<A NAME="x1">interface Usocket is in:
</A><UL>
<LI><A HREF="../../../m3core/src/unix/Common/Usocket.i3.html#0TOP0">m3core/src/unix/Common/Usocket.i3</A>
<LI><A HREF="../../../m3core/src/unix/linux-common/Usocket.i3.html#0TOP0">m3core/src/unix/linux-common/Usocket.i3</A>
</UL>
<P>
<HR>
<A NAME="x2">interface Uerror is in:
</A><UL>
<LI><A HREF="../../../m3core/src/unix/Common/Uerror.i3.html#0TOP0">m3core/src/unix/Common/Uerror.i3</A>
<LI><A HREF="../../../m3core/src/unix/linux-common/Uerror.i3.html#0TOP0">m3core/src/unix/linux-common/Uerror.i3</A>
</UL>
<P>
<HR>
<A NAME="x3">interface Unix is in:
</A><UL>
<LI><A HREF="../../../m3core/src/unix/Common/Unix.i3.html#0TOP0">m3core/src/unix/Common/Unix.i3</A>
<LI><A HREF="../../../m3core/src/unix/linux-common/Unix.i3.html#0TOP0">m3core/src/unix/linux-common/Unix.i3</A>
</UL>
<P>
<HR>
<A NAME="x4">interface Uuio is in:
</A><UL>
<LI><A HREF="../../../m3core/src/unix/Common/Uuio.i3.html#0TOP0">m3core/src/unix/Common/Uuio.i3</A>
<LI><A HREF="../../../m3core/src/unix/linux-common/Uuio.i3.html#0TOP0">m3core/src/unix/linux-common/Uuio.i3</A>
</UL>
<P>
<HR>
<A NAME="x5">interface Utypes is in:
</A><UL>
<LI><A HREF="../../../m3core/src/unix/Common/Utypes.i3.html#0TOP0">m3core/src/unix/Common/Utypes.i3</A>
<LI><A HREF="../../../m3core/src/unix/linux-common/Utypes.i3.html#0TOP0">m3core/src/unix/linux-common/Utypes.i3</A>
</UL>
<P>
<PRE>























</PRE>
</BODY>
</HTML>
