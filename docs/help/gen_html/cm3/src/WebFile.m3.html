<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: cm3/src/WebFile.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>cm3/src/WebFile.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright (C) 1994, Digital Equipment Corporation           
 All rights reserved.                                        
 See the file COPYRIGHT for a full description.              
                                                             
 Last modified on Mon Apr 17 09:00:54 PDT 1995 by kalsow     

<P><PRE>MODULE <module><implements><A HREF="WebFile.i3.html">WebFile</A></implements></module>;

IMPORT <A HREF="../../libm3/derived/IntIntTbl.i3.html">IntIntTbl</A>, <A HREF="../../libm3/src/etimer/ETimer.i3.html">ETimer</A>, <A HREF="../../libm3/src/os/Common/File.i3.html">File</A>, <A HREF="../../m3core/src/text/Text.i3.html">Text</A>, <A HREF="../../libm3/src/os/Common/OSError.i3.html">OSError</A>;
IMPORT <A HREF="../../m3middle/src/M3Buf.i3.html">M3Buf</A>, <A HREF="#x1">M3ID</A>, <A HREF="../../m3middle/src/M3File.i3.html">M3File</A>, <A HREF="../../m3middle/src/Target.i3.html">Target</A>;
IMPORT <A HREF="#x2">M3Path</A>, <A HREF="Utils.i3.html">Utils</A>, <A HREF="#x3">Msg</A>;

CONST
  InfoFile = &quot;.M3WEB&quot;;

TYPE
  InfoEntry = REF RECORD
    next  : InfoEntry;
    file  : M3ID.T;
    txt   : TEXT;
    chars : CharList;
    start : INTEGER;
    len   : INTEGER;
  END;

TYPE
  CharList = REF ARRAY OF CHAR;

VAR
  new_info : InfoEntry := NIL;

PROCEDURE <A NAME="Update"><procedure>Update</procedure></A> (file, info: TEXT) =
  VAR
    x  := NEW (InfoEntry);
    nm := M3Path.Parse (file);
    id := M3ID.Add (M3Path.Join (NIL, nm.base, nm.kind));
  BEGIN
    x.next  := new_info;  new_info := x;
    x.file  := id;
    x.txt   := info;
    x.chars := NIL;
    x.start := 0;
    x.len   := Text.Length (info);
  END Update;

PROCEDURE <A NAME="Dump"><procedure>Dump</procedure></A> () =
  VAR x: InfoEntry;
  BEGIN
    IF (new_info = NIL) THEN RETURN END;
    x := new_info;
    new_info := NIL;  (* in case we die while updating *)

    ETimer.Push (ETimer.New (&quot;updating web info&quot;));
      DumpFile (x, ParseFile (Inhale (), x));
    ETimer.Pop ();
  END Dump;

PROCEDURE <A NAME="Inhale"><procedure>Inhale</procedure></A> (): CharList =
  VAR
    rd  : File.T;
    buf : CharList := NIL;
    len : INTEGER;
  BEGIN
    IF Utils.LocalModTime (InfoFile) = Utils.NO_TIME THEN RETURN NIL END;
    rd  := Utils.OpenReader (InfoFile, fatal := FALSE);
    IF (rd = NIL) THEN RETURN NIL END;
    TRY
      len := VAL(rd.status().size, INTEGER);
      IF (len &gt; 0) THEN
        buf := NEW (CharList, len);
        EVAL M3File.Read (rd, buf^, len);
      END;
    EXCEPT OSError.E(ec) =&gt;
      Msg.Error (ec, &quot;unable to read &quot;, InfoFile);
      buf := NIL;
    END;
    Utils.CloseReader (rd, InfoFile);
    IF (buf # NIL) AND (len # NUMBER(buf^)) THEN
      Msg.Error (NIL, &quot;unable to read &quot;, InfoFile);
      buf := NIL;
    END;
    RETURN buf;
  END Inhale;

PROCEDURE <A NAME="ParseFile"><procedure>ParseFile</procedure></A> (txt: CharList;  updates: InfoEntry): InfoEntry =
  VAR
    cursor := 0;
    name_start, name_len: INTEGER;
    len, entry_len, body_cursor: INTEGER;
    updated: IntIntTbl.T;
    key, val: INTEGER;
    survivors, x: InfoEntry := NIL;
    ch: CHAR;
  BEGIN
    IF (txt = NIL) THEN RETURN NIL; END;
    len := NUMBER (txt^);
    IF (len &lt;= 0) THEN RETURN NIL END;

    (* build a table of the updated entries *)
    updated := NEW (IntIntTbl.Default).init ();
    WHILE (updates # NIL) DO
      EVAL updated.put (updates.file, 0);
      updates := updates.next;
    END;

    (* read the table of contents *)
    body_cursor := 0;
    LOOP
      IF (cursor &gt;= len) THEN RETURN BadFile(&quot;eof in toc&quot;); END;

      (* read a file name *)
      name_start := cursor;
      WHILE (cursor &lt; len) AND (txt[cursor] # ' ') DO INC (cursor); END;
      name_len := cursor - name_start;
      IF (cursor &gt;= len) THEN RETURN BadFile (&quot;eof in name&quot;); END;
      INC (cursor);

      (* read a length *)
      entry_len := 0;  ch := '0';
      LOOP
        IF (cursor &gt;= len) THEN RETURN BadFile (&quot;eof in len&quot;); END;
        ch := txt[cursor]; INC (cursor);
        IF (ch &lt; '0') OR ('9' &lt; ch) THEN EXIT END;
        entry_len := 10 * entry_len + ORD (ch) - ORD ('0');
      END;
      DEC (cursor);

      (* skip to next line *)
      WHILE (cursor &lt; len) AND (txt[cursor] # '\n') DO INC (cursor) END;
      INC (cursor);

      IF (name_len = 1) AND (txt[name_start] = '$') THEN EXIT END;

      key := M3ID.FromStr (SUBARRAY (txt^, name_start, name_len));
      IF NOT updated.get (key, val) THEN
        (* save this entry *)
        survivors := NEW (InfoEntry, next := survivors, file := key,
                          txt := NIL, chars := txt,
                          start := body_cursor, len := entry_len);

      END;
      INC (body_cursor, entry_len);
    END;

    (* put the list back in file order *)
    survivors := ReverseList (survivors);

    (* and patch the entries to compensate for the table of contents length *)
    x := survivors;
    WHILE (x # NIL) DO
      INC (x.start, cursor);
      IF (x.start &gt; len) THEN RETURN BadFile (&quot;eof in body&quot;); END;
      x := x.next;
    END;

    RETURN survivors;
  END ParseFile;

PROCEDURE <A NAME="ReverseList"><procedure>ReverseList</procedure></A> (a: InfoEntry): InfoEntry =
  VAR b, c: InfoEntry;
  BEGIN
    b := NIL;
    WHILE (a # NIL) DO
      c := a.next;
      a.next := b;
      b := a;
      a := c;
    END;
    RETURN b;
  END ReverseList;

PROCEDURE <A NAME="BadFile"><procedure>BadFile</procedure></A> (msg: TEXT): InfoEntry =
  BEGIN
    Msg.Error (NIL, &quot;web info file \&quot;&quot; &amp; InfoFile &amp; &quot;\&quot; is damaged: &quot;, msg);
    RETURN NIL;
  END BadFile;

PROCEDURE <A NAME="DumpFile"><procedure>DumpFile</procedure></A> (a, b: InfoEntry) =
  VAR
    wr := Utils.OpenWriter (InfoFile, fatal := TRUE);
    buf := M3Buf.New ();
  BEGIN
    M3Buf.AttachDrain (buf, wr);
    DumpHeaders (buf, a);
    DumpHeaders (buf, b);
    M3Buf.PutText (buf, &quot;$ 0&quot;);
    M3Buf.PutText (buf, Target.EOL);
    DumpBodies (buf, a);
    DumpBodies (buf, b);
    M3Buf.Flush (buf, wr);
    Utils.CloseWriter (wr, InfoFile);
  END DumpFile;

PROCEDURE <A NAME="DumpHeaders"><procedure>DumpHeaders</procedure></A> (buf: M3Buf.T;  e: InfoEntry) =
  BEGIN
    WHILE (e # NIL) DO
      M3ID.Put      (buf, e.file);
      M3Buf.PutChar (buf, ' ');
      M3Buf.PutInt  (buf, e.len);
      M3Buf.PutText (buf, Target.EOL);
      e := e.next;
    END;
  END DumpHeaders;

PROCEDURE <A NAME="DumpBodies"><procedure>DumpBodies</procedure></A> (buf: M3Buf.T;  e: InfoEntry) =
  BEGIN
    WHILE (e # NIL) DO
      IF (e.txt # NIL)
        THEN M3Buf.PutSubText (buf, e.txt, e.start, e.len);
        ELSE M3Buf.PutSub (buf, SUBARRAY (e.chars^, e.start, e.len));
      END;
      e := e.next;
    END;
  END DumpBodies;

BEGIN
END WebFile.
</PRE>
</inModule>
<HR>
<A NAME="x1">interface M3ID is in:
</A><UL>
<LI><A HREF="../../m3middle/src/M3ID.i3.html#0TOP0">m3middle/src/M3ID.i3</A>
<LI><A HREF="../../m3scan/src/M3ID.i3.html#0TOP0">m3scan/src/M3ID.i3</A>
</UL>
<P>
<HR>
<A NAME="x2">interface M3Path is in:
</A><UL>
<LI><A HREF="M3Path.i3.html#0TOP0">cm3/src/M3Path.i3</A>
<LI><A HREF="../../m3quake/src/M3Path.i3.html#0TOP0">m3quake/src/M3Path.i3</A>
</UL>
<P>
<HR>
<A NAME="x3">interface Msg is in:
</A><UL>
<LI><A HREF="Msg.i3.html#0TOP0">cm3/src/Msg.i3</A>
<LI><A HREF="../../m3tohtml/src/Msg.i3.html#0TOP0">m3tohtml/src/Msg.i3</A>
</UL>
<P>
<PRE>























</PRE>
</BODY>
</HTML>
