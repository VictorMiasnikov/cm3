<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: db/src/DB.i3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>db/src/DB.i3</H2></A><HR>
<inInterface>
<PRE></PRE> Copyright (C) 1996-2000, Critical Mass, Inc.  All Rights Reserved. 
 See file COPYRIGHT-CMASS for details. 

<P>	Modified by Darko Volaric September 2002
		Added functionality to support use of multiple database client interfaces concurrently.
		Supported interfaces are: PostgreSQL, MySQL and ODBC. This interface should be source
		compatible with the previous version. Please email darko@peter.com.au with any problems.


<P> 
   DB is the safe Modula-3 interface for relational databases.  It
   defines two primary abstractions: database connections and
   statements.


<P><PRE>INTERFACE <interface><A HREF="DB.m3.html">DB</A></interface>;
</PRE>------------------------------------------------------- connections ---

<P><PRE>TYPE
	InterfaceList = REF ARRAY OF Interface;
	(* A database interface, usualy one for each type of database server *)
	<A HREF="DB.m3.html#Interface">Interface</A> &lt;: InterfacePublic;
	InterfacePublic = OBJECT
		name: TEXT;
	METHODS
		set_default();
		connect(
			database, user_id, password: TEXT;
			server: TEXT := NIL;
		): T  RAISES {Error};
		get_data_sources(): DescList  RAISES {Error};
		get_drivers(): DescList  RAISES {Error};
	END;

PROCEDURE <A HREF="DB.m3.html#GetInterfaces">GetInterfaces</A>(): InterfaceList;
</PRE><BLOCKQUOTE><EM>	Returns a list of Interface objects that describe the available interfaces
		in this implementation </EM></BLOCKQUOTE><PRE>

PROCEDURE <A HREF="DB.m3.html#GetDefaultInterface">GetDefaultInterface</A>(): Interface;

PROCEDURE <A HREF="DB.m3.html#FindInterface">FindInterface</A>(name: TEXT): Interface  RAISES {Error};
</PRE><BLOCKQUOTE><EM>	Returns the interface with the given name, or NIL if not found </EM></BLOCKQUOTE><PRE>
</PRE>  The following functions are provided for compatibily with the previous version of the interface. 
		The methods of the Interface object should be used instead. 

<P><PRE>PROCEDURE <A HREF="DB.m3.html#Connect">Connect</A> (
	database, user_id, password: TEXT; (* Establish a connection to the named &quot;database&quot; using &quot;user_id&quot; and
																				&quot;password&quot; as authentication credentials. *)
	server: TEXT := NIL;	(* Connect to the server running on this host. May be a host name or IP number *)
	interface: Interface := NIL; (* Use this interface to connect. If not specified the default interface is used *)
): T  RAISES {Error};

PROCEDURE <A HREF="DB.m3.html#GetDataSources">GetDataSources</A> (): DescList  RAISES {Error};
</PRE><BLOCKQUOTE><EM> Returns the names and descriptions of the data sources that are
   available from the local server. </EM></BLOCKQUOTE><PRE>

PROCEDURE <A HREF="DB.m3.html#GetDrivers">GetDrivers</A> (): DescList  RAISES {Error};
</PRE><BLOCKQUOTE><EM> Returns the names and descriptions of the drivers that are
   available from the local server. </EM></BLOCKQUOTE><PRE>

TYPE DescList = REF RECORD  name, description: TEXT;  next: DescList;  END;
</PRE><P>
   A <CODE>DB.T</CODE>, or connection, represents a single connection to a database.
   Multiple connections may exist within one application and each may be
   used concurrently by multiple threads.


<P><PRE>TYPE
  (* a database connection *)
  T = OBJECT METHODS
    disconnect ()              RAISES {Error};
    new_stmt (): Stmt          RAISES {Error};
    auto_commit (on: BOOLEAN)  RAISES {Error};
    commit ()                  RAISES {Error};
    abort ()                   RAISES {Error};
  END;
</PRE><P>
  Given a database connection <CODE>db</CODE>,
<P>
  <CODE>db.disconnect()</CODE> closes the connection to <CODE>db</CODE>.  Further attempts
  to use the connection are checked runtime errors.  If a connection
  is garbage collected, the runtine will attempt to disconnect it.
  But, users should not rely on timely garbage collection to close
  their connections.
<P>
  <CODE>db.new_stmt()</CODE> returns a new statement that can be used to query or
  update the database <CODE>db</CODE>.
<P>
  The <CODE>current transaction</CODE> is any sequence of one or more statements
  that has been executed, but not commited or aborted.
<P>
  <CODE>db.auto_commit(on)</CODE> enables (disables) automatic commits on <CODE>db</CODE>
  after each statement is executed if <CODE>on</CODE> is <CODE>TRUE</CODE> (<CODE>FALSE</CODE>).
  Initially connections have auto commiting enabled.  When auto
  commiting is disabled, <CODE>db.commit()</CODE> or <CODE>db.abort()</CODE> must be
  explicitly called to finish the current transaction.
<P>
  <CODE>db.commit()</CODE> commits the current transaction on <CODE>db</CODE>.
<P>
  <CODE>db.abort()</CODE> aborts the current transaction on <CODE>db</CODE>.


<P>-------------------------------------------------------- statements ---

  A <CODE>db.Stmt</CODE>, or statement, represents a database query or update.
  Each statement is bound to and executes against one connection.  A
  single connection may have multiple outstanding statements.
  Operations on a statement are serialized.


<P><PRE>TYPE
  (* a SQL database statement (query or update) *)
  Stmt = MUTEX OBJECT METHODS
    prepare (operation: TEXT)       RAISES {Error};
    execute (operation: TEXT)       RAISES {Error};
    fetch ():  Results              RAISES {Error};
    done ()                         RAISES {Error};
    close ()                        RAISES {Error};
    get_cursor_name (): TEXT        RAISES {Error};
    set_cursor_name (nm: TEXT)      RAISES {Error};
    num_rows (): INTEGER            RAISES {Error};
    describe_result (): ResultDesc  RAISES {Error};
    connection (): T;
  END;

  Results = REF ARRAY OF REFANY;

  ResultDesc = REF ARRAY OF ColumnDesc;
  ColumnDesc = RECORD
    name      : TEXT;
    type      : DataType;
    precision : INTEGER;  (* total number of digits *)
    scale     : INTEGER;  (* number of digits right of the decimal place *)
    nullable  : Nullable;
  END;

  Nullable = { Yes, No, Unknown };
</PRE><P>
  ANSI SQL and its extensions define a set of primitive data types.
  The values stored in a column of the database are of a single type.
  For each SQL type, this interface defines a corresponding Modula-3
  reference type.   The vector of results returned by <CODE>st.fetch()</CODE>
  will contain values corresponding to these Modula-3 types.


<P><PRE>TYPE
  (* ANSI and extended SQL datatypes  *)
  DataType = {     (* Modula-3 type in the result vector *)
    Null,          (* NULL *)
    Char,          (* RefString *)
    VarChar,       (* RefString *)
    LongVarChar,   (* RefString *)
    Numeric,       (* REF LONGREAL -- ?? *)
    Decimal,       (* REF LONGREAL -- ?? *)
    BigInt,        (* RefBigInt    (64-bit signed integer) *)
    Integer,       (* REF INTEGER  (32-bit signed integer) *)
    SmallInt,      (* REF INTEGER  (16-bit signed integer) *)
    TinyInt,       (* REF INTEGER  (8-bit signed integer) *)
    Real,          (* REF REAL *)
    Float,         (* REF LONGREAL *)
    Double,        (* REF LONGREAL *)
    Bit,           (* REF BOOLEAN *)
    LongVarBinary, (* RefString *)
    VarBinary,     (* RefString *)
    Binary,        (* RefString *)
    Date,          (* RefDate *)
    Time,          (* RefTime *)
    Timestamp      (* RefTimestamp *)
  };
  RefString = REF UNTRACED REF ARRAY OF CHAR;
  RefBigInt = REF RECORD lo, hi: INTEGER END; (* == 2^32*hi + lo *)
  RefDate   = REF RECORD year, month, day: INTEGER END;
  RefTime   = REF RECORD hour, minute, second: INTEGER END;
  RefTimestamp = REF RECORD
                  year, month, day: INTEGER;
                  hour, minute, second, fraction: INTEGER;
                 END;
</PRE><P>
  Given a statement <CODE>st</CODE>,
<P>
  <CODE>st.prepare(s)</CODE> will prepare the SQL statement <CODE>s</CODE> for execution.
<P>
  <CODE>st.execute(NIL)</CODE> will excute or reexecute the statment previously
  prepared by <CODE>st.prepare(s)</CODE>.  It is a checked runtime error if no
  statement is prepared.
<P>
  <CODE>st.execute(s)</CODE> will execute the SQL statement <CODE>s</CODE> once.  Any prepared
  statements are lost.
<P>
  <CODE>st.fetch()</CODE> returns a non-NIL array of references to the values
  corresponding to the next row of the result of the last statement
  executed by <CODE>st</CODE>.  If there are no more results, NIL is returned.
  Note, the array of references and the reference values themselves
  are reused by subsequent calls to <CODE>fetch</CODE>.  It is the programmer's
  responsibility to copy values out of the result vector if they're
  needed across multiple calls to <CODE>fetch</CODE>.  The result elements
  corresponding to columns containing <CODE>nullified</CODE> values will be
  <CODE>NIL</CODE>.  It is a checked runtime error to call <CODE>fetch</CODE> before the
  statement has been executed.
<P>
  <CODE>st.done()</CODE> finishes a single statement execution and result fetching
  cycle.  Then, <CODE>st</CODE> can be reused for another cycle.
<P>
  <CODE>st.close()</CODE> releases the resources used by <CODE>st</CODE>.  It is a checked
  runtime error to reuse a closed statement.  If a statement is garbage
  collected, the runtine will attempt to disconnect it.  Users should
  not rely on timely garbage collection to close their statements.
<P>
  <CODE>st.get_cursor_name()</CODE> returns the name attached to <CODE>st</CODE>s cursor.
  If no name is attached, <CODE>NIL</CODE> is returned.  Attached cursor names
  can be referenced by other statements to coordinate multi-statement
  transactions.
<P>
  <CODE>st.set_cursor_name(nm)</CODE> attaches the name <CODE>nm</CODE> to <CODE>st</CODE>s cursor.
<P>
  <CODE>st.connection()</CODE> returns the underlying database connection used by <CODE>st</CODE>.


<P>--------------------------------------------- errors and exceptions ---

<P><PRE>EXCEPTION Error (ErrorDesc);
</PRE><BLOCKQUOTE><EM> <P>
  Routines in this interface raise <CODE>DB.Error</CODE> whenever an operation
  fails.  The failure may be due to a failure in the underlying
  database or network, or the failure may be due to programming
  errors.  Blatant programming errors detected by this implementation
  will trigger checked runtime errors.  That is, your program will
  crash.  
</EM></BLOCKQUOTE><PRE>

TYPE
  ErrorDesc = REF RECORD
    state       : ARRAY [0..5] OF CHAR;  (* e.g. &quot;M1001&quot; with zero termination *)
    description : TEXT;
    native_err  : INTEGER; (* lower level driver or DBMS error code *)
  END;

END DB.
</PRE>
</inInterface>
<PRE>























</PRE>
</BODY>
</HTML>
