<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: db/src/DB.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>db/src/DB.m3</H2></A><HR>
<inModule>
<PRE>MODULE <module><implements><A HREF="DB.i3.html">DB</A></implements></module>;
</PRE>	Darko Volaric September 2002
		An adaption of the previous version to allow concurrent connections to
		different database interfaces. This module is used as to glue the database specific
		modules together and provide backward compatiblity. 
		Please email darko@peter.com.au with any problems.


<P>
<P><PRE>IMPORT
	<A HREF="PostgreSQLDB.i3.html">PostgreSQLDB</A>, <A HREF="ODBCDB.i3.html">ODBCDB</A>, <A HREF="MySQLDB.i3.html">MySQLDB</A>, <A HREF="../../m3core/src/text/Text.i3.html">Text</A>;

VAR
	Default: Interface := NIL;
	Interfaces : InterfaceList := NIL;

REVEAL
	<A NAME="Interface">Interface</A> = InterfacePublic BRANDED OBJECT
	OVERRIDES
		set_default := SetDefaultInterface;
	END;

PROCEDURE <A NAME="Init"><procedure>Init</procedure></A>() =
BEGIN
	Interfaces := NEW(InterfaceList, 3);
	Interfaces^[0] := PostgreSQLDB.GetInterface();
	Interfaces^[1] := ODBCDB.GetInterface();
	Interfaces^[2] := MySQLDB.GetInterface();
	FOR i := 0 TO LAST(Interfaces^) DO
		IF Interfaces[i] # NIL THEN
			Default := Interfaces[i];
			EXIT;
		END;
	END;
	IF Default = NIL THEN
		(* FIXME: Riase a error, but should an execption be raised at module init time? *)
	END;
END Init;

PROCEDURE <A NAME="GetInterfaces"><procedure>GetInterfaces</procedure></A>(): InterfaceList =
BEGIN
	RETURN Interfaces;
END GetInterfaces;

PROCEDURE <A NAME="SetDefaultInterface"><procedure>SetDefaultInterface</procedure></A>(this: Interface) =
BEGIN
	Default := this;
END SetDefaultInterface;

PROCEDURE <A NAME="FindInterface"><procedure>FindInterface</procedure></A>(name: TEXT): Interface  RAISES {Error} =
BEGIN
	IF name = NIL THEN name := &quot;&quot; END;
	(* there's not going to be alot of interfaces... linear search is good! *)
	FOR i := 0 TO LAST(Interfaces^) DO
		IF Interfaces[i] # NIL AND Text.Equal(name, Interfaces[i].name) THEN
			RETURN Interfaces[i];
		END;
	END;
	RAISE Error(NEW(ErrorDesc,
		state := ARRAY[0..5] OF CHAR{' ',..},
		description := &quot;Interface '&quot; &amp; name &amp; &quot;' not found.&quot;,
		native_err := 0
	));
END FindInterface;

PROCEDURE <A NAME="GetDefaultInterface"><procedure>GetDefaultInterface</procedure></A>(): Interface =
BEGIN
	RETURN Default;
END GetDefaultInterface;

PROCEDURE <A NAME="Connect"><procedure>Connect</procedure></A> (
	database, user_id, password: TEXT;
	server: TEXT := NIL;
	interface: Interface := NIL
): T RAISES {Error} =
BEGIN
	IF interface = NIL THEN
		RETURN Default.connect(database, user_id, password, server);
	ELSE
		RETURN interface.connect(database, user_id, password, server);
	END;
END Connect;

PROCEDURE <A NAME="GetDataSources"><procedure>GetDataSources</procedure></A> (): DescList RAISES {Error} =
BEGIN
	RETURN Default.get_data_sources();
END GetDataSources;

PROCEDURE <A NAME="GetDrivers"><procedure>GetDrivers</procedure></A> (): DescList RAISES {Error} =
BEGIN
	RETURN Default.get_drivers();
END GetDrivers;

BEGIN
	Init();
END DB.
</PRE>
</inModule>
<PRE>























</PRE>
</BODY>
</HTML>
