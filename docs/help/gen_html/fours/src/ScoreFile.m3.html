<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: fours/src/ScoreFile.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>fours/src/ScoreFile.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright (C) 1995, Digital Equipment Corporation           
 All rights reserved.                                        
 See the file COPYRIGHT for a full description.              
                                                             
 Last modified on Fri Jun 16 08:20:51 PDT 1995 by kalsow     

<P><PRE>MODULE <module><implements><A HREF="#x1">ScoreFile</A></implements></module>;

IMPORT <A HREF="../../m3core/src/text/Text.i3.html">Text</A>, <A HREF="../../libm3/src/rw/Wr.i3.html">Wr</A>, <A HREF="../../libm3/src/rw/FileWr.i3.html">FileWr</A>, <A HREF="../../libm3/src/os/Common/FS.i3.html">FS</A>, <A HREF="../../libm3/src/os/Common/OSError.i3.html">OSError</A>, <A HREF="../../libm3/src/os/Common/File.i3.html">File</A>, <A HREF="../../m3core/src/thread/Common/Thread.i3.html">Thread</A>;
IMPORT <A HREF="#x2">Buf</A>, <A HREF="../../m3core/src/time/Common/Date.i3.html">Date</A>, <A HREF="../../libm3/src/os/Common/RegularFile.i3.html">RegularFile</A>, <A HREF="../../m3core/src/time/Common/Time.i3.html">Time</A>, <A HREF="../../libm3/src/fmtlex/Fmt.i3.html">Fmt</A>, <A HREF="../../libm3/derived/AtomList.i3.html">AtomList</A>, <A HREF="../../libm3/src/atom/Atom.i3.html">Atom</A>;

PROCEDURE <A NAME="Get"><procedure>Get</procedure></A> (f: FileName; p: Player; VAR(*OUT*) s: Score): BOOLEAN
  RAISES {Error} =
  VAR
    player := FixName (p);
    buf    := Inhale (f);
    cursor := FindEntry (buf, player);
  BEGIN
    IF (cursor &lt; 0) THEN RETURN FALSE; END;
    ScanScore (buf, cursor + Text.Length (player), s);
    RETURN TRUE;
  END Get;

PROCEDURE <A NAME="Enumerate"><procedure>Enumerate</procedure></A> (f: FileName;  enum: EnumProc) RAISES {Error} =
  VAR
    buf     := Inhale (f);
    buf_len := NUMBER (buf^);
    cursor  := 0;
    score   : Score;
    player  : Player;
  BEGIN
    WHILE (cursor &lt; buf_len) DO
      player := ScanName (buf, cursor);
      IF (player # NIL) THEN
        ScanScore (buf, cursor, score);
        enum (player, score);
      END;
      cursor := NextLine (buf, cursor);
    END;
  END Enumerate;

CONST
  NoScore = Score { 0.0d0, 0, 0, 0, 0, 0.0d0 };

PROCEDURE <A NAME="Put"><procedure>Put</procedure></A> (f: FileName;  p: Player;  READONLY s: Score) RAISES {Error} =
  VAR
    player := FixName (p);
    fd     : RegularFile.T;
    wr     : Wr.T;
    buf    : Buf.T;
    x0, x1 : INTEGER;
    ss     : Score;
  BEGIN
    TRY
      TRY
        (* open and read the file *)
        fd := FS.OpenFile (f, truncate := FALSE, create := FS.CreateOption.Ok);
        LockFile (fd);
        buf := Inhale (f, fd);

        (* find the matching entry *)
        x0 := FindEntry (buf, player);
        IF (x0 &gt;= 0) THEN
          ScanScore (buf, x0 + Text.Length (player), ss);
          x1 := NextLine (buf, x0);
        ELSE
          ss := NoScore;
          x0 := NUMBER (buf^);
          x1 := x0;
        END;

        (* merge the old and new scores *)
        INC (ss.n_games, s.n_games);
        ss.n_seconds := ss.n_seconds + s.n_seconds;
        IF (s.best_score &gt; ss.best_score) THEN
          ss.best_score := s.best_score;
          ss.best_rows  := s.best_rows;
          ss.best_level := s.best_level;
          ss.best_date  := s.best_date;
        END;

        wr := NEW (FileWr.T).init (fd);
	Wr.Seek (wr, 0);

        (* write the new score *)
        Wr.PutText (wr, player);
        Wr.PutChar (wr, ' ');
        Wr.PutText (wr, Fmt.Int (ROUND (ss.best_date - Epoch ())));
        Wr.PutChar (wr, ' ');
        Wr.PutText (wr, Fmt.Int (ss.best_level));
        Wr.PutChar (wr, ' ');
        Wr.PutText (wr, Fmt.Int (ss.best_rows));
        Wr.PutChar (wr, ' ');
        Wr.PutText (wr, Fmt.Int (ss.best_score));
        Wr.PutChar (wr, ' ');
        Wr.PutText (wr, Fmt.Int (ss.n_games));
        Wr.PutChar (wr, ' ');
        Wr.PutText (wr, Fmt.Int (ROUND (ss.n_seconds * 10.0d0)));
        Wr.PutText (wr, Wr.EOL);

        (* append the existing scores *)
        IF (x0 &gt; 0) THEN
          Wr.PutString (wr, SUBARRAY (buf^, 0, x0));
        END;
        IF (x1 &lt; NUMBER (buf^)) THEN
          Wr.PutString (wr, SUBARRAY (buf^, x1, NUMBER (buf^) - x1));
        END;

        Wr.Flush (wr);
        fd.unlock ();
      EXCEPT
      | OSError.E(ec)  =&gt;  FileProblem (ec);
      | Thread.Alerted =&gt;  RAISE Error (&quot;interrupted&quot;);
      | Wr.Failure(ec) =&gt;  FileProblem (ec);
      END;
    FINALLY
      Close (wr);
      Close (fd);
    END;
  END Put;
</PRE>-------------------------------------------------------------- internal ---

<P><PRE>PROCEDURE <A NAME="Inhale"><procedure>Inhale</procedure></A> (path: TEXT;  src: File.T := NIL): Buf.T RAISES {Error} =
  VAR buf: Buf.T;
  BEGIN
    TRY
      buf := Buf.FromFile (path, src);
    EXCEPT
      OSError.E (ec) =&gt; FileProblem (ec);
    END;
    RETURN buf;
  END Inhale;

PROCEDURE <A NAME="FindEntry"><procedure>FindEntry</procedure></A> (buf: Buf.T;  p: Player): INTEGER =
  VAR
    buf_len := NUMBER (buf^);
    nm_len  := Text.Length (p);
    nm      := NEW (Buf.T, nm_len);
    cursor  := 0;
  BEGIN
    Text.SetChars (nm^, p);
    WHILE (cursor &lt;= buf_len - nm_len) DO
      IF SUBARRAY (buf^, cursor, nm_len) = nm^ THEN
        RETURN cursor;
      END;
      cursor := NextLine (buf, cursor);
    END;
    RETURN -1;
  END FindEntry;

PROCEDURE <A NAME="NextLine"><procedure>NextLine</procedure></A> (buf: Buf.T;  cursor: INTEGER): INTEGER =
  VAR len := NUMBER (buf^);
  BEGIN
    WHILE (cursor &lt; len) AND (buf[cursor] # '\n') DO INC (cursor); END;
    RETURN cursor + 1;
  END NextLine;

PROCEDURE <A NAME="ScanScore"><procedure>ScanScore</procedure></A> (buf: Buf.T;  cursor: INTEGER;  VAR s: Score)
  RAISES {Error} =
  BEGIN
    s.best_date  := FLOAT (GetInt (buf, cursor), Time.T) + Epoch ();
    s.best_level := GetInt (buf, cursor);
    s.best_rows  := GetInt (buf, cursor);
    s.best_score := GetInt (buf, cursor);
    s.n_games    := GetInt (buf, cursor);
    s.n_seconds  := FLOAT (GetInt (buf, cursor), Time.T) / 10.0d0;
  END ScanScore;

PROCEDURE <A NAME="FixName"><procedure>FixName</procedure></A> (pp: Player): Player =
  VAR
    p    := UserName (pp);
    plen := Text.Length (p);
    buf  := NEW (REF ARRAY OF CHAR, plen + 6);
    blen := 0;
    num  := Fmt.Int (plen);
  BEGIN
    (* prepend the length *)
    Text.SetChars (buf^, num);
    blen := Text.Length (num);

    buf[blen] := ' ';  INC (blen);

    (* add the name *)
    Text.SetChars (SUBARRAY (buf^, blen, NUMBER (buf^) - blen), p);
    INC (blen, plen);

    (* replace any nasty characters *)
    FOR i := 0 TO blen-1 DO
      IF (buf[i] = '\n') THEN buf[i] := ' '; END;
    END;

    RETURN Text.FromChars (SUBARRAY (buf^, 0, blen));
  END FixName;

PROCEDURE <A NAME="UserName"><procedure>UserName</procedure></A> (nm: TEXT): TEXT =
  BEGIN
    IF (nm = NIL) THEN nm := &quot;&lt;unknown&gt;&quot;; END;
    RETURN nm;
  END UserName;

PROCEDURE <A NAME="ScanName"><procedure>ScanName</procedure></A> (buf: Buf.T;  VAR cursor: INTEGER): Player =
  VAR val := 0;  z := cursor;  len := NUMBER (buf^);  player: Player;
  BEGIN
    WHILE (cursor &lt; len) AND (buf[cursor] = ' ') DO INC (cursor); END;
    z := cursor;
    WHILE (z &lt; len) AND ('0' &lt;= buf[z]) AND (buf[z] &lt;= '9') DO
      val := 10 * val + ORD (buf[z]) - ORD ('0');
      INC (z);
    END;
    IF (z = cursor) THEN RETURN NIL END;
    cursor := z + 1;  (* skip the blank *)
    val := MIN (val, NUMBER (buf^) - cursor);
    player := Text.FromChars (SUBARRAY (buf^, cursor, val));
    INC (cursor, val);
    RETURN player;
  END ScanName;

PROCEDURE <A NAME="GetInt"><procedure>GetInt</procedure></A> (buf: Buf.T;  VAR cursor: INTEGER): INTEGER RAISES {Error} =
  VAR val := 0;  z := cursor;  len := NUMBER (buf^);
  BEGIN
    WHILE (cursor &lt; len) AND (buf[cursor] = ' ') DO INC (cursor); END;
    z := cursor;
    WHILE (z &lt; len) AND ('0' &lt;= buf[z]) AND (buf[z] &lt;= '9') DO
      val := 10 * val + ORD (buf[z]) - ORD ('0');
      INC (z);
    END;
    IF (z = cursor) THEN
      RAISE Error (&quot;bad file format: missing integer value&quot;);
    END;
    cursor := z;
    RETURN val;
  END GetInt;

PROCEDURE <A NAME="LockFile"><procedure>LockFile</procedure></A> (fd: RegularFile.T) RAISES {OSError.E, Error} =
  VAR delay := 1.0d0; (* seconds *)
  BEGIN
    FOR i := 0 TO 3 DO
      IF fd.lock () THEN RETURN END;
      Thread.Pause (delay);
      delay := delay + delay;
    END;
    IF fd.lock () THEN RETURN END;
    RAISE Error (&quot;unable to lock the score file&quot;);
  END LockFile;

PROCEDURE <A NAME="Close"><procedure>Close</procedure></A> (r: REFANY) =
  BEGIN
    TYPECASE r OF
    | NULL =&gt;        (* ignore *)
    | Wr.T(wr) =&gt;    TRY Wr.Close (wr) EXCEPT Wr.Failure, Thread.Alerted=&gt; END;
    | File.T(fd) =&gt;  TRY fd.close()    EXCEPT OSError.E =&gt; END;
    ELSE             (* ignore *)
    END;
  END Close;

VAR
  epoch: Time.T := 0.0d0;

PROCEDURE <A NAME="Epoch"><procedure>Epoch</procedure></A> (): Time.T =
  &lt;*FATAL Date.Error*&gt;
  CONST
    T0 = Date.T{1995, Date.Month.Jan, 1, 0,0,0,0, &quot;&quot;, Date.WeekDay.Sun};
  BEGIN
    IF (epoch = 0.0d0) THEN epoch := Date.ToTime (T0); END;
    RETURN epoch;
  END Epoch;

PROCEDURE <A NAME="FileProblem"><procedure>FileProblem</procedure></A> (ec: AtomList.T) RAISES {Error} =
  VAR msg := &quot;file system problem&quot;;
  BEGIN
    WHILE (ec # NIL) DO
      msg := msg &amp; &quot; ** &quot; &amp; Atom.ToText (ec.head);
      ec := ec.tail;
    END;
    RAISE Error (msg);
  END FileProblem;

BEGIN
END ScoreFile.
</PRE>
</inModule>
<HR>
<A NAME="x1">interface ScoreFile is in:
</A><UL>
<LI><A HREF="../../columns/src/ScoreFile.i3.html#0TOP0">columns/src/ScoreFile.i3</A>
<LI><A HREF="ScoreFile.i3.html#0TOP0">fours/src/ScoreFile.i3</A>
</UL>
<P>
<HR>
<A NAME="x2">interface Buf is in:
</A><UL>
<LI><A HREF="../../cm3ide/src/utils/Buf.i3.html#0TOP0">cm3ide/src/utils/Buf.i3</A>
<LI><A HREF="../../libbuf/src/Buf.i3.html#0TOP0">libbuf/src/Buf.i3</A>
</UL>
<P>
<PRE>























</PRE>
</BODY>
</HTML>
