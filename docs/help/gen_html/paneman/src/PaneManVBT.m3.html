<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: paneman/src/PaneManVBT.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>paneman/src/PaneManVBT.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright (c) 2000 California Institute of Technology 
 All rights reserved. See the file COPYRIGHT for a full description. 
 $Id: PaneManVBT.m3.html,v 1.3 2010-04-29 17:19:46 wagner Exp $ 

<P><PRE>MODULE <module><implements><A HREF="PaneManVBT.i3.html">PaneManVBT</A></implements></module>;
IMPORT <A HREF="../../ui/src/split/HVSplit.i3.html">HVSplit</A>, <A HREF="../../ui/src/split/HVBar.i3.html">HVBar</A>;
IMPORT <A HREF="PaneFrame.i3.html">PaneFrame</A>;
IMPORT <A HREF="KeyDaemon.i3.html">KeyDaemon</A>;
IMPORT <A HREF="RequestDaemon.i3.html">RequestDaemon</A>;
IMPORT <A HREF="../derived/PaneList.i3.html">PaneList</A>, <A HREF="../derived/PaneListExtras.i3.html">PaneListExtras</A>;
IMPORT <A HREF="../../ui/src/vbt/VBT.i3.html">VBT</A>;
IMPORT <A HREF="../../m3core/src/text/Text.i3.html">Text</A>;
IMPORT <A HREF="../../ui/src/vbt/Pixmap.i3.html">Pixmap</A>;
IMPORT <A HREF="../../libm3/src/fmtlex/Fmt.i3.html">Fmt</A>, <A HREF="#x1">Debug</A>;
IMPORT <A HREF="../../ui/src/split/Split.i3.html">Split</A>, <A HREF="../../libm3/src/geometry/Axis.i3.html">Axis</A>;
IMPORT <A HREF="../derived/StarterList.i3.html">StarterList</A>;
IMPORT <A HREF="StarterScan.i3.html">StarterScan</A>;
IMPORT <A HREF="../derived/StarterScanList.i3.html">StarterScanList</A>;
IMPORT <A HREF="../derived/ListPaneStarter.i3.html">ListPaneStarter</A>;
IMPORT <A HREF="MiniPane.i3.html">MiniPane</A>;
IMPORT <A HREF="PathnameUtils.i3.html">PathnameUtils</A>;
CONST
  DebugLevel = 90;
&lt;* FATAL Split.NotAChild *&gt;
REVEAL
  <A NAME="T">T</A> = Public BRANDED OBJECT
    daemon: KeyDaemon.T;
    visPanes, primPanes: PaneList.T;
    (* visPanes.head is selected *)
    (* primPanes: all panes excluding clones *)
    starters: StarterList.T;
    miniPane: MiniPane.T;
    inputPane: PaneFrame.T; (* the pane that caused input to be called *)
    inputPrompt: TEXT;
    inputCallback: InputCallback := NIL;
    pathCallback: PathCallback;
    paneUpdater: PaneUpdater;
  OVERRIDES
    (* anyone calling these methods must lock mu. *)
    focusTo := FocusTo;
    rotFocus := RotFocus;
    splitPane := SplitPane;
    unSplit := UnSplit;
    unSplit1 := UnSplit1;
    setPane := SetPane;
    listPanes := ListPanes;
    inputPath := InputPath;
    cookedKey := CookedKey;
    print := Print;
    input := Input;
    setPaneUpdater := SetPaneUpdater;
    getSelectedPane := GetSelectedPane;

    (* these methods lock mu. *)
    init := Init;
    misc := Misc;
    key := Key;
  END;

TYPE
  Act={Replace, Delete};
  NullPaneUpdater = PaneUpdater OBJECT OVERRIDES apply := NullApply; END;

PROCEDURE <A NAME="NullApply"><procedure>NullApply</procedure></A>(&lt;*UNUSED*&gt;self: NullPaneUpdater;
                    &lt;*UNUSED*&gt;pane: VBT.T) =
  BEGIN END NullApply;

PROCEDURE <A NAME="SetPaneUpdater"><procedure>SetPaneUpdater</procedure></A>(self: T; pu: PaneUpdater) =
  VAR
    cur := self.visPanes;
  BEGIN
    self.paneUpdater := pu;
    WHILE cur # NIL DO
      pu.apply(cur.head);
      cur := cur.tail;
    END;
  END SetPaneUpdater;

PROCEDURE <A NAME="TwoStepReplace"><procedure>TwoStepReplace</procedure></A>(s: Split.T; old, new: VBT.T) =
  VAR
    pred: VBT.T;
  BEGIN
    pred := Split.Pred(s, old);
    Debug.S(&quot;2SR: Gonna Delete.&quot;, DebugLevel);
    Split.Delete(s, old);
    Debug.S(&quot;2SR: Gonna Secret Delete.&quot;, DebugLevel);
    Split.Delete(old, new);
    Debug.S(&quot;2SR: Gonna Insert.&quot;, DebugLevel);
    Split.Insert(s, pred, new);
    Debug.S(&quot;2SR: Didn't we make it&quot;, DebugLevel);
  END TwoStepReplace;

PROCEDURE <A NAME="FindHead"><procedure>FindHead</procedure></A>(self: T; action: Act; elem: VBT.T := NIL) =
  VAR
    head := self.visPanes.head;
  PROCEDURE Search(fromnode, parent: VBT.T) =
    VAR
      ch1,ch2: VBT.T;
    BEGIN
      Debug.S(&quot;FindHead.Search&quot;, DebugLevel);
      TYPECASE fromnode OF
      | T (e) =&gt;
        BEGIN
          &lt;* ASSERT e = self *&gt;
          Debug.S(&quot;Looking at first PaneManVBT child&quot;, DebugLevel);
          ch1 := Split.Nth(e, 0);
          IF ch1=head THEN
            Debug.S(&quot;Found in PaneManVBT&quot;, DebugLevel);
            IF action=Act.Replace THEN
              Split.Replace(e, ch1, elem);
            END;
          ELSE
            &lt;* ASSERT ISTYPE(ch1, PaneFrame.T) OR ISTYPE(ch1, HVSplit.T) *&gt;
            Search(ch1, e);
          END;
        END;
      | PaneFrame.T (e) =&gt; Debug.S(&quot;Leaf: &quot; &amp; e.title, DebugLevel);
      | Split.T (e) =&gt;
        BEGIN
          &lt;*ASSERT e # self *&gt;
          Debug.S(&quot;Looking at Split children&quot;, DebugLevel);
          ch1 := Split.Nth(e, 0);
          ch2 := Split.Nth(e, 2);
          IF ch1=head THEN
            Debug.S(&quot;Found in Split[1]&quot;, DebugLevel);
            IF action=Act.Replace THEN
              Split.Replace(e, ch1, elem);
            ELSE
              TwoStepReplace(parent, e, ch2);
              Split.Delete(e, ch1);
            END;
          ELSIF ch2=head THEN
            Debug.S(&quot;Found in Split[3]&quot;);
            IF action=Act.Replace THEN
              Split.Replace(e, ch2, elem);
            ELSE
              TwoStepReplace(parent, e, ch1);
              Split.Delete(e, ch2);
            END;
          END;
          Search(ch1, e);
          Search(ch2, e);
        END; (* Split.T *)
      ELSE
        &lt;* ASSERT FALSE *&gt;
      END; (* TYPECASE *)
    END Search;
  BEGIN
    Search(self, NIL);
  END FindHead;

PROCEDURE <A NAME="CountFocusable"><procedure>CountFocusable</procedure></A>(self: T): INTEGER =
  VAR
    cur := self.visPanes;
    count: INTEGER := 0;
  BEGIN
    WHILE cur # NIL DO
      IF cur.head.canHilite THEN
        count := count + 1;
        IF count &gt;= 2 THEN
          RETURN count;
        END;
      END;
      cur := cur.tail;
    END;
    RETURN count;
  END CountFocusable;

PROCEDURE <A NAME="RotForward"><procedure>RotForward</procedure></A>(self: T) =
  BEGIN
    self.visPanes := PaneList.Cons(
                         PaneListExtras.LastDelD(self.visPanes),
                         self.visPanes);
  END RotForward;

PROCEDURE <A NAME="RotFocus"><procedure>RotFocus</procedure></A>(self: T; reverse: BOOLEAN := FALSE) =
  VAR
    count := CountFocusable(self);
  BEGIN
    IF (count &gt;= 2) OR (count=1 AND NOT self.visPanes.head.hilited) THEN
      IF self.visPanes.head.hilited THEN
        self.visPanes.head.frameHilite(FALSE);
      END;
      REPEAT
        IF reverse THEN
          self.visPanes := PaneList.AppendD(self.visPanes.tail,
                                      PaneList.List1(self.visPanes.head));
        ELSE
          RotForward(self);
        END;
      UNTIL self.visPanes.head.canHilite;
      self.visPanes.head.frameHilite(TRUE);
    END;
  END RotFocus;

PROCEDURE <A NAME="FocusTo"><procedure>FocusTo</procedure></A>(self: T; pane: VBT.T) =
  VAR
    original := self.visPanes.head;
  BEGIN
    IF NOT NARROW(pane, PaneFrame.T).hilited THEN
      WHILE self.visPanes.head # pane DO
        RotForward(self);
        &lt;* ASSERT original # self.visPanes.head *&gt;  (*not found*)
      END;
      Debug.S(&quot;Re-hiliting due to a Focus-to&quot;, DebugLevel);
      IF original.hilited THEN
        Debug.S(&quot;FocusTo unhilites original&quot;, DebugLevel);
        original.frameHilite(FALSE);
      END;
      Debug.S(&quot;FocusTo hilites pane&quot;, DebugLevel);
      self.visPanes.head.frameHilite(TRUE);
    END;
  END FocusTo;

PROCEDURE <A NAME="SwapFocus"><procedure>SwapFocus</procedure></A>(self: T; to: PaneFrame.T) =
  BEGIN
    to.canHilite := TRUE;
    IF to # self.visPanes.head THEN
      self.visPanes.head.frameHilite(FALSE);
      self.visPanes := PaneList.Cons(to, PaneListExtras.DeleteD(
                                             self.visPanes, to));
      to.frameHilite(TRUE);
    END;
  END SwapFocus;

PROCEDURE <A NAME="SplitPane"><procedure>SplitPane</procedure></A>(self: T; hv: Axis.T) =
  BEGIN
    IF self.miniPane = self.visPanes.head THEN
      self.print(&quot;Cannot split minipane&quot;);
    ELSE
      VAR
        ch1 := self.visPanes.head;
        ch2 := ch1.frameClone();
        split := HVSplit.New(hv);
        bar := HVBar.New(size := 1.0, txt := Pixmap.Solid);
      BEGIN
        Debug.S(&quot;PaneManVBT.SplitPane&quot;, DebugLevel);
        self.visPanes.head.frameHilite(FALSE);
        Debug.S(&quot;SplitPane: FindHead&quot;);
        &lt;* ASSERT ch1 # NIL *&gt;
        &lt;* ASSERT ch2 # NIL *&gt;
        (* self.paneUpdater.apply(ch2); *)
        FindHead(self, Act.Replace, split);
        Debug.S(&quot;SplitPane: AddChild&quot;, DebugLevel);
        Split.AddChild(split, ch1, bar, ch2);
        Debug.S(&quot;SplitPane: Augment visPanes&quot;, DebugLevel);
        self.visPanes := PaneList.Cons(ch2, self.visPanes);
        ch2.frameHilite(TRUE);
        ch2.frameVisible(TRUE);
        Debug.S(&quot;Completed SplitPane&quot;, DebugLevel);
      END;
    END;
  END SplitPane;

PROCEDURE <A NAME="HidePane"><procedure>HidePane</procedure></A>(p: PaneFrame.T) =
  BEGIN
    IF p.hilited THEN
      p.frameHilite(FALSE);
      p.frameVisible(FALSE);
      IF NOT p.primary THEN
        p.frameDiscard();
      END;
    END;
  END HidePane;
</PRE> a hack: call this proc to ensure that the focus is reacquired after
   changing which pane is focused. 
<PRE>PROCEDURE <A NAME="ReAcquire"><procedure>ReAcquire</procedure></A>(self: T) =
  VAR
    cd: VBT.MiscRec;
  BEGIN
    Debug.S(&quot;PaneMan: ReAcquire&quot;, DebugLevel);
    cd.type := VBT.TakeSelection;
    cd.time := self.time;
    cd.detail := VBT.NullDetail;
    cd.selection := VBT.KBFocus;
    self.visPanes.head.focusWarn();
    MiscNoLock(self, cd);
    self.visPanes.head.hilited := FALSE;
  END ReAcquire;

PROCEDURE <A NAME="UnSplit"><procedure>UnSplit</procedure></A>(self: T) =
  VAR
    base := Split.Nth(self, 0);
    head := self.visPanes.head;
  BEGIN
    IF base = head THEN
    ELSIF head = self.miniPane THEN
    ELSE
</PRE><BLOCKQUOTE><EM>      head.frameHilite(FALSE);  </EM></BLOCKQUOTE><PRE>
      FindHead(self, Act.Delete);
      base := Split.Nth(self, 0);
      &lt;* ASSERT base # self.visPanes.head *&gt;
      Split.Replace(self,
                    base,
                    self.visPanes.head);
      Debug.S(&quot;UnSplit: Replaced base with head&quot;, DebugLevel);
      WHILE self.visPanes.tail # NIL DO
        self.visPanes := self.visPanes.tail;
        IF self.visPanes.head # self.miniPane THEN
          FindHead(self, Act.Delete);
          HidePane(self.visPanes.head);
        END;
      END;
      self.visPanes := PaneList.List2(head, self.miniPane);
      ReAcquire(self);
    END;
  END UnSplit;

PROCEDURE <A NAME="UnSplit1"><procedure>UnSplit1</procedure></A>(self: T) =
  VAR
    base := Split.Nth(self, 0);
  BEGIN
    IF base = self.visPanes.head THEN
      self.print(&quot;Cannot hide sole visible pane&quot;);
    ELSE
      FindHead(self, Act.Delete);
      HidePane(self.visPanes.head);
      self.visPanes := self.visPanes.tail;
      IF self.visPanes.head.canHilite THEN
        self.visPanes.head.frameHilite(TRUE);
      ELSE
        RotFocus(self);
      END;
    END;
  END UnSplit1;

PROCEDURE <A NAME="Misc"><procedure>Misc</procedure></A>(self: T; READONLY cd: VBT.MiscRec) =
  BEGIN
    LOCK self.mu DO
      MiscNoLock(self, cd);
    END;
  END Misc;

PROCEDURE <A NAME="MiscNoLock"><procedure>MiscNoLock</procedure></A>(self: T; READONLY cd: VBT.MiscRec) =
  BEGIN
    self.time := cd.time;
    IF cd.type = VBT.TakeSelection THEN
      TRY
        VBT.Acquire(self, cd.selection, cd.time);
      EXCEPT
        VBT.Error =&gt;
      END;
      self.visPanes.head.frameHilite(TRUE);
    ELSIF cd.type = VBT.Lost THEN
      IF self.stealFocusWarning THEN
  Debug.S(&quot;PaneMan: Not unhiliting because of the focus warning.&quot;, DebugLevel);
        self.stealFocusWarning := FALSE;
      ELSE
        Debug.S(&quot;PaneMan: Unhilighting due to focus loss.&quot;, DebugLevel);
        self.visPanes.head.frameHilite(FALSE);
      END;
    ELSE
      HVSplit.T.misc(self, cd);
    END;
  END MiscNoLock;

PROCEDURE <A NAME="Key"><procedure>Key</procedure></A>(self: T; READONLY key: VBT.KeyRec) =
  VAR
    head := self.visPanes.head;
  BEGIN
    LOCK self.mu DO
      self.time := key.time;
      Debug.S(&quot;PaneMan Got Key.&quot;, DebugLevel);
      IF head.canHilite AND NOT head.hilited THEN
        (* hack: See ReAcquire. *)
        Debug.S(&quot;Noticed head was not hilited. Fixing.&quot;);
        head.frameHilite(TRUE);
      END;
      self.daemon.key(key);
    END;
  END Key;

PROCEDURE <A NAME="CookedKey"><procedure>CookedKey</procedure></A>(self: T; READONLY key: VBT.KeyRec) =
  VAR
    cb := self.inputCallback;
    t: TEXT;
    hook := FALSE;
  PROCEDURE Parse(): BOOLEAN =
    BEGIN
      t := self.miniPane.getText();
      IF Text.Equal(Text.Sub(t, 0, Text.Length(self.inputPrompt)),
                    self.inputPrompt) THEN
        t := Text.Sub(t, Text.Length(self.inputPrompt), LAST(INTEGER));
        RETURN TRUE;
      ELSE
        self.print(&quot;prompt was changed&quot;);
        RETURN FALSE;
      END;
    END Parse;
  BEGIN
    Debug.S(&quot;Cooked: &quot; &amp; Fmt.Bool(key.wentDown) &amp;
      Fmt.Int(key.whatChanged), DebugLevel);
    IF key.wentDown AND self.miniPane.hilited AND self.inputCallback # NIL THEN
      CASE key.whatChanged OF
      | 65293, 65241 =&gt;
        hook := TRUE;
        self.focusTo(self.inputPane);
        self.miniPane.canHilite := FALSE;
        self.inputCallback := NIL;
        IF Parse() THEN
          cb.accept(t);
        END;
      | 65289 =&gt;
        hook := TRUE;
        IF Parse() THEN
          cb.complete(t);
          self.miniPane.setText(self.inputPrompt &amp; t);
        END;
      | ORD('g') =&gt;
        IF VBT.Modifier.Control IN key.modifiers THEN
          self.focusTo(self.inputPane);
          self.miniPane.canHilite := FALSE;
          self.inputCallback := NIL;
          self.print(&quot;cancel&quot;);
        END;
      ELSE
      END;
    END;
    IF NOT hook THEN
      self.visPanes.head.key(key);
    END;
  END CookedKey;

PROCEDURE <A NAME="FindPane"><procedure>FindPane</procedure></A>(self: T; path: TEXT): PaneFrame.T =
  VAR
    cur: PaneList.T;
  BEGIN
</PRE><BLOCKQUOTE><EM>    Debug.S(<CODE>yo</CODE>); </EM></BLOCKQUOTE><PRE>
    IF path # NIL THEN
      cur := self.primPanes;
      WHILE cur # NIL DO
</PRE><BLOCKQUOTE><EM>        Debug.S(cur.head.path &amp; <CODE> &amp; </CODE> &amp; path,0); </EM></BLOCKQUOTE><PRE>
        (* I want Pathname.Equal *)
        IF Text.Equal(cur.head.path, path) THEN
          RETURN cur.head;
        END;
        cur := cur.tail;
      END;
    END;
    RETURN NIL;
  END FindPane;

PROCEDURE <A NAME="SetPane"><procedure>SetPane</procedure></A>(self: T; from: StarterScan.T): BOOLEAN =
  VAR
    elem: PaneFrame.T;
    path: TEXT;
  BEGIN
    Debug.S(&quot;PaneManVBT.SetPane&quot;, DebugLevel);
    path := StarterScan.GetPath(from);
    elem := FindPane(self, path);
    IF elem = NIL THEN
      Debug.S(&quot;Not an existing path&quot;, DebugLevel);
      elem := StarterScan.NewPaneFrame(self.starters, self, from);
    ELSIF elem.visible THEN
      Debug.S(&quot;An existing path&quot;, DebugLevel);
      IF elem = self.visPanes.head THEN
        elem := NIL;
      ELSE
        elem := elem.frameClone();
      END;
    END;
    IF elem # NIL THEN
      self.paneUpdater.apply(elem);
      FindHead(self, Act.Replace, elem);
      HidePane(self.visPanes.head);
      self.visPanes := PaneList.Cons(elem, self.visPanes.tail);
      self.primPanes := PaneList.Cons(elem, self.primPanes);
      elem.frameHilite(TRUE);
      elem.frameVisible(TRUE);
      RETURN TRUE;
    END;
    RETURN FALSE;
  END SetPane;

PROCEDURE <A NAME="ListPanes"><procedure>ListPanes</procedure></A>(self: T) =
  BEGIN
    IF self.miniPane = self.visPanes.head THEN
      self.print(&quot;Cannot list panes in minipane&quot;)
    ELSE
      EVAL self.setPane(StarterScan.FromStarter(ListPaneStarter.S,
                                                &quot;All Panes&quot;));
    END;
  END ListPanes;

PROCEDURE <A NAME="Print"><procedure>Print</procedure></A>(self: T; message: TEXT) =
  BEGIN
    (* self.rotFocus(); *)
    self.miniPane.canHilite := FALSE;
    self.miniPane.setText(message);
  END Print;

PROCEDURE <A NAME="Input"><procedure>Input</procedure></A>(self: T; prompt, default: TEXT; result: InputCallback) =
  BEGIN
    IF self.miniPane = self.visPanes.head THEN
      self.print(&quot;Command attempted to use minipane while in minipane&quot;);
    ELSE
      self.inputPane := self.visPanes.head;
      self.inputPrompt := prompt;
      self.print(prompt &amp; default);
      SwapFocus(self, self.miniPane);
      self.inputCallback := result;
      ReAcquire(self);
    END;
  END Input;

TYPE
  PathCallback = InputCallback OBJECT
    v: T;
    forWrite: BOOLEAN;
  OVERRIDES
    accept := AcceptPath;
    complete := CompletePath;
  END;

PROCEDURE <A NAME="CompletePath"><procedure>CompletePath</procedure></A>(&lt;*UNUSED*&gt;cb: PathCallback; VAR t: TEXT) =
  BEGIN
    t := PathnameUtils.Complete(t);
  END CompletePath;

PROCEDURE <A NAME="AcceptPath"><procedure>AcceptPath</procedure></A>(cb: PathCallback; result: TEXT) =
  VAR
    self := cb.v;
  BEGIN
    IF cb.forWrite THEN
      self.visPanes.head.frameWrite(result);
    ELSE
      EVAL self.setPane(StarterScan.FromPath(result));
      ReAcquire(self);
    END;
  END AcceptPath;

PROCEDURE <A NAME="InputPath"><procedure>InputPath</procedure></A>(self: T; forWrite: BOOLEAN) =
  VAR
    path := self.visPanes.head.path;
    prompt: TEXT;
  BEGIN
    IF forWrite THEN
      prompt := &quot;Write file: &quot;;
    ELSE
      prompt := &quot;Find file: &quot;;
      path := PathnameUtils.SlashedPrefix(path);
    END;
    self.pathCallback.forWrite := forWrite;
    self.input(prompt, path, self.pathCallback);
  END InputPath;

PROCEDURE <A NAME="Init"><procedure>Init</procedure></A>(self: T; starters: StarterList.T;
               startingStarters: StarterScanList.T := NIL): T =
  VAR
    bar := HVBar.New(size := 1.0, txt := Pixmap.Solid);
    docPane: PaneFrame.T;
    starts := startingStarters;
  BEGIN
    self.mu := NEW(MUTEX);
    LOCK self.mu DO
      Debug.S(&quot;Initing the PaneMan.&quot;);
      EVAL HVSplit.T.init(self, Axis.T.Ver, adjustable := FALSE);
      self.paneUpdater := NEW(NullPaneUpdater);
      self.pathCallback := NEW(PathCallback, v := self);
      self.starters := starters;
      self.request := NEW(RequestDaemon.T).init(self);
      self.daemon := NEW(KeyDaemon.T).init(self);
      self.miniPane := NEW(MiniPane.T).init(&quot;No MiniPane Path&quot;,
                                            &quot;MiniPane&quot;, &quot;MiniPane&quot;, self,
                                            NIL);
      self.miniPane.canHilite := FALSE;
      IF starts = NIL THEN
        starts := StarterScanList.List1(StarterScan.Default());
      END;
      docPane := StarterScan.NewPaneFrame(self.starters, self, starts.head);
      starts := starts.tail;
      self.visPanes := PaneList.List2(docPane, self.miniPane);
      self.primPanes := PaneList.List1(docPane);
      self.visPanes.head.hilite(TRUE);
      Split.AddChild(self, docPane, bar, self.miniPane);
      WHILE starts # NIL DO
        SplitPane(self, Axis.T.Ver);
        EVAL SetPane(self, starts.head);
        starts := starts.tail;
      END;
      RETURN self;
    END;
  END Init;

PROCEDURE <A NAME="GetSelectedPane"><procedure>GetSelectedPane</procedure></A>(self: T): VBT.T =
  BEGIN
    RETURN self.visPanes.head;
  END GetSelectedPane;

BEGIN
END PaneManVBT.
</PRE>
</inModule>
<HR>
<A NAME="x1">interface Debug is in:
</A><UL>
<LI><A HREF="../../cit_util/src/Debug.i3.html#0TOP0">cit_util/src/Debug.i3</A>
<LI><A HREF="../../debug/src/Debug.i3.html#0TOP0">debug/src/Debug.i3</A>
</UL>
<P>
<PRE>























</PRE>
</BODY>
</HTML>
