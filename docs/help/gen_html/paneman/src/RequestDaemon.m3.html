<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: paneman/src/RequestDaemon.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>paneman/src/RequestDaemon.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright (c) 2000 California Institute of Technology 
 All rights reserved. See the file COPYRIGHT for a full description. 
 $Id: RequestDaemon.m3.html,v 1.3 2010-04-29 17:19:46 wagner Exp $ 

<P><PRE>MODULE <module><implements><A HREF="RequestDaemon.i3.html">RequestDaemon</A></implements></module>;
IMPORT <A HREF="PaneManVBT.i3.html">PaneManVBT</A>;
IMPORT <A HREF="PaneManOp.i3.html">PaneManOp</A>;
IMPORT <A HREF="PaneManRequest.i3.html">PaneManRequest</A>;
IMPORT <A HREF="../../m3core/src/thread/Common/Thread.i3.html">Thread</A>;
IMPORT <A HREF="../derived/PMRequestChan.i3.html">PMRequestChan</A>;
IMPORT <A HREF="StarterScan.i3.html">StarterScan</A>;
REVEAL
  <A NAME="T">T</A> = Public BRANDED &quot;RequestDaemon&quot; OBJECT
    pm: PaneManVBT.T;
    t: Thread.T;
    r: PMRequestChan.T;
  OVERRIDES
    init := Init;

    startPane := StartPane;
    print := Print;
    input := Input;
  END;

PROCEDURE <A NAME="Init"><procedure>Init</procedure></A>(self: T; pm: PaneManVBT.T): T =
  VAR
    k := NEW(Closure, daemon := self);
  BEGIN
    self.pm := pm;
    self.r := NEW(PMRequestChan.T).init();
    self.t := Thread.Fork(k);
    RETURN self;
  END Init;

TYPE
  InputCallback = PaneManOp.InputCallback;
  Request = PaneManRequest.T;
  Kind = PaneManRequest.Kind;

PROCEDURE <A NAME="StartPane"><procedure>StartPane</procedure></A>(self: T; s: StarterScan.T) =
  BEGIN
    self.r.send(Request{kind := Kind.Start, s := s});
  END StartPane;

PROCEDURE <A NAME="Print"><procedure>Print</procedure></A>(self: T; message: TEXT) =
  BEGIN
    self.r.send(Request{kind := Kind.Print, message := message});
  END Print;

PROCEDURE <A NAME="Input"><procedure>Input</procedure></A>(self: T; prompt, default: TEXT; result: InputCallback) =
  BEGIN
    self.r.send(Request{kind := Kind.Input, message := prompt,
                        default := default, cb := result});
  END Input;

TYPE
  Closure = Thread.Closure OBJECT
    daemon: T;
  OVERRIDES
    apply := Apply;
  END;

PROCEDURE <A NAME="Apply"><procedure>Apply</procedure></A>(c: Closure): REFANY =
  VAR
    self := c.daemon;
    r: PaneManRequest.T;
    pm := self.pm;
  BEGIN
    LOOP
      r := self.r.recv();
      LOCK pm.mu DO
        CASE r.kind OF
        | Kind.Start =&gt; EVAL pm.setPane(r.s);
        | Kind.Print =&gt; pm.print(r.message);
        | Kind.Input =&gt; pm.input(r.message, r.default, r.cb);
        END;
      END;
    END;
  END Apply;

BEGIN
END RequestDaemon.
</PRE>
</inModule>
<PRE>























</PRE>
</BODY>
</HTML>
