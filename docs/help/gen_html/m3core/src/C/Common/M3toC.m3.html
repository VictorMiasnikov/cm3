<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: m3core/src/C/Common/M3toC.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>m3core/src/C/Common/M3toC.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright (C) 1994, Digital Equipment Corporation           
 All rights reserved.                                        
 See the file COPYRIGHT for a full description.              
                                                             
 Last modified on Tue Jun 21 11:56:30 PDT 1994 by kalsow     
      modified on Fri May  7 21:05:40 PDT 1993 by mjordan    
      modified on Wed Mar 13 01:14:25 1991 by muller         
      modified on Tue Apr 24 16:40:16 1990 by jerome         

<P><PRE>UNSAFE MODULE <module><implements><A HREF="M3toC.i3.html">M3toC</A></implements></module>;

IMPORT <A HREF="Ctypes.i3.html">Ctypes</A>, <A HREF="Cstdlib.i3.html">Cstdlib</A>, <A HREF="Cstring.i3.html">Cstring</A>;
IMPORT <A HREF="../../text/Text.i3.html">Text</A>, <A HREF="../../text/TextClass.i3.html">TextClass</A>, <A HREF="../../text/Text8.i3.html">Text8</A>, <A HREF="../../text/Text8CString.i3.html">Text8CString</A>;
IMPORT <A HREF="../../thread/Common/Scheduler.i3.html">Scheduler</A>;

VAR
  zeroValue := 0;
  zeroPtr   := LOOPHOLE (ADR (zeroValue), Ctypes.char_star);

TYPE
  CharPtr  = UNTRACED REF CHAR;
  ArrayPtr = UNTRACED REF ARRAY OF CHAR;
  OpenArray = RECORD
    start  : ADDRESS;
    length : INTEGER;
  END;

PROCEDURE <A NAME="CopyTtoS"><procedure>CopyTtoS</procedure></A> (t: TEXT): Ctypes.char_star =
  VAR info: TextClass.Info;  arr: OpenArray;
  BEGIN
    IF (t = NIL) THEN RETURN zeroPtr; END;
    t.get_info (info);
    Scheduler.DisableSwitching();
    arr.start  := Cstdlib.malloc (info.length + 1);
    Scheduler.EnableSwitching();
    arr.length := info.length;
    Text.SetChars (LOOPHOLE (ADR (arr), ArrayPtr)^, t, 0);
    LOOPHOLE (arr.start + info.length, CharPtr)^ := '\000';
    RETURN arr.start;
  END CopyTtoS;

PROCEDURE <A NAME="FreeCopiedS"><procedure>FreeCopiedS</procedure></A> (s: Ctypes.char_star) =
  BEGIN
    IF (s # zeroPtr) THEN Cstdlib.free (s); END;
  END FreeCopiedS;

PROCEDURE <A NAME="SharedTtoS"><procedure>SharedTtoS</procedure></A> (t: TEXT): Ctypes.char_star =
  VAR info: TextClass.Info;
  BEGIN
    IF (t = NIL) THEN RETURN zeroPtr; END;
    t.get_info (info);
    IF info.start # NIL AND NOT info.wide THEN
      (* make sure the thing is null terminated! *)
      IF LOOPHOLE (info.start + info.length, Ctypes.char_star)^ = 0 THEN
        RETURN info.start;
      END;
    END;
    RETURN CopyTtoS (t);
  END SharedTtoS;

PROCEDURE <A NAME="FreeSharedS"><procedure>FreeSharedS</procedure></A> (t: TEXT;  s: Ctypes.char_star) =
  VAR info: TextClass.Info;
  BEGIN
    IF (s # zeroPtr) THEN
      t.get_info (info);
      IF (info.start # s) THEN Cstdlib.free (s); END;
    END;
  END FreeSharedS;

PROCEDURE <A NAME="FlatTtoS"><procedure>FlatTtoS</procedure></A> (t: TEXT): Ctypes.char_star =
  VAR info: TextClass.Info;
  BEGIN
    IF (t = NIL) THEN RETURN zeroPtr; END;
    t.get_info (info);
    IF info.start # NIL AND NOT info.wide THEN
      (* make sure the thing is null terminated! *)
      IF LOOPHOLE (info.start + info.length, Ctypes.char_star)^ = 0 THEN
        RETURN info.start;
      END;
    END;
    (* force a runtime fault *)
    VAR i: CARDINAL; BEGIN i := -1; &lt;*NOWARN*&gt; END;
    RETURN NIL;
  END FlatTtoS;

PROCEDURE <A NAME="StoT"><procedure>StoT</procedure></A> (s: Ctypes.char_star): TEXT =
  BEGIN
    RETURN Text8CString.New (s);
  END StoT;

PROCEDURE <A NAME="CopyStoT"><procedure>CopyStoT</procedure></A> (s: Ctypes.char_star): TEXT =
  VAR len := Cstring.strlen (s);  t := Text8.Create (len);
  BEGIN
    EVAL Cstring.memcpy (ADR (t.contents[0]), s, len);
    RETURN t;
  END CopyStoT;

BEGIN
END M3toC.
</PRE>
</inModule>
<PRE>























</PRE>
</BODY>
</HTML>
