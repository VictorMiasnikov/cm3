<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: m3core/src/runtime/common/RTAllocator.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>m3core/src/runtime/common/RTAllocator.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright (C) 1993, Digital Equipment Corporation         
 All rights reserved.                                      
 See the file COPYRIGHT for a full description.            
                                                           
  Portions Copyright 1996-2000, Critical Mass, Inc.        
 See file COPYRIGHT-CMASS for details.                     
                                                           
| Last modified on Thu May  4 14:02:27 PDT 1995 by kalsow  
|      modified on Wed Jun  2 15:00:17 PDT 1993 by muller  
|      modified on Wed Apr 21 13:14:37 PDT 1993 by mcjones 
|      modified on Wed Mar 10 11:01:47 PST 1993 by mjordan 
|      modified on Tue Mar  9 08:45:18 PST 1993 by jdd     

<P><PRE>UNSAFE MODULE <module>RTAllocator</module>
EXPORTS <A HREF="RTAllocator.i3.html"><implements>RTAllocator</A></implements>, <A HREF="RTAllocCnts.i3.html"><implements>RTAllocCnts</A></implements>, <A HREF="RTHooks.i3.html"><implements>RTHooks</A></implements>, <A HREF="RTHeapRep.i3.html"><implements>RTHeapRep</A></implements>;

IMPORT <A HREF="../../C/Common/Cstdlib.i3.html">Cstdlib</A>, <A HREF="RT0.i3.html">RT0</A>, <A HREF="RTMisc.i3.html">RTMisc</A>, <A HREF="RTOS.i3.html">RTOS</A>, <A HREF="RTType.i3.html">RTType</A>, <A HREF="../../thread/Common/Scheduler.i3.html">Scheduler</A>, <A HREF="#x1">RTThread</A>;
IMPORT <A HREF="RuntimeError.i3.html">RuntimeError</A> AS RTE, <A HREF="../../word/Word.i3.html">Word</A>;
FROM <A HREF="RTType.i3.html">RTType</A> IMPORT Typecode;
</PRE> In the following procedures, <CODE>RTType.Get(tc)</CODE> will fail if <CODE>tc</CODE> is not
   proper. 

<P><PRE>TYPE
  TK = RT0.TypeKind;
</PRE>----------------------------------------------------------- RTAllocator ---

<P><PRE>PROCEDURE <A NAME="NewTraced"><procedure>NewTraced</procedure></A>(tc: Typecode): REFANY
  RAISES {OutOfMemory} =
  VAR res := GetTraced(RTType.Get(tc));
  BEGIN
    IF (res = NIL) THEN RAISE OutOfMemory; END;
    RETURN res;
  END NewTraced;

PROCEDURE <A NAME="NewUntraced"><procedure>NewUntraced</procedure></A>(tc: Typecode): ADDRESS
  RAISES {OutOfMemory} =
  VAR res := GetUntracedRef(RTType.Get(tc));
  BEGIN
    IF (res = NIL) THEN RAISE OutOfMemory; END;
    RETURN res;
  END NewUntraced;

PROCEDURE <A NAME="NewUntracedObject"><procedure>NewUntracedObject</procedure></A>(tc: Typecode): UNTRACED ROOT
  RAISES {OutOfMemory} =
  VAR res := GetUntracedObj(RTType.Get(tc));
  BEGIN
    IF (res = NIL) THEN RAISE OutOfMemory; END;
    RETURN res;
  END NewUntracedObject;

PROCEDURE <A NAME="NewTracedArray"><procedure>NewTracedArray</procedure></A>(tc: Typecode; READONLY s: Shape): REFANY
  RAISES {OutOfMemory} =
  VAR res := GetOpenArray(RTType.Get(tc), s);
  BEGIN
    IF (res = NIL) THEN RAISE OutOfMemory; END;
    RETURN res;
  END NewTracedArray;

PROCEDURE <A NAME="NewUntracedArray"><procedure>NewUntracedArray</procedure></A>(tc: Typecode; READONLY s: Shape): ADDRESS
  RAISES {OutOfMemory} =
  VAR res := GetUntracedOpenArray(RTType.Get(tc), s);
  BEGIN
    IF (res = NIL) THEN RAISE OutOfMemory; END;
    RETURN res;
  END NewUntracedArray;

PROCEDURE <A NAME="Clone"><procedure>Clone</procedure></A> (ref: REFANY): REFANY
  RAISES {OutOfMemory} =
  VAR
    hdr: RefHeader;  def: RT0.TypeDefn;  dataSize: CARDINAL;
    res: ADDRESS;
    thread := RTThread.MyHeapState();
  BEGIN
    IF (ref = NIL) THEN RETURN NIL; END;
    IF Word.And(LOOPHOLE(ref, Word.T), 1) # 0 THEN RETURN ref; END;

    hdr := LOOPHOLE(ref, ADDRESS) - ADRSIZE(Header);
    def := RTType.Get(hdr.typecode);
    dataSize := ReferentSize(hdr);

    INC(thread.inCritical);
    res := AllocTraced(dataSize, def.dataAlignment, thread^);
    IF res = NIL THEN DEC(thread.inCritical); RAISE OutOfMemory; END;
    RTMisc.Copy(LOOPHOLE(ref, ADDRESS), res, dataSize);
    IF def.kind = ORD (TK.Array) THEN
      (* open array: update the internal pointer *)
      LOOPHOLE(res, UNTRACED REF ADDRESS)^ := res + def.dataSize;
    END;
    DEC(thread.inCritical);

    IF countsOn THEN
      IF def.kind = ORD (TK.Array)
        THEN BumpSize(def.typecode, dataSize)
        ELSE BumpCnt (def.typecode);
      END;
    END;
    IF (callback # NIL) THEN callback (LOOPHOLE(res, REFANY)) END;
    RETURN LOOPHOLE(res, REFANY);
  END Clone;
</PRE>--------------------------------------------------------------- RTHooks ---

<P><PRE>PROCEDURE <A NAME="Allocate"><procedure>Allocate</procedure></A> (defn: ADDRESS): REFANY =
  VAR res := GetTraced(defn);
  BEGIN
    IF (res = NIL) THEN RTE.Raise(RTE.T.OutOfMemory); END;
    RETURN res;
  END Allocate;

PROCEDURE <A NAME="AllocateTracedRef"><procedure>AllocateTracedRef</procedure></A> (defn: ADDRESS): REFANY =
  VAR res := GetTracedRef(defn);
  BEGIN
    IF (res = NIL) THEN RTE.Raise(RTE.T.OutOfMemory); END;
    RETURN res;
  END AllocateTracedRef;

PROCEDURE <A NAME="AllocateTracedObj"><procedure>AllocateTracedObj</procedure></A> (defn: ADDRESS): ROOT =
  VAR res := GetTracedObj(defn);
  BEGIN
    IF (res = NIL) THEN RTE.Raise(RTE.T.OutOfMemory); END;
    RETURN res;
  END AllocateTracedObj;

PROCEDURE <A NAME="AllocateUntracedRef"><procedure>AllocateUntracedRef</procedure></A> (defn: ADDRESS): ADDRESS =
  VAR res := GetUntracedRef(defn);
  BEGIN
    IF (res = NIL) THEN RTE.Raise(RTE.T.OutOfMemory); END;
    RETURN res;
  END AllocateUntracedRef;

PROCEDURE <A NAME="AllocateUntracedObj"><procedure>AllocateUntracedObj</procedure></A> (defn: ADDRESS): UNTRACED ROOT =
  VAR res := GetUntracedObj(defn);
  BEGIN
    IF (res = NIL) THEN RTE.Raise(RTE.T.OutOfMemory); END;
    RETURN res;
  END AllocateUntracedObj;

PROCEDURE <A NAME="AllocateOpenArray"><procedure>AllocateOpenArray</procedure></A> (defn: ADDRESS; READONLY s: Shape): REFANY =
  VAR res := GetOpenArray(defn, s);
  BEGIN
    IF (res = NIL) THEN RTE.Raise(RTE.T.OutOfMemory); END;
    RETURN res;
  END AllocateOpenArray;

PROCEDURE <A NAME="AllocateUntracedOpenArray"><procedure>AllocateUntracedOpenArray</procedure></A> (defn : ADDRESS;
                                     READONLY s: Shape): ADDRESS =
  VAR res := GetUntracedOpenArray(defn, s);
  BEGIN
    IF (res = NIL) THEN RTE.Raise(RTE.T.OutOfMemory); END;
    RETURN res;
  END AllocateUntracedOpenArray;

PROCEDURE <A NAME="DisposeUntracedRef"><procedure>DisposeUntracedRef</procedure></A> (VAR a: ADDRESS) =
  BEGIN
    IF a # NIL THEN
      Scheduler.DisableSwitching();
      Cstdlib.free(a);
      a := NIL;
      Scheduler.EnableSwitching();
    END;
  END DisposeUntracedRef;

PROCEDURE <A NAME="DisposeUntracedObj"><procedure>DisposeUntracedObj</procedure></A> (VAR a: UNTRACED ROOT) =
  VAR def: RT0.TypeDefn;
  BEGIN
    IF a # NIL THEN
      Scheduler.DisableSwitching();
      def := RTType.Get (TYPECODE (a));
      Cstdlib.free (a - MAX(BYTESIZE(Header), def.dataAlignment));
      a := NIL;
      Scheduler.EnableSwitching();
    END;
  END DisposeUntracedObj;
</PRE>-------------------------------------------------------------- internal ---

<P><PRE>PROCEDURE <A NAME="GetTraced"><procedure>GetTraced</procedure></A> (def: RT0.TypeDefn): REFANY =
  BEGIN
    CASE def.kind OF
    | ORD(TK.Ref) =&gt; RETURN GetTracedRef(def);
    | ORD(TK.Obj) =&gt; RETURN GetTracedObj(def);
    ELSE
      RTE.Raise(RTE.T.ValueOutOfRange);
    END;
    RETURN NIL;
  END GetTraced;

PROCEDURE <A NAME="GetTracedRef"><procedure>GetTracedRef</procedure></A> (def: RT0.TypeDefn): REFANY =
  VAR
    res: ADDRESS;
    thread := RTThread.MyHeapState();
  BEGIN
    IF def.typecode = 0 OR def.traced # 1 OR def.kind # ORD(TK.Ref) THEN
      RTE.Raise(RTE.T.ValueOutOfRange);
    END;

    INC(thread.inCritical);
    res := AllocTraced(def.dataSize, def.dataAlignment, thread^);
    IF res = NIL THEN DEC(thread.inCritical); RETURN NIL; END;
    LOOPHOLE(res - ADRSIZE(Header), RefHeader)^ :=
        Header{typecode := def.typecode, dirty := TRUE};
    IF def.initProc # NIL THEN def.initProc(res) END;
    DEC(thread.inCritical);

    IF countsOn THEN BumpCnt(def.typecode) END;
    IF (callback # NIL) THEN callback (LOOPHOLE(res, REFANY)) END;
    RETURN LOOPHOLE(res, REFANY);
  END GetTracedRef;

PROCEDURE <A NAME="GetTracedObj"><procedure>GetTracedObj</procedure></A> (def: RT0.TypeDefn): ROOT =
  VAR
    res: ADDRESS;
    thread := RTThread.MyHeapState();
  BEGIN
    IF def.typecode = 0 OR def.traced # 1 OR def.kind # ORD(TK.Obj) THEN
      RTE.Raise(RTE.T.ValueOutOfRange);
    END;

    INC(thread.inCritical);
    res := AllocTraced(def.dataSize, def.dataAlignment, thread^);
    IF res = NIL THEN DEC(thread.inCritical); RETURN NIL; END;
    LOOPHOLE(res - ADRSIZE(Header), RefHeader)^ :=
        Header{typecode := def.typecode, dirty := TRUE};
    InitObj (res, LOOPHOLE(def, RT0.ObjectTypeDefn));
    DEC(thread.inCritical);

    IF countsOn THEN BumpCnt(def.typecode) END;
    IF (callback # NIL) THEN callback (LOOPHOLE(res, REFANY)) END;
    RETURN LOOPHOLE(res, REFANY);
  END GetTracedObj;

PROCEDURE <A NAME="GetUntracedRef"><procedure>GetUntracedRef</procedure></A> (def: RT0.TypeDefn): ADDRESS =
  VAR res : ADDRESS;
  BEGIN
    IF def.typecode = 0 OR def.traced # 0 OR def.kind # ORD(TK.Ref) THEN
      RTE.Raise(RTE.T.ValueOutOfRange);
    END;
    Scheduler.DisableSwitching();
    res := Cstdlib.calloc(1, def.dataSize);
    Scheduler.EnableSwitching();
    IF res = NIL THEN RETURN NIL END;
    IF def.initProc # NIL THEN def.initProc(res); END;
    IF countsOn THEN BumpCnt(def.typecode) END;
    RETURN res;
  END GetUntracedRef;

PROCEDURE <A NAME="GetUntracedObj"><procedure>GetUntracedObj</procedure></A> (def: RT0.TypeDefn): UNTRACED ROOT =
  (* NOTE: result requires special treatment by DisposeUntracedObj *)
  VAR
    hdrSize := MAX(BYTESIZE(Header), def.dataAlignment);
    res     : ADDRESS;
  BEGIN
    IF def.typecode = 0 OR def.traced # 0 OR def.kind # ORD(TK.Obj) THEN
      RTE.Raise(RTE.T.ValueOutOfRange);
    END;
    Scheduler.DisableSwitching();
    res := Cstdlib.malloc(hdrSize + def.dataSize);
    Scheduler.EnableSwitching();
    IF res = NIL THEN RETURN NIL END;
    res := res + hdrSize;
    LOOPHOLE(res - ADRSIZE(Header), RefHeader)^ :=
        Header{typecode := def.typecode};
    InitObj (res, LOOPHOLE(def, RT0.ObjectTypeDefn));
    IF countsOn THEN BumpCnt (def.typecode) END;
    RETURN res;
  END GetUntracedObj;

PROCEDURE <A NAME="InitObj"><procedure>InitObj</procedure></A> (res: ADDRESS;  def: RT0.ObjectTypeDefn) =
  BEGIN
    LOOPHOLE(res, UNTRACED REF ADDRESS)^ := def.defaultMethods;
    WHILE def # NIL DO
      IF def.common.initProc # NIL THEN def.common.initProc(res); END;
      IF def.common.kind # ORD (TK.Obj) THEN EXIT; END;
      def := LOOPHOLE (def.parent, RT0.ObjectTypeDefn);
    END;
  END InitObj;

PROCEDURE <A NAME="GetOpenArray"><procedure>GetOpenArray</procedure></A> (def: RT0.TypeDefn; READONLY s: Shape): REFANY =
  VAR
    res: ADDRESS;
    dataSize: CARDINAL;
    thread := RTThread.MyHeapState();
  BEGIN
    IF def.typecode = 0 OR def.traced # 1 OR def.kind # ORD(TK.Array) THEN
      RTE.Raise(RTE.T.ValueOutOfRange);
    END;
    dataSize := ArraySize(LOOPHOLE(def, RT0.ArrayTypeDefn), s);

    INC(thread.inCritical);
    res := AllocTraced(dataSize, def.dataAlignment, thread^);
    IF res = NIL THEN DEC(thread.inCritical); RETURN NIL; END;
    LOOPHOLE(res - ADRSIZE(Header), RefHeader)^ :=
        Header{typecode := def.typecode, dirty := TRUE};
    InitArray (res, LOOPHOLE(def, RT0.ArrayTypeDefn), s);
    DEC(thread.inCritical);

    IF countsOn THEN BumpSize (def.typecode, dataSize) END;
    IF (callback # NIL) THEN callback (LOOPHOLE(res, REFANY)) END;
    RETURN LOOPHOLE(res, REFANY);
  END GetOpenArray;

PROCEDURE <A NAME="GetUntracedOpenArray"><procedure>GetUntracedOpenArray</procedure></A> (def: RT0.TypeDefn; READONLY s: Shape): ADDRESS =
  VAR
    res : ADDRESS;
    dataSize: CARDINAL;
  BEGIN
    IF def.typecode = 0 OR def.traced # 0 OR def.kind # ORD(TK.Array) THEN
      RTE.Raise(RTE.T.ValueOutOfRange);
    END;
    dataSize := ArraySize(LOOPHOLE(def, RT0.ArrayTypeDefn), s);

    Scheduler.DisableSwitching();
    res := Cstdlib.calloc(1, dataSize);
    Scheduler.EnableSwitching();
    IF res = NIL THEN RETURN NIL END;
    InitArray (res, LOOPHOLE(def, RT0.ArrayTypeDefn), s);

    IF countsOn THEN BumpSize (def.typecode, dataSize) END;
    RETURN res;
  END GetUntracedOpenArray;

PROCEDURE <A NAME="InitArray"><procedure>InitArray</procedure></A> (res: ADDRESS; def: RT0.ArrayTypeDefn; READONLY s: Shape) =
  BEGIN
    WITH data_start = res + def.common.dataSize DO
      LOOPHOLE(res, UNTRACED REF ADDRESS)^ := data_start;
    END;
    FOR i := 0 TO NUMBER(s) - 1 DO
      LOOPHOLE(res + ADRSIZE(ADDRESS) + i * ADRSIZE(INTEGER),
               UNTRACED REF INTEGER)^ := s[i];
    END;
    IF def.common.initProc # NIL THEN def.common.initProc(res) END;
  END InitArray;

PROCEDURE <A NAME="ArraySize"><procedure>ArraySize</procedure></A> (def: RT0.ArrayTypeDefn;  READONLY s: Shape): CARDINAL =
  VAR n_elts := 1;  c: CARDINAL;
  BEGIN
    FOR i := 0 TO NUMBER(s) - 1 DO
      c := s[i];  (* force a range check *)
      n_elts := c * n_elts;
    END;
    RETURN RTMisc.Upper(def.common.dataSize + def.elementSize * n_elts,
                        BYTESIZE(Header));
  END ArraySize;

PROCEDURE <A NAME="AllocTraced"><procedure>AllocTraced</procedure></A> (dataSize, dataAlignment: CARDINAL;
                       VAR thread: ThreadState): ADDRESS =
  VAR
    res       := thread.pool.next + ADRSIZE(Header);
    cur_align := Word.And(LOOPHOLE(res, INTEGER), MaxAlignMask);
    alignment := align[cur_align, dataAlignment];
    nextPtr   := res + (alignment + dataSize);
  BEGIN
    IF nextPtr &gt; thread.pool.limit THEN
      (* not enough space left in the pool, take the long route *)
      res := NIL;  nextPtr := NIL;  (* in case of GC... *)
      TRY
        &lt;*ASSERT thread.inCritical &gt; 0*&gt;
        DEC(thread.inCritical);
        RTOS.LockHeap();
        (* make sure the collector gets a chance to keep up with NEW... *)
        CollectEnough();
        RETURN LongAlloc (dataSize, dataAlignment, thread.pool);
      FINALLY
        RTOS.UnlockHeap();
        INC(thread.inCritical);
      END;
    END;

    (* Align the referent *)
    IF alignment # 0 THEN
      InsertFiller(thread.pool.next, alignment);
      thread.pool.next := thread.pool.next + alignment;
      res := thread.pool.next + ADRSIZE(Header);
    END;

    thread.pool.next := nextPtr;
    RETURN res;
  END AllocTraced;
</PRE>---------------------------------------------------------- RTAllocCnts ---

<P><PRE>PROCEDURE <A NAME="BumpCnt"><procedure>BumpCnt</procedure></A> (tc: RT0.Typecode) =
  BEGIN
    TRY
      RTOS.LockHeap();
      IF (tc &gt;= n_types) THEN ExpandCnts (tc); END;
      WITH z = n_objects[tc] DO z := Word.Plus (z, 1) END;
    FINALLY
      RTOS.UnlockHeap();
    END;
  END BumpCnt;

PROCEDURE <A NAME="BumpSize"><procedure>BumpSize</procedure></A> (tc: RT0.Typecode;  size: INTEGER) =
  BEGIN
    TRY
      RTOS.LockHeap();
      IF (tc &gt;= n_types) THEN ExpandCnts (tc); END;
      WITH z = n_objects[tc] DO z := Word.Plus (z, 1)    END;
      WITH z = n_bytes[tc]   DO z := Word.Plus (z, size) END;
    FINALLY
      RTOS.UnlockHeap();
    END;
  END BumpSize;

PROCEDURE <A NAME="ExpandCnts"><procedure>ExpandCnts</procedure></A> (tc: RT0.Typecode) =
  VAR
    goal      := MAX (tc, RTType.MaxTypecode ());
    new_cnt   : INTEGER := 512;
    new_mem   : INTEGER;
    new_cnts  : ADDRESS;
    new_sizes : ADDRESS;
    old_cnts  := n_objects;
    old_sizes := n_bytes;
  BEGIN
    IF (n_types &gt; 0) THEN new_cnt := n_types; END;
    WHILE (new_cnt &lt;= goal) DO INC (new_cnt, new_cnt); END;

    new_mem   := new_cnt * BYTESIZE (INTEGER);
    new_cnts  := Malloc (new_mem);
    new_sizes := Malloc (new_mem);

    IF (old_cnts # NIL) THEN
      RTMisc.Copy (old_cnts,  new_cnts,  n_types * BYTESIZE (INTEGER));
      RTMisc.Copy (old_sizes, new_sizes, n_types * BYTESIZE (INTEGER));
    END;

    n_objects := new_cnts;
    n_bytes   := new_sizes;
    n_types   := new_cnt;
    (* &quot;n_types&quot; is assigned last in case anyone is reading the arrays
       while we're updating them... *)

    IF (old_cnts # NIL) THEN
      Cstdlib.free (old_cnts);
      Cstdlib.free (old_sizes);
    END;
  END ExpandCnts;

PROCEDURE <A NAME="Malloc"><procedure>Malloc</procedure></A> (size: INTEGER): ADDRESS =
  VAR res: ADDRESS;
  BEGIN
    Scheduler.DisableSwitching();
    res := Cstdlib.calloc(1, size);
    Scheduler.EnableSwitching();
    IF res = NIL THEN RTE.Raise(RTE.T.OutOfMemory) END;
    RETURN res;
  END Malloc;

BEGIN
END RTAllocator.
</PRE>
</inModule>
<HR>
<A NAME="x1">interface RTThread is in:
</A><UL>
<LI><A HREF="../POSIX/RTThread.i3.html#0TOP0">m3core/src/runtime/POSIX/RTThread.i3</A>
<LI><A HREF="RTThread.i3.html#0TOP0">m3core/src/runtime/common/RTThread.i3</A>
</UL>
<P>
<PRE>























</PRE>
</BODY>
</HTML>
