<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: m3core/src/runtime/common/RTProcess.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>m3core/src/runtime/common/RTProcess.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright (C) 1990, Digital Equipment Corporation           
 All rights reserved.                                        
 See the file COPYRIGHT for a full description.              
 Last modified on Wed Dec 21 13:47:07 PST 1994 by kalsow     
      modified on Sat Jun 27 22:22:30 PDT 1992 by muller     

<P><PRE>UNSAFE MODULE <module><implements><A HREF="RTProcess.i3.html">RTProcess</A></implements></module>;

IMPORT <A HREF="RTHeapRep.i3.html">RTHeapRep</A>, <A HREF="RTException.i3.html">RTException</A>, <A HREF="RTIO.i3.html">RTIO</A>, <A HREF="RTOS.i3.html">RTOS</A>;
</PRE>-------------------------------- program startup/shutdown -----------------

<P><PRE>TYPE
  Exitor = UNTRACED BRANDED &quot;RTProcess.Exitor&quot; REF RECORD
             proc: PROCEDURE ();
             next: Exitor;
           END;
</PRE> Exitors are untraced because we don't want to depend on the
   collector during a crash.  Since they're never disposed, we
   have a small memory leak, probably about (3 * 16 bytes)/process. 

<P><PRE>VAR
  exitors: Exitor := NIL;

PROCEDURE <A NAME="RegisterExitor"><procedure>RegisterExitor</procedure></A> (p: PROCEDURE ()) =
  BEGIN
    exitors := NEW (Exitor, proc := p, next := exitors);
  END RegisterExitor;

PROCEDURE <A NAME="InvokeExitors"><procedure>InvokeExitors</procedure></A> () =
  VAR tmp: Exitor;
  BEGIN
    (* run the registered &quot;exit&quot; routines *)
    WHILE exitors # NIL DO
      (* to ensure progress, remove an element from the list before
         invoking it *)
      tmp := exitors;
      exitors := exitors.next;
      tmp.proc ();
    END;
  END InvokeExitors;

PROCEDURE <A NAME="Exit"><procedure>Exit</procedure></A> (n: INTEGER) =
  BEGIN
    InvokeExitors ();
    RTOS.Exit (n);
  END Exit;

PROCEDURE <A NAME="Crash"><procedure>Crash</procedure></A> (msg: TEXT) =
  BEGIN
    IF (msg # NIL) THEN
      RTIO.PutText (&quot;\n*** &quot;);
      RTIO.PutText (msg);
      RTIO.PutText (&quot;\n&quot;);
    END;
    RTException.DumpStack ();
    RTIO.Flush ();

    (* run the registered &quot;exit&quot; routines *)
    InvokeExitors ();

    (* crash *)
    EVAL RTHeapRep.Crash ();
    RTOS.Crash ();
  END Crash;
</PRE>------------------------------------------------------ Ctl-C interrupts ---

<P><PRE>VAR
  cur_handler: InterruptHandler := NIL;
  cur_enable : InterruptHandler := NIL;
  cur_disable: InterruptHandler := NIL;
  enabled    : BOOLEAN := FALSE;

PROCEDURE <A NAME="OnInterrupt"><procedure>OnInterrupt</procedure></A> (p: InterruptHandler): InterruptHandler =
  (* This procedure should be atomic... but I doubt anyone cares. *)
  VAR old := cur_handler;
  BEGIN
    IF enabled = (p = NIL) THEN
      IF enabled
        THEN IF (cur_disable # NIL) THEN cur_disable (); END;
        ELSE IF (cur_enable  # NIL) THEN cur_enable ();  END;
      END;
      enabled := NOT enabled;
    END;

    cur_handler := p;
    RETURN old;
  END OnInterrupt;

PROCEDURE <A NAME="RegisterInterruptSetup"><procedure>RegisterInterruptSetup</procedure></A> (enable, disable: PROCEDURE ()) =
  BEGIN
    cur_enable := enable;
    cur_disable := disable;
  END RegisterInterruptSetup;

BEGIN
END RTProcess.
</PRE>
</inModule>
<PRE>























</PRE>
</BODY>
</HTML>
