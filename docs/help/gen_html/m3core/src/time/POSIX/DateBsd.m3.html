<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: m3core/src/time/POSIX/DateBsd.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>m3core/src/time/POSIX/DateBsd.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright (C) 1994, Digital Equipment Corporation        
 All rights reserved.                                     
 See the file COPYRIGHT for a full description.           
                                                          
 Last modified on Thu May 15 13:54:02 PDT 1997 by heydon  
      modified on Fri Oct 28 14:12:53 PDT 1994 by kalsow  
      modified on Thu Jan 28 10:45:24 PST 1993 by mjordan 
      modified on Fri Dec  4 17:35:53 PST 1992 by mcjones 

<P><PRE>UNSAFE MODULE <module>DateBsd</module> EXPORTS <A HREF="../Common/Date.i3.html"><implements>Date</A></implements>;

IMPORT <A HREF="../Common/Time.i3.html">Time</A>, <A HREF="../../C/Common/M3toC.i3.html">M3toC</A>, <A HREF="#x1">Utime</A>, <A HREF="TimePosix.i3.html">TimePosix</A>;

VAR mu := NEW(MUTEX);
</PRE><BLOCKQUOTE><EM> Protect the static storage used by <CODE>Utime.localtime</CODE> and <CODE>Utime.gmtime</CODE>
   (see the localtime(3) manpage). </EM></BLOCKQUOTE><PRE>

REVEAL <A NAME="TimeZone">TimeZone</A> = BRANDED &quot;Date.TimeZone&quot; REF INTEGER;

PROCEDURE <A NAME="FromTime"><procedure>FromTime</procedure></A>(t: Time.T; z: TimeZone := NIL): T =
  VAR
    date : T;
    tv   : Utime.struct_timeval;
    tm   : Utime.struct_tm_star;
  BEGIN
    tv := TimePosix.ToUtime(t);
    LOCK mu DO
      IF (z = NIL) OR (z^ = Local^) THEN
        tm := Utime.localtime(ADR(tv.tv_sec));
      ELSIF z^ = UTC^ THEN
        tm := Utime.gmtime(ADR(tv.tv_sec));
      ELSE
        (* unknown timezone *)
        &lt;* ASSERT FALSE *&gt;
      END;
      date.year    := tm.tm_year + 1900;
      date.month   := VAL(tm.tm_mon, Month);
      date.day     := tm.tm_mday;
      date.hour    := tm.tm_hour;
      date.minute  := tm.tm_min;
      date.second  := tm.tm_sec;
      date.weekDay := VAL(tm.tm_wday, WeekDay);
      (* The &quot;tm.tm_gmtoff&quot; field is seconds *east* of GMT, whereas
         the &quot;date.offset&quot; field is seconds *west* of GMT, so a
         negation is necessary. *)
      date.offset  := - (tm.tm_gmtoff);
      date.zone    := M3toC.CopyStoT (tm.tm_zone);
    END;
    RETURN date;
  END FromTime;

PROCEDURE <A NAME="ToTime"><procedure>ToTime</procedure></A>(READONLY d: T): Time.T RAISES {Error} =
</PRE><BLOCKQUOTE><EM> This function uses mktime(3) to convert a <CODE>Utime.struct_tm</CODE> into a
   <CODE>Utime.time_t</CODE>, which is then converted via <CODE>FLOAT</CODE> to a <CODE>Time.T</CODE>.
   The mktime(3) function ignores the <CODE>tm_gmtoff</CODE> field of its argument,
   so we account for the timezone information <CODE>d.offset</CODE> by determining
   the offset of any local time and adjusting the result by the difference
   between <CODE>d.offset</CODE> and the local offset. </EM></BLOCKQUOTE><PRE>
  CONST
    SecsPerHour = 60 * 60;
  VAR
    tm: Utime.struct_tm;
    time, now: Utime.time_t;
    local_now: Utime.struct_tm_star;
    t: Time.T;
  BEGIN
    (* prepare call to mktime(3) *)
    tm.tm_sec    := d.second;
    tm.tm_min    := d.minute;
    tm.tm_hour   := d.hour;
    tm.tm_mday   := d.day;
    tm.tm_mon    := ORD(d.month);
    tm.tm_year   := d.year - 1900;
    (* tm.tm_wday ignored *)
    tm.tm_isdst  := 0; (* tell mktime that DST is not in effect *)
    (* tm_zone, tm_gmtoff ignored *)
    time := Utime.mktime(ADR(tm));
    IF time = -1 THEN RAISE Error END;

    (* adjust result to reflect &quot;d.offset&quot; *)
    EVAL Utime.time(ADR(now));
    local_now := Utime.localtime(ADR(now));
    IF local_now.tm_isdst &gt; 0 THEN
      (* decrement the local time zone by one hour if DST is in effect *)
      DEC(local_now.tm_gmtoff, 1 * SecsPerHour)
    END;
    (* As above, we must negate &quot;d.offset&quot; to account for the
       opposite sense of that field compared to Unix. *)
    DEC(time, (-d.offset) - local_now.tm_gmtoff);

    (* convert to a &quot;Time.T&quot; *)
    t := FLOAT(time, LONGREAL);
    RETURN t;
  END ToTime;

BEGIN
  Local := NEW(TimeZone);  Local^ := 0;
  UTC   := NEW(TimeZone);  UTC^   := 1;
END DateBsd.
</PRE>
</inModule>
<HR>
<A NAME="x1">interface Utime is in:
</A><UL>
<LI><A HREF="../../unix/Common/Utime.i3.html#0TOP0">m3core/src/unix/Common/Utime.i3</A>
<LI><A HREF="../../unix/linux-common/Utime.i3.html#0TOP0">m3core/src/unix/linux-common/Utime.i3</A>
</UL>
<P>
<PRE>























</PRE>
</BODY>
</HTML>
