<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: m3core/src/text/TextSub.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>m3core/src/text/TextSub.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright 1996-2000, Critical Mass, Inc.  All rights reserved. 
 See file COPYRIGHT-CMASS for details. 

<P><PRE>UNSAFE MODULE <module>TextSub</module> EXPORTS <A HREF="Text.i3.html"><implements>Text</A></implements>, <A HREF="TextSub.i3.html"><implements>TextSub</A></implements>;

IMPORT <A HREF="TextCat.i3.html">TextCat</A>, <A HREF="TextClass.i3.html">TextClass</A>, <A HREF="Text8.i3.html">Text8</A>, <A HREF="Text16.i3.html">Text16</A>, <A HREF="TextLiteral.i3.html">TextLiteral</A>;

REVEAL
  <A NAME="TT">TT</A> = Public BRANDED &quot;TextSub.T&quot; OBJECT OVERRIDES
    get_info       := MyGetInfo;
    get_char       := MyGetChar;
    get_wide_char  := MyGetWideChar;
    get_chars      := MyGetChars;
    get_wide_chars := MyGetWideChars;
  END;

PROCEDURE <A NAME="New"><procedure>New</procedure></A> (t: TEXT; start, length: CARDINAL): TEXT =
  BEGIN
    RETURN Sub (t, start, length);
  END New;
</PRE> Text.Sub 
<PRE>PROCEDURE <A NAME="Sub"><procedure>Sub</procedure></A> (t: TEXT; start, length: CARDINAL): TEXT =
  VAR info: TextClass.Info;  new_len: INTEGER;  root: TEXT;
  BEGIN
    t.get_info (info);
    new_len := MIN (info.length - start, length);
    IF (new_len &lt;= 0)           THEN RETURN &quot;&quot;; END;
    IF (new_len = info.length)  THEN RETURN t;  END;
    IF (new_len = 1) THEN RETURN FromWideChar (t.get_wide_char (start)); END;

    (* Descend as far as possible through subtexts and concatenations. *)
    root := t;
    LOOP
      TYPECASE t OF
      | TT(tt) =&gt;  t := tt.base;  INC (start, tt.start);
      | TextCat.T(tc) =&gt;
          IF start + new_len &lt;= tc.a_len THEN
            t := tc.a;
          ELSIF start &gt;= tc.a_len THEN
            t := tc.b;
            DEC (start, tc.a_len);
          ELSE
            EXIT;
          END;
      ELSE
        EXIT;
      END;
    END;
    IF t # root THEN
      t.get_info (info);
      IF start = 0 AND new_len = info.length THEN
        RETURN t;
      END;
    END;

    IF  (info.length &gt;= 256)          (* It's big *)
    AND (new_len * 4 &lt;= info.length)  (* It's shrinking substantially *)
    AND (new_len &lt;= 16384) THEN       (* It's not huge *)
      VAR tc := TYPECODE (t); BEGIN
        (* don't bother flattening literals, they're not in the heap anyway! *)
        IF (tc # TYPECODE (TextLiteral.T)) THEN
          IF info.wide THEN
            VAR r := Text16.Create (new_len); BEGIN
              t.get_wide_chars (SUBARRAY (r.contents^, 0, new_len),  start);
              RETURN r;
            END;
          ELSE
            VAR r := Text8.Create (new_len); BEGIN
              t.get_chars (SUBARRAY (r.contents^, 0, new_len),  start);
              RETURN r;
            END;
          END;
        END;
      END;
    END;

    RETURN NEW (TT, base := t, start := start, len := new_len);
  END Sub;

PROCEDURE <A NAME="MyGetInfo"><procedure>MyGetInfo</procedure></A> (t: TT;  VAR info: TextClass.Info) =
  BEGIN
    t.base.get_info (info);
    info.length := t.len;
    IF (info.start # NIL) THEN
      IF info.wide
        THEN INC (info.start, t.start * ADRSIZE (WIDECHAR));
        ELSE INC (info.start, t.start * ADRSIZE (CHAR));
      END;
    END;
  END MyGetInfo;

PROCEDURE <A NAME="MyGetChar"><procedure>MyGetChar</procedure></A> (t: TT;  i: CARDINAL): CHAR =
  BEGIN
    RETURN t.base.get_char (i + t.start);
  END MyGetChar;

PROCEDURE <A NAME="MyGetWideChar"><procedure>MyGetWideChar</procedure></A> (t: TT;  i: CARDINAL): WIDECHAR =
  BEGIN
    RETURN t.base.get_wide_char (i + t.start);
  END MyGetWideChar;

PROCEDURE <A NAME="MyGetChars"><procedure>MyGetChars</procedure></A> (t: TT;  VAR a: ARRAY OF CHAR;  start: CARDINAL) =
  BEGIN
    t.base.get_chars (a, start + t.start);
  END MyGetChars;

PROCEDURE <A NAME="MyGetWideChars"><procedure>MyGetWideChars</procedure></A> (t: TT;  VAR a: ARRAY OF WIDECHAR;  start: CARDINAL) =
  BEGIN
    t.base.get_wide_chars (a, start + t.start);
  END MyGetWideChars;

BEGIN
END TextSub.
</PRE>
</inModule>
<PRE>























</PRE>
</BODY>
</HTML>
