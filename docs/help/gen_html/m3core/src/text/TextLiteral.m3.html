<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: m3core/src/text/TextLiteral.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>m3core/src/text/TextLiteral.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright 1996-2000, Critical Mass, Inc.  All rights reserved. 
 See file COPYRIGHT-CMASS for details. 

<P><PRE>UNSAFE MODULE <module>TextLiteral</module> EXPORTS <A HREF="TextLiteral.i3.html"><implements>TextLiteral</A></implements>, <A HREF="../runtime/common/RTHooks.i3.html"><implements>RTHooks</A></implements>;

IMPORT <A HREF="TextClass.i3.html">TextClass</A>, <A HREF="../runtime/common/RTMisc.i3.html">RTMisc</A>, <A HREF="../word/Word.i3.html">Word</A>;

TYPE
  CPtr  = UNTRACED REF CHAR;
  WCPtr = UNTRACED REF WIDECHAR;

PROCEDURE <A NAME="TextLitInfo"><procedure>TextLitInfo</procedure></A> (t: T;  VAR info: TextClass.Info) =
  BEGIN
    info.start  := ADR (t.buf[0]);
    info.length := ABS (t.cnt);
    info.wide   := (t.cnt &lt; 0);
  END TextLitInfo;

PROCEDURE <A NAME="TextLitGetChar"><procedure>TextLitGetChar</procedure></A> (t: T;  i: CARDINAL): CHAR =
  BEGIN
    IF (t.cnt &gt;= 0) THEN
      IF i &gt;= t.cnt THEN (* force a subscript fault *) i := MaxBytes;  END;
      RETURN VAL (t.buf[i], CHAR);
    ELSE
      IF i &gt;= -t.cnt THEN (* force a subscript fault *) i := MaxBytes;  END;
      VAR p := LOOPHOLE (ADR (t.buf[i+i]), WCPtr); BEGIN
        RETURN VAL (Word.And (ORD (p^), 16_ff), CHAR);
      END;
    END;
  END TextLitGetChar;

PROCEDURE <A NAME="TextLitGetWideChar"><procedure>TextLitGetWideChar</procedure></A> (t: T;  i: CARDINAL): WIDECHAR =
  BEGIN
    IF (t.cnt &gt;= 0) THEN
      IF i &gt;= t.cnt THEN (* force a subscript fault *) i := MaxBytes;  END;
      RETURN VAL (t.buf[i], WIDECHAR);
    ELSE
      IF i &gt;= -t.cnt THEN (* force a subscript fault *) i := MaxBytes;  END;
      VAR p := LOOPHOLE (ADR (t.buf[i+i]), UNTRACED REF WIDECHAR); BEGIN
        RETURN p^;
      END;
    END;
  END TextLitGetWideChar;

PROCEDURE <A NAME="TextLitGetChars"><procedure>TextLitGetChars</procedure></A> (t: T;  VAR a: ARRAY OF CHAR;  start: CARDINAL) =
  VAR n: INTEGER;
  BEGIN
    IF (t.cnt &gt;= 0) THEN
      n := MIN (NUMBER (a), t.cnt - start);
      IF (n &gt; 0) THEN
        RTMisc.Copy (ADR (t.buf[start]), ADR (a[0]), n * BYTESIZE (CHAR));
      END;
    ELSE
      n := MIN (NUMBER (a), (-t.cnt) - start);
      IF (n &gt; 0) THEN
        VAR
          tp := LOOPHOLE (ADR (t.buf[start+start]), WCPtr);
          ap := LOOPHOLE (ADR (a[0]), CPtr);
        BEGIN
          WHILE (n &gt; 0) DO
            ap^ := VAL (Word.And (ORD (tp^), 16_ff), CHAR);
            INC (tp, ADRSIZE (tp^));  INC (ap, ADRSIZE (ap^));  DEC (n);
          END;
        END;
      END;
    END;
  END TextLitGetChars;

PROCEDURE <A NAME="TextLitGetWideChars"><procedure>TextLitGetWideChars</procedure></A> (t: T;  VAR a: ARRAY OF WIDECHAR;  start: CARDINAL) =
  VAR n: INTEGER;
  BEGIN
    IF (t.cnt &gt;= 0) THEN
      n := MIN (NUMBER (a), t.cnt - start);
      IF (n &gt; 0) THEN
        VAR
          tp := LOOPHOLE (ADR (t.buf[start]), CPtr);
          ap := LOOPHOLE (ADR (a[0]), WCPtr);
        BEGIN
          WHILE (n &gt; 0) DO
            ap^ := VAL (ORD (tp^), WIDECHAR);
            INC (tp, ADRSIZE (tp^));  INC (ap, ADRSIZE (ap^));  DEC (n);
          END;
        END;
      END;
    ELSE
      n := MIN (NUMBER (a), (-t.cnt) - start);
      IF (n &gt; 0) THEN
        RTMisc.Copy (ADR (t.buf[start+start]), ADR (a[0]), n * BYTESIZE (WIDECHAR));
      END;
    END;
  END TextLitGetWideChars;

BEGIN
END TextLiteral.
</PRE>
</inModule>
<PRE>























</PRE>
</BODY>
</HTML>
