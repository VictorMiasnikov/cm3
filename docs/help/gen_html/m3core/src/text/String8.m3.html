<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: m3core/src/text/String8.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>m3core/src/text/String8.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright 1996-2000, Critical Mass, Inc.  All rights reserved. 
 See file COPYRIGHT-CMASS for details. 

<P><PRE>UNSAFE MODULE <module><implements><A HREF="String8.i3.html">String8</A></implements></module>;

IMPORT <A HREF="../C/Common/Cstring.i3.html">Cstring</A>, <A HREF="../word/Word.i3.html">Word</A>;

TYPE Ptr = UNTRACED REF CHAR;

PROCEDURE <A NAME="Equal"><procedure>Equal</procedure></A> (a, b: ADDRESS;  len: CARDINAL): BOOLEAN =
  BEGIN
    IF (len = 0) THEN RETURN TRUE; END;
    IF (a = NIL) OR (b = NIL) THEN RETURN FALSE; END;
    RETURN Cstring.memcmp (a, b, len) = 0;
  END Equal;

PROCEDURE <A NAME="Compare"><procedure>Compare</procedure></A> (a: ADDRESS;  len_a: CARDINAL;
                   b: ADDRESS;  len_b: CARDINAL): [-1..1] =
  VAR i: INTEGER;
  BEGIN
    i := Cstring.memcmp (a, b, MIN (len_a, len_b));
    IF    (i &lt; 0)         THEN  RETURN -1;
    ELSIF (i &gt; 0)         THEN  RETURN +1;
    ELSIF (len_a &lt; len_b) THEN  RETURN -1;
    ELSIF (len_a &gt; len_b) THEN  RETURN +1;
    ELSE                        RETURN 0;
    END;
  END Compare;

PROCEDURE <A NAME="Hash"><procedure>Hash</procedure></A> (a: ADDRESS;  len: CARDINAL;  initial: INTEGER): INTEGER =
  VAR p := LOOPHOLE (a, Ptr);  result := initial;
  BEGIN
    IF (p = NIL) THEN RETURN result; END;
    WHILE (len &gt; 0) DO
      result := Word.Xor (Word.LeftRotate (result, 13), ORD (p^));
      INC (p, ADRSIZE (p^));  DEC (len);
    END;
    RETURN result;
  END Hash;

PROCEDURE <A NAME="FindChar"><procedure>FindChar</procedure></A> (a: ADDRESS;  len: CARDINAL;  c: CHAR): INTEGER =
  VAR p := LOOPHOLE (a, Ptr);  cnt := len;
  BEGIN
    IF (p = NIL) THEN RETURN -1; END;
    WHILE (cnt &gt; 0) DO
      IF (p^ = c) THEN RETURN len - cnt; END;
      INC (p, ADRSIZE (p^));  DEC (cnt);
    END;
    RETURN -1;
  END FindChar;

PROCEDURE <A NAME="FindCharR"><procedure>FindCharR</procedure></A> (a: ADDRESS;  len: CARDINAL;  c: CHAR): INTEGER =
  VAR p := LOOPHOLE (a, Ptr);
  BEGIN
    IF (p = NIL) THEN RETURN -1; END;
    INC (p, len * ADRSIZE (p^));
    WHILE (len &gt; 0) DO
      DEC (p, ADRSIZE (p^));  DEC (len);
      IF (p^ = c) THEN RETURN len; END;
    END;
    RETURN -1;
  END FindCharR;

PROCEDURE <A NAME="ArrayStart"><procedure>ArrayStart</procedure></A> (READONLY a: ARRAY OF CHAR): ADDRESS =
  BEGIN
    IF NUMBER (a) &lt; 1 THEN RETURN NIL; END;
    RETURN ADR (a[0]);
  END ArrayStart;

BEGIN
END String8.
</PRE>
</inModule>
<PRE>























</PRE>
</BODY>
</HTML>
