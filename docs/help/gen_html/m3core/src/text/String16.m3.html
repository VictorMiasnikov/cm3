<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: m3core/src/text/String16.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>m3core/src/text/String16.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright 1996-2000, Critical Mass, Inc.  All rights reserved. 
 See file COPYRIGHT-CMASS for details. 

<P><PRE>UNSAFE MODULE <module><implements><A HREF="String16.i3.html">String16</A></implements></module>;

IMPORT <A HREF="../word/Word.i3.html">Word</A>;

TYPE Ptr = UNTRACED REF WIDECHAR;

PROCEDURE <A NAME="Equal"><procedure>Equal</procedure></A> (a, b: ADDRESS;  len: CARDINAL): BOOLEAN =
</PRE><BLOCKQUOTE><EM> Return <CODE>TRUE</CODE> if the strings of <CODE>len</CODE> characters starting
   at <CODE>a</CODE> and <CODE>b</CODE> have the same (case-sensitive) contents. </EM></BLOCKQUOTE><PRE>
</PRE> PRE: a MOD ADRSIZE(WIDECHAR) = 0 AND b MOD ADRSIZE(WIDECHAR) = 0  
  <PRE>VAR pa := LOOPHOLE (a, Ptr);  pb := LOOPHOLE (b, Ptr);
  BEGIN
    IF (len = 0) THEN RETURN TRUE; END;
    IF (a = NIL) OR (b = NIL) THEN RETURN FALSE; END;
    WHILE (len &gt; 0) AND (pa^ = pb^) DO
      DEC (len);  INC (pa, ADRSIZE (pa^));  INC (pb, ADRSIZE (pb^));
    END;
    RETURN (len = 0);
  END Equal;

PROCEDURE <A NAME="Compare"><procedure>Compare</procedure></A> (a: ADDRESS;  len_a: CARDINAL;
                   b: ADDRESS;  len_b: CARDINAL): [-1..1] =
</PRE><BLOCKQUOTE><EM> Return <CODE>-1</CODE> if string <CODE>a</CODE> occurs before string <CODE>b</CODE>, <CODE>0</CODE> if the
   strings are equal, and <CODE>+1</CODE> if <CODE>a</CODE> occurs after <CODE>b</CODE> in
   lexicographic order. </EM></BLOCKQUOTE><PRE>
</PRE> PRE: a MOD ADRSIZE(WIDECHAR) = 0 AND b MOD ADRSIZE(WIDECHAR) = 0  
  <PRE>CONST Map = ARRAY BOOLEAN OF [-1..+1] { +1, -1 };
  VAR
    pa  := LOOPHOLE (a, Ptr);
    pb  := LOOPHOLE (b, Ptr);
    len := MIN (len_a, len_b);
  BEGIN
    WHILE (len &gt; 0) AND (pa^ = pb^) DO
      DEC (len);  INC (pa, ADRSIZE (pa^));  INC (pb, ADRSIZE (pb^));
    END;
    IF    (len # 0)       THEN RETURN Map [pa^ &lt; pb^];
    ELSIF (len_a # len_b) THEN RETURN Map [len_a &lt; len_b];
    ELSE                       RETURN 0;
    END;
  END Compare;

PROCEDURE <A NAME="Hash"><procedure>Hash</procedure></A> (a: ADDRESS;  len: CARDINAL;  initial: INTEGER): INTEGER =
</PRE><BLOCKQUOTE><EM> Return a hash function of the contents of string <CODE>a</CODE> starting
   with the value <CODE>initial</CODE>. </EM></BLOCKQUOTE><PRE>
</PRE> PRE: a MOD ADRSIZE(WIDECHAR) = 0  
  <PRE>VAR p := LOOPHOLE (a, Ptr);  result := initial;
  BEGIN
    IF (p = NIL) THEN RETURN result; END;
    WHILE (len &gt; 0) DO
      result := Word.Xor (Word.LeftRotate (result, 13), ORD (p^));
      INC (p, ADRSIZE (p^));  DEC (len);
    END;
    RETURN result;
  END Hash;

PROCEDURE <A NAME="FindChar"><procedure>FindChar</procedure></A> (a: ADDRESS;  len: CARDINAL;  c: WIDECHAR): INTEGER =
</PRE><BLOCKQUOTE><EM> If <CODE>c = a[i]</CODE> for some <CODE>i</CODE> in <CODE>[0~..~len-1]</CODE>, return the
   smallest such <CODE>i</CODE>; otherwise, return <CODE>-1</CODE>. </EM></BLOCKQUOTE><PRE>
</PRE> PRE: a MOD ADRSIZE(WIDECHAR) = 0  
  <PRE>VAR p := LOOPHOLE (a, Ptr);  cnt := len;
  BEGIN
    IF (p = NIL) THEN RETURN -1; END;
    WHILE (cnt &gt; 0) DO
      IF (p^ = c) THEN RETURN len - cnt; END;
      INC (p, ADRSIZE (p^));  DEC (cnt);
    END;
    RETURN -1;
  END FindChar;

PROCEDURE <A NAME="FindCharR"><procedure>FindCharR</procedure></A> (a: ADDRESS;  len: CARDINAL;  c: WIDECHAR): INTEGER =
</PRE><BLOCKQUOTE><EM> If <CODE>c = a[i]</CODE> for some <CODE>i</CODE> in <CODE>[0~..~len-1]</CODE>, return the
   largest such <CODE>i</CODE>; otherwise, return <CODE>-1</CODE>. </EM></BLOCKQUOTE><PRE>
</PRE> PRE: a MOD ADRSIZE(WIDECHAR) = 0  
  <PRE>VAR p := LOOPHOLE (a, Ptr);
  BEGIN
    IF (p = NIL) THEN RETURN -1; END;
    INC (p, len * ADRSIZE (p^));
    WHILE (len &gt; 0) DO
      DEC (p, ADRSIZE (p^));  DEC (len);
      IF (p^ = c) THEN RETURN len; END;
    END;
    RETURN -1;
  END FindCharR;

CONST WideCharBitsize = BITSIZE (WIDECHAR);
CONST WideCharsPerWord = BITSIZE (Word.T) DIV BITSIZE (WIDECHAR);
CONST CharMask = Word.Not (16_FF);
CONST CharMask1 = Word.Not (Word.Shift (16_FF, WideCharBitsize));
CONST CharMask2 = Word.Not (Word.Shift (16_FF, 2*WideCharBitsize));
CONST CharMask3 = Word.Not (Word.Shift (16_FF, 3*WideCharBitsize));
CONST CharsInWordMask = Word.And ( Word.And (CharMask, CharMask1)
                                 , Word.And (CharMask2, CharMask3)
                                 );

TYPE WordPtr = UNTRACED REF Word.T;

PROCEDURE <A NAME="HasWideChars"><procedure>HasWideChars</procedure></A> (a: ADDRESS; len: CARDINAL): BOOLEAN =
</PRE><BLOCKQUOTE><EM> Return ORD(a[i]) &gt; LAST (CHAR), for some <CODE>i</CODE> in <CODE>[0~..~len-1]</CODE>. </EM></BLOCKQUOTE><PRE>
</PRE> PRE: a MOD ADRSIZE(WIDECHAR) = 0 
 This algorithm assumes ADRSIZE(Word.T) MOD ADRSIZE(WIDECHAR) = 0.   
 This algorithm assumes FIRST(CHAR) = 0 AND LAST(CHAR) = 16_FF.   
  <PRE>VAR p := LOOPHOLE (a, Ptr);
  BEGIN
    IF (p = NIL) THEN RETURN FALSE; END;
    (* Do non-word-aligned WIDECHARs at the left. *)
    WHILE Word.Mod (LOOPHOLE(p, Word.T), ADRSIZE (Word.T)) # 0 AND len &gt; 0 DO
      IF Word.And (ORD (p^), CharMask) # 0 THEN RETURN TRUE; END;
      INC (p, ADRSIZE (p^));
      DEC (len);
    END;
    (* Do word-aligned WIDECHARs in the middle. *)
    WHILE len &gt;= WideCharsPerWord DO
      IF Word.And (ORD (LOOPHOLE (p, WordPtr)^), CharsInWordMask) # 0 THEN
        RETURN TRUE;
      END;
      INC (p, ADRSIZE (Word.T));
      DEC (len, WideCharsPerWord);
    END;
    (* Do remaining non-word-aligned WIDECHARs at the right. *)
    WHILE len &gt; 0 DO
      IF Word.And (ORD (p^), CharMask) # 0 THEN RETURN TRUE; END;
      INC (p, ADRSIZE (p^));
      DEC (len);
    END;
    RETURN FALSE;
  END HasWideChars;

PROCEDURE <A NAME="ArrayStart"><procedure>ArrayStart</procedure></A> (READONLY a: ARRAY OF WIDECHAR): ADDRESS =
</PRE><BLOCKQUOTE><EM> Returns the address of the first character of <CODE>a</CODE> if it is
   non-empty, otherwise returns <CODE>NIL</CODE>.  WARNING: the returned
   address is only valid as long as <CODE>a</CODE> does not move.  To
   prevent heap allocated arrays from moving, keep the returned
   address on the stack. </EM></BLOCKQUOTE><PRE>
  BEGIN
    IF NUMBER (a) &lt; 1 THEN RETURN NIL; END;
    RETURN ADR (a[0]);
  END ArrayStart;

BEGIN
END String16.
</PRE>
</inModule>
<PRE>























</PRE>
</BODY>
</HTML>
