<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: m3core/src/text/Text8.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>m3core/src/text/Text8.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright 1996-2000, Critical Mass, Inc.  All rights reserved. 
 See file COPYRIGHT-CMASS for details. 

<P><PRE>UNSAFE MODULE <module><implements><A HREF="Text8.i3.html">Text8</A></implements></module>;

IMPORT <A HREF="TextClass.i3.html">TextClass</A>, <A HREF="Text8Short.i3.html">Text8Short</A>;

REVEAL
  <A NAME="T">T</A> = Public BRANDED &quot;Text8.T&quot; OBJECT OVERRIDES
    get_info  := GetInfo;
    get_char  := GetChar;
    get_chars := GetChars;
  END;

PROCEDURE <A NAME="New"><procedure>New</procedure></A> (READONLY a: ARRAY OF CHAR): TEXT =
  VAR n := NUMBER (a);  t: T;
  BEGIN
    IF n &lt;= Text8Short.MaxLength THEN RETURN Text8Short.New (a); END;
    t := Create (n);
    IF (n &gt; 0) THEN SUBARRAY (t.contents^, 0, n) := a; END;
    RETURN t;
  END New;

PROCEDURE <A NAME="Create"><procedure>Create</procedure></A> (n: CARDINAL): T =
  VAR t := NEW (T);
  BEGIN
    t.contents := NEW (REF ARRAY OF CHAR, n + 1);
    t.contents[n] := '\000';
    RETURN t;
  END Create;

PROCEDURE <A NAME="GetInfo"><procedure>GetInfo</procedure></A> (t: T;  VAR info: TextClass.Info) =
  BEGIN
    info.start  := ADR (t.contents[0]);
    info.length := MAX (0, LAST (t.contents^));
    info.wide   := FALSE;
  END GetInfo;

PROCEDURE <A NAME="GetChar"><procedure>GetChar</procedure></A> (t: T;  i: CARDINAL): CHAR =
  BEGIN
    IF i = LAST (t.contents^) THEN (* force a subscript fault *) INC (i) END;
    RETURN t.contents[i];
  END GetChar;

PROCEDURE <A NAME="GetChars"><procedure>GetChars</procedure></A> (t: T;  VAR a: ARRAY OF CHAR;  start: CARDINAL) =
  VAR n := MIN (NUMBER (a), LAST (t.contents^) - start);
  BEGIN
    IF (n &gt; 0) THEN
      SUBARRAY (a, 0, n) := SUBARRAY (t.contents^, start, n);
    END;
  END GetChars;

BEGIN
END Text8.
</PRE>
</inModule>
<PRE>























</PRE>
</BODY>
</HTML>
