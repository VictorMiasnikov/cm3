<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: cmvbt/src/FrameVBT.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>cmvbt/src/FrameVBT.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright 1996-2000, Critical Mass, Inc. All Rights Reserved. 
 See file COPYRIGHT-CMASS for details. 

<P><PRE>MODULE <module><implements><A HREF="FrameVBT.i3.html">FrameVBT</A></implements></module>;

IMPORT <A HREF="../../libm3/src/geometry/Axis.i3.html">Axis</A>, <A HREF="../../ui/src/split/BdrVBTClass.i3.html">BdrVBTClass</A>, <A HREF="../../ui/src/split/BorderedVBT.i3.html">BorderedVBT</A>, <A HREF="../../ui/src/split/FilterClass.i3.html">FilterClass</A>, <A HREF="../../ui/src/vbt/Font.i3.html">Font</A>, <A HREF="../../ui/src/vbt/MouseSplit.i3.html">MouseSplit</A>;
IMPORT <A HREF="../../libm3/src/geometry/Point.i3.html">Point</A>, <A HREF="../../libm3/src/geometry/Rect.i3.html">Rect</A>, <A HREF="../../libm3/src/geometry/Region.i3.html">Region</A>, <A HREF="../../vbtkit/src/lego/Shadow.i3.html">Shadow</A>, <A HREF="../../vbtkit/src/lego/ShadowPaint.i3.html">ShadowPaint</A>, <A HREF="../../m3core/src/text/Text.i3.html">Text</A>, <A HREF="../../ui/src/vbt/VBT.i3.html">VBT</A>, <A HREF="../../ui/src/vbt/VBTClass.i3.html">VBTClass</A>;

CONST
  ChiselWidth = 2;  (* pixels *)

REVEAL
  <A NAME="T">T</A> = Public BRANDED OBJECT
        title: Text.T;
        font: Font.T;
        shadow: Shadow.T;
      OVERRIDES
        init := Init;
        rescreen := Rescreen;
        reshape := Reshape;
        repaintBorder := RepaintBorder;
      END;

PROCEDURE <A NAME="New"><procedure>New</procedure></A> (ch: VBT.T;  title: TEXT;  fnt: Font.T := Font.BuiltIn;
                shadow: Shadow.T := NIL): T =
  BEGIN
    RETURN NEW (T).init (ch, title, fnt, shadow);
  END New;

PROCEDURE <A NAME="Init"><procedure>Init</procedure></A> (v     : T;
                ch    : VBT.T;
                title : TEXT;
                fnt   : Font.T;
                shadow: Shadow.T := NIL): T =
  BEGIN
    IF (shadow = NIL) THEN shadow := Shadow.None; END;
    v.title  := title;
    v.font   := fnt;
    v.shadow := shadow;
    EVAL BorderedVBT.T.init (v, ch, BorderSize (v), shadow.bg);
    RETURN v
  END Init;

PROCEDURE <A NAME="RepaintBorder"><procedure>RepaintBorder</procedure></A> (v: T; READONLY clip: Rect.T) =
  CONST Z = MAX (1, ChiselWidth DIV 2);
  VAR
    dom     := VBT.Domain(v);
    dh      := v.bSize[Axis.T.Hor] DIV 2;
    dv      := v.bSize[Axis.T.Ver] DIV 2;
    bbox    := VBT.BoundingBox (v, v.title, v.font);
    pt      := Point.Add (Rect.NorthWest (dom),
                          Point.T {h := 3 * dh, v := dv + dv - bbox.south});
    bkgnd   := Rect.Change (Rect.Move (bbox, pt), -2, +2, 0, 0);
    chisout := Rect.Change (dom, dw := dh+Z, de := -dh-Z, ds := -dv-Z, dn := dv+Z);
    chisin  := Rect.Change (chisout, ChiselWidth, -ChiselWidth,
                                    +ChiselWidth, -ChiselWidth);
  BEGIN
    BorderedVBT.T.repaintBorder (v, clip);
    ShadowPaint.Border (v, Region.FromRect(clip), v.shadow,
                        Shadow.Style.Chiseled, chisin, chisout);
    VBT.PaintTint (v, Rect.Meet (clip, bkgnd), op := v.shadow.bg);
    VBT.PaintText (v, clip, pt, v.font, v.title, op := v.shadow.bgFg);
  END RepaintBorder;

PROCEDURE <A NAME="Rescreen"><procedure>Rescreen</procedure></A> (v: T; READONLY cd: VBT.RescreenRec) RAISES {} =
  BEGIN
    BorderedVBT.SetSize (v, BorderSize (v));
    BorderedVBT.T.rescreen (v, cd);
  END Rescreen;

PROCEDURE <A NAME="Reshape"><procedure>Reshape</procedure></A>(v: T;  READONLY cd: VBT.ReshapeRec) =
  (* LL = VBT.mu *)
  BEGIN
    BorderedVBT.SetSize (v, BorderSize (v));
    BorderedVBT.T.reshape (v, cd);
    RepaintBorder (v, Rect.Full);
  END Reshape;

PROCEDURE <A NAME="BorderSize"><procedure>BorderSize</procedure></A> (v: T): REAL =
  VAR
    txt_height := Rect.VerSize (VBT.BoundingBox(v, v.title, v.font));
    border := MAX (ChiselWidth + 2, txt_height + MAX (txt_height DIV 2, 2));
  BEGIN
    RETURN PixelsToMM (v, border);
  END BorderSize;

PROCEDURE <A NAME="PixelsToMM"><procedure>PixelsToMM</procedure></A> (v: T;  pixels: INTEGER): REAL =
  VAR
    pv := VBT.MMToPixels (v, 10.0, Axis.T.Ver);
    ph := VBT.MMToPixels (v, 10.0, Axis.T.Hor);
    xx := MIN (pv, ph);
  BEGIN
    IF xx = 0.0 THEN RETURN 10.0; END;
    RETURN FLOAT (pixels) * 10.0 / xx;
  END PixelsToMM;

BEGIN
END FrameVBT.
</PRE>
</inModule>
<PRE>























</PRE>
</BODY>
</HTML>
