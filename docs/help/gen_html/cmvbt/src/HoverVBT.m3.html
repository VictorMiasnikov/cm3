<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: cmvbt/src/HoverVBT.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>cmvbt/src/HoverVBT.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright 1996-2000, Critical Mass, Inc. All Rights Reserved. 
 See file COPYRIGHT-CMASS for details. 

<P><PRE>MODULE <module><implements><A HREF="HoverVBT.i3.html">HoverVBT</A></implements></module>;

IMPORT <A HREF="../../ui/src/split/Filter.i3.html">Filter</A>, <A HREF="../../libm3/src/geometry/Point.i3.html">Point</A>, <A HREF="../../libm3/src/geometry/Rect.i3.html">Rect</A>, <A HREF="../../m3core/src/thread/Common/Thread.i3.html">Thread</A>, <A HREF="../../m3core/src/time/Common/Time.i3.html">Time</A>, <A HREF="../../ui/src/vbt/Trestle.i3.html">Trestle</A>, <A HREF="../../ui/src/vbt/VBT.i3.html">VBT</A>, <A HREF="../../m3core/src/word/Word.i3.html">Word</A>;

REVEAL
  <A NAME="T">T</A> = Filter.T BRANDED OBJECT
        state     : State    := State.Looking;
        timer     : Timer    := NIL;
      OVERRIDES
        init     := Init;
        position := Position;
      END;

TYPE
  State = {
    Looking,  (* =&gt; haven't noticed mouse over a registered vbt *)
    Hovering, (* =&gt; we have noticed one, waiting for the timer to expire *)
    Delivered (* =&gt; made the callback, now waiting for the mouse to leave *)
  };

TYPE
  Child = REF RECORD
    v     : VBT.T;
    cb    : CallBack;
    ref   : REFANY;
    delay : Time.T;
    next  : Child;
  END;

VAR
  mu := NEW (MUTEX);
  registered : Child := NIL;

PROCEDURE <A NAME="New"><procedure>New</procedure></A> (ch: VBT.T): T =
  BEGIN
    RETURN NEW(T).init(ch);
  END New;

PROCEDURE <A NAME="Init"><procedure>Init</procedure></A> (v: T;  ch: VBT.T): Filter.T =
  BEGIN
    EVAL Filter.T.init (v, ch);
    VBT.SetCage (v, VBT.GoneCage);
    RETURN v;
  END Init;

PROCEDURE <A NAME="Register"><procedure>Register</procedure></A> (v: VBT.T;  delay: Time.T;  cb: CallBack;  ref: REFANY) =
  VAR x: Child;
  BEGIN
    LOCK mu DO
      x := registered;
      WHILE (x # NIL) DO
        IF (x.v = v) THEN
          x.delay := delay;
          x.cb := cb;
          x.ref := ref;
          RETURN;
        END;
        x := x.next;
      END;
      registered := NEW (Child, v := v, delay := delay, cb := cb,
                         ref := ref, next := registered);
    END;
  END Register;

PROCEDURE <A NAME="Position"><procedure>Position</procedure></A> (v: T;  READONLY cd: VBT.PositionRec) =
  VAR x: Child;  p, hDelta: Point.T;
  BEGIN
    CASE v.state OF
    | State.Looking =&gt;
        IF cd.cp.gone THEN
          VBT.SetCage (v, VBT.GoneCage);
          RETURN;
        END;

        p := Trestle.ScreenOf (v, cd.cp.pt).q;
        hDelta := Point.Sub (cd.cp.pt, p);

        LOCK mu DO
          x := registered;
          LOOP
            IF (x = NIL) THEN
              (* no matches, still looking *)
              VBT.SetCage (v, VBT.CageFromPosition (cd.cp));
              RETURN;
            END;
            IF HoverParent (x.v) = v THEN
              VAR
                chDom := VBT.Domain (x.v);
                nw    := Rect.NorthWest (chDom);
                qnw   := Trestle.ScreenOf (x.v, nw).q;
                delta := Point.Sub (qnw, nw);
                qDom  := Rect.Move (chDom, delta);
              BEGIN
                IF Rect.Member (p, qDom) THEN
                  v.state  := State.Hovering;
                  StartTimer (v, x);
                  VBT.SetCage (v, VBT.CageFromRect(Rect.Move(qDom, hDelta), cd.cp));
                  RETURN;
                END;
              END;
            END;
            x := x.next;
          END;
        END;

    | State.Hovering =&gt;
        (* oops, the mouse left the target before its timeout... *)
        AbortTimer (v);
        v.state  := State.Looking;
        VBT.SetCage (v, VBT.GoneCage);

    | State.Delivered =&gt;
        (* the mouse finally left the target vbt *)
        v.state  := State.Looking;
        VBT.SetCage (v, VBT.GoneCage);
    END;
  END Position;

PROCEDURE <A NAME="HoverParent"><procedure>HoverParent</procedure></A> (v: VBT.T): T =
  (* Find the nearest ancestor of &quot;v&quot; that is a hover vbt *)
  BEGIN
    LOOP
      TYPECASE v OF
      | T(t) =&gt; RETURN t;
      ELSE v := VBT.Parent (v);
      END;
    END;
  END HoverParent;
</PRE>------------------------------------------------------- timeout thread ---

<P><PRE>TYPE
  Timer = Thread.Closure OBJECT
    self      : Thread.T := NIL;
    hover     : T        := NIL;
    target    : Child    := NIL;    (* the current hover target *)
    start     : Time.T   := 0.0d0;  (* when we started the delay timer *)
    start_uid : INTEGER  := 0;      (* the &quot;target&quot;'s UID *)
    uid       : INTEGER  := 0;      (* UID for each timer activation *)
    mu        : MUTEX    := NIL;
    burp      : Thread.Condition := NIL;
  OVERRIDES
    apply := TimerRoot;
  END;

PROCEDURE <A NAME="StartTimer"><procedure>StartTimer</procedure></A> (v: T;  x: Child) =
  VAR t := v.timer;
  BEGIN
    IF (t = NIL) THEN
      t := NEW (Timer);  v.timer := t;
      t.hover := v;
      t.mu    := NEW (MUTEX);
      t.burp  := NEW (Thread.Condition);
      t.self  := Thread.Fork (t);
    END;

    LOCK t.mu DO
      t.uid       := Word.Plus (t.uid, 1);
      t.target    := x;
      t.start     := Time.Now ();
      t.start_uid := t.uid;
    END;
    Thread.Signal (t.burp);
  END StartTimer;

PROCEDURE <A NAME="AbortTimer"><procedure>AbortTimer</procedure></A> (v: T) =
  VAR t := v.timer;
  BEGIN
    LOCK t.mu DO
      t.uid    := Word.Plus (t.uid, 1);
      t.target := NIL;
    END;
    Thread.Alert (t.self);
  END AbortTimer;

PROCEDURE <A NAME="TimerRoot"><procedure>TimerRoot</procedure></A> (t: Timer): REFANY =
  VAR
    x     : Child;
    seq   : INTEGER;
    pause : Time.T;
    now   : Time.T;
  BEGIN
    LOOP
      (* wait for a timer to be scheduled *)
      LOCK t.mu DO
        WHILE (t.target = NIL) OR (t.start_uid # t.uid) DO
          Thread.Wait (t.mu, t.burp);
        END;
        x   := t.target;
        seq := t.start_uid;
        now := Time.Now ();
        pause := (t.start + x.delay) - now;
      END;

      IF (pause &gt; 0.0d0) THEN
        (* wait... *)
        TRY Thread.AlertPause (pause);
        EXCEPT Thread.Alerted =&gt; x := NIL; (* aborted *)
        END;
      END;

      IF (x # NIL) THEN
        LOCK t.mu DO
          IF (t.uid = seq)
            THEN t.target := NIL;    (* this timeout is still current. *)
            ELSE x := NIL;           (* darn, this timeout was canceled *)
          END;
        END;

        IF x # NIL THEN MakeCallback (t, x); END;
      END;

    END;
  END TimerRoot;

PROCEDURE <A NAME="MakeCallback"><procedure>MakeCallback</procedure></A> (t: Timer;  x: Child) =
  CONST Here = VBT.CursorPosition { Point.Origin, 0, FALSE, FALSE };
  VAR
    dom, cDom : Rect.T;
    nw, qnw   : Point.T;
    cnw, cqnw : Point.T;
    d0, d1    : Point.T;
  BEGIN
    (* let the hover vbt know that this event was delivered *)
    LOCK VBT.mu DO
      dom   := VBT.Domain (t.hover);
      nw    := Rect.NorthWest (dom);
      qnw   := Trestle.ScreenOf (t.hover, nw).q;
      d0    := Point.Sub (nw, qnw);

      cDom  := VBT.Domain (x.v);
      cnw   := Rect.NorthWest (cDom);
      cqnw  := Trestle.ScreenOf (x.v, cnw).q;
      d1    := Point.Sub (cqnw, cnw);

      (* map the child domain to screen coordinates *)
      cDom  := Rect.Move (cDom, d1);

      (* and then map it back to the hover vbt's coordinates *)
      cDom  := Rect.Move (cDom, d0);

      (* finally, reset the hover VBT's cage *)
      t.hover.state := State.Delivered;
      VBT.SetCage (t.hover, VBT.CageFromRect (cDom, Here));
    END;

    (* and deliver it... *)
    x.cb (x.v, x.ref);
  END MakeCallback;

BEGIN
END HoverVBT.
</PRE>
</inModule>
<PRE>























</PRE>
</BODY>
</HTML>
