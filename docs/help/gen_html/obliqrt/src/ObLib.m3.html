<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: obliqrt/src/ObLib.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>obliqrt/src/ObLib.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright 1991 Digital Equipment Corporation.               
 Distributed only by permission.                             

<P><PRE>UNSAFE MODULE <module><implements><A HREF="ObLib.i3.html">ObLib</A></implements></module>;
IMPORT <A HREF="../../synloc/src/SynLocation.i3.html">SynLocation</A>, <A HREF="ObCommand.i3.html">ObCommand</A>, <A HREF="../../m3core/src/text/Text.i3.html">Text</A>, <A HREF="../../libm3/src/pickle/ver2/Pickle2.i3.html">Pickle2</A> AS Pickle, <A HREF="../../libm3/src/rw/Wr.i3.html">Wr</A>, <A HREF="../../m3core/src/thread/Common/Thread.i3.html">Thread</A>,
       <A HREF="../../libm3/src/uid/Common/TimeStamp.i3.html">TimeStamp</A>, <A HREF="../../m3core/src/fingerprint/Fingerprint.i3.html">Fingerprint</A>, <A HREF="../derived/ObLibTbl.i3.html">ObLibTbl</A>, <A HREF="../../libm3/src/rw/Rd.i3.html">Rd</A>, <A HREF="../../sharedobj/src/WeakerRef.i3.html">WeakerRef</A>, <A HREF="../../m3core/src/weakref/WeakRef.i3.html">WeakRef</A>,
       <A HREF="../../libm3/src/pickle/ver2/PickleStubs.i3.html">PickleStubs</A>, <A HREF="../../m3core/src/runtime/common/RTAllocator.i3.html">RTAllocator</A>;

  PROCEDURE <A NAME="Setup"><procedure>Setup</procedure></A>()  =
  BEGIN
    libraries := NIL;
    helpCommandSet := ObCommand.NewSet();
  END Setup;

  PROCEDURE <A NAME="Extend"><procedure>Extend</procedure></A>(library: T; env: Env): Env =
    BEGIN
      IF Lookup(library.name, env)#NIL THEN
        RETURN NEW(Env, library:=library, rest:=env);
      ELSE
        RETURN NEW(Env, library:=library, rest:=env);
      END;
    END Extend;

  PROCEDURE <A NAME="Register"><procedure>Register</procedure></A>(library: T) =
    BEGIN
      libraries := Extend(library, libraries);
    END Register;

  PROCEDURE <A NAME="RegisterHelp"><procedure>RegisterHelp</procedure></A>(name: TEXT; helpProc: HelpProc) =
    BEGIN
      ObCommand.Register(helpCommandSet,
          NEW(ObCommand.T, name:=name,
              sortingName:=&quot;mod &quot; &amp; name,
              Exec:=helpProc));
    END RegisterHelp;

  PROCEDURE <A NAME="Lookup"><procedure>Lookup</procedure></A>(name: TEXT; env: Env): Env =
    BEGIN
      LOOP
        IF env=NIL THEN RETURN NIL END;
        IF Text.Equal(name, env.library.name) THEN RETURN env END;
        env := env.rest;
      END;
    END Lookup;

  PROCEDURE <A NAME="LookupFixity"><procedure>LookupFixity</procedure></A>(opName: TEXT; env: Env; VAR (*out*)libName: TEXT)
      : OpFixity =
    VAR opCodes: REF ARRAY OF OpCode;
    BEGIN
      LOOP
        IF env=NIL THEN RETURN OpFixity.Undefined END;
        opCodes := env.library.opCodes;
        FOR i:=0 TO NUMBER(opCodes^)-1 DO
          IF Text.Equal(opName, opCodes^[i].name) THEN
            libName := env.library.name;
            RETURN opCodes^[i].fixity;
          END;
        END;
        env := env.rest;
      END;
    END LookupFixity;

  PROCEDURE <A NAME="EncodeTermOp"><procedure>EncodeTermOp</procedure></A>(self: T; opName: TEXT;
      VAR(*out*)code: OpCode; &lt;*UNUSED*&gt;location: SynLocation.T): BOOLEAN =
    BEGIN
      FOR i:=FIRST(self.opCodes^) TO LAST(self.opCodes^) DO
        IF Text.Equal(opName, self.opCodes^[i].name) THEN
          code := self.opCodes^[i];
          RETURN TRUE;
        END;
      END;
      RETURN FALSE;
    END EncodeTermOp;

REVEAL
  <A NAME="ObLibSpecial">ObLibSpecial</A> = Pickle.Special BRANDED OBJECT
                       OVERRIDES
                         write := WriteLib;
                         read := ReadLib;
                       END;

PROCEDURE <A NAME="LookupFP"><procedure>LookupFP</procedure></A> (fp: Fingerprint.T; newLib: T): T =
  VAR lib: T := NIL;
      wref, wrefOld: WeakerRef.T;
  BEGIN
    LOCK mu DO
      IF libTbl.get(fp, wref) THEN
        lib := WeakRef.ToRef(wref.weakRef);
      END;
      IF lib = NIL AND newLib # NIL THEN
        wref := NEW(WeakerRef.T, weakRef:=WeakRef.FromRef(newLib, CleanupLib));
        IF NOT libTbl.put(fp, wref) THEN
          EVAL libTbl.delete(fp, wrefOld);
          EVAL libTbl.put(fp, wref);
        END;
        lib := newLib;
      END;
      RETURN lib;
    END;
  END LookupFP;

PROCEDURE <A NAME="CheckFP"><procedure>CheckFP</procedure></A> (lib: T) =
  VAR wref: WeakerRef.T;
  BEGIN
    LOCK mu DO
      IF Fingerprint.Equal(lib.ts, Fingerprint.Zero) THEN
        lib.ts := ComputeFP();
        wref := NEW(WeakerRef.T, weakRef:=WeakRef.FromRef(lib, CleanupLib));
        EVAL libTbl.put(lib.ts, wref);
      END;
    END;
  END CheckFP;

PROCEDURE <A NAME="ComputeFP"><procedure>ComputeFP</procedure></A>() : Fingerprint.T =
  VAR ts := TimeStamp.New();
  BEGIN
    RETURN Fingerprint.FromChars(
             LOOPHOLE(ts, ARRAY [0..15] OF CHAR), Fingerprint.OfEmpty);
  END ComputeFP;

PROCEDURE <A NAME="CleanupLib"><procedure>CleanupLib</procedure></A>(&lt;*UNUSED*&gt;READONLY self: WeakRef.T; ref: REFANY) =
  VAR lib: T := ref;
      val: WeakerRef.T;
  BEGIN
    LOCK mu DO
      EVAL libTbl.delete(lib.ts, val);
    END;
  END CleanupLib;

VAR
  libTbl: ObLibTbl.T := NEW(ObLibTbl.Default).init();

VAR
  mu := NEW(MUTEX);

PROCEDURE <A NAME="WriteLib"><procedure>WriteLib</procedure></A> (&lt;*UNUSED*&gt;ts: ObLibSpecial;
                    ref: REFANY; out: Pickle.Writer)
  RAISES {Pickle.Error, Wr.Failure, Thread.Alerted} =
  VAR o := NARROW(ref, T);
  BEGIN
    CheckFP(o);
    out.writeType(TYPECODE(ref));
    PickleStubs.OutBytes(out, o.ts.byte);
    PickleStubs.OutText(out, o.name);
    PickleStubs.OutRef(out, o.opCodes);
  END WriteLib;

PROCEDURE <A NAME="ReadLib"><procedure>ReadLib</procedure></A> (&lt;*UNUSED*&gt;ts: ObLibSpecial;
                   in: Pickle.Reader;
                   id: Pickle.RefID):REFANY
  RAISES {Pickle.Error, Rd.EndOfFile, Rd.Failure, Thread.Alerted} =
  &lt;*FATAL RTAllocator.OutOfMemory*&gt;
  VAR ac := in.readType();
      lib := NARROW(RTAllocator.NewTraced(ac),T);
  BEGIN
    PickleStubs.InBytes(in, lib.ts.byte);
    lib := LookupFP(lib.ts, lib);
    in.noteRef(lib, id);

    lib.name := PickleStubs.InText(in);
    lib.opCodes := PickleStubs.InRef(in);
    RETURN lib;
  END ReadLib;

BEGIN
  Pickle.RegisterSpecial(NEW(ObLibSpecial, sc := TYPECODE(T)));
END ObLib.
</PRE>
</inModule>
<PRE>























</PRE>
</BODY>
</HTML>
