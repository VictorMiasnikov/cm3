<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: obliqrt/src/ObValue.i3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>obliqrt/src/ObValue.i3</H2></A><HR>
<inInterface>
<PRE>
</PRE> Copyright 1991 Digital Equipment Corporation.               
 Distributed only by permission.                             
<PRE>&lt;*PRAGMA SHARED *&gt;

INTERFACE <interface><A HREF="#x1">ObValue</A></interface>;
IMPORT <A HREF="../../synloc/src/SynWr.i3.html">SynWr</A>, <A HREF="../../synloc/src/SynLocation.i3.html">SynLocation</A>, <A HREF="../../libm3/src/rw/Rd.i3.html">Rd</A>, <A HREF="../../libm3/src/rw/Wr.i3.html">Wr</A>, <A HREF="ObTree.i3.html">ObTree</A>, <A HREF="../../netobj/src/netobjrt/NetObj.i3.html">NetObj</A>, <A HREF="../../m3core/src/thread/Common/Thread.i3.html">Thread</A>, <A HREF="../../libm3/derived/AtomList.i3.html">AtomList</A>;
IMPORT <A HREF="../../sharedobj/src/SharedObj.i3.html">SharedObj</A>;

  EXCEPTION
    Exception(ExceptionPacket); (* trappable     *)
    Error(ErrorPacket);         (* only trappable by try-else *)
    ServerError(TEXT);  (* like Error, but has no location info;
                           immediately converted to Error
                           on the way back from a server *)
  TYPE
    ErrorPacket =
      BRANDED &quot;ErrorPacket&quot; OBJECT
        msg: TEXT;
        location: SynLocation.T;
      END;

    ExceptionPacket =
      BRANDED &quot;ExceptionPacket&quot; OBJECT
        msg: TEXT;
        location: SynLocation.T;
        exception: ValException;
        data: Val;
      END;

    <A HREF="ObValue.m3.html#Tbl">Tbl</A> &lt;: REFANY;

  TYPE
    Vals = ARRAY OF Val;

    Env = BRANDED OBJECT
	name: ObTree.IdeName;
        rest: Env;
      END;

    LocalEnv =
      Env BRANDED OBJECT
	val: Val;
      END;

    GlobalEnv =
      REF Vals;

    Val =
      BRANDED &quot;Val&quot; OBJECT
      END;

    ValOk =
      Val BRANDED &quot;ValOk&quot; OBJECT
      END;

    ValBool =
      Val BRANDED &quot;ValBool&quot; OBJECT
	bool: BOOLEAN;
      END;

    ValChar =
      Val BRANDED &quot;ValChar&quot; OBJECT
	char: CHAR;
      END;

    ValText =
      Val BRANDED &quot;ValText&quot; OBJECT
	text: TEXT; (* Non-NIL ! *)
      END;

    ValInt =
      Val BRANDED &quot;ValInt&quot; OBJECT
	int: INTEGER;
	temp: BOOLEAN:=FALSE;
      END;

    ValReal =
      Val BRANDED &quot;ValReal&quot; OBJECT
	real: LONGREAL;
	temp: BOOLEAN:=FALSE;
      END;

    ValException =
      Val BRANDED &quot;ValException&quot; OBJECT
        name: TEXT;
      END;

    ValOption =
      Val BRANDED &quot;ValOption&quot; OBJECT
	tag: TEXT;
        val: Val;
      END;

    ValVar =
      Val BRANDED &quot;ValVar&quot; OBJECT
        remote: RemVar;
      END;

    ValArray =
      Val BRANDED &quot;ValArray&quot; OBJECT
	remote: RemArray;
      END;

    ValFun =
      Val BRANDED &quot;ValFun&quot; OBJECT
        fun: ObTree.TermFun;
	global: GlobalEnv;
      END;

    ValMeth =
      Val BRANDED &quot;ValMeth&quot; OBJECT
        meth: ObTree.TermMeth;
	global: GlobalEnv;
      END;

    ValAlias =
      Val BRANDED &quot;ValAlias&quot; OBJECT
	label: TEXT;
	labelIndexHint: INTEGER;
        obj: ValObj;
      END;

    ValAnything = (* to be subtyped *)
      Val BRANDED &quot;ValAnything&quot; OBJECT
        what: TEXT; (* Only used for printing. *)
        tag: TEXT := NIL; (* Used to identify the type with reflection *)
        picklable: BOOLEAN := TRUE;
      METHODS
        Is(other: ValAnything): BOOLEAN := IsSelfOther;
          (* Override with what you want the &quot;is&quot; primitive to do
             to determine equality of two ValAnything values.
             The default method returns self=other. *)
        Print(): TEXT := PrintWhat;
          (* Override with a routine to print this value.
             The default method returns the &quot;what&quot;field. *)
        Copy(tbl: Tbl; loc: SynLocation.T): ValAnything
            RAISES {Error, SharedObj.Error, NetObj.Error,
                    Thread.Alerted} := CopyError;
          (* Override with a routine that makes a copy of this value
             (the default CopyError raises error, CopyId returns self).
               Use this pattern:
               IF tbl.Get(self, (*out*)newVal) THEN RETURN newVal END;
               newVal := NEW(ValAnything, what:=self.what,
                              ... all private fields set to NIL ... );
               EVAL tbl.Put(self, newVal);
               ... fill private fields, possibly calling:
                   newVal.field:=ObValue.CopyVal(self.field, tbl, loc) ...
               RETURN newVal;
          *)
      END;

    (* There are 3 kinds of objects, Remote (Network Objects),
       Replicated (Shared Objects) and Simple (normal Modula-3
       Objects).  The Obliq wrappers contain a reference to the real
       data objects in an appropriately typed data field.

       These are ValObj wrapper methods allow each subtype to redirect
       the method calls to their approriate data object (RemObj,
       ReplObj or SimpleObj). *)
    ValObj =
      Val BRANDED &quot;ValObj&quot; OBJECT METHODS
        Who(VAR(*out*) protected, serialized: BOOLEAN): TEXT
          RAISES {NetObj.Error, SharedObj.Error, Thread.Alerted};
        Select(label: TEXT; internal: BOOLEAN; VAR hint: INTEGER): Val
          RAISES {Error, Exception, ServerError, SharedObj.Error,
                  NetObj.Error, Thread.Alerted};
        Invoke(label: TEXT; argNo: INTEGER; READONLY args: Vals;
               internal: BOOLEAN; VAR hint: INTEGER): Val
          RAISES {Error, Exception, ServerError, SharedObj.Error,
                  NetObj.Error, Thread.Alerted};
        Update(label: TEXT; val: Val; internal: BOOLEAN; VAR hint: INTEGER)
          RAISES {ServerError, SharedObj.Error,
                  NetObj.Error, Thread.Alerted};
        Redirect(val: Val; internal: BOOLEAN)
          RAISES {ServerError, SharedObj.Error,
                  NetObj.Error, Thread.Alerted};
        Has(label: TEXT; VAR hint: INTEGER): BOOLEAN
          RAISES {NetObj.Error, Thread.Alerted, SharedObj.Error};
        Obtain(internal: BOOLEAN): REF ObjFields
          RAISES {ServerError, SharedObj.Error, NetObj.Error, Thread.Alerted};
      END;

    <A HREF="ObValue.m3.html#ValRemObj">ValRemObj</A> &lt;: ValRemObjPublic;
    ValRemObjPublic = ValObj OBJECT
        remote: RemObj;
      END;

    <A HREF="ObValue.m3.html#ValReplObj">ValReplObj</A> &lt;: ValReplObjPublic;
    ValReplObjPublic = ValObj OBJECT
        replica: ReplObj;
      END;

    <A HREF="ObValue.m3.html#ValSimpleObj">ValSimpleObj</A> &lt;: ValSimpleObjPublic;
    ValSimpleObjPublic = ValObj OBJECT
        simple: SimpleObj;
      END;

    ObjFields = ARRAY OF Field;
    Field = RECORD
        label: TEXT;
        field: Val;
        (* ValMeth for method fields,
           ValAlias for alias fields,
           other Val for value fields *)
        update: BOOLEAN := FALSE;
        (* TRUE for replicated object update method, FALSE otherwise *)
      END;

    ValEngine =
      Val BRANDED &quot;ValEngine&quot; OBJECT
        remote: RemEngine;
      END;

    ValFileSystem =
      ValAnything BRANDED &quot;ValFileSystem&quot; OBJECT
        remote: RemFileSystem;
      OVERRIDES
        Is := FileSystemIs;
        Copy := CopyId;
      END;

    ValProcessor =
      ValAnything BRANDED &quot;ValProcessor&quot; OBJECT
      OVERRIDES
        Copy := CopyId;
      END;

    RemVar =
      NetObj.T BRANDED &quot;RemVar&quot; OBJECT
      METHODS
        Get(): Val RAISES {NetObj.Error, Thread.Alerted};
        Set(val: Val) RAISES {NetObj.Error, Thread.Alerted};
      END;

    <A HREF="ObValue.m3.html#RemVarServer">RemVarServer</A> &lt;:
      RemVar;

   RemArray =
     NetObj.T BRANDED &quot;RemArray&quot; OBJECT
     METHODS
       Size(): INTEGER RAISES {NetObj.Error, Thread.Alerted};
       Get(i: INTEGER): Val RAISES {ServerError, NetObj.Error, Thread.Alerted};
       Set(i: INTEGER; val: Val)
         RAISES {ServerError, NetObj.Error, Thread.Alerted};
       Sub(start,size: INTEGER): ValArray
           RAISES {ServerError, NetObj.Error, Thread.Alerted};
         (* Extract the subarray self[start for size]. *)
       Upd(start, size: INTEGER; READONLY other: REF Vals)
           RAISES {ServerError, NetObj.Error, Thread.Alerted};
         (* Update self[start for size] with other[0 for size]. *)
       Obtain(): REF Vals RAISES {NetObj.Error, Thread.Alerted};
         (* Return self.array if local, or a copy of it if remote.
            Modifying the result of Obtain may violate network transparency. *)
     END;

   <A HREF="ObValue.m3.html#RemArrayServer">RemArrayServer</A> &lt;:
     RemArray;

   RemObj =
      NetObj.T BRANDED &quot;RemObj&quot; OBJECT
      METHODS
        Who(VAR(*out*) protected, serialized: BOOLEAN): TEXT
          RAISES {NetObj.Error, Thread.Alerted};
        Select(label: TEXT; internal: BOOLEAN; VAR hint: INTEGER): Val
          RAISES {Error, Exception, ServerError, SharedObj.Error,
                  NetObj.Error, Thread.Alerted};
        Invoke(label: TEXT; argNo: INTEGER; READONLY args: Vals;
          internal: BOOLEAN; VAR hint: INTEGER): Val
          RAISES {Error, Exception, ServerError, SharedObj.Error,
                  NetObj.Error, Thread.Alerted};
        Update(label: TEXT; val: Val; internal: BOOLEAN; VAR hint: INTEGER)
          RAISES {ServerError, SharedObj.Error,
                  NetObj.Error, Thread.Alerted};
        Redirect(val: Val; internal: BOOLEAN)
          RAISES {ServerError, SharedObj.Error,
                  NetObj.Error, Thread.Alerted};
        Has(label: TEXT; VAR hint: INTEGER): BOOLEAN
          RAISES {NetObj.Error, Thread.Alerted};
          (* Whether a field called label exists. *)
        Obtain(internal: BOOLEAN): REF ObjFields
          RAISES {ServerError, NetObj.Error, Thread.Alerted};
          (* Return self.fields if local, or a copy of it if remote.
             Modifying the result of Obtain may violate network transparency.
             *)
      END;

    <A HREF="ObValue.m3.html#RemObjServer">RemObjServer</A> &lt;:
      RemObjServerPublic;
    RemObjServerPublic =
      RemObj BRANDED &quot;RemObjServerPublic&quot; OBJECT
        who: TEXT;
        sync: Sync;
      END;
    (* RemObjServer, a subtype of RemObj, is the standard implementation of
       local objects. Another subtype of RemObj is automatically produced by
       the Network Objects stub generator, for remote surrogates. Further
       subtypes of RemObj can be defined and used as client-specific
       pseudo-objects. In the latter case, the result of Who is used for
       printing; copying, cloning, and pickling operate on the results of
       Who and Obtain.
       *)

    <A HREF="ObValue.m3.html#NonRemObjHookServer">NonRemObjHookServer</A> &lt;: NonRemObjHook;
    NonRemObjHook =
      NetObj.T BRANDED &quot;NonRemObjHook&quot; OBJECT
      METHODS
        init(remObj: ValObj): NonRemObjHook  RAISES {NetObj.Error,
                                                     Thread.Alerted};
        Get(): ValObj RAISES {NetObj.Error, Thread.Alerted};
      END;
    (* NonRemObjHook is used by the obliq net_import and net_export
       commands to allow replicated and simple objects to be exported and
      imported. *)

    <A HREF="../derived/ObValueSO.m3.html#ReplObjStd">ReplObjStd</A> &lt;: ReplObj;
        &lt;* SHARED UPDATE METHODS ReplObjStd.init, ReplObjStd.InvokeUpdate,
                                 ReplObjStd.Update *&gt;

    ReplObj &lt;: ReplObjPublic;
    ReplObjPublic = SharedObj.T BRANDED &quot;ReplObjServerPublic&quot; OBJECT
      METHODS
        init (): ReplObj RAISES {SharedObj.Error};
        Who(VAR(*out*) protected: BOOLEAN): TEXT
          RAISES {SharedObj.Error};
        (* All replicated objects are protected and serialized! *)
        Select(label: TEXT; VAR hint: INTEGER): Val
          RAISES {Error, Exception, ServerError, SharedObj.Error};
        Invoke(label: TEXT; argNo: INTEGER; READONLY args: Vals;
               VAR hint: INTEGER): Val
          RAISES {Error, Exception, ServerError, SharedObj.Error};
        Update(label: TEXT; val: Val; internal: BOOLEAN;
               VAR hint: INTEGER) RAISES {ServerError, SharedObj.Error};
        Has(label: TEXT; VAR hint: INTEGER): BOOLEAN RAISES {SharedObj.Error};
          (* Whether a field called label exists. *)
        Obtain(internal: BOOLEAN): REF ObjFields
          RAISES {ServerError, SharedObj.Error};
          (* Return self.fields. Modifying the result of Obtain may
             violate network transparency. *)
      END;
    (* ReplObj is the implementation of replicated objects.
       The default internal implementation is ReplObjStd.  Further
       subtypes of ReplObj can be defined and used as client-specific
       pseudo-objects.
       *)

   <A HREF="ObValue.m3.html#SimpleObj">SimpleObj</A> &lt;: SimpleObjPublic;
   SimpleObjPublic = BRANDED &quot;SimpleObjPublic&quot; OBJECT
        who: TEXT;
        sync: Sync;
      METHODS
        Who(VAR(*out*) protected, serialized: BOOLEAN): TEXT;
        Select(label: TEXT; internal: BOOLEAN; VAR hint: INTEGER): Val
          RAISES {Error, Exception, ServerError,
                  SharedObj.Error, NetObj.Error, Thread.Alerted};
        Invoke(label: TEXT; argNo: INTEGER; READONLY args: Vals;
          internal: BOOLEAN; VAR hint: INTEGER): Val
          RAISES {Error, Exception, ServerError,
                  SharedObj.Error, NetObj.Error, Thread.Alerted};
        Update(label: TEXT; val: Val; internal: BOOLEAN; VAR hint: INTEGER)
          RAISES {ServerError, SharedObj.Error, NetObj.Error, Thread.Alerted};
        Redirect(val: Val; internal: BOOLEAN)
          RAISES {ServerError, SharedObj.Error, NetObj.Error, Thread.Alerted};
        Has(label: TEXT; VAR hint: INTEGER): BOOLEAN;
          (* Whether a field called label exists. *)
        Obtain(internal: BOOLEAN): REF ObjFields RAISES {ServerError};
          (* Return self.fields if local, or a copy of it if remote.
             Modifying the result of Obtain may violate network transparency.
             *)
      END;

    Sync =
      BRANDED &quot;Sync&quot; OBJECT
        mutex: Thread.Mutex;
      END;

    RemEngine =
      NetObj.T BRANDED &quot;RemEngine&quot; OBJECT
      METHODS
        Who(): TEXT RAISES {NetObj.Error, Thread.Alerted};
        Eval(proc: Val; mySelf: ValObj): Val
          RAISES {Error, Exception, ServerError, NetObj.Error, Thread.Alerted};
      END;

    RemEngineServer =
      RemEngine BRANDED &quot;RemEngineServer&quot; OBJECT
        who: TEXT;
        arg: Val;
      OVERRIDES
        Who := EngineWho;
        Eval := EngineEval;
      END;

    RemFileSystem =
      NetObj.T BRANDED &quot;RemFileSystem&quot; OBJECT
      METHODS
        OpenRead(fileName: TEXT): Rd.T
          RAISES {NetObj.Error, ServerError, Thread.Alerted};
        OpenWrite(fileName: TEXT): Wr.T
          RAISES {NetObj.Error, ServerError, Thread.Alerted};
        OpenAppend(fileName: TEXT): Wr.T
          RAISES {NetObj.Error, ServerError, Thread.Alerted};
      END;

    <A HREF="ObValue.m3.html#RemFileSystemServer">RemFileSystemServer</A> &lt;: RemFileSystem;

    ArgArray = ARRAY [1..8] OF Val;

  VAR
    valOk: Val;
    netException, threadAlerted, sharedException, sharedFatal: ValException;
    showNetObjMsgs: BOOLEAN;
    machineAddress: TEXT;
    localProcessor: ValProcessor;

  PROCEDURE <A HREF="ObValue.m3.html#Setup">Setup</A>();
  (* To be called before any other use of this module. *)

  PROCEDURE <A HREF="ObValue.m3.html#Is">Is</A>(v1,v2: Val; location: SynLocation.T): BOOLEAN;

  PROCEDURE <A HREF="ObValue.m3.html#NewText">NewText</A>(text: TEXT): Val;
    (* Create an Obliq text containing a non-NIL m3 TEXT. *)

  PROCEDURE <A HREF="ObValue.m3.html#NewVar">NewVar</A>(val: Val): ValVar;
    (* Create a new variable. *)

  PROCEDURE <A HREF="ObValue.m3.html#NewArray">NewArray</A>(READONLY vals: Vals): ValArray;
    (* Create a new array. *)
  PROCEDURE <A HREF="ObValue.m3.html#NewArrayFromVals">NewArrayFromVals</A>(vals: REF Vals): ValArray;
    (* Careful: the vals passed in are shared and may get modified later. *)
  PROCEDURE <A HREF="ObValue.m3.html#ArrayCat">ArrayCat</A>(vals1, vals2: REF Vals): Val RAISES {};

  PROCEDURE <A HREF="ObValue.m3.html#NewObject">NewObject</A>(READONLY fields: ObjFields;
                      who: TEXT:=&quot;&quot;; protected: BOOLEAN:=FALSE;
                      sync: Sync:=NIL): ValObj;
    (* Create a new object. *)
  PROCEDURE <A HREF="ObValue.m3.html#NewObjectFromFields">NewObjectFromFields</A>(fields: REF ObjFields;
                                who: TEXT; protected: BOOLEAN;
                                sync: Sync): ValObj;
    (* Careful: the fields passed in are shared and may get modified later. *)
  PROCEDURE <A HREF="ObValue.m3.html#NewReplObject">NewReplObject</A>(READONLY fields: ObjFields;
                      who: TEXT:=&quot;&quot;; protected: BOOLEAN:=FALSE): ValObj
    RAISES {SharedObj.Error};
    (* Create a new replicated object. *)
  PROCEDURE <A HREF="ObValue.m3.html#NewReplObjectFromFields">NewReplObjectFromFields</A>(fields: REF ObjFields;
                                who: TEXT; protected: BOOLEAN): ValObj
    RAISES {SharedObj.Error};
    (* Careful: the fields passed in are shared and may get modified later. *)
  PROCEDURE <A HREF="ObValue.m3.html#NewSimpleObject">NewSimpleObject</A>(READONLY fields: ObjFields;
                      who: TEXT:=&quot;&quot;; protected: BOOLEAN:=FALSE;
                      sync: Sync:=NIL): ValObj;
    (* Create a new simple object. *)
  PROCEDURE <A HREF="ObValue.m3.html#NewSimpleObjectFromFields">NewSimpleObjectFromFields</A>(fields: REF ObjFields;
                                who: TEXT; protected: BOOLEAN;
                                sync: Sync): ValObj;
    (* Careful: the fields passed in are shared and may get modified later. *)

  PROCEDURE <A HREF="ObValue.m3.html#ToSimpleObj">ToSimpleObj</A>(READONLY obj: ValObj; mySelf: ValObj): ValObj
    RAISES {ServerError, NetObj.Error, SharedObj.Error, Thread.Alerted};
    (* Create a SimpleObj from another ValObj. *)
  PROCEDURE <A HREF="ObValue.m3.html#ToReplObj">ToReplObj</A>(READONLY obj: ValObj; mySelf: ValObj;
                      READONLY updateMethods: ARRAY OF TEXT): ValObj
    RAISES {ServerError, NetObj.Error, SharedObj.Error, Thread.Alerted};
    (* Create a ReplObj from another ValObj. *)
  PROCEDURE <A HREF="ObValue.m3.html#ToRemObj">ToRemObj</A>(READONLY obj: ValObj; mySelf: ValObj): ValObj
    RAISES {ServerError, NetObj.Error, SharedObj.Error, Thread.Alerted};
    (* Create a RemObj from another ValObj. *)

  PROCEDURE <A HREF="ObValue.m3.html#SetObjPickler">SetObjPickler</A>(obj: ValObj; picklerIn: ValSimpleObj;
                          picklerOut: ValSimpleObj; mySelf: ValObj)
      RAISES {ServerError, NetObj.Error, SharedObj.Error, Thread.Alerted};
    (* mySelf is the object invoking the pickler setting.
       set the pickler objects &quot;picklerIn&quot; and &quot;picklerOut&quot; for an object
       &quot;obj.&quot;  These objects are valid iff they have the set of fields
       corresponding to {obj's fields - obj's method fields} (ie. all
       of obj's non-method fields.) *)

  PROCEDURE <A HREF="ObValue.m3.html#ObjClone1">ObjClone1</A>(remObj: ValObj; mySelf: ValObj): ValObj
      RAISES {ServerError, NetObj.Error, SharedObj.Error, Thread.Alerted};
    (* mySelf is the object invoking cloning, if any, or NIL.
       Then cloning is self-inflicted if Is(remObj, mySelf) *)
  PROCEDURE <A HREF="ObValue.m3.html#ObjClone">ObjClone</A>(READONLY valObjs: ARRAY OF ValObj;
                     mySelf: ValObj): ValObj
    RAISES {ServerError, NetObj.Error, SharedObj.Error, Thread.Alerted};
    (* mySelf is the object invoking cloning, if any, or NIL.
       Then cloning is self-inflicted if for any Is(remObjs[i],mySelf). *)

  PROCEDURE <A HREF="ObValue.m3.html#ObjNotify">ObjNotify</A>(val: Val; notifyProc: ValFun);
    (* Create a NetObjNotifier on val if it is a remote data value.
       When val becomes inaccesible, notifyProc is called.
       notifyProc should take two arguments, the value and a text
       string which will contain &quot;Dead&quot; or &quot;Failed&quot;.   See
       NetObjNotifier.i3 for details. *)

  PROCEDURE <A HREF="ObValue.m3.html#NewAlias">NewAlias</A>(obj: ValObj; label: TEXT; location: SynLocation.T)
    : ValAlias RAISES {Error, Exception};

  PROCEDURE <A HREF="ObValue.m3.html#EngineWho">EngineWho</A>(self: RemEngineServer): TEXT RAISES {NetObj.Error};
  PROCEDURE <A HREF="ObValue.m3.html#EngineEval">EngineEval</A>(self: RemEngineServer; proc: Val; mySelf: ValObj): Val
          RAISES {Error, Exception, ServerError, NetObj.Error};

  PROCEDURE <A HREF="ObValue.m3.html#NewFileSystem">NewFileSystem</A>(readOnly: BOOLEAN): ValFileSystem;
  PROCEDURE <A HREF="ObValue.m3.html#FileSystemIs">FileSystemIs</A>(self: ValFileSystem; other: ValAnything): BOOLEAN;

  PROCEDURE <A HREF="ObValue.m3.html#SameException">SameException</A>(exc1, exc2: ValException): BOOLEAN;

  PROCEDURE <A HREF="ObValue.m3.html#RaiseError">RaiseError</A>(msg: TEXT; location: SynLocation.T) RAISES {Error};
  PROCEDURE <A HREF="ObValue.m3.html#RaiseException">RaiseException</A>(exception: ValException; msg: TEXT;
    loc: SynLocation.T) RAISES {Exception};
  PROCEDURE <A HREF="ObValue.m3.html#RaiseNetException">RaiseNetException</A>(msg: TEXT; atoms: AtomList.T; loc: SynLocation.T)
    RAISES {Exception};
  PROCEDURE <A HREF="ObValue.m3.html#RaiseSharedException">RaiseSharedException</A>(msg: TEXT; atoms: AtomList.T;
                                 loc: SynLocation.T) RAISES {Exception};

  PROCEDURE <A HREF="ObValue.m3.html#ErrorMsg">ErrorMsg</A>(swr: SynWr.T; packet: ErrorPacket);
  PROCEDURE <A HREF="ObValue.m3.html#ExceptionMsg">ExceptionMsg</A>(wr: SynWr.T; packet: ExceptionPacket);

  PROCEDURE <A HREF="ObValue.m3.html#BadOp">BadOp</A>(pkg, op: TEXT; location: SynLocation.T) RAISES {Error};
  PROCEDURE <A HREF="ObValue.m3.html#BadArgType">BadArgType</A>(argNo: INTEGER; expected, pkg, op: TEXT;
    location: SynLocation.T) RAISES {Error};
  PROCEDURE <A HREF="ObValue.m3.html#BadArgVal">BadArgVal</A>(argNo: INTEGER; expected, pkg, op: TEXT;
    location: SynLocation.T) RAISES {Error};
  PROCEDURE <A HREF="ObValue.m3.html#BadArgsNoMsg">BadArgsNoMsg</A>(desired, found: INTEGER;
    routineKind, routineName: TEXT): TEXT;

  PROCEDURE <A HREF="ObValue.m3.html#IsSelfOther">IsSelfOther</A>(self, other: ValAnything): BOOLEAN;
  PROCEDURE <A HREF="ObValue.m3.html#PrintWhat">PrintWhat</A>(self: ValAnything): TEXT;
  PROCEDURE <A HREF="ObValue.m3.html#CopyError">CopyError</A>(self: ValAnything; tbl: Tbl;
                      loc: SynLocation.T): ValAnything RAISES {Error};
  PROCEDURE <A HREF="ObValue.m3.html#CopyId">CopyId</A>(self: ValAnything; tbl: Tbl;
                   loc: SynLocation.T): ValAnything;

  PROCEDURE <A HREF="ObValue.m3.html#InhibitTransmission">InhibitTransmission</A>(tc: INTEGER; reason: TEXT);
  (* Inhibits the network transmission of any object whose type has
     typecode &quot;tc&quot;; an obliq exception will result. To be used on all
     M3 types that are embedded in obliq values but that have only
     &quot;local&quot; meaning, such as threads. The reason should be a string
     like &quot;threads cannot be transmitted/duplicated&quot;. *)

  PROCEDURE <A HREF="ObValue.m3.html#NewTbl">NewTbl</A>(): Tbl;
  (* A new empty table for CopyVal. *)

  PROCEDURE <A HREF="ObValue.m3.html#TblGet">TblGet</A> (self: Tbl; old: REFANY; VAR (*out*) new: REFANY): BOOLEAN;
  PROCEDURE <A HREF="ObValue.m3.html#TblPut">TblPut</A> (self: Tbl; old, new: REFANY);

  PROCEDURE <A HREF="ObValue.m3.html#CopyVal">CopyVal</A>(val: Val; tbl: Tbl; loc: SynLocation.T): Val
    RAISES {Error, SharedObj.Error, NetObj.Error, Thread.Alerted};
  (* Make a complete local copy of val, preserving sharing and circularities.
     Substructures are fetched over the network, if necessary. Protected
     objects and non-transferrable values produce errors. *)

  PROCEDURE <A HREF="ObValue.m3.html#CopyValToLocal">CopyValToLocal</A>(val: Val; tbl: Tbl; loc: SynLocation.T)
    : Val RAISES {Error, SharedObj.Error, NetObj.Error, Thread.Alerted};
  PROCEDURE <A HREF="ObValue.m3.html#CopyLocalToVal">CopyLocalToVal</A>(val: Val; tbl: Tbl; loc: SynLocation.T)
    : Val RAISES {Error, SharedObj.Error, NetObj.Error, Thread.Alerted};
  (* Internal use only. *)

  VAR
    sysCallFailure: ValException;

  TYPE
    SysCallClosure = OBJECT
    METHODS
      SysCall(READONLY args: Vals; loc: SynLocation.T:=NIL): Val
        RAISES{Error, Exception};
      (* To be overridden. It should return an obliq Val, or raise an error
         by calling RaiseError, or raise an exception by calling
         RaiseException. The raised exception should normally be
         sysCallFailure. The loc parameter should be passed through whenever
         appropriate, or appriate error locations should be synthesized.
         Alternatively, errors and exceptions may be left uncought, so
         that they propagate back to Obliq; however this may result
         in poor error location reporting. *)
    END;

  PROCEDURE <A HREF="ObValue.m3.html#RegisterSysCall">RegisterSysCall</A>(name: TEXT; clos: SysCallClosure);
    (* To register a Modula-3 procedure that can be called from Obliq,
       under a given name. Re-registering for the same name overrides
       the previous proc for that name. Use clos=NIL to unregister. *)

  PROCEDURE <A HREF="ObValue.m3.html#FetchSysCall">FetchSysCall</A>(name: TEXT; VAR(*out*) clos: SysCallClosure): BOOLEAN;
    (* To fetch a registered Modula-3 procedure by its given name.
       Returns FALSE if not found. *)

END ObValue.
</PRE>
</inInterface>
<HR>
<A NAME="x1">ObValue's implementation  is in:
</A><UL>
<LI><A HREF="../derived/ObValueSO.m3.html#0TOP0">obliqrt/derived/ObValueSO.m3</A>
<LI><A HREF="../derived/ObValue_NonRemObjHook_v1.m3.html#0TOP0">obliqrt/derived/ObValue_NonRemObjHook_v1.m3</A>
<LI><A HREF="../derived/ObValue_RemArray_v1.m3.html#0TOP0">obliqrt/derived/ObValue_RemArray_v1.m3</A>
<LI><A HREF="../derived/ObValue_RemEngine_v1.m3.html#0TOP0">obliqrt/derived/ObValue_RemEngine_v1.m3</A>
<LI><A HREF="../derived/ObValue_RemFileSystem_v1.m3.html#0TOP0">obliqrt/derived/ObValue_RemFileSystem_v1.m3</A>
<LI><A HREF="../derived/ObValue_RemObj_v1.m3.html#0TOP0">obliqrt/derived/ObValue_RemObj_v1.m3</A>
<LI><A HREF="../derived/ObValue_RemVar_v1.m3.html#0TOP0">obliqrt/derived/ObValue_RemVar_v1.m3</A>
<LI><A HREF="ObValue.m3.html#0TOP0">obliqrt/src/ObValue.m3</A>
</UL>
<P>
<PRE>























</PRE>
</BODY>
</HTML>
