<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: juno-app/src/Juno.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>juno-app/src/Juno.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright (C) 1995, Digital Equipment Corporation                         
 All rights reserved.                                                      
 See the file COPYRIGHT for a full description.                            
                                                                           
 Last modified on Fri Feb  4 11:42:43 PST 2000 by heydon                   
      modified on Wed Aug 27 17:38:48 PDT 1997 by leifer                   
      modified on Thu Nov 30 13:56:40 PST 1995 by gnelson                  
      modified on Sat Aug 22 23:32:17 PDT 1992 by myers                    
<PRE>&lt;* PRAGMA LL *&gt;

MODULE <module>Juno</module> EXPORTS <A HREF="../../m3core/src/main/Main.i3.html"><implements>Main</A></implements>;

IMPORT <A HREF="#x1">Source</A>, <A HREF="Drawing.i3.html">Drawing</A>, <A HREF="#x2">Editor</A>, <A HREF="EditorUI.i3.html">EditorUI</A>, <A HREF="SaveState.i3.html">SaveState</A>, <A HREF="PublicView.i3.html">PublicView</A>, <A HREF="CurrCmd.i3.html">CurrCmd</A>;
IMPORT   <A HREF="Marquee.i3.html">Marquee</A>, <A HREF="#x3">View</A>, <A HREF="JunoError.i3.html">JunoError</A>, <A HREF="JunoWM.i3.html">JunoWM</A>, <A HREF="ToolBox.i3.html">ToolBox</A>, <A HREF="../../ui/src/split/DblBufferVBT.i3.html">DblBufferVBT</A>, <A HREF="Drag.i3.html">Drag</A>;
IMPORT   <A HREF="JunoUIImpl.i3.html">JunoUIImpl</A>, <A HREF="PrintImpl.i3.html">PrintImpl</A>, <A HREF="PSImpl.i3.html">PSImpl</A>, <A HREF="RandomImpl.i3.html">RandomImpl</A>, <A HREF="TextImpl.i3.html">TextImpl</A>, <A HREF="TimeImpl.i3.html">TimeImpl</A>;
IMPORT   <A HREF="UnitImpl.i3.html">UnitImpl</A>;
IMPORT   <A HREF="FVFilter.i3.html">FVFilter</A>, <A HREF="JunoConfig.i3.html">JunoConfig</A>, <A HREF="JunoRsrc.i3.html">JunoRsrc</A>, <A HREF="JunoVersion.i3.html">JunoVersion</A>, <A HREF="JunoZeus.i3.html">JunoZeus</A>;
IMPORT <A HREF="../../juno-compiler/src/BuiltInSlots.i3.html">BuiltInSlots</A>, <A HREF="../../juno-compiler/src/JunoAST.i3.html">JunoAST</A>, <A HREF="../../juno-compiler/src/JunoASTUtils.i3.html">JunoASTUtils</A>, <A HREF="../../juno-compiler/src/JunoCompile.i3.html">JunoCompile</A>, <A HREF="../../juno-compiler/src/JunoCompileErr.i3.html">JunoCompileErr</A>;
IMPORT   <A HREF="../../juno-compiler/src/JunoScope.i3.html">JunoScope</A>, <A HREF="../../juno-compiler/src/JunoUnparse.i3.html">JunoUnparse</A>, <A HREF="../../juno-compiler/src/JunoLex.i3.html">JunoLex</A>, <A HREF="../../juno-compiler/src/JunoParse.i3.html">JunoParse</A>;
IMPORT <A HREF="../../juno-machine/src/JunoRT.i3.html">JunoRT</A>;
IMPORT <A HREF="../../formsvbt/src/FormsVBT.i3.html">FormsVBT</A>, <A HREF="../../vbtkit/src/etext/TextEditVBT.i3.html">TextEditVBT</A>, <A HREF="../../vbtkit/src/etext/TextPort.i3.html">TextPort</A>, <A HREF="../../vbtkit/src/lego/ListVBT.i3.html">ListVBT</A>;
IMPORT <A HREF="../../ui/src/vbt/Trestle.i3.html">Trestle</A>, <A HREF="../../ui/src/vbt/TrestleComm.i3.html">TrestleComm</A>, <A HREF="../../ui/src/vbt/VBT.i3.html">VBT</A>, <A HREF="../../ui/src/vbt/VBTClass.i3.html">VBTClass</A>, <A HREF="../../ui/src/split/Filter.i3.html">Filter</A>, <A HREF="../../ui/src/split/Split.i3.html">Split</A>, <A HREF="../../ui/src/split/HVSplit.i3.html">HVSplit</A>, <A HREF="../../ui/src/split/ZSplit.i3.html">ZSplit</A>;
IMPORT   <A HREF="../../libm3/src/geometry/Axis.i3.html">Axis</A>, <A HREF="../../ui/src/split/PackSplit.i3.html">PackSplit</A>, <A HREF="../../ui/src/vbt/PaintOp.i3.html">PaintOp</A>, <A HREF="../../ui/src/split/TextVBT.i3.html">TextVBT</A>, <A HREF="../../libm3/src/geometry/Rect.i3.html">Rect</A>, <A HREF="../../libm3/src/geometry/Point.i3.html">Point</A>, <A HREF="../../ui/src/vbt/Font.i3.html">Font</A>;
IMPORT <A HREF="../../libm3/src/os/Common/FS.i3.html">FS</A>, <A HREF="../../libm3/src/os/Common/RegularFile.i3.html">RegularFile</A>, <A HREF="../../libm3/src/os/Common/Pathname.i3.html">Pathname</A>, <A HREF="../../libm3/src/rw/FileRd.i3.html">FileRd</A>, <A HREF="../../libm3/src/rw/FileWr.i3.html">FileWr</A>, <A HREF="../../libm3/src/os/Common/Pipe.i3.html">Pipe</A>, <A HREF="../../libm3/src/os/Common/Process.i3.html">Process</A>, <A HREF="../../libm3/src/os/Common/OSError.i3.html">OSError</A>, <A HREF="../../libm3/src/fmtlex/Lex.i3.html">Lex</A>;
IMPORT   <A HREF="../../libm3/src/rw/Rd.i3.html">Rd</A>, <A HREF="../../libm3/src/rw/Wr.i3.html">Wr</A>, <A HREF="../../libm3/src/rw/Stdio.i3.html">Stdio</A>, <A HREF="../../m3core/src/text/Text.i3.html">Text</A>, <A HREF="../../libm3/src/rw/TextRd.i3.html">TextRd</A>, <A HREF="../../libm3/src/rw/TextWr.i3.html">TextWr</A>, <A HREF="../../m3core/src/thread/Common/Thread.i3.html">Thread</A>, <A HREF="../../libm3/src/atom/Atom.i3.html">Atom</A>, <A HREF="../../vbtkit/src/vbtkitutils/Rsrc.i3.html">Rsrc</A>, <A HREF="../../libm3/src/fmtlex/Fmt.i3.html">Fmt</A>, <A HREF="../../libm3/src/params/Params.i3.html">Params</A>;
IMPORT   <A HREF="../../netobj/src/netobjrt/NetObj.i3.html">NetObj</A>, <A HREF="../../libm3/src/pickle/ver1/Pickle.i3.html">Pickle</A>, <A HREF="../../libm3/derived/TextList.i3.html">TextList</A>, <A HREF="../../libm3/derived/TextListSort.i3.html">TextListSort</A>, <A HREF="../../libm3/src/params/Env.i3.html">Env</A>;

&lt;* FATAL Wr.Failure, Rd.Failure, Thread.Alerted *&gt;
&lt;* FATAL FormsVBT.Error, FormsVBT.Unimplemented *&gt;

TYPE
  SyncStat = { NotDone, Save, Discard, Cancel };

  Window = FormsVBT.T OBJECT
    root: View.Root;
    scope: JunoScope.T;
    toolbox: ToolBox.T;
    builtInSize: CARDINAL;
    fileName: TEXT;
    fileStale: BOOLEAN;
    checkpointStale: BOOLEAN;
    saveAsFName: TEXT;

    mu: MUTEX;
    untilDone: Thread.Condition;
    stat: SyncStat
  METHODS
    init(d: Drawing.T): Window := NewForm
    &lt;* LL.sup &lt; VBT.mu *&gt;
    (* Initialize the window on drawing &quot;d&quot;. *)
  OVERRIDES
    misc := Misc
  END;
</PRE> If <CODE>w: Window</CODE>, then <CODE>w.fileName</CODE> is the complete absolute pathname
   of the current module file, and <CODE>w.fileStale</CODE> iff the disk file
   is out-of-date with respect to the contents of the current module
   editor.  The value of <CODE>checkPointStale</CODE> is <CODE>TRUE</CODE> if the checkpoint file
   differs from the state of the editor and source view. 

<P><PRE>EXCEPTION OpenFailure;
</PRE><BLOCKQUOTE><EM> An attempt was made to open something other than a file </EM></BLOCKQUOTE><PRE>
</PRE> ========================= Compilation Procedures ======================== 

<P><PRE>PROCEDURE <A NAME="MakeCurrMod"><procedure>MakeCurrMod</procedure></A>(w: Window; time: VBT.TimeStamp): BOOLEAN =
</PRE><BLOCKQUOTE><EM> If the current module is out-of-date, recompile it and install its
   definitions in the top-level scope. If it changed, update the toolbox. On
   return, <CODE>w.root.eTrue</CODE> will be <CODE>TRUE</CODE> iff the module compiled successfully.
   Return <CODE>TRUE</CODE> iff the current module is up-to-date. </EM></BLOCKQUOTE><PRE>
  &lt;* LL.sup &lt; w.root.editor *&gt;
  VAR dummy: JunoAST.Id; mod: JunoScope.Mod; BEGIN
    IF w.root.eTrue THEN RETURN TRUE END;
    IF CompileEditor(w.root, w.root.editor, time, w.scope, dummy, mod) THEN
      w.root.eTrue := TRUE;
      CurrCmd.SetScope(w.root.ccmd, mod.public_scp);
      ToolBox.Update(Split.Succ(w.toolbox, NIL), w.root.editor,
    	w.root, w.builtInSize, anon := TRUE)
    END;
    RETURN w.root.eTrue
  END MakeCurrMod;

PROCEDURE <A NAME="MakeCurrCmd"><procedure>MakeCurrCmd</procedure></A>(w: Window; time: VBT.TimeStamp; skipify: BOOLEAN) =
</PRE><BLOCKQUOTE><EM> Parse, compile, run, and pretty-print the current command. Also, if the
   current command compiled successfully, disable the <CODE>Solve</CODE> button. Requires
   that the current module is up-to-date, i.e., that <CODE>w.root.eTrue</CODE>. </EM></BLOCKQUOTE><PRE>
  BEGIN
    &lt;* ASSERT w.root.eTrue OR CurrCmd.GetAST(w.root.ccmd) = JunoAST.SkipVal *&gt;
    IF Source.Make(w.root.source, time, skipify) THEN
      (* disable the &quot;Solve&quot; button *)
      FormsVBT.MakeDormant(w, SolveButton)
    END
  END MakeCurrCmd;

PROCEDURE <A NAME="CompileModAndCmd"><procedure>CompileModAndCmd</procedure></A>(w: Window; ts: VBT.TimeStamp): BOOLEAN =
</PRE><BLOCKQUOTE><EM> Compile the current module (if necessary) and then the current command (if
   necessary), unparsing the current command if successful. If either of the
   compilations fails, highlight the error using timestamp <CODE>ts</CODE> in the
   appropriate window. If compiling the current module was successful, update
   the toolbox if necessary. Return <CODE>TRUE</CODE> iff both compilations were
   successful. </EM></BLOCKQUOTE><PRE>
  BEGIN
    RETURN MakeCurrMod(w, ts) AND
      Source.Compile(w.root.source, ts, skipify := FALSE)
  END CompileModAndCmd;

PROCEDURE <A NAME="MakeModAndCmd"><procedure>MakeModAndCmd</procedure></A>(w: Window; ts: VBT.TimeStamp; skipify: BOOLEAN) =
</PRE><BLOCKQUOTE><EM> Compile the current module and the current command, and run the current
   command, replacing its body by <CODE>SKIP</CODE> if <CODE>skipify</CODE> is TRUE. If the current
   module changed, update the toolbox. If there was an error compiling the
   current module, don't update the toolbox or compile and run the current
   command. </EM></BLOCKQUOTE><PRE>
  BEGIN
    IF NOT MakeCurrMod(w, ts) THEN RETURN END;
    MakeCurrCmd(w, ts, skipify)
  END MakeModAndCmd;
</PRE> ======================== Module Editor Procedures ======================= 

<P><PRE>CONST NoTimeStamp: VBT.TimeStamp = 0;

PROCEDURE <A NAME="CompileEditor"><procedure>CompileEditor</procedure></A>(
    rt: View.Root;
    ed: Editor.T;
    time: VBT.TimeStamp;
    scp: JunoScope.T;
    VAR (*OUT*) modName: JunoAST.Id;
    VAR (*OUT*) entity: JunoScope.Mod;
    uniqueModName := TRUE): BOOLEAN =
</PRE><BLOCKQUOTE><EM> Parse (if necessary), compile, and unparse the the editor <CODE>ed</CODE>, installing
   buttons for the UI declarations in <CODE>rt</CODE>, setting <CODE>modName</CODE> to the name
   of the current module (if any, or to <CODE>NIL</CODE> if none), and setting <CODE>entity</CODE>
   to a module entity whose <CODE>public</CODE> and <CODE>scp</CODE> scopes have parent scope <CODE>scp</CODE>
   and that contain bindings for public and all declarations, respectively.
<P>
   Returns <CODE>TRUE</CODE> if there was no error in the compilation. If there was an
   error, it is displayed using time <CODE>time</CODE>. </EM></BLOCKQUOTE><PRE>
  BEGIN
    IF
      Editor.Compile(ed, time, scp, modName, entity, uniqueModName) AND
      EditorUI.CompileUI(rt, ed, time, entity.scp)
    THEN
      Editor.Unparse(ed);
      RETURN TRUE
    END;
    RETURN FALSE
  END CompileEditor;

PROCEDURE <A NAME="CompileModule"><procedure>CompileModule</procedure></A>(w: Window; mod: TEXT; rd: Rd.T; augment := FALSE):
  BOOLEAN =
</PRE><BLOCKQUOTE><EM> Create a new <CODE>Editor.T</CODE> on the module named <CODE>mod</CODE> whose contents are read
   from <CODE>rd</CODE>, compile it, bind its module scope into <CODE>w.scope</CODE>, and install it
   in the root list of modules, and (if there are no errors) return <CODE>TRUE</CODE>.
   If there are errors, return <CODE>FALSE</CODE>.
<P>
   Let <CODE>scp</CODE> denote the scope containing the definitions in <CODE>mod</CODE>. If <CODE>augment
   = FALSE</CODE>, then <CODE>mod</CODE> must be unbound in <CODE>w.scope</CODE>. In this case, <CODE>mod</CODE> is
   bound to <CODE>scp</CODE> in <CODE>w.scope</CODE>. Otherwise, <CODE>mod</CODE> is bound to some scope
   <CODE>oldScp</CODE> in <CODE>w.scope</CODE>. In this case, any binding <CODE>(name, entity)</CODE> in <CODE>scp</CODE>
   for which <CODE>name</CODE> is *not* bound in <CODE>oldScp</CODE> is added to <CODE>oldScp</CODE>. As the
   name implies, this is used to augment the scope for a built-in module
   like <CODE>PS</CODE> with the names of constants, global variables, etc. defined in a
   <CODE>.juno</CODE> file. </EM></BLOCKQUOTE><PRE>
  &lt;* LL.sup &lt; VBT.mu *&gt;
  &lt;* FATAL JunoScope.NameClash *&gt;
  VAR
    src := Rd.GetText(rd, LAST(CARDINAL));
    ed := NEW(Editor.T).init(src, readOnly := TRUE);
    modNm: JunoAST.Id; modScp: JunoScope.Mod;
    success: BOOLEAN;
    ent: JunoScope.Mod;
  BEGIN
    (* Compile the new module *)
    success := CompileEditor(w.root, ed, NoTimeStamp, w.scope, modNm, modScp,
      uniqueModName := NOT augment);
    IF NOT success THEN RETURN FALSE END;
    IF modNm = NIL THEN
      LOCK VBT.mu DO
        JunoError.Display(startup, &quot;Missing MODULE header in module \&quot;&quot;
          &amp; mod &amp; &quot;\&quot;&quot;)
        (* Must use &quot;startup&quot; since &quot;w&quot; is not yet connected to the
           window system. *)
      END;
      RETURN FALSE
    END;
    ent := JunoScope.Lookup(w.scope, modNm, localOnly := TRUE);
    &lt;* ASSERT augment = (ent # NIL) *&gt;
    IF ent = NIL THEN
      JunoScope.Bind(w.scope, modNm, modScp)
    ELSE
      (* augment the existing scope with the public names *)
      VAR names := JunoScope.Names(modScp.public_scp); BEGIN
        FOR i := 0 TO LAST(names^) DO
          IF JunoScope.Lookup(ent.scp, names[i]) = NIL THEN
            VAR e := JunoScope.Lookup(modScp.public_scp, names[i]); BEGIN
              &lt;* ASSERT e # NIL AND ent.public_scp = ent.scp *&gt;
              JunoScope.Bind(ent.public_scp, names[i], e)
            END
          END
        END
      END
    END;
    (* Add the module to the list of modules *)
    w.root.modules := NEW(View.EditorList, view := ed,
      form := NIL, mod := mod, next := w.root.modules);
    RETURN TRUE
  END CompileModule;

PROCEDURE <A NAME="CompileModules"><procedure>CompileModules</procedure></A>(
    w: Window;
    rd: Rd.T;
    VAR (*INOUT*) modList: TextList.T;
    fromRsrc := FALSE) =
</PRE><BLOCKQUOTE><EM> Read the modules named on each line of the stream <CODE>rd</CODE>, and compile each
   one via a call to <CODE>CompileModule</CODE>. Blank lines and lines beginning with <CODE>%</CODE>
   in <CODE>rd</CODE> are ignored. A line containing <CODE>EOF</CODE> terminates reading.
<P>
   Each other line should contain the filename of a Juno module file. For
   each, a module name is formed from the filename by stripping the extension
   from the last component of the filename. These module names are prepended
   to the list <CODE>modList</CODE>. If the filename is followed by the text
   <CODE>(builtin)</CODE>, then the scope formed from the module is used to *augment* an
   existing built-in scope. By default, the module files are read from the
   file system. If <CODE>fromRsrc</CODE> is true, then the files are opened as resources
   via calls to <CODE>RsrcOpen</CODE>.
<P>
   For each named file, the corresponding module name is shown in the
   <CODE>initModule</CODE> text field of the <CODE>startup</CODE> screen.
<P>
   If an error occurs in any file, then <CODE>w.fileName</CODE> is changed to the
   name of the file containing the error, and the rest of the files
   are ignored. In these error cases, the name of the offending module
   is not appended to <CODE>modList</CODE>.
</EM></BLOCKQUOTE><PRE>
  &lt;* FATAL Rd.EndOfFile *&gt;
  CONST CommentChar = '%'; BuiltinText = &quot;(builtin)&quot;;
  VAR
    ln, fileName, modName: TEXT;
    fileRd: Rd.T;
    lnRd := NEW(TextRd.T);
    builtin: BOOLEAN; (* module name followed by BuiltinText? *)
  BEGIN
    TRY
      WHILE NOT Rd.EOF(rd) DO
    	ln := Rd.GetLine(rd);
        IF Text.Length(ln) &gt; 0 AND Text.GetChar(ln, 0) # CommentChar THEN
          IF Text.Equal(ln, &quot;EOF&quot;) THEN EXIT END;
          EVAL lnRd.init(ln);
          Lex.Skip(lnRd);
          fileName := Lex.Scan(lnRd);
          modName := Pathname.LastBase(fileName);
          builtin := FALSE;
          IF NOT Rd.EOF(lnRd) THEN
            Lex.Skip(lnRd);
            IF Text.Equal(Lex.Scan(lnRd), BuiltinText) THEN
              builtin := TRUE
            END
          END;
          FormsVBT.PutText(startup, &quot;initModule&quot;, modName);
          IF fromRsrc THEN
            TRY
              fileRd := RsrcOpen(fileName);
              IF NOT CompileModule(w, modName, fileRd, augment := builtin) THEN
                Wr.PutText(Stdio.stderr, &quot;Compiler error in bundled module &quot;
                  &amp; fileName &amp; &quot;\n&quot;);
                Wr.Flush(Stdio.stderr);
                RETURN
              END
            EXCEPT Rsrc.NotFound =&gt;
              Wr.PutText(Stdio.stderr, &quot;Error: unable to find \&quot;&quot;);
              Wr.PutText(Stdio.stderr, fileName &amp; &quot;\&quot; resource\n&quot;);
              Wr.Flush(Stdio.stderr);
              RETURN
            END
          ELSE
            TRY
              fileRd := FileRd.Open(fileName);
              IF NOT CompileModule(w, modName, fileRd, augment := builtin) THEN
                w.fileName := fileName;
                RETURN
              END
            EXCEPT OSError.E =&gt;
                Wr.PutText(Stdio.stderr, &quot;Error: unable to open file \&quot;&quot;);
                Wr.PutText(Stdio.stderr, fileName &amp; &quot;\&quot;\n&quot;);
                Wr.Flush(Stdio.stderr);
                RETURN
            END
          END;
          (* only add &quot;modName&quot; to the list of modulese *after*
             it has been successfully compiled *)
          modList := TextList.Cons(modName, modList);
        END
      END
    EXCEPT
      Rd.Failure =&gt;
        Wr.PutText(Stdio.stderr, &quot;Fatal error: Rd.Failure on config file\n&quot;);
        Wr.Flush(Stdio.stderr)
    END
  END CompileModules;

PROCEDURE <A NAME="OpenModule"><procedure>OpenModule</procedure></A>(w: Window; mod: TEXT; hasPrivate := TRUE; show := TRUE) =
  &lt;* LL.sup = VBT.mu *&gt;
  VAR p := FindModule(w.root.modules, mod); BEGIN
    &lt;* ASSERT p # NIL *&gt;
    IF p.form = NIL THEN
      p.form := NewToolbox(w, p.view, hasPrivate := hasPrivate)
    END;
    IF show AND VBT.Parent(p.form) = NIL THEN
      Split.AddChild(w.toolbox, p.form)
    END
  END OpenModule;

PROCEDURE <A NAME="FindModule"><procedure>FindModule</procedure></A>(curr: View.EditorList; mod: TEXT): View.EditorList =
</PRE><BLOCKQUOTE><EM> Search for the module named <CODE>mod</CODE> in the list <CODE>curr</CODE>; return <CODE>NIL</CODE> if no
   such module appears in the list. </EM></BLOCKQUOTE><PRE>
  BEGIN
    WHILE curr # NIL AND NOT Text.Equal(curr.mod, mod) DO
      curr := curr.next
    END;
    RETURN curr
  END FindModule;

TYPE ToolType = { Point, Templ, None };

PROCEDURE <A NAME="CodeDeclName"><procedure>CodeDeclName</procedure></A>(decl: JunoAST.Decl): JunoAST.Id =
</PRE><BLOCKQUOTE><EM> Return the name of the predicate, function, or procedure declaration
   <CODE>decl</CODE>. It is a checked run-time error if <CODE>decl</CODE> is not one of these
   three types. </EM></BLOCKQUOTE><PRE>
  VAR hdr: JunoAST.Header; BEGIN
    TYPECASE decl OF &lt;* NOWARN *&gt;
      JunoAST.PredDecl (pd) =&gt; hdr := pd.header
    | JunoAST.FuncDecl (fd) =&gt; hdr := fd.header
    | JunoAST.ProcDecl (pd) =&gt; hdr := pd.header
    END;
    RETURN hdr.name
  END CodeDeclName;

PROCEDURE <A NAME="NewUIDecl"><procedure>NewUIDecl</procedure></A>(toolType: ToolType; nm: JunoAST.Id): JunoAST.UIDecl =
</PRE><BLOCKQUOTE><EM> Return a new PointTool or Template UI declaration whose type is determined
   from <CODE>toolType</CODE> and the name of whose argument is <CODE>nm</CODE>. It is a checked
   run-time error for <CODE>toolType</CODE> to be other than <CODE>Point</CODE> or <CODE>Templ</CODE>. </EM></BLOCKQUOTE><PRE>
  VAR res := NEW(JunoAST.UIDecl, bp := JunoAST.End); BEGIN
    res.args := JunoASTUtils.NewExprList(
      JunoASTUtils.QIdFromId(nm), bp := JunoAST.End);
    CASE toolType OF &lt;* NOWARN *&gt;
      ToolType.Point =&gt; res.name := Editor.PointToolSym
    | ToolType.Templ =&gt; res.name := Editor.TemplToolSym
    END;
    RETURN res
  END NewUIDecl;

PROCEDURE <A NAME="InstallDecl"><procedure>InstallDecl</procedure></A>(w: Window; decl: JunoAST.Decl; toolType: ToolType):
  BOOLEAN =
</PRE><BLOCKQUOTE><EM> Install the top-level procedure, predicate, or function declaration <CODE>decl</CODE>
   in <CODE>w.root.editor</CODE>, and add a PointTool or Template UI declaration as well
   according to <CODE>toolType</CODE>. The new declaration is compiled and installed in
   the current-command scope. The booleans in <CODE>w.root</CODE> are also set
   appropriately.
<P>
   If a declaration with the same name as <CODE>decl</CODE> is already defined in the
   current-command scope, and error message is displayed, and no action is
   taken. Returns TRUE iff the declaration was installed successfully. </EM></BLOCKQUOTE><PRE>
  &lt;* LL.sup = VBT.mu *&gt;
  VAR
    declName := CodeDeclName(decl);
    scp := CurrCmd.GetScope(w.root.ccmd);
  BEGIN
    IF JunoScope.Lookup(scp, declName) # NIL THEN
      JunoError.Display(w.root.editor,
        &quot;\&quot;&quot; &amp; Atom.ToText(declName) &amp; &quot;\&quot; is already defined&quot;);
      RETURN FALSE
    END;
    BindDecl(scp, declName, decl);
    WITH ed = w.root.editor DO
      Editor.AddTree(ed, decl);
      IF toolType # ToolType.None THEN
        Editor.AddTree(ed, NewUIDecl(toolType, declName));
        ToolBox.Update(Split.Succ(w.toolbox, NIL), ed, w.root,
          w.builtInSize, anon := TRUE)
      END
    END;
    w.root.astTrue := TRUE;
    w.root.sTrue := FALSE;
    w.root.dTrue := FALSE;
    SetModuleState(w, ModState.Stale);
    RETURN TRUE
  END InstallDecl;

PROCEDURE <A NAME="BadFoldArg"><procedure>BadFoldArg</procedure></A>(w: Window; id: JunoAST.Id) =
</PRE><BLOCKQUOTE><EM> Display an error to <CODE>w</CODE> as a result of the <CODE>CurrCmd.BadFoldArg</CODE> exception
   with argument <CODE>id</CODE>. </EM></BLOCKQUOTE><PRE>
  BEGIN
    JunoError.Display(w.root.source,
      &quot;The current command\nhas no local variable\nnamed \&quot;&quot;
      &amp; Atom.ToText(id) &amp; &quot;\&quot;&quot;)
  END BadFoldArg;

PROCEDURE <A NAME="DoFold"><procedure>DoFold</procedure></A>(
    w: Window;
    ts: VBT.TimeStamp;
    hdr: JunoAST.PredHeader;
    foldKind := CurrCmd.FoldKind.Proc;
    toolType := ToolType.None) =
</PRE><BLOCKQUOTE><EM> Requires that the current module and current command are both
   up-to-date. </EM></BLOCKQUOTE><PRE>
  &lt;* LL.sup = VBT.mu *&gt;
  VAR decl: JunoAST.Decl; BEGIN
    TRY
      decl := CurrCmd.FoldByHeader(w.root.ccmd, hdr, foldKind);
      IF InstallDecl(w, decl, toolType) THEN
    	CurrCmd.ChangeAST(w.root.ccmd, JunoAST.SkipVal);
    	MakeCurrCmd(w, ts, skipify := FALSE)
      END
    EXCEPT
      CurrCmd.BadFoldArg (id) =&gt; BadFoldArg(w, id)
    END
  END DoFold;

PROCEDURE <A NAME="DoFoldAsAnim"><procedure>DoFoldAsAnim</procedure></A>(w: Window; ts: VBT.TimeStamp;
  hdr: JunoAST.PredHeader; sliderPts: JunoAST.IdList) =
</PRE><BLOCKQUOTE><EM> Requires that the current module and current command are both
   up-to-date. </EM></BLOCKQUOTE><PRE>
  &lt;* LL.sup = VBT.mu *&gt;
  VAR
    ccmd := w.root.ccmd;
    noError: BOOLEAN;
    procDecl, frameDecl, animDecl: JunoAST.ProcDecl;
    animCmd: JunoAST.Cmd;
  BEGIN
    TRY
      procDecl := CurrCmd.FoldAnim(ccmd, hdr, sliderPts);
      frameDecl := CurrCmd.FoldAnimFrame(ccmd, hdr, sliderPts);
      animDecl := CurrCmd.FoldAnimCreator(ccmd, hdr, frameDecl.header.name);
      animCmd := CurrCmd.FoldAnimCmd(ccmd, hdr.ins, animDecl.header.name);
      IF InstallDecl(w, procDecl, ToolType.Point) THEN
    	noError := InstallDecl(w, frameDecl, ToolType.None);
    	noError := noError AND InstallDecl(w, animDecl, ToolType.None);
    	&lt;* ASSERT noError *&gt;
    	CurrCmd.ChangeAST(ccmd, animCmd);
    	MakeCurrCmd(w, ts, skipify := TRUE)
      END
    EXCEPT
      CurrCmd.BadFoldArg (id) =&gt; BadFoldArg(w, id)
    END
  END DoFoldAsAnim;

PROCEDURE <A NAME="BindDecl"><procedure>BindDecl</procedure></A>(scp: JunoScope.T; nm: JunoAST.Id; decl: JunoAST.Decl) =
</PRE><BLOCKQUOTE><EM> Bind <CODE>nm</CODE> to a scope entity formed from the predicate or procedure
   declaration <CODE>decl</CODE> in <CODE>scp</CODE>, and then compile <CODE>decl</CODE> and install the
   resulting byte-code in the global code table. Requires that <CODE>nm</CODE> is not
   already bound in <CODE>scp</CODE>, and that no two formals in <CODE>decl</CODE> have the same
   name. </EM></BLOCKQUOTE><PRE>
  &lt;* FATAL JunoCompileErr.Error, JunoScope.NameClash *&gt;
  VAR codeEnt: JunoScope.Code; BEGIN
    TYPECASE decl OF &lt;* NOWARN *&gt;
      JunoAST.PredDecl (p) =&gt;
        VAR predEnt := JunoScope.NewPred(p, mod := NIL); BEGIN
          JunoCompile.PredDecl(p.header.name, predEnt, scp);
          codeEnt := predEnt
        END
    | JunoAST.ProcDecl (p) =&gt;
        VAR procEnt := JunoScope.NewProc(p, mod := NIL); BEGIN
          EVAL JunoCompile.ProcDecl(p.header.name, procEnt, scp);
          codeEnt := procEnt
        END
    END;
    JunoScope.Bind(scp, nm, codeEnt)
  END BindDecl;
</PRE> =========================== I/O Procedures ============================== 

<P><PRE>PROCEDURE <A NAME="WritePostScriptToFile"><procedure>WritePostScriptToFile</procedure></A>(w: Window; fname: TEXT; showPage := TRUE) =
&lt;* LL.sup &lt; w *&gt;
</PRE><BLOCKQUOTE><EM> Write PostScript for the current command associated with <CODE>w</CODE> to the file
   named <CODE>fname</CODE>. If there is an error, display an error message in <CODE>w</CODE>. </EM></BLOCKQUOTE><PRE>
  BEGIN
    TRY
      WritePostScriptToWr(w, FileWr.Open(fname), showPage)
    EXCEPT OSError.E, Wr.Failure =&gt;
      JunoError.Display(w, &quot;Error opening/writing file:\n\&quot;&quot;
        &amp; fname &amp; &quot;\&quot;&quot;)
    END
  END WritePostScriptToFile;

PROCEDURE <A NAME="PipePostScriptToCmd"><procedure>PipePostScriptToCmd</procedure></A>(
    w: Window;
    cmd: TEXT;
    READONLY args: ARRAY OF TEXT;
    showPage := TRUE) =
</PRE><BLOCKQUOTE><EM> Run the command <CODE>cmd</CODE> with arguments <CODE>args</CODE>, piping the PostScript code
   produced by running the current command of the window <CODE>fv</CODE> to the standard
   input of the new process. If <CODE>showPage</CODE> is <CODE>TRUE</CODE>, then a final <CODE>showpage</CODE>
   command is appended to the PostScript; this is usually necessary for
   printing, but not for previewing. </EM></BLOCKQUOTE><PRE>
  VAR hwSelf, hrChild: Pipe.T; p: Process.T; wr: Wr.T; BEGIN
    TRY
      Pipe.Open((*OUT*) hrChild, (*OUT*) hwSelf);
      p := Process.Create(cmd, args, stdin := hrChild);
      TRY hrChild.close() EXCEPT OSError.E =&gt; (*SKIP*) END;
      wr := NEW(FileWr.T).init(hwSelf);
      WritePostScriptToWr(w, wr, showPage);
      EVAL Process.Wait(p)
    EXCEPT OSError.E, Wr.Failure =&gt;
      VAR cmdLine := cmd; BEGIN
        FOR i := 0 TO LAST(args) DO cmdLine := cmdLine &amp; &quot; &quot; &amp; args[i] END;
        JunoError.Display(w, &quot;Error forking process:\n\&quot;&quot; &amp; cmdLine &amp; &quot;\&quot;&quot;)
      END
    END
  END PipePostScriptToCmd;

PROCEDURE <A NAME="WritePostScriptToWr"><procedure>WritePostScriptToWr</procedure></A>(w: Window; wr: Wr.T; showPage: BOOLEAN)
  RAISES { Wr.Failure } =
</PRE><BLOCKQUOTE><EM> Write the PostScript for <CODE>w.root.ccmd</CODE> to <CODE>wr</CODE>; send a final <CODE>showpage</CODE>
   command iff <CODE>showPage</CODE> is TRUE. Display any error messages on <CODE>w</CODE>. Close
   <CODE>wr</CODE> when done. </EM></BLOCKQUOTE><PRE>
  BEGIN
    psImpl.startToFile(wr);
    TRY
      psImpl.prologue();
      PSImpl.Reset(w.root.drawing, inExec := FALSE);
      TRY EVAL CurrCmd.Run(w.root.ccmd, skipify := FALSE) EXCEPT
        CurrCmd.CompileError (errorMsg) =&gt;
          JunoError.Display(w, errorMsg)
      | CurrCmd.RuntimeError (errorRec) =&gt;
          JunoError.Display(w, errorRec.errorMsg);
          JunoError.DisplayPS(wr, errorRec.errorMsg)
      END;
      psImpl.epilogue(showPage := showPage)
    FINALLY
      psImpl.endToFile();
      Wr.Close(wr)
    END
  END WritePostScriptToWr;

PROCEDURE <A NAME="GetFileName"><procedure>GetFileName</procedure></A>(fv: FormsVBT.T; browserName: TEXT): TEXT =
</PRE><BLOCKQUOTE><EM> Returns the name of the selected file in the FileBrowser named
   <CODE>browserName</CODE> in the form <CODE>w</CODE>. If this this file names a directory, the
   directory name is set back into the FileBrowser and <CODE>NIL</CODE> is returned. </EM></BLOCKQUOTE><PRE>
  &lt;* LL.sup = VBT.mu *&gt;
  VAR fname := FormsVBT.GetText(fv, browserName); BEGIN
    TRY
      IF FS.Status(fname).type = FS.DirectoryFileType THEN
    	FormsVBT.PutText(fv, browserName, fname); RETURN NIL
      END
    EXCEPT
      OSError.E =&gt; (* SKIP - file does not exist; it will be created *)
    END;
    RETURN fname
  END GetFileName;

PROCEDURE <A NAME="SaveCurrMod"><procedure>SaveCurrMod</procedure></A>(w: Window; ts: VBT.TimeStamp): BOOLEAN =
</PRE><BLOCKQUOTE><EM> If <CODE>w.fileStale</CODE>, then pop up a dialogue asking the user if the current
   file should be saved. If the user chooses <CODE>Cancel</CODE>, return FALSE
   immediately; if the user chooses <CODE>Discard</CODE>, then return TRUE immediately.
   If the user chooses <CODE>Save</CODE>, then attempt to save the current module under
   the name <CODE>w.fileName</CODE>; return TRUE iff the save was successful. </EM></BLOCKQUOTE><PRE>
  VAR stat: SyncStat; BEGIN
    IF w.fileStale THEN
      LOCK w.mu DO w.stat := SyncStat.NotDone END;
      FormsVBT.PutText(w, &quot;saveName&quot;, Pathname.Last(w.fileName));
      FormsVBT.PopUp(w, &quot;saveChangesWindow&quot;, time := ts);
      LOCK w.mu DO
        WHILE w.stat = SyncStat.NotDone DO
          Thread.Wait(w.mu, w.untilDone)
        END;
        stat := w.stat
      END;
      FormsVBT.PopDown(w, &quot;saveChangesWindow&quot;);
      IF stat = SyncStat.Cancel THEN
        RETURN FALSE
      ELSIF stat = SyncStat.Discard THEN
        TRY FS.DeleteFile(CheckpointName(w.fileName)) EXCEPT
          OSError.E =&gt; (*SKIP*)
        END;
        RETURN TRUE
      ELSIF stat = SyncStat.Save THEN
        RETURN PutFile(w, w.fileName, ts)
      END
    END;
    RETURN TRUE
  END SaveCurrMod;

PROCEDURE <A NAME="GetFile"><procedure>GetFile</procedure></A>(w: Window; name: TEXT; time: VBT.TimeStamp)
  RAISES {OSError.E, OpenFailure} =
</PRE><BLOCKQUOTE><EM> Opens the file named <CODE>name</CODE>, or creates it if the file does not exist.
   Then reads the contents of this file into <CODE>w</CODE>'s editor, and makes
   those new contents. Raises <CODE>OSError.E</CODE> if the file cannot be opened for
   reading or created. This procedure requires that <CODE>name # NIL</CODE> and that
   <CODE>name</CODE> does not name a directory. </EM></BLOCKQUOTE><PRE>
  &lt;* LL.sup = VBT.mu *&gt;
  VAR txt: TEXT; type: Atom.T; create := FALSE; BEGIN
    &lt;* ASSERT name # NIL *&gt;
    (* Read the text from the file *)
    TRY
      type := FS.Status(name).type;
      IF type # RegularFile.FileType THEN RAISE OpenFailure END
    EXCEPT OSError.E =&gt; create := TRUE
    END;
    IF create THEN
      (* file does not exist -- create a new empty file *)
      FS.OpenFile(name, truncate := FALSE).close();
      txt := &quot;&quot;
    ELSE
      VAR rd: FileRd.T; BEGIN
        rd := NEW(FileRd.T).init(FS.OpenFileReadonly(name));
        txt := Rd.GetText(rd, LAST(CARDINAL));
        Rd.Close(rd) (* this closes underlying File.T *)
      END
    END;

    (* Install the text and compile the current module *)
    FormsVBT.PutText(w, &quot;currFile&quot;, Pathname.Last(name));
    TextPort.SetText(w.root.editor, txt);
    w.root.eTrue := FALSE;
    VAR cmd: JunoAST.Cmd := NIL; ed := w.root.editor; nm: JunoAST.Id; BEGIN
      IF MakeCurrMod(w, time) THEN
        (* Pop the current command *)
        cmd := Editor.PopCurrCmd(ed, nm)
      END;
      IF cmd # NIL THEN
        UnbindDecl(CurrCmd.GetScope(w.root.ccmd), nm);
        ToolBox.Update(Split.Succ(w.toolbox, NIL), ed, w.root,
    	  w.builtInSize, anon := TRUE)
      ELSE
        (* Make or Pop failed; set curr cmd to &quot;SKIP&quot; *)
        cmd := JunoAST.SkipVal
      END;
      CurrCmd.ChangeAST(w.root.ccmd, cmd)
    END;
    w.root.astTrue := TRUE;
    w.root.sTrue := FALSE;
    w.root.dTrue := FALSE;
    MakeCurrCmd(w, time, w.root.skipify);
    SetModuleState(w, ModState.UpToDate)
  END GetFile;

PROCEDURE <A NAME="PutFile"><procedure>PutFile</procedure></A>(w: Window; name: TEXT; ts: VBT.TimeStamp): BOOLEAN =
</PRE><BLOCKQUOTE><EM> Write the contents of <CODE>w</CODE>'s editor window and a pushed version of the
   current command to the file named <CODE>name</CODE>. Before pushing, both the current
   module and command are compiled.
<P>
   Returns TRUE only in the event of success; in this case, the background of
   the name is set to light grey, and <CODE>w.fileStale</CODE> is set to FALSE. </EM></BLOCKQUOTE><PRE>
  &lt;* LL.sup = VBT.mu *&gt;
  VAR wr: Wr.T; proc: JunoAST.Decl := NIL; BEGIN
    IF NOT CompileModAndCmd(w, ts) THEN RETURN FALSE END;
    w.root.source.update(); (* unparse compiled current command *)
    IF CurrCmd.GetAST(w.root.ccmd) # JunoAST.SkipVal
       OR Editor.NextCmdNum(w.root.editor) &gt; 0 THEN
      proc := CurrCmd.FoldByName(w.root.ccmd,
        Editor.NextCmdName(w.root.editor),
        kind := CurrCmd.FoldKind.ProcNoArgs)
    END;
    TRY
      wr := FileWr.Open(name);
      IF wr # NIL THEN
	Wr.PutText(wr, TextPort.GetText(w.root.editor));
        IF proc # NIL THEN
          JunoUnparse.Block(wr, proc, tokens := LAST(INTEGER),
            prec := JunoConfig.realPrec);
          Wr.PutChar(wr, '\n')
        END;
	Wr.Close(wr);
      END
    EXCEPT
      OSError.E =&gt;
        JunoError.Display(w, &quot;Could not open file for writing:\n&quot; &amp; name);
        RETURN FALSE
    | Wr.Failure =&gt;
        JunoError.Display(w, &quot;Error writing file:\n&quot; &amp; name);
        RETURN FALSE
    END;
    SetModuleState(w, ModState.UpToDate);
    TextPort.SetModified(w.root.editor, FALSE);
    VAR te := NARROW(Filter.Child(w.root.source), TextEditVBT.T); BEGIN
      TextPort.SetModified(te.tp, FALSE)
    END;
    TRY FS.DeleteFile(CheckpointName(name)) EXCEPT OSError.E =&gt; (*SKIP*) END;
    RETURN TRUE
  END PutFile;
</PRE> ====================== Top-Level Window Procedures ====================== 

<P><PRE>CONST SolveButton = &quot;solve&quot;;

PROCEDURE <A NAME="NewForm"><procedure>NewForm</procedure></A>(w: Window; d: Drawing.T): Window =
</PRE><BLOCKQUOTE><EM> Initialize <CODE>w</CODE> by reading its description from the form <CODE>Juno.fv</CODE>. Attach
   procedures to the buttons in <CODE>w</CODE>'s interface, and initialize its toolbox. </EM></BLOCKQUOTE><PRE>
  &lt;* LL.sup &lt; VBT.mu *&gt;
  &lt;* FATAL Rsrc.NotFound *&gt;
  BEGIN
    TRY
      EVAL w.initFromRsrc(&quot;Juno.fv&quot;, JunoRsrc.Path, raw := TRUE);
      LOCK VBT.mu DO JunoConfig.SetFonts(w) END;
      FormsVBT.MakeDormant(w, SolveButton);
      FormsVBT.AttachProc(w, &quot;config&quot;,           DoMakePassive);
      FormsVBT.AttachProc(w, &quot;configOk&quot;,         DoConfigure);
      FormsVBT.AttachProc(w, &quot;configCancel&quot;,     DoMakeActive);
      FormsVBT.AttachProc(w, &quot;quit&quot;,           	 DoQuit);
      FormsVBT.AttachProc(w, &quot;load&quot;,           	 DoLoad);
</PRE><BLOCKQUOTE><EM> NOT YET IMPLEMENTED:
      FormsVBT.AttachProc(w, <CODE>savejava</CODE>,         DoSaveJava);
</EM></BLOCKQUOTE><PRE>
      FormsVBT.AttachProc(w, &quot;reload&quot;,         	 DoReload);
      FormsVBT.AttachProc(w, &quot;clearall&quot;,       	 DoClearAll);
      FormsVBT.AttachProc(w, &quot;loadBrowser&quot;,    	 DoLoadFile);
      FormsVBT.AttachProc(w, &quot;loadCancel&quot;,     	 DoMakeActive);
      FormsVBT.AttachProc(w, &quot;save&quot;,           	 DoSave);
      FormsVBT.AttachProc(w, &quot;saveAs&quot;,         	 DoMakePassive);
      FormsVBT.AttachProc(w, &quot;saveBrowser&quot;,    	 DoSaveAs);
      FormsVBT.AttachProc(w, &quot;saveAsCancel&quot;,   	 DoMakeActive);
      FormsVBT.AttachProc(w, &quot;saveAsOverwrite&quot;,	 DoSaveAsOverwrite);
      FormsVBT.AttachProc(w, &quot;overwriteCancel&quot;,	 DoMakeActive);
      FormsVBT.AttachProc(w, &quot;moduleBrowser&quot;,  	 DoModule);
      FormsVBT.AttachProc(w, &quot;print&quot;,          	 DoPrint);
      FormsVBT.AttachProc(w, &quot;preview&quot;,        	 DoPreview);
      FormsVBT.AttachProc(w, &quot;savePS&quot;,         	 DoSavePS);
      FormsVBT.AttachProc(w, &quot;savePSas&quot;,       	 DoMakePassive);
      FormsVBT.AttachProc(w, &quot;savePSBrowser&quot;,  	 DoSavePSAs);
      FormsVBT.AttachProc(w, &quot;savePSCancel&quot;,   	 DoMakeActive);
      FormsVBT.AttachProc(w, &quot;labels&quot;,         	 DoLabels);
      FormsVBT.AttachProc(w, &quot;autoUpdate&quot;,     	 DoAutoUpdate);
      FormsVBT.AttachProc(w, &quot;run&quot;,            	 DoRun);
      FormsVBT.AttachProc(w, SolveButton,      	 DoSolve);
      FormsVBT.AttachProc(w, &quot;stop&quot;,           	 DoStop);
      FormsVBT.AttachProc(w, &quot;fold&quot;,           	 DoFoldDialog);
      FormsVBT.AttachProc(w, &quot;foldOk&quot;,         	 DoFoldWork);
      FormsVBT.AttachProc(w, &quot;declName&quot;,       	 DoFoldWork);
      FormsVBT.AttachProc(w, &quot;foldCancel&quot;,     	 DoMakeActive);
      FormsVBT.AttachProc(w, &quot;foldAsAnim&quot;,       DoFoldAsAnimDialog);
      FormsVBT.AttachProc(w, &quot;foldAsAnimOk&quot;,     DoFoldAsAnimWork);
      FormsVBT.AttachProc(w, &quot;animName&quot;,         DoFoldAsAnimWork);
      FormsVBT.AttachProc(w, &quot;sliderPts&quot;,        DoFoldAsAnimWork);
      FormsVBT.AttachProc(w, &quot;foldAsAnimCancel&quot;, DoMakeActive);
      FormsVBT.AttachProc(w, &quot;saveChanges&quot;,    	 DoSaveChanges);
      FormsVBT.AttachProc(w, &quot;discardChanges&quot;, 	 DoDiscardChanges);
      FormsVBT.AttachProc(w, &quot;cancelChanges&quot;,  	 DoCancelChanges);
      FormsVBT.AttachProc(w, &quot;pushCurrCmd&quot;,    	 DoPushCurrCmd);
      FormsVBT.AttachProc(w, &quot;popCurrCmd&quot;,     	 DoPopCurrCmd)
    EXCEPT
      FormsVBT.Error (msg) =&gt;
        Wr.PutText(Stdio.stderr, &quot;FormsVBT error: &quot; &amp; msg &amp; &quot;\n&quot;);
        Wr.Flush(Stdio.stderr);
        &lt;* ASSERT FALSE *&gt;
    END;
    RETURN InitToolbox(w, d)
  END NewForm;

VAR (* READONLY *)
  LoadInitialModule := VBT.GetMiscCodeType(&quot;LoadInitialModule&quot;);

PROCEDURE <A NAME="FindSavedState"><procedure>FindSavedState</procedure></A>(VAR (*OUT*) st: SaveState.T; nm: Pathname.T):
  BOOLEAN =
  VAR rd: Rd.T; BEGIN
    TRY
      rd := FileRd.Open(CheckpointName(nm));
      RETURN SaveState.Restore(st, rd)
    EXCEPT
      OSError.E =&gt; RETURN FALSE
    END
  END FindSavedState;

PROCEDURE <A NAME="Misc"><procedure>Misc</procedure></A>(w: Window; READONLY cd: VBT.MiscRec) =
</PRE><BLOCKQUOTE><EM> If <CODE>cd.type # LoadInitialModule</CODE>, then forward this event to the parent's
   <CODE>misc</CODE> method. Otherwise, call <CODE>GetFile</CODE> to load the current module; if an
   error occurs opening this file, report it to the standard error output and
   crash. </EM></BLOCKQUOTE><PRE>
  &lt;* LL.sup = VBT.mu *&gt;
  VAR st: SaveState.T; BEGIN
    IF cd.type # LoadInitialModule THEN
      FormsVBT.T.misc(w, cd)
    ELSE
      IF FindSavedState(st, w.fileName) THEN
        (* recover from checkpoint *)
        w.fileName := st.file;
        w.fileStale := TRUE;
        w.checkpointStale := FALSE;
        FormsVBT.PutText(w, &quot;currFile&quot;, Pathname.Last(st.file));
        TextPort.SetText(w.root.editor, st.editor);
        Source.SetText(w.root.source, st.source);
        w.root.eTrue := FALSE;
        w.root.astTrue := FALSE;
        w.root.sTrue := TRUE;
        w.root.dTrue := TRUE;
        SetModuleState(w, ModState.Stale);
        DoRun(w, NIL, NIL, cd.time);
        JunoError.Display(w, &quot;Restoring from checkpoint.\n&quot; &amp;
          &quot;To discard checkpoint,\nopen file again.&quot;);
      ELSE
        TRY GetFile(w, w.fileName, cd.time) EXCEPT
          OSError.E =&gt;
	    Wr.PutText(Stdio.stderr, &quot;Fatal error: cannot open or create \&quot;&quot;);
	    Wr.PutText(Stdio.stderr, w.fileName);
            Wr.PutText(Stdio.stderr, &quot;\&quot; in the current directory\n&quot;);
            Wr.Flush(Stdio.stderr);
	    Process.Exit(1)
        | OpenFailure =&gt;
	    Wr.PutText(Stdio.stderr, &quot;Fatal error: \&quot;&quot;);
	    Wr.PutText(Stdio.stderr, w.fileName);
            Wr.PutText(Stdio.stderr, &quot;\&quot; is not a file\n&quot;);
            Wr.Flush(Stdio.stderr);
            Process.Exit(1)
        END
      END;
      LOCK w.mu DO w.stat := SyncStat.Save END; (* anything except &quot;NotDone&quot; *)
      Thread.Signal(w.untilDone)
    END
  END Misc;

PROCEDURE <A NAME="InitToolbox"><procedure>InitToolbox</procedure></A>(w: Window; d: Drawing.T): Window =
</PRE><BLOCKQUOTE><EM> Initialize <CODE>w.toolbox</CODE> to contain buttons for the built-in tools, and set
   <CODE>w.builtInSize</CODE>. Return <CODE>w</CODE>. </EM></BLOCKQUOTE><PRE>
  VAR tb1 := HVSplit.New(Axis.T.Ver); BEGIN
    Split.AddChildArray(tb1, ARRAY OF VBT.T{
      NEW(ToolBox.Button).init(w.root, &quot;Create&quot;, Drawing.NewCreateTool()),
      NEW(ToolBox.Button).init(w.root, &quot;Freeze&quot;, Drawing.NewFreezeTool()),
      NEW(ToolBox.Button).init(w.root, &quot;Drag&quot;,   Drag.NewTool()),
      NEW(ToolBox.Button).init(w.root, &quot;Adjust&quot;, Drawing.NewAdjustTool()),
      NEW(ToolBox.Button).init(w.root, &quot;Grid On&quot;,Drawing.NewGridTools(tb1, d)),
      NEW(ToolBox.Button).init(w.root, &quot;Rel&quot;,    Drawing.NewRelTool()),
      NEW(ToolBox.Button).init(w.root, &quot;Rel1&quot;,   Drawing.NewRel1Tool()),
      NEW(ToolBox.Button).init(w.root, &quot;Hor&quot;,
        Drawing.NewPredTool(JunoASTUtils.QIdFromId(Drawing.HorToolSym),  2)),
      NEW(ToolBox.Button).init(w.root, &quot;Ver&quot;,
        Drawing.NewPredTool(JunoASTUtils.QIdFromId(Drawing.VerToolSym),  2)),
      NEW(ToolBox.Button).init(w.root, &quot;Cong&quot;,
        Drawing.NewPredTool(JunoASTUtils.QIdFromId(Drawing.CongToolSym), 4)),
      NEW(ToolBox.Button).init(w.root, &quot;Para&quot;,
        Drawing.NewPredTool(JunoASTUtils.QIdFromId(Drawing.ParaToolSym), 4))});
    w.builtInSize := Split.NumChildren(tb1);
    w.toolbox := PackSplit.New(Axis.T.Ver,
      op := PaintOp.FromRGB(0.8, 0.8, 0.8));
    Split.Insert(w.toolbox, pred := NIL, new := tb1);
    RETURN w
  END InitToolbox;
</PRE> ============================= UI Callbacks ============================== 

<P><PRE>PROCEDURE <A NAME="DoMakePassive"><procedure>DoMakePassive</procedure></A>(fv: FormsVBT.T; &lt;*UNUSED*&gt; event: TEXT;
    &lt;*UNUSED*&gt; data: REFANY; &lt;*UNUSED*&gt; ts: VBT.TimeStamp) =
  BEGIN
    FVFilter.MakePassive(fv, &quot;background&quot;)
  END DoMakePassive;

PROCEDURE <A NAME="DoMakeActive"><procedure>DoMakeActive</procedure></A>(fv: FormsVBT.T; &lt;*UNUSED*&gt; event: TEXT;
    &lt;*UNUSED*&gt; data: REFANY; &lt;*UNUSED*&gt; ts: VBT.TimeStamp) =
  BEGIN
    FVFilter.MakeActive(fv, &quot;background&quot;)
  END DoMakeActive;

TYPE
  MenuClosure = Thread.Closure BRANDED &quot;Juno.MenuClosure&quot; OBJECT
    fv: FormsVBT.T;
    ts: VBT.TimeStamp
  END;
  MenuRec = BRANDED &quot;Juno.MenuRec&quot; OBJECT
    fv: FormsVBT.T;
    ts: VBT.TimeStamp
  END;
</PRE> <CODE>Configure</CODE> ------------------------------------------------------------- 

<P><PRE>PROCEDURE <A NAME="ParseConfigWindow"><procedure>ParseConfigWindow</procedure></A>(fv: FormsVBT.T; nm: TEXT)
  RAISES {JunoConfig.Error}=
  &lt;* LL.sup = VBT.mu *&gt;
  VAR rd := TextRd.New(FormsVBT.GetText(fv, nm)); BEGIN
    JunoConfig.ParseConfigFile(rd)
  END ParseConfigWindow;

PROCEDURE <A NAME="DoConfigure"><procedure>DoConfigure</procedure></A>(fv: FormsVBT.T; &lt;*UNUSED*&gt; event: TEXT;
    &lt;*UNUSED*&gt; data: REFANY; &lt;*UNUSED*&gt; ts: VBT.TimeStamp) =
</PRE><BLOCKQUOTE><EM> Callback for <CODE>Ok</CODE> menu button in <CODE>Configure...</CODE> dialogue. </EM></BLOCKQUOTE><PRE>
  &lt;* LL.sup = VBT.mu *&gt;
  BEGIN
    TRY
      (* parse new configuration settings *)
      ParseConfigWindow(fv, &quot;configDefault&quot;);
      ParseConfigWindow(fv, &quot;configOverrides&quot;);

      (* change the fonts of the top-level window *)
      JunoConfig.SetFonts(fv);

      (* mark the top-level window for redisplay and set its origin *)
      VAR
        w: Window := fv;
        ch: Drawing.Child := Filter.Child(w.root.currView);
      BEGIN
        VBT.Mark(w);
        ch.setOrigin(JunoConfig.origin)
      END;

      (* change the fonts of any installed module view (help) windows *)
      VAR curr: View.EditorList := w.root.modules; BEGIN
        WHILE curr # NIL DO
          IF curr.form # NIL THEN
            VAR helpWindow := NARROW(curr.form, Toolbox).helpWindow; BEGIN
              IF helpWindow # NIL THEN
                JunoConfig.SetFonts(helpWindow);
                VBT.Mark(helpWindow)
              END
            END
          END;
          curr := curr.next
        END
      END;

      (* clean up *)
      FormsVBT.PopDown(fv, &quot;configWindow&quot;);
      FVFilter.MakeActive(fv, &quot;background&quot;)
    EXCEPT
      JunoConfig.Error (msg) =&gt;
        JunoError.Display(w, &quot;Error parsing configuration file:\n&quot; &amp; msg)
    END
  END DoConfigure;
</PRE> <CODE>Quit</CODE> ------------------------------------------------------------------ 

<P><PRE>PROCEDURE <A NAME="DoQuit"><procedure>DoQuit</procedure></A>(fv: FormsVBT.T; &lt;*UNUSED*&gt; event: TEXT;
    &lt;*UNUSED*&gt; data: REFANY; ts: VBT.TimeStamp) =
</PRE><BLOCKQUOTE><EM> Callback for <CODE>Quit</CODE> menu button. </EM></BLOCKQUOTE><PRE>
  &lt;* LL.sup = VBT.mu *&gt;
  BEGIN
    FVFilter.MakePassive(fv, &quot;background&quot;);
    EVAL Thread.Fork(NEW(MenuClosure, fv := fv, ts := ts,
      apply := DoQuitWork))
  END DoQuit;

PROCEDURE <A NAME="DoQuitWork"><procedure>DoQuitWork</procedure></A>(cl: MenuClosure): REFANY =
  BEGIN
    IF SaveCurrMod(cl.fv, cl.ts) THEN
      VAR w: Window := cl.fv; BEGIN
        Trestle.Delete(w);
        IF zeusOption THEN
          Trestle.Delete(animObj.w)
        END
      END
    ELSE
      FVFilter.MakeActive(cl.fv, &quot;background&quot;)
    END;
    RETURN NIL
  END DoQuitWork;

PROCEDURE <A NAME="DoLoad"><procedure>DoLoad</procedure></A>(fv: FormsVBT.T; &lt;*UNUSED*&gt; event: TEXT;
  &lt;*UNUSED*&gt; data: REFANY; ts: VBT.TimeStamp) =
</PRE><BLOCKQUOTE><EM> Callback for <CODE>New/Open...</CODE> menu button. </EM></BLOCKQUOTE><PRE>
  &lt;* LL.sup = VBT.mu *&gt;
  BEGIN
    FVFilter.MakePassive(fv, &quot;background&quot;);
    EVAL Thread.Fork(NEW(MenuClosure, fv := fv, ts := ts,
      apply := DoLoadWork))
  END DoLoad;
</PRE> <CODE>Open/New...</CODE> ----------------------------------------------------------- 

<P><PRE>PROCEDURE <A NAME="DoLoadWork"><procedure>DoLoadWork</procedure></A>(cl: MenuClosure): REFANY =
</PRE><BLOCKQUOTE><EM> First try to save the current module (if necessary). If that succeeds,
   pop-up the load file dialogue, which will be processed by the <CODE>DoLoadFile</CODE>
   callback. </EM></BLOCKQUOTE><PRE>
  BEGIN
    IF SaveCurrMod(cl.fv, cl.ts)
      THEN FormsVBT.PopUp(cl.fv, &quot;loadWindow&quot;, time := cl.ts)
      ELSE FVFilter.MakeActive(cl.fv, &quot;background&quot;)
    END;
    RETURN NIL
  END DoLoadWork;

PROCEDURE <A NAME="DoReload"><procedure>DoReload</procedure></A>(fv: FormsVBT.T; &lt;*UNUSED*&gt; event: TEXT;
  &lt;*UNUSED*&gt; data: REFANY; ts: VBT.TimeStamp) =
</PRE><BLOCKQUOTE><EM> Callback for <CODE>Reload</CODE> menu button. </EM></BLOCKQUOTE><PRE>
  &lt;* LL.sup = VBT.mu *&gt;
  VAR win:=NARROW(fv,Window); BEGIN
    FVFilter.MakePassive(fv, &quot;background&quot;);
    EVAL DoLoadFileWork(NEW(FileIORec,
      fv := fv, ts := ts, fname := win.fileName));
  END DoReload;
</PRE> NOT YET IMPLEMENTED:
<P>
&lt;* LL.sup = VBT.mu *&gt;
PROCEDURE DoSaveJava(fv: FormsVBT.T; &lt;*UNUSED*&gt; event: TEXT;
    &lt;*UNUSED*&gt; data: REFANY; &lt;*UNUSED*&gt; ts: VBT.TimeStamp) =
(* Callback for <CODE>SaveJava</CODE> menu button. 
  <PRE>VAR
    win := NARROW(fv,Window);
    fn := win.fileName;
    java_path := Pathname.Prefix(fn);
    java_package := Pathname.LastBase(fn);
    java_package_path := Pathname.Join(pn1, pn2, NIL);
  BEGIN
    FVFilter.MakePassive(fv, &quot;background&quot;);
    JunoError.Display(fv, &quot;DoSaveJava: not implemented yet&quot;);
    FVFilter.MakeActive(fv, &quot;background&quot;);
  END DoSaveJava;
*)

&lt;* LL.sup = VBT.mu *&gt;
PROCEDURE <A NAME="DoClearAll"><procedure>DoClearAll</procedure></A>(fv: FormsVBT.T; &lt;*UNUSED*&gt; event: TEXT;
    &lt;*UNUSED*&gt; data: REFANY; time: VBT.TimeStamp) =
  VAR name := &quot;Untitled.juno&quot;; w := NARROW(fv,Window); BEGIN
    &lt;* ASSERT name # NIL *&gt;
    (* Read the text from the file *)
    FormsVBT.PutText(w, &quot;currFile&quot;, Pathname.Last(name));
    TextPort.SetText(w.root.editor, &quot;&quot;);
    w.root.eTrue := FALSE;
    EVAL MakeCurrMod(w, time);
    CurrCmd.ChangeAST(w.root.ccmd, JunoAST.SkipVal);
    w.root.astTrue := TRUE;
    w.root.sTrue := FALSE;
    w.root.dTrue := FALSE;
    MakeCurrCmd(w, time, w.root.skipify);
    SetModuleState(w, ModState.UpToDate)
  END DoClearAll;

TYPE
  FileIOClosure = MenuClosure BRANDED &quot;Juno.FileIOClosure&quot; OBJECT
    fname: TEXT
  END;
  FileIORec = MenuRec OBJECT
    fname: TEXT
  END;

PROCEDURE <A NAME="DoLoadFile"><procedure>DoLoadFile</procedure></A>(fv: FormsVBT.T; &lt;*UNUSED*&gt; event: TEXT;
  &lt;*UNUSED*&gt; data: REFANY; ts: VBT.TimeStamp) =
</PRE><BLOCKQUOTE><EM> Load the file requested by the user in the <CODE>loadBrowser</CODE> field of <CODE>fv</CODE>.
   Display the name of the new file with a light gray background in the
   <CODE>currFile</CODE> field of <CODE>fv</CODE>. </EM></BLOCKQUOTE><PRE>
  &lt;* LL.sup = VBT.mu *&gt;
  VAR fname: TEXT; BEGIN
    fname := GetFileName(fv, &quot;loadBrowser&quot;);
    IF fname = NIL THEN RETURN END;
    FormsVBT.PopDown(fv, &quot;loadWindow&quot;);
    FVFilter.MakePassive(fv, &quot;background&quot;, FVFilter.CursorKind.Working);
</PRE><BLOCKQUOTE><EM><P>
    EVAL Thread.Fork(NEW(FileIOClosure, fv := fv, ts := ts,
      fname := fname, apply := DoLoadFileWork))
</EM></BLOCKQUOTE><PRE>
    EVAL DoLoadFileWork(NEW(FileIORec, fv := fv, ts := ts, fname := fname))
  END DoLoadFile;

PROCEDURE <A NAME="DoLoadFileWork"><procedure>DoLoadFileWork</procedure></A>(cl: FileIORec): REFANY =
  &lt;* FATAL OpenFailure *&gt;
  VAR w: Window := cl.fv; BEGIN
    TRY
      TRY
    	GetFile(w, cl.fname, cl.ts);
    	w.fileName := cl.fname;
    	w.checkpointStale := TRUE
      EXCEPT
    	OSError.E =&gt;
    	  JunoError.Display(w, &quot;Could not open file for reading:\n\&quot;&quot;
    	    &amp; cl.fname &amp; &quot;\&quot;&quot;)
      END
    FINALLY
      FVFilter.MakeActive(cl.fv, &quot;background&quot;)
    END;
    RETURN NIL
  END DoLoadFileWork;
</PRE> <CODE>Save...</CODE> --------------------------------------------------------------- 

<P><PRE>PROCEDURE <A NAME="DoSave"><procedure>DoSave</procedure></A>(fv: FormsVBT.T; &lt;*UNUSED*&gt; event: TEXT;
  &lt;*UNUSED*&gt; data: REFANY; ts: VBT.TimeStamp) =
</PRE><BLOCKQUOTE><EM> Save the contents of the editor to <CODE>fv.filename</CODE>. Change the background
   color of the <CODE>currFile</CODE> field to light gray, and set the editor's
   <CODE>modified</CODE> attribute to false. </EM></BLOCKQUOTE><PRE>
  &lt;* LL.sup = VBT.mu *&gt;
  BEGIN
    FVFilter.MakePassive(fv, &quot;background&quot;, FVFilter.CursorKind.Working);
</PRE><BLOCKQUOTE><EM><P>
    EVAL Thread.Fork(NEW(MenuClosure, fv := fv, ts := ts, apply := DoSaveWork))
</EM></BLOCKQUOTE><PRE>
    EVAL DoSaveWork(NEW(MenuRec, fv := fv, ts := ts))
  END DoSave;

PROCEDURE <A NAME="DoSaveWork"><procedure>DoSaveWork</procedure></A>(cl: MenuRec): REFANY =
</PRE><BLOCKQUOTE><EM> Save the current file; make the background active when done. </EM></BLOCKQUOTE><PRE>
  VAR w: Window := cl.fv; BEGIN
    EVAL PutFile(w, w.fileName, cl.ts);
    FVFilter.MakeActive(cl.fv, &quot;background&quot;);
    RETURN NIL
  END DoSaveWork;
</PRE> <CODE>Save As...</CODE> ------------------------------------------------------------ 

<P><PRE>PROCEDURE <A NAME="DoSaveAs"><procedure>DoSaveAs</procedure></A>(fv: FormsVBT.T; &lt;*UNUSED*&gt; event: TEXT;
  &lt;*UNUSED*&gt; data: REFANY; ts: VBT.TimeStamp) =
</PRE><BLOCKQUOTE><EM> Save the contents of the editor to the filename specified by the user in
   the <CODE>saveBrowser</CODE> component of <CODE>fv</CODE>. Set <CODE>fv.filename</CODE> and the <CODE>currFile</CODE>
   component to contain this new name, change the color of the <CODE>currFile</CODE>
   field to light gray, and set the editor's <CODE>modified</CODE> attribute to false. </EM></BLOCKQUOTE><PRE>
  &lt;* LL.sup = VBT.mu *&gt;
  VAR fname: TEXT; BEGIN
    FormsVBT.PopDown(fv, &quot;saveAsWindow&quot;);
    fname := GetFileName(fv, &quot;saveBrowser&quot;);
    IF fname = NIL THEN RETURN END;
    FVFilter.MakePassive(fv, &quot;background&quot;, FVFilter.CursorKind.Working);
    TRY
      EVAL FS.Status(fname);
      (* file already exists; check that the user wants to overwrite it *)
      NARROW(fv, Window).saveAsFName := fname;
      FormsVBT.PutText(fv, &quot;overwriteFileName&quot;, Pathname.Last(fname));
      FormsVBT.PopUp(fv, &quot;saveAsConfirmWindow&quot;)
    EXCEPT OSError.E =&gt;
      (* file does not exist -- so save it directly *)
</PRE><BLOCKQUOTE><EM><P>
      EVAL Thread.Fork(NEW(FileIOClosure, fv := fv, ts := ts,
	fname := fname, apply := DoSaveAsWork))
</EM></BLOCKQUOTE><PRE>
      EVAL DoSaveAsWork(NEW(FileIORec, fv := fv, ts := ts,
	fname := fname))
    END;
  END DoSaveAs;

PROCEDURE <A NAME="DoSaveAsOverwrite"><procedure>DoSaveAsOverwrite</procedure></A>(fv: FormsVBT.T; &lt;*UNUSED*&gt; event: TEXT;
  &lt;*UNUSED*&gt; data: REFANY; ts: VBT.TimeStamp) =
</PRE><BLOCKQUOTE><EM> This callback procedure is invoked after the user has chosen <CODE>Save As...</CODE>,
   specified a file that already exists, and confirmed that he/she would
   indeed like to overwrite the file. </EM></BLOCKQUOTE><PRE>
  VAR fname := NARROW(fv, Window).saveAsFName; BEGIN
</PRE><BLOCKQUOTE><EM><P>
      EVAL Thread.Fork(NEW(FileIOClosure, fv := fv, ts := ts,
	fname := fname, apply := DoSaveAsWork))
</EM></BLOCKQUOTE><PRE>
      EVAL DoSaveAsWork(NEW(FileIORec, fv := fv, ts := ts,
	fname := fname))
  END DoSaveAsOverwrite;

PROCEDURE <A NAME="DoSaveAsWork"><procedure>DoSaveAsWork</procedure></A>(cl: FileIORec): REFANY =
  VAR w: Window := cl.fv; BEGIN
    TRY
      IF PutFile(w, cl.fname, cl.ts) THEN
    	FormsVBT.PutText(cl.fv, &quot;currFile&quot;, Pathname.Last(cl.fname));
    	TRY FS.DeleteFile(CheckpointName(w.fileName)) EXCEPT
          OSError.E =&gt; (*SKIP*)
        END;
    	w.fileName := cl.fname;
    	w.checkpointStale := TRUE
      END
    FINALLY
      FVFilter.MakeActive(cl.fv, &quot;background&quot;)
    END;
    RETURN NIL
  END DoSaveAsWork;
</PRE> <CODE>Open Module...</CODE> -------------------------------------------------------- 

<P><PRE>PROCEDURE <A NAME="DoModule"><procedure>DoModule</procedure></A>(fv: FormsVBT.T; &lt;*UNUSED*&gt; event: TEXT;
  &lt;*UNUSED*&gt; data: REFANY; ts: VBT.TimeStamp) =
  &lt;* LL.sup = VBT.mu *&gt;
  BEGIN
    FVFilter.MakePassive(fv, &quot;background&quot;, FVFilter.CursorKind.Working);
</PRE><BLOCKQUOTE><EM><P>
    EVAL Thread.Fork(NEW(MenuClosure, fv := fv, ts := ts,
      apply := DoModuleWork))
</EM></BLOCKQUOTE><PRE>
    EVAL DoModuleWork(NEW(MenuRec, fv := fv, ts := ts))
  END DoModule;

PROCEDURE <A NAME="DoModuleWork"><procedure>DoModuleWork</procedure></A>(cl: MenuRec): REFANY =
  VAR w: Window := cl.fv; BEGIN
    TRY
      OpenModule(w, FormsVBT.GetText(cl.fv, &quot;moduleBrowser&quot;),
    	hasPrivate := TRUE)
    FINALLY
      FVFilter.MakeActive(cl.fv, &quot;background&quot;)
    END;
    RETURN NIL
  END DoModuleWork;
</PRE> <CODE>Print</CODE> ----------------------------------------------------------------- 

<P><PRE>PROCEDURE <A NAME="DoPrint"><procedure>DoPrint</procedure></A>(fv: FormsVBT.T; &lt;*UNUSED*&gt; event: TEXT;
    &lt;*UNUSED*&gt; data: REFANY; ts: VBT.TimeStamp) =
  &lt;* LL.sup = VBT.mu *&gt;
  BEGIN
    FVFilter.MakePassive(fv, &quot;background&quot;, FVFilter.CursorKind.Working);
    EVAL Thread.Fork(NEW(MenuClosure, fv := fv, ts := ts,
      apply := DoPrintWork))
</PRE><BLOCKQUOTE><EM><P>
    EVAL DoPrintWork(NEW(MenuRec, fv := fv, ts := ts))
</EM></BLOCKQUOTE><PRE>
  END DoPrint;

PROCEDURE <A NAME="DoPrintWork"><procedure>DoPrintWork</procedure></A>(cl: MenuClosure): REFANY =
  VAR w: Window := cl.fv; BEGIN
    TRY
      TRY
      	VAR cmd: TEXT; args: REF ARRAY OF TEXT; BEGIN
      	  JunoConfig.ParseCmd(JunoConfig.printCmd, (*OUT*) cmd, (*OUT*) args,
            titleVal := Pathname.Last(Pathname.ReplaceExt(w.fileName, &quot;ps&quot;)));
      	  PipePostScriptToCmd(cl.fv, cmd, args^)
      	END
      EXCEPT JunoConfig.Error (msg) =&gt;
        JunoError.Display(w, &quot;Error in external print command:\n&quot; &amp; msg)
      END
    FINALLY
      FVFilter.MakeActive(cl.fv, &quot;background&quot;)
    END;
    RETURN NIL
  END DoPrintWork;
</PRE> <CODE>Preview</CODE> --------------------------------------------------------------- 

<P><PRE>PROCEDURE <A NAME="DoPreview"><procedure>DoPreview</procedure></A>(fv: FormsVBT.T; &lt;*UNUSED*&gt; event: TEXT;
    &lt;*UNUSED*&gt; data: REFANY; ts: VBT.TimeStamp) =
  &lt;* LL.sup = VBT.mu *&gt;
  BEGIN
    FVFilter.MakePassive(fv, &quot;background&quot;, FVFilter.CursorKind.Working);
    EVAL Thread.Fork(NEW(MenuClosure, fv := fv, ts := ts,
      apply := DoPreviewWork))
</PRE><BLOCKQUOTE><EM><P>
    EVAL DoPreviewWork(NEW(MenuRec, fv := fv, ts := ts))
</EM></BLOCKQUOTE><PRE>
  END DoPreview;

VAR previewNum := 1;
</PRE><BLOCKQUOTE><EM> counter of the number of the next preview document </EM></BLOCKQUOTE><PRE>

TYPE
  PreviewClosure = Thread.Closure OBJECT
    fileName: TEXT;
    w: Window;
  OVERRIDES
    apply := ForkPreviewer
  END;

PROCEDURE <A NAME="DoPreviewWork"><procedure>DoPreviewWork</procedure></A>(cl: MenuClosure): REFANY =
</PRE><BLOCKQUOTE><EM> Write the PostScript output to a temporary file, then fork a thread to run
   the PostScript previewer named by <CODE>JunoConfig.previewCmd</CODE> to view that
   file, deleting the file when the forked preview process exits. </EM></BLOCKQUOTE><PRE>
  VAR nm := &quot;/tmp/juno-&quot; &amp; Fmt.Int(Process.GetMyID()) &amp; &quot;-&quot;; BEGIN
    nm := nm &amp; Fmt.Int(previewNum) &amp; &quot;.ps&quot;; INC(previewNum);
    WritePostScriptToFile(cl.fv, nm, showPage := FALSE);
    EVAL Thread.Fork(NEW(PreviewClosure, fileName := nm, w := cl.fv));
    FVFilter.MakeActive(cl.fv, &quot;background&quot;);
    RETURN NIL
  END DoPreviewWork;

PROCEDURE <A NAME="ForkPreviewer"><procedure>ForkPreviewer</procedure></A>(self: PreviewClosure): REFANY =
</PRE><BLOCKQUOTE><EM> Fork a PostScript preview process running on the file <CODE>self.fileName</CODE>, wait
   for the process to exit, and then delete the temporary file. </EM></BLOCKQUOTE><PRE>
  BEGIN
    TRY
      TRY
        (* fork a PostScript preview process *)
        VAR cmd: TEXT; args: REF ARRAY OF TEXT; display := disp; BEGIN
          IF display = NIL THEN
            display := Env.Get(&quot;DISPLAY&quot;);
            IF display = NIL THEN display := &quot;:0.0&quot; END
          END;
          TRY
            JunoConfig.ParseCmd(JunoConfig.previewCmd, (*OUT*)cmd, (*OUT*)args,
              titleVal := Pathname.Last(Pathname.ReplaceExt(w.fileName, &quot;ps&quot;)),
              displayVal := display, filenameVal := self.fileName);
            EVAL Process.Wait(Process.Create(cmd, args^))
          EXCEPT JunoConfig.Error (msg) =&gt;
            JunoError.Display(self.w,
              &quot;Error in external preview command:\n&quot; &amp; msg)
          END
        END
      FINALLY
        (* delete the temporary file in any event *)
        FS.DeleteFile(self.fileName)
      END
    EXCEPT
      OSError.E =&gt; (* SKIP *)
    END;
    RETURN NIL
  END ForkPreviewer;
</PRE> <CODE>Save PostScript As...</CODE> ------------------------------------------------- 

<P><PRE>PROCEDURE <A NAME="DoSavePS"><procedure>DoSavePS</procedure></A>(fv: FormsVBT.T; &lt;*UNUSED*&gt; event: TEXT;
    &lt;*UNUSED*&gt; data: REFANY; &lt;*UNUSED*&gt; ts: VBT.TimeStamp) =
  &lt;* LL.sup = VBT.mu *&gt;
  VAR w: Window := fv; BEGIN
    FVFilter.MakePassive(fv, &quot;background&quot;, FVFilter.CursorKind.Working);
    WritePostScriptToFile(w, Pathname.ReplaceExt(w.fileName, &quot;ps&quot;));
    FVFilter.MakeActive(fv, &quot;background&quot;)
  END DoSavePS;

PROCEDURE <A NAME="DoSavePSAs"><procedure>DoSavePSAs</procedure></A>(fv: FormsVBT.T; &lt;*UNUSED*&gt; event: TEXT;
    &lt;*UNUSED*&gt; data: REFANY; ts: VBT.TimeStamp) =
  &lt;* LL.sup = VBT.mu *&gt;
  VAR fname: TEXT; BEGIN
    fname := GetFileName(fv, &quot;savePSBrowser&quot;);
    IF fname = NIL THEN RETURN END;
    FormsVBT.PopDown(fv, &quot;savePSWindow&quot;);
    FVFilter.MakePassive(fv, &quot;background&quot;, FVFilter.CursorKind.Working);
    EVAL Thread.Fork(NEW(FileIOClosure, fv := fv, ts := ts,
      fname := fname, apply := DoSavePSAsWork))
</PRE><BLOCKQUOTE><EM><P>
    EVAL DoSavePSAsWork(NEW(FileIORec, fv := fv, ts := ts,
      fname := fname))
</EM></BLOCKQUOTE><PRE>
  END DoSavePSAs;

PROCEDURE <A NAME="DoSavePSAsWork"><procedure>DoSavePSAsWork</procedure></A>(cl: FileIOClosure): REFANY =
  BEGIN
    WritePostScriptToFile(cl.fv, cl.fname);
    FVFilter.MakeActive(cl.fv, &quot;background&quot;);
    RETURN NIL
  END DoSavePSAsWork;
</PRE> <CODE>Labels</CODE> Menu ----------------------------------------------------------- 

<P><PRE>PROCEDURE <A NAME="DoLabels"><procedure>DoLabels</procedure></A>(fv: FormsVBT.T; &lt;*UNUSED*&gt; event: TEXT;
    &lt;*UNUSED*&gt; data: REFANY; &lt;*UNUSED*&gt; ts: VBT.TimeStamp) =
  &lt;* LL.sup = VBT.mu *&gt;
  VAR w: Window := fv; option: TEXT; style: [0..2]; BEGIN
    option := FormsVBT.GetChoice(fv, &quot;labels&quot;);
    IF Text.Equal(option, &quot;letters&quot;) THEN style := 2
    ELSIF Text.Equal(option, &quot;crosses&quot;) THEN style := 1
    ELSIF Text.Equal(option, &quot;nothing&quot;) THEN style := 0
    ELSE &lt;* ASSERT FALSE *&gt;
    END;
    Drawing.SetLabelStyle(w.root.drawing, style)
  END DoLabels;
</PRE> <CODE>Redisplay</CODE> Menu -------------------------------------------------------- 

<P><PRE>PROCEDURE <A NAME="DoAutoUpdate"><procedure>DoAutoUpdate</procedure></A>(fv: FormsVBT.T; &lt;*UNUSED*&gt; event: TEXT;
    &lt;*UNUSED*&gt; data: REFANY; &lt;*UNUSED*&gt; ts: VBT.TimeStamp) =
  &lt;* LL.sup = VBT.mu *&gt;
  VAR w: Window := fv; option: TEXT; BEGIN
    option := FormsVBT.GetChoice(fv, &quot;autoUpdate&quot;);
    IF Text.Equal(option, &quot;autoRun&quot;) THEN w.root.skipify := FALSE
    ELSIF Text.Equal(option, &quot;autoSolve&quot;) THEN w.root.skipify := TRUE
    ELSE &lt;* ASSERT FALSE *&gt;
    END
  END DoAutoUpdate;
</PRE> <CODE>Run</CODE>/<CODE>Solve</CODE>/<CODE>Stop</CODE> Buttons -------------------------------------------- 

<P><PRE>TYPE
  RunClosure = MenuClosure OBJECT
    skipify: BOOLEAN
  END;

PROCEDURE <A NAME="DoRun"><procedure>DoRun</procedure></A>(fv: FormsVBT.T; &lt;*UNUSED*&gt; event: TEXT;
    &lt;*UNUSED*&gt; data: REFANY; ts: VBT.TimeStamp) =
  &lt;* LL.sup = VBT.mu *&gt;
  BEGIN
    FVFilter.MakePassive(fv, &quot;background&quot;, FVFilter.CursorKind.Working);
</PRE><BLOCKQUOTE><EM><P>
    EVAL Thread.Fork(NEW(RunClosure, fv := fv, ts := ts,
      skipify := FALSE, apply := DoRunWork));
</EM></BLOCKQUOTE><PRE>
    EVAL DoRunWork(NEW(RunClosure, fv := fv, ts := ts,
      skipify := FALSE))
  END DoRun;

PROCEDURE <A NAME="DoSolve"><procedure>DoSolve</procedure></A>(fv: FormsVBT.T; &lt;*UNUSED*&gt; event: TEXT;
    &lt;*UNUSED*&gt; data: REFANY; ts: VBT.TimeStamp) =
  &lt;* LL.sup = VBT.mu *&gt;
  BEGIN
    FVFilter.MakePassive(fv, &quot;background&quot;, FVFilter.CursorKind.Working);
</PRE><BLOCKQUOTE><EM><P>
    EVAL Thread.Fork(NEW(RunClosure, fv := fv, ts := ts,
      skipify := TRUE, apply := DoRunWork))
</EM></BLOCKQUOTE><PRE>
    EVAL DoRunWork(NEW(RunClosure, fv := fv, ts := ts,
      skipify := TRUE))
  END DoSolve;

PROCEDURE <A NAME="DoRunWork"><procedure>DoRunWork</procedure></A>(cl: RunClosure): REFANY =
  VAR w: Window := cl.fv; BEGIN
    EVAL Drawing.FinishTextTool(w.root.drawing);
    TRY MakeModAndCmd(cl.fv, cl.ts, cl.skipify) FINALLY
      FVFilter.MakeActive(cl.fv, &quot;background&quot;)
    END;
    RETURN NIL
  END DoRunWork;

PROCEDURE <A NAME="DoStop"><procedure>DoStop</procedure></A>(fv: FormsVBT.T; &lt;*UNUSED*&gt; event: TEXT;
  &lt;*UNUSED*&gt; data: REFANY; &lt;*UNUSED*&gt; ts: VBT.TimeStamp) =
  &lt;* LL.sup = VBT.mu *&gt;
  BEGIN
    TRY
      JunoRT.Interrupt()
    FINALLY
      FVFilter.MakeActive(fv, &quot;background&quot;)
    END
  END DoStop;
</PRE> <CODE>Fold...</CODE> --------------------------------------------------------------- 

<P><PRE>PROCEDURE <A NAME="DoFoldDialog"><procedure>DoFoldDialog</procedure></A>(fv: FormsVBT.T; &lt;*UNUSED*&gt; event: TEXT;
  &lt;*UNUSED*&gt; data: REFANY; ts: VBT.TimeStamp) =
</PRE><BLOCKQUOTE><EM> This procedure is called when the user chooses <CODE>Fold...</CODE>. </EM></BLOCKQUOTE><PRE>
  &lt;* LL.sup = VBT.mu *&gt;
  BEGIN
    FVFilter.MakePassive(fv, &quot;background&quot;, FVFilter.CursorKind.Working);
</PRE><BLOCKQUOTE><EM><P>
    EVAL Thread.Fork(NEW(MenuClosure, fv := fv, ts := ts,
      apply := DoFoldDialogWork))
</EM></BLOCKQUOTE><PRE>
    EVAL DoFoldDialogWork(NEW(MenuRec, fv := fv, ts := ts))
  END DoFoldDialog;

PROCEDURE <A NAME="DoFoldDialogWork"><procedure>DoFoldDialogWork</procedure></A>(cl: MenuRec): REFANY =
</PRE><BLOCKQUOTE><EM> This procedure fills in the default values for the dialog and pops up the
   window. The <CODE>DoFoldWork</CODE> procedure is invoked when the user clicks the <CODE>OK</CODE>
   button. </EM></BLOCKQUOTE><PRE>
  VAR w: Window := cl.fv; BEGIN
    IF NOT CompileModAndCmd(w, cl.ts) THEN
      FVFilter.MakeActive(cl.fv, &quot;background&quot;);
      RETURN NIL
    END;
    VAR name: TEXT; BEGIN
      IF ISTYPE(CurrCmd.GetCmd(CurrCmd.GetAST(w.root.ccmd)), JunoAST.Skip) THEN
    	name := CurrCmd.NewDeclName(w.root.ccmd, &quot;Pred&quot;);
    	FormsVBT.MakeSelected(cl.fv, &quot;predType&quot;);
      ELSE
    	name := CurrCmd.NewDeclName(w.root.ccmd, &quot;Proc&quot;);
    	FormsVBT.MakeSelected(cl.fv, &quot;procType&quot;)
      END;
      FormsVBT.PutText(cl.fv, &quot;declName&quot;, name, append := FALSE);
    END;
    FormsVBT.MakeSelected(cl.fv, &quot;pointTool&quot;);
    FVFilter.MakePassive(cl.fv, &quot;background&quot;, FVFilter.CursorKind.Passive);
    FormsVBT.PopUp(cl.fv, &quot;foldWindow&quot;, time := cl.ts);
    RETURN NIL
  END DoFoldDialogWork;

PROCEDURE <A NAME="ToolTypeFromName"><procedure>ToolTypeFromName</procedure></A>(nm: TEXT): ToolType =
  BEGIN
    IF Text.Equal(nm, &quot;pointTool&quot;) THEN
      RETURN ToolType.Point
    ELSIF Text.Equal(nm, &quot;noTool&quot;) THEN
      RETURN ToolType.None
    ELSE
      &lt;* ASSERT FALSE *&gt;
    END
  END ToolTypeFromName;

PROCEDURE <A NAME="ParseProcName"><procedure>ParseProcName</procedure></A>(w: Window; nm: TEXT;
  VAR (*OUT*) hdr: JunoAST.PredHeader): BOOLEAN =
</PRE><BLOCKQUOTE><EM> Parse the field named <CODE>nm</CODE> of the form <CODE>w</CODE> as a predicate header, set
   <CODE>hdr</CODE> to the AST of the parsed result if successful, and return TRUE.
   Otherwise, pop up an error message over the window <CODE>w</CODE> and return FALSE. </EM></BLOCKQUOTE><PRE>
  &lt;* FATAL Rd.Failure *&gt;
  BEGIN
    TRY
      VAR junk: CARDINAL; BEGIN
        JunoParse.FoldHeader(TextRd.New(FormsVBT.GetText(w, nm)), hdr, junk)
      END;
      IF hdr.ins = NIL THEN
        hdr.ins := CurrCmd.GetFoldArgs(w.root.ccmd)
      END
    EXCEPT
      JunoParse.Error, JunoLex.Error =&gt;
    	JunoError.Display(w, &quot;Fold name syntax is Id or Id(IdList)&quot;);
        RETURN FALSE
    END;
    RETURN TRUE
  END ParseProcName;

PROCEDURE <A NAME="ParseVarList"><procedure>ParseVarList</procedure></A>(fv: FormsVBT.T; nm: TEXT;
  VAR (*OUT*) pts: JunoAST.IdList): BOOLEAN =
</PRE><BLOCKQUOTE><EM> Parse the field named <CODE>nm</CODE> of the form <CODE>fv</CODE> as a list of identifiers
   (variable names), set <CODE>pts</CODE> to the parsed result, and return TRUE.
   Otherwise, pop up an error message over the window <CODE>fv</CODE> and return FALSE.
</EM></BLOCKQUOTE><PRE>
  BEGIN
    TRY
      VAR junk: CARDINAL; BEGIN
        JunoParse.IdList(TextRd.New(FormsVBT.GetText(fv, nm)), pts, junk)
      END
    EXCEPT
      JunoParse.Error, JunoLex.Error =&gt;
    	JunoError.Display(fv, &quot;Fold points syntax is \&quot;id, id, ...\&quot;&quot;);
        RETURN FALSE
    END;
    RETURN TRUE
  END ParseVarList;

PROCEDURE <A NAME="DoFoldWork"><procedure>DoFoldWork</procedure></A>(fv: FormsVBT.T; &lt;*UNUSED*&gt; event: TEXT;
  &lt;*UNUSED*&gt; data: REFANY; ts: VBT.TimeStamp) =
</PRE><BLOCKQUOTE><EM> This procedure is called when the user has set the values in the <CODE>Fold...</CODE>
   dialogue and clicked <CODE>OK</CODE> (or hit &lt;Enter&gt; in the type-in field). It
   extracts the values from the dialogue and folds the current command
   appropriately. </EM></BLOCKQUOTE><PRE>
  &lt;* LL.sup = VBT.mu *&gt;
  VAR hdr: JunoAST.PredHeader; BEGIN
    FormsVBT.PopDown(fv, &quot;foldWindow&quot;);
    IF ParseProcName(fv, &quot;declName&quot;, (*OUT*) hdr) THEN
      VAR
    	foldKind: CurrCmd.FoldKind;
    	kind := FormsVBT.GetChoice(fv, &quot;typeRadio&quot;);
    	toolKind := ToolTypeFromName(FormsVBT.GetChoice(fv, &quot;toolRadio&quot;));
      BEGIN
    	IF Text.Equal(kind, &quot;predType&quot;) THEN
    	  foldKind := CurrCmd.FoldKind.Pred
    	ELSIF Text.Equal(kind, &quot;procType&quot;) THEN
    	  foldKind := CurrCmd.FoldKind.Proc
    	ELSIF Text.Equal(kind, &quot;templType&quot;) THEN
    	  foldKind := CurrCmd.FoldKind.ProcNoArgs;
    	  toolKind := ToolType.Templ
    	ELSE
    	  &lt;* ASSERT FALSE *&gt;
    	END;
    	DoFold(fv, ts, hdr, foldKind, toolType := toolKind)
      END
    END;
    FVFilter.MakeActive(fv, &quot;background&quot;)
  END DoFoldWork;
</PRE> <CODE>Fold As Animation...</CODE> -------------------------------------------------- 

<P><PRE>PROCEDURE <A NAME="DoFoldAsAnimDialog"><procedure>DoFoldAsAnimDialog</procedure></A>(fv: FormsVBT.T; &lt;*UNUSED*&gt; event: TEXT;
  &lt;*UNUSED*&gt; data: REFANY; ts: VBT.TimeStamp) =
</PRE><BLOCKQUOTE><EM> This procedure is called when the user chooses <CODE>Fold As Animation...</CODE>. </EM></BLOCKQUOTE><PRE>
  &lt;* LL.sup = VBT.mu *&gt;
  BEGIN
    FVFilter.MakePassive(fv, &quot;background&quot;, FVFilter.CursorKind.Working);
</PRE><BLOCKQUOTE><EM><P>
    EVAL Thread.Fork(NEW(MenuClosure, fv := fv, ts := ts,
      apply := DoFoldAsAnimDialogWork))
</EM></BLOCKQUOTE><PRE>
    EVAL DoFoldAsAnimDialogWork(NEW(MenuRec, fv := fv, ts := ts))
  END DoFoldAsAnimDialog;

VAR SliderDrawName := JunoASTUtils.QIdFromTexts(&quot;Slider&quot;, &quot;Draw&quot;);

PROCEDURE <A NAME="SliderArgs"><procedure>SliderArgs</procedure></A>(ccmd: CurrCmd.T): TEXT =
</PRE><BLOCKQUOTE><EM> Search the body of <CODE>ccmd</CODE> for a procedure call statement to <CODE>Slider.Draw</CODE>,
   and return an unparsing of the arguments to that call. If no such call can
   be found, return an empty text. </EM></BLOCKQUOTE><PRE>
  VAR
    res: TEXT;
    body := CurrCmd.GetCmd(CurrCmd.GetAST(ccmd));
    call := JunoASTUtils.FirstProcCall(body, SliderDrawName);
  BEGIN
    IF call = NIL THEN res := &quot;&quot; ELSE
      &lt;* FATAL Wr.Failure *&gt;
      VAR wr := TextWr.New(); BEGIN
        JunoUnparse.P(wr, call.ins, width := LAST(INTEGER),
          prec := JunoConfig.realPrec);
        res := TextWr.ToText(wr)
      END
    END;
    RETURN res
  END SliderArgs;

PROCEDURE <A NAME="DoFoldAsAnimDialogWork"><procedure>DoFoldAsAnimDialogWork</procedure></A>(cl: MenuRec): REFANY =
</PRE><BLOCKQUOTE><EM> This procedure fills in the default values for the dialog and pops up the
   window. The <CODE>DoFoldAsAnimWork</CODE> procedure is invoked when the user clicks
   the <CODE>OK</CODE> button. </EM></BLOCKQUOTE><PRE>
  VAR w: Window := cl.fv; BEGIN
    IF NOT CompileModAndCmd(w, cl.ts) THEN
      FVFilter.MakeActive(cl.fv, &quot;background&quot;);
      RETURN NIL
    END;
    FormsVBT.PutText(cl.fv, &quot;animName&quot;,
      CurrCmd.NewDeclName(w.root.ccmd, &quot;Anim&quot;));
    FormsVBT.PutText(cl.fv, &quot;sliderPts&quot;, SliderArgs(w.root.ccmd));
    FVFilter.MakePassive(cl.fv, &quot;background&quot;, FVFilter.CursorKind.Passive);
    FormsVBT.PopUp(cl.fv, &quot;foldAsAnimWindow&quot;, time := cl.ts);
    RETURN NIL
  END DoFoldAsAnimDialogWork;

PROCEDURE <A NAME="DoFoldAsAnimWork"><procedure>DoFoldAsAnimWork</procedure></A>(fv: FormsVBT.T; &lt;*UNUSED*&gt; event: TEXT;
  &lt;*UNUSED*&gt; data: REFANY; ts: VBT.TimeStamp) =
</PRE><BLOCKQUOTE><EM> This procedure is called when the user has set the values in the <CODE>Fold As
   Animation...</CODE> dialogue and clicked <CODE>OK</CODE> (or hit &lt;Enter&gt; in one of the
   type-in fields. It extracts the values from the dialogue and folds the
   current command appropriately. </EM></BLOCKQUOTE><PRE>
  &lt;* LL.sup = VBT.mu *&gt;
  VAR hdr: JunoAST.PredHeader; pts: JunoAST.IdList; BEGIN
    FormsVBT.PopDown(fv, &quot;foldAsAnimWindow&quot;);
    IF ParseProcName(fv, &quot;animName&quot;, (*OUT*) hdr) AND
       ParseVarList(fv, &quot;sliderPts&quot;, (*OUT*) pts) THEN
      IF pts.size # 3
        THEN JunoError.Display(fv, &quot;You must specify exactly 3 slider points&quot;)
        ELSE DoFoldAsAnim(fv, ts, hdr, sliderPts := pts)
      END
    END;
    FVFilter.MakeActive(fv, &quot;background&quot;)
  END DoFoldAsAnimWork;
</PRE> <CODE>Push Current Command</CODE> -------------------------------------------------- 

<P><PRE>PROCEDURE <A NAME="DoPushCurrCmd"><procedure>DoPushCurrCmd</procedure></A>(fv: FormsVBT.T; &lt;*UNUSED*&gt; event: TEXT;
  &lt;*UNUSED*&gt; data: REFANY; ts: VBT.TimeStamp) =
  &lt;* LL.sup = VBT.mu *&gt;
  BEGIN
    FVFilter.MakePassive(fv, &quot;background&quot;, FVFilter.CursorKind.Working);
</PRE><BLOCKQUOTE><EM><P>
    EVAL Thread.Fork(NEW(MenuClosure, fv := fv, ts := ts,
      apply := DoPushCurrCmdWork))
</EM></BLOCKQUOTE><PRE>
    EVAL DoPushCurrCmdWork(NEW(MenuRec, fv := fv, ts := ts))
  END DoPushCurrCmd;

PROCEDURE <A NAME="DoPushCurrCmdWork"><procedure>DoPushCurrCmdWork</procedure></A>(cl: MenuRec): REFANY =
  VAR w: Window := cl.fv; BEGIN
    TRY
      (* Build current module and command; return if compilation error *)
      IF NOT CompileModAndCmd(w, cl.ts) THEN RETURN NIL END;
      (* Otherwise, push current command *)
      VAR nm := Editor.NextCmdName(w.root.editor); BEGIN
        DoFold(w, cl.ts, NEW(JunoAST.PredHeader, name := nm),
          CurrCmd.FoldKind.ProcNoArgs, ToolType.None)
      END
    FINALLY
      FVFilter.MakeActive(cl.fv, &quot;background&quot;)
    END;
    RETURN NIL
  END DoPushCurrCmdWork;
</PRE> <CODE>Pop Current Command</CODE> --------------------------------------------------- 

<P><PRE>PROCEDURE <A NAME="DoPopCurrCmd"><procedure>DoPopCurrCmd</procedure></A>(fv: FormsVBT.T; &lt;*UNUSED*&gt; event: TEXT;
    &lt;*UNUSED*&gt; data: REFANY; ts: VBT.TimeStamp) =
  &lt;* LL.sup = VBT.mu *&gt;
  BEGIN
    FVFilter.MakePassive(fv, &quot;background&quot;, FVFilter.CursorKind.Working);
</PRE><BLOCKQUOTE><EM><P>
    EVAL Thread.Fork(NEW(MenuClosure, fv := fv, ts := ts,
      apply := DoPopCurrCmdWork))
</EM></BLOCKQUOTE><PRE>
    EVAL DoPopCurrCmdWork(NEW(MenuRec, fv := fv, ts := ts))
  END DoPopCurrCmd;

PROCEDURE <A NAME="DoPopCurrCmdWork"><procedure>DoPopCurrCmdWork</procedure></A>(cl: MenuRec): REFANY =
  VAR w: Window := cl.fv; BEGIN
    TRY
      IF NOT CompileModAndCmd(w, cl.ts) THEN RETURN NIL END;
      IF ISTYPE(CurrCmd.GetAST(w.root.ccmd), JunoAST.Skip) THEN
    	VAR ed := w.root.editor; nm: JunoAST.Id; cmd: JunoAST.Cmd; BEGIN
    	  cmd := Editor.PopCurrCmd(ed, nm);
    	  IF cmd # NIL THEN
    	    UnbindDecl(CurrCmd.GetScope(w.root.ccmd), nm);
    	    ToolBox.Update(Split.Succ(w.toolbox, NIL), ed, w.root,
    	      w.builtInSize, anon := TRUE);
    	    CurrCmd.ChangeAST(w.root.ccmd, cmd);
    	    w.root.astTrue := TRUE;
    	    w.root.sTrue := FALSE;
    	    w.root.dTrue := FALSE;
            SetModuleState(w, ModState.Stale);
    	    MakeCurrCmd(w, cl.ts, w.root.skipify)
    	  END
    	END
      END
    FINALLY
      FVFilter.MakeActive(cl.fv, &quot;background&quot;)
    END;
    RETURN NIL
  END DoPopCurrCmdWork;

PROCEDURE <A NAME="UnbindDecl"><procedure>UnbindDecl</procedure></A>(scp: JunoScope.T; nm: JunoAST.Id) =
</PRE><BLOCKQUOTE><EM> Remove <CODE>nm</CODE>'s binding from <CODE>scp</CODE>. Requires that <CODE>nm</CODE> is bound in <CODE>scp</CODE>. </EM></BLOCKQUOTE><PRE>
  &lt;* FATAL JunoScope.NotFound *&gt;
  BEGIN
    EVAL JunoScope.Unbind(scp, nm)
  END UnbindDecl;
</PRE> Save/Discard/Cancel Changes --------------------------------------------- 

<P><PRE>PROCEDURE <A NAME="DoSaveChanges"><procedure>DoSaveChanges</procedure></A>(fv: FormsVBT.T; &lt;*UNUSED*&gt; event: TEXT;
    &lt;*UNUSED*&gt; data: REFANY; &lt;*UNUSED*&gt; ts: VBT.TimeStamp) =
  &lt;* LL.sup = VBT.mu *&gt;
  VAR w: Window := fv; BEGIN
    LOCK w.mu DO w.stat := SyncStat.Save END;
    Thread.Signal(w.untilDone)
  END DoSaveChanges;

PROCEDURE <A NAME="DoDiscardChanges"><procedure>DoDiscardChanges</procedure></A>(fv: FormsVBT.T; &lt;*UNUSED*&gt; event: TEXT;
    &lt;*UNUSED*&gt; data: REFANY; &lt;*UNUSED*&gt; ts: VBT.TimeStamp) =
  &lt;* LL.sup = VBT.mu *&gt;
  VAR w: Window := fv; BEGIN
    LOCK w.mu DO w.stat := SyncStat.Discard END;
    Thread.Signal(w.untilDone)
  END DoDiscardChanges;

PROCEDURE <A NAME="DoCancelChanges"><procedure>DoCancelChanges</procedure></A>(fv: FormsVBT.T; &lt;*UNUSED*&gt; event: TEXT;
    &lt;*UNUSED*&gt; data: REFANY; &lt;*UNUSED*&gt; ts: VBT.TimeStamp) =
  &lt;* LL.sup = VBT.mu *&gt;
  VAR w: Window := fv; BEGIN
    LOCK w.mu DO w.stat := SyncStat.Cancel END;
    Thread.Signal(w.untilDone)
  END DoCancelChanges;
</PRE> ========================= ToolBox Procedures ============================ 

<P><PRE>TYPE
  Toolbox = FormsVBT.T OBJECT
    w: Window;
    ed: Editor.T;
    helpWindow: VBT.T := NIL; (* top-level installed window (if any) *)
    hasPrivate: BOOLEAN
  END;
</PRE> A <CODE>Toolbox</CODE> is a window that represents the tools for a module. It is
   created from a FormsVBT form. The form has a menu for displaying help on
   the module and for performing other operations on the toolbox. The toolbox
   itself contains point, text, and set tools. 

<P><PRE>PROCEDURE <A NAME="NewToolbox"><procedure>NewToolbox</procedure></A>(w: Window; v: Editor.T; hasPrivate: BOOLEAN): Toolbox =
</PRE><BLOCKQUOTE><EM> Create and return a toolbox form for <CODE>v</CODE>. </EM></BLOCKQUOTE><PRE>
  &lt;* FATAL Rsrc.NotFound *&gt;
  &lt;* LL.sup = VBT.mu *&gt;
  VAR
    res: Toolbox := NEW(Toolbox, w := w, ed := v,
      hasPrivate := hasPrivate).initFromRsrc(
        &quot;Toolbox.fv&quot;, JunoRsrc.Path, raw := TRUE);
    moduleName := Atom.ToText(Editor.ModuleName(v));
  BEGIN
    JunoConfig.SetFonts(res);
    IF moduleName = NIL THEN moduleName := &quot;&lt;Untitled&gt;&quot; END;
    TRY
      FormsVBT.PutText(res, &quot;toolboxName&quot;, moduleName);
      FormsVBT.PutGeneric(res, &quot;toolboxButtons&quot;, ButtonView(w.root, v));
      FormsVBT.AttachProc(res, &quot;help&quot;, DoHelp);
      FormsVBT.AttachProc(res, &quot;close&quot;, DoCloseToolbox);
    EXCEPT
      FormsVBT.Error (msg) =&gt;
        Wr.PutText(Stdio.stderr, &quot;FormsVBT error: &quot; &amp; msg &amp; &quot;\n&quot;);
        Wr.Flush(Stdio.stderr);
        &lt;* ASSERT FALSE *&gt;
    END;
    RETURN res
  END NewToolbox;

PROCEDURE <A NAME="ButtonView"><procedure>ButtonView</procedure></A>(rt: View.Root; ed: Editor.T): VBT.T =
</PRE><BLOCKQUOTE><EM> Return the button view on the module view <CODE>ed</CODE>. </EM></BLOCKQUOTE><PRE>
  VAR res := HVSplit.New(hv := Axis.T.Ver, adjustable := FALSE); BEGIN
    ToolBox.Update(res, ed, rt, anon := FALSE);
    RETURN res
  END ButtonView;

PROCEDURE <A NAME="DoHelp"><procedure>DoHelp</procedure></A>(
    fv: FormsVBT.T;
    &lt;*UNUSED*&gt; event: TEXT;
    &lt;*UNUSED*&gt; data: REFANY;
    &lt;*UNUSED*&gt; ts: VBT.TimeStamp) =
  &lt;* LL.sup = VBT.mu *&gt;
  VAR tb: Toolbox := fv; BEGIN
    IF tb.helpWindow = NIL THEN
      &lt;* FATAL Rsrc.NotFound *&gt;
      VAR public, private: VBT.T; BEGIN
        IF tb.hasPrivate
          THEN private := NEW(TextEditVBT.T, tp := tb.ed).init()
          ELSE private := TextVBT.New(&quot;No private view&quot;)
        END;
        public := NEW(TextEditVBT.T,
          tp := NEW(PublicView.T).init(tb.ed)).init();
        tb.helpWindow := NEW(FormsVBT.T).initFromRsrc(
          &quot;Editor2.fv&quot;, JunoRsrc.Path, raw := TRUE);
        FormsVBT.PutGeneric(tb.helpWindow, &quot;publicView&quot;, public);
        FormsVBT.PutGeneric(tb.helpWindow, &quot;privateView&quot;, private);
        JunoConfig.SetFonts(tb.helpWindow);
      END
    ELSE
      (* force help window into &quot;public&quot; view *)
      FormsVBT.PutChoice(tb.helpWindow, &quot;view&quot;, &quot;public&quot;);
      FormsVBT.PutInteger(tb.helpWindow, &quot;body&quot;, 0);
    END;
    &lt;* FATAL TrestleComm.Failure *&gt;
    VAR
      wScrn := Trestle.ScreenOf(tb.w, Rect.NorthEast(VBT.Domain(w)));
      wST := ScreenType(wScrn.trsl, wScrn.id);
      tbScrn := Trestle.ScreenOf(tb.helpWindow, Point.Origin);
    BEGIN
      (* delete help window if it is on the wrong Trestle *)
      IF tbScrn.trsl # NIL AND tbScrn.trsl # wScrn.trsl THEN
        Trestle.Delete(tb.helpWindow);
        tbScrn.trsl := NIL
      END;
      (* guarantee that the help window is attached to the right Trestle *)
      IF tbScrn.trsl = NIL THEN
        Trestle.Attach(tb.helpWindow, wScrn.trsl);
        VAR mod := FormsVBT.GetText(tb, &quot;toolboxName&quot;); BEGIN
          Trestle.Decorate(tb.helpWindow, instance := mod, windowTitle := mod)
        END
      END;
      (* guarantee that the help window has the right screen type so we can
         measure it *)
      IF VBT.ScreenTypeOf(tb.helpWindow) # wST THEN
        VBTClass.Rescreen(tb.helpWindow, wST)
      END;
      (* intall the help window so its northeast corner is just inside the
         northeast corner of the main Juno window *)
      VAR s := VBTClass.GetShape(tb.helpWindow, Axis.T.Hor,
        n := 0, clearNewShape := FALSE);
      BEGIN
        Trestle.Overlap(tb.helpWindow, wScrn.id,
          Point.Add(wScrn.q, Point.T{-(s.pref+24), 24}))
      END
    END
  END DoHelp;

PROCEDURE <A NAME="ScreenType"><procedure>ScreenType</procedure></A>(trsl: Trestle.T; id: Trestle.ScreenID): VBT.ScreenType
    RAISES {TrestleComm.Failure} =
  VAR scrns := Trestle.GetScreens(trsl); i := 0; BEGIN
    WHILE scrns[i].id # id DO INC(i) END;
    RETURN scrns[i].type
  END ScreenType;

PROCEDURE <A NAME="DoCloseToolbox"><procedure>DoCloseToolbox</procedure></A>(
    fv: FormsVBT.T;
    &lt;*UNUSED*&gt; event: TEXT;
    &lt;*UNUSED*&gt; data: REFANY;
    &lt;*UNUSED*&gt; ts: VBT.TimeStamp) =
  &lt;* LL.sup = VBT.mu *&gt;
  BEGIN
    Split.Delete(VBT.Parent(fv), fv)
  END DoCloseToolbox;
</PRE> ================================ Main Body ============================== 

<P><PRE>TYPE
  SourceView = Source.T OBJECT
    w: Window
  OVERRIDES
    modified := SourceModified
  END;

  EditorView = Editor.T OBJECT
    w: Window
  OVERRIDES
    txtModified := EditorModified
  END;

PROCEDURE <A NAME="SourceModified"><procedure>SourceModified</procedure></A>(sv: SourceView; how: View.ModKind) =
</PRE><BLOCKQUOTE><EM> Callback procedure when text in the source window (current command) is
   modified. </EM></BLOCKQUOTE><PRE>
  &lt;* LL.sup &lt; sv *&gt;
  VAR w := sv.w; BEGIN
    w.checkpointStale := TRUE;
    w.root.sTrue := FALSE;
    SetModuleState(w, ModState.Stale);
    IF how # View.ModKind.ImplicitConsistent THEN
      FormsVBT.MakeActive(w, SolveButton);
      IF how = View.ModKind.Explicit THEN
    	ToolBox.Unselect(w.root);
    	w.root.dTrue := FALSE;
    	w.root.astTrue := FALSE
      END
    END
  END SourceModified;

PROCEDURE <A NAME="EditorModified"><procedure>EditorModified</procedure></A>(ev: EditorView) =
</PRE><BLOCKQUOTE><EM> Callback procedure when text in the editor window is modified. </EM></BLOCKQUOTE><PRE>
  &lt;* LL.sup &lt; ev *&gt;
  VAR w := ev.w; BEGIN
    w.checkpointStale := TRUE;
    w.root.eTrue := FALSE;
    SetModuleState(w, ModState.Stale);
    FormsVBT.MakeActive(w, SolveButton);
    ToolBox.Unselect(w.root);
    w.root.dTrue := FALSE;
    (* If a public part of the module changed, we have to recompile the
       current command. Since we have no way of telling (for now) if the
       change was to the public or private part of the module, we have to
       recompile the current command in all cases. *)
    w.root.ccmd.codeValid := FALSE
  END EditorModified;

TYPE ModState = { Stale, UpToDate };

PROCEDURE <A NAME="SetModuleState"><procedure>SetModuleState</procedure></A>(w: Window; modState: ModState) =
  CONST ModStateColor = ARRAY ModState OF TEXT{&quot;LightRed&quot;, &quot;LightGray&quot;}; BEGIN
    w.fileStale := (modState = ModState.Stale);
    FormsVBT.PutTextProperty(w, &quot;currFile&quot;, &quot;BgColor&quot;, ModStateColor[modState])
  END SetModuleState;

PROCEDURE <A NAME="PrintUsage"><procedure>PrintUsage</procedure></A>(msg: TEXT) =
</PRE><BLOCKQUOTE><EM> Print a usage message and exit the program. </EM></BLOCKQUOTE><PRE>
  BEGIN
    Wr.PutText(Stdio.stderr, &quot;Fatal error: &quot;);
    Wr.PutText(Stdio.stderr, msg);
    Wr.PutChar(Stdio.stderr, '\n');
    Wr.PutText(Stdio.stderr, &quot;Usage: Juno [-zeus] [-display &lt;Xdisplay&gt;] &quot;);
    Wr.PutText(Stdio.stderr, &quot;[-geometry &lt;Xgeometry&gt;] [filename]\n&quot;);
    Wr.Flush(Stdio.stderr);
    Process.Exit(1)
  END PrintUsage;

PROCEDURE <A NAME="RsrcOpen"><procedure>RsrcOpen</procedure></A>(nm: TEXT): Rd.T RAISES {Rsrc.NotFound} =
  BEGIN RETURN Rsrc.Open(nm, JunoRsrc.Path) END RsrcOpen;

PROCEDURE <A NAME="CompileFile"><procedure>CompileFile</procedure></A>(root: View.Root; fileName: TEXT; parent: JunoScope.T):
    JunoScope.Mod =
</PRE><BLOCKQUOTE><EM> Build the module in the resource named <CODE>fileName</CODE> under the scope <CODE>parent</CODE>,
   and return the resulting module entity. Both the public and private scope
   in the resulting entity are created with a parent scope of <CODE>parent</CODE>.
   Requires that there are no compilation errors in the module. </EM></BLOCKQUOTE><PRE>
  &lt;* FATAL Rsrc.NotFound *&gt;
  VAR
    rd: Rd.T := RsrcOpen(fileName);
    te := NEW(Editor.T).init(Rd.GetText(rd, LAST(CARDINAL)));
    modNm: JunoAST.Id; mod: JunoScope.Mod;
  BEGIN
    IF NOT CompileEditor(root, te, NoTimeStamp, parent, modNm, mod) THEN
      Process.Crash(&quot;compilation error in \&quot;&quot; &amp; fileName &amp; &quot;\&quot;&quot;)
    END;
    Rd.Close(rd);
    RETURN mod
  END CompileFile;

PROCEDURE <A NAME="FillBrowser"><procedure>FillBrowser</procedure></A>(v: ListVBT.T; modList: TextList.T) =
</PRE><BLOCKQUOTE><EM> Prepend the names in <CODE>modList</CODE> to <CODE>v</CODE>. </EM></BLOCKQUOTE><PRE>
  VAR i := 0; BEGIN
    v.insertCells(0, TextList.Length(modList));
    WHILE modList # NIL DO
      v.setValue(i, modList.head);
      modList := modList.tail; INC(i)
    END
  END FillBrowser;

TYPE CheckpointClosure = Thread.Closure OBJECT
    w: Window
  OVERRIDES
    apply := MakeCheckpoint
  END;

VAR
  checkpointThread: Thread.T;

PROCEDURE <A NAME="CheckpointName"><procedure>CheckpointName</procedure></A>(nm: Pathname.T): Pathname.T =
  BEGIN
    RETURN Pathname.ReplaceExt(nm, &quot;bak&quot;)
  END CheckpointName;

PROCEDURE <A NAME="MakeCheckpoint"><procedure>MakeCheckpoint</procedure></A>(self:CheckpointClosure): REFANY =
  VAR w := self.w; BEGIN
    LOOP
      Thread.Pause(FLOAT(JunoConfig.chkptIntv, LONGREAL));
      LOCK VBT.mu DO
        IF w.fileStale AND w.checkpointStale THEN
          TRY
            VAR
              wr := FileWr.Open(CheckpointName(w.fileName));
              st: SaveState.T;
            BEGIN
              st.file := w.fileName;
              st.editor := TextPort.GetText(w.root.editor);
              st.source := Source.GetText(w.root.source);
              SaveState.Save(st, wr);
              w.checkpointStale := FALSE;
              Wr.Close(wr)
            END
          EXCEPT
            OSError.E, Wr.Failure, Thread.Alerted =&gt; (*SKIP*)
          END
        END
      END
    END
  END MakeCheckpoint;

PROCEDURE <A NAME="SetTEFont"><procedure>SetTEFont</procedure></A>(te: TextEditVBT.T; f: Font.T) =
  &lt;* LL.sup = VBT.mu *&gt;
  BEGIN te.tp.setFont(f) END SetTEFont;

&lt;* FATAL Split.NotAChild, JunoScope.NameClash *&gt;
VAR
  startup: FormsVBT.T;
  builtInScope, modScope: JunoScope.T;
  root: View.Root;
  drawing: Drawing.T;
  w: Window;
  source: SourceView;
  editor: EditorView;
  psImpl: PSImpl.Impl;
  zeusOption := FALSE;
  animObj: JunoZeus.T;
  disp, geom: TEXT := NIL;
  fileName := &quot;Untitled.juno&quot;;
  writepkl := FALSE;
  readpkl  := TRUE;
  configFile: Pathname.T := NIL;
BEGIN
  (* bump minimum stack size to twice the default *)
  Thread.MinDefaultStackSize(6000);

  (* process command-line arguments *)
  VAR
    i := 1; arg: TEXT;
    origin := -1;      (* ordinal of &quot;JunoConfig.Origin&quot; type *)
    orientation := -1; (* ordinal of &quot;JunoConfig.Orientation&quot; type *)
  BEGIN
    WHILE i &lt; Params.Count DO
      arg := Params.Get(i);
      IF Text.Length(arg) = 0 THEN
        PrintUsage(&quot;empty argument #&quot; &amp; Fmt.Int(i))
      END;
      IF Text.GetChar(arg, 0) = '-' THEN
        IF Text.Equal(arg, &quot;-zeus&quot;) THEN
          zeusOption := TRUE; INC(i)
        ELSIF Text.Equal(arg, &quot;-southwest&quot;) THEN
          origin := ORD(JunoConfig.Origin.SW); INC(i)
        ELSIF Text.Equal(arg, &quot;-center&quot;) THEN
          origin := ORD(JunoConfig.Origin.Center); INC(i)
        ELSIF Text.Equal(arg, &quot;-portrait&quot;) THEN
          orientation := ORD(JunoConfig.Orientation.Portrait); INC(i)
        ELSIF Text.Equal(arg, &quot;-landscape&quot;) THEN
          orientation := ORD(JunoConfig.Orientation.Landscape); INC(i)
        ELSIF Text.Equal(arg, &quot;-config&quot;) THEN
          INC(i);
          IF i &gt;= Params.Count THEN
            PrintUsage(arg &amp; &quot; option requires an argument&quot;)
          END;
          configFile := Params.Get(i); INC(i)
        ELSIF Text.Equal(arg, &quot;-display&quot;) THEN
          INC(i);
          IF i &gt;= Params.Count THEN
            PrintUsage(arg &amp; &quot; option requires an argument&quot;)
          END;
          disp := Params.Get(i); INC(i)
        ELSIF Text.Equal(arg, &quot;-geometry&quot;) THEN
          INC(i);
          IF i &gt;= Params.Count THEN
            PrintUsage(arg &amp; &quot; option requires an argument&quot;)
          END;
          geom := Params.Get(i); INC(i);
        ELSIF Text.Equal(arg, &quot;-writepkl&quot;) THEN
          writepkl := TRUE
        ELSIF Text.Equal(arg, &quot;-nopkl&quot;) THEN
          readpkl := FALSE
        ELSE
          PrintUsage(&quot;unrecognized flag: \&quot;&quot; &amp; arg &amp; &quot;\&quot;&quot;)
        END
      ELSE
        fileName := arg; INC(i);
        EXIT (* filename only allowed as last argument *)
      END
    END;
    IF i &lt; Params.Count THEN PrintUsage(&quot;too many arguments&quot;) END;

    TRY
      (* initialize values from configuration file *)
      configFile := JunoConfig.Init(configFile);

      (* change overrides from the command-line *)
      IF origin # -1 THEN
  	JunoConfig.origin := VAL(origin, JunoConfig.Origin)
      END;
      IF orientation # -1 THEN
  	JunoConfig.orientation := VAL(orientation, JunoConfig.Orientation)
      END;
    EXCEPT
      OSError.E =&gt;
  	Wr.PutText(Stdio.stderr, &quot;Error: unable to open configuration file \&quot;&quot;
  	  &amp; configFile &amp; &quot;\&quot;\n&quot;);
  	Process.Exit(1);
    | JunoConfig.Error (msg) =&gt;
  	Wr.PutText(Stdio.stderr, &quot;Error reading configuration file:\n&quot;);
  	Wr.PutText(Stdio.stderr, &quot;  &quot; &amp; msg &amp; &quot;\n&quot;);
  	Process.Exit(1);
    END
  END;

  TRY
    (* install top-level window *)
    &lt;* FATAL Rsrc.NotFound *&gt;
    BEGIN
      startup := NEW(FormsVBT.T).initFromRsrc(
    	&quot;Startup.fv&quot;, JunoRsrc.Path, raw := TRUE)
    END;
    IF NOT writepkl THEN
      JunoWM.Install(startup, disp, geom, applName := &quot;Juno&quot;)
    END;

    (* initialize globals *)
    builtInScope := CompileFile(root, &quot;BuiltIn.juno&quot;, parent:=NIL).public_scp;
    BuiltInSlots.Init(builtInScope);
    modScope := JunoScope.New(p := builtInScope, size := 50);
    root := NEW(View.Root, ccmd := NIL, marquee := NIL,
      dTrue := TRUE, astTrue := TRUE, sTrue := FALSE, eTrue := FALSE);
    root.ccmd := CurrCmd.New(ast := JunoAST.SkipVal,
      scp := JunoScope.New(modScope));
    root.marquee := NEW(Marquee.T).init(font := JunoConfig.codeFont);
    drawing := NEW(Drawing.T).init(
      NEW(Drawing.Child).init(JunoConfig.origin), root);
    w := NEW(Window, root := root, fileName := fileName, scope := modScope,
      mu := NEW(MUTEX), untilDone := NEW(Thread.Condition)).init(drawing);
    source := NEW(SourceView, w := w).init(root);
    editor := NEW(EditorView, w := w).init(&quot;&quot;);
    psImpl := PSImpl.New(root);
    root.source := source;
    root.drawing := drawing;
    root.currView := drawing;
    root.editor := editor;

    (* fill in generic windows in top-level form *)
    FormsVBT.PutGeneric(w, &quot;toolbox&quot;, w.toolbox);
    FormsVBT.PutGeneric(w, &quot;drawing&quot;, NEW(DblBufferVBT.T).init(drawing));
    FormsVBT.PutGeneric(w, &quot;source&quot;, ZSplit.New(source));
    FormsVBT.PutGeneric(w, &quot;editor&quot;, NEW(TextEditVBT.T, tp := editor).init());

    (* fill in configuration overrides (if any) *)
    LOCK VBT.mu DO
      SetTEFont(FormsVBT.GetVBT(w, &quot;configDefault&quot;),   JunoConfig.codeFont);
      SetTEFont(FormsVBT.GetVBT(w, &quot;configOverrides&quot;), JunoConfig.codeFont);
    END;
    IF configFile # NIL THEN
      VAR wr := TextWr.New(); BEGIN
        TRY
          &lt;* FATAL Wr.Failure *&gt; (* TextWr.T's never fail *)
          VAR rd := FileRd.Open(configFile); BEGIN
            (* copy &quot;rd&quot; to &quot;wr&quot; *)
            WHILE NOT Rd.EOF(rd) DO
              &lt;* FATAL Rd.EndOfFile *&gt;
              BEGIN Wr.PutChar(wr, Rd.GetChar(rd)) END
            END
          END
        EXCEPT
          OSError.E, Rd.Failure =&gt;
            Wr.PutText(wr, &quot;; Error reading configuration file:\n&quot;);
            Wr.PutText(wr, &quot;; \&quot;&quot; &amp; configFile &amp; &quot;\&quot;&quot;);
        END;
        (* put the text in the &quot;Configure&quot; dialog box *)
        LOCK VBT.mu DO
          FormsVBT.PutText(w, &quot;configOverrides&quot;, TextWr.ToText(wr))
        END
      END
    END;

    (* Bind modules with external procedures *)
    JunoScope.Bind(modScope, Atom.FromText(&quot;PS&quot;), psImpl);
    JunoScope.Bind(modScope, Atom.FromText(&quot;Print&quot;), PrintImpl.New());
    JunoScope.Bind(modScope, Atom.FromText(&quot;Random&quot;), RandomImpl.New());
    JunoScope.Bind(modScope, Atom.FromText(&quot;Text&quot;), TextImpl.New());
    JunoScope.Bind(modScope, Atom.FromText(&quot;Time&quot;), TimeImpl.New());
    JunoScope.Bind(modScope, Atom.FromText(&quot;Unit&quot;), UnitImpl.New(root));
    JunoScope.Bind(modScope, JunoUIImpl.ModSym, JunoUIImpl.New(root));

    (* Compile modules specified in &quot;Bundled.modlist&quot; and &quot;Juno.modlist&quot;
       (if any) configuration files. *)
    &lt;* FATAL Rsrc.NotFound *&gt;
    VAR modList: TextList.T := NIL; BEGIN
      CompileModules(w, RsrcOpen(&quot;Bundled.modlist&quot;), modList, fromRsrc :=TRUE);
      TRY CompileModules(w, FileRd.Open(&quot;Juno.modlist&quot;), modList) EXCEPT
        OSError.E =&gt; (* SKIP *)
      END;
      FormsVBT.PutText(startup, &quot;initModule&quot;, &quot;&quot;);
      FillBrowser(FormsVBT.GetVBT(w, &quot;moduleBrowser&quot;),
        TextListSort.SortD(modList));
    END;

    (* Open the built-in modules *)
    LOCK VBT.mu DO
      OpenModule(w, &quot;PS&quot;,     hasPrivate := FALSE);
      OpenModule(w, &quot;Print&quot;,  hasPrivate := FALSE, show := FALSE);
      OpenModule(w, &quot;Random&quot;, hasPrivate := FALSE, show := FALSE);
      OpenModule(w, &quot;Text&quot;,   hasPrivate := FALSE, show := FALSE);
      OpenModule(w, &quot;Time&quot;,   hasPrivate := FALSE, show := FALSE);
      OpenModule(w, &quot;Unit&quot;,   hasPrivate := FALSE, show := FALSE);
      OpenModule(w, &quot;JunoUI&quot;, hasPrivate := FALSE, show := FALSE)
    END;

    (* Switch TSplit to its first child (i.e, the Juno window) *)
    FormsVBT.PutText(w, &quot;version&quot;, JunoVersion.Name);
    FormsVBT.PutGeneric(startup, &quot;mainChild&quot;, w);
    FormsVBT.PutInteger(startup, &quot;tsplit&quot;, 0);

    (* Load the initial module *)
    LOCK w.mu DO
      w.stat := SyncStat.NotDone;
      &lt;* FATAL VBT.Error *&gt; BEGIN
        VBT.Forge(w, LoadInitialModule)
      END;
      WHILE w.stat = SyncStat.NotDone DO
        Thread.Wait(w.mu, w.untilDone)
      END
    END;

    (* Install animation window *)
    IF zeusOption THEN
      animObj := NEW(JunoZeus.T).init(w, w.root, JunoConfig.origin);
      Trestle.Install(animObj.w, windowTitle := &quot;Juno Animation&quot;);
      &lt;* FATAL Thread.Alerted *&gt; BEGIN
        NetObj.Export(&quot;JunoZeus&quot;, animObj)
      END
    END;

    FVFilter.MakeActive(w, &quot;background&quot;);
    IF NOT writepkl THEN
      checkpointThread := Thread.Fork(NEW(CheckpointClosure, w := w));
      Trestle.AwaitDelete(startup)
    ELSE
      &lt;* FATAL OSError.E, Pickle.Error *&gt;
      VAR wr := FileWr.Open(&quot;big.pkl&quot;); BEGIN
        Editor.SaveSlots(wr);
        JunoCompile.SaveSlots(wr);
        Pickle.Write(wr, w);
        Wr.Flush(wr);
        Wr.Close(wr)
      END
    END
  EXCEPT
  | JunoWM.Error (txt) =&gt;
      PrintUsage(txt)
  | TrestleComm.Failure =&gt;
      Wr.PutText(Stdio.stderr, &quot;Fatal error: &quot;);
      Wr.PutText(Stdio.stderr, &quot;unable to connect to window system.\n&quot;);
      Wr.Flush(Stdio.stderr)
  | NetObj.Error =&gt;
      Wr.PutText(Stdio.stderr, &quot;Fatal error: &quot;);
      Wr.PutText(Stdio.stderr, &quot;unable to export JunoZeus object.\n&quot;);
      Wr.Flush(Stdio.stderr)
  END
END Juno.
</PRE>
</inModule>
<HR>
<A NAME="x1">interface Source is in:
</A><UL>
<LI><A HREF="../../cm3ide/src/nodes/Source.i3.html#0TOP0">cm3ide/src/nodes/Source.i3</A>
<LI><A HREF="Source.i3.html#0TOP0">juno-app/src/Source.i3</A>
</UL>
<P>
<HR>
<A NAME="x2">interface Editor is in:
</A><UL>
<LI><A HREF="../../cm3ide/src/misc/Editor.i3.html#0TOP0">cm3ide/src/misc/Editor.i3</A>
<LI><A HREF="Editor.i3.html#0TOP0">juno-app/src/Editor.i3</A>
</UL>
<P>
<HR>
<A NAME="x3">interface View is in:
</A><UL>
<LI><A HREF="View.i3.html#0TOP0">juno-app/src/View.i3</A>
<LI><A HREF="../../zeus/src/View.i3.html#0TOP0">zeus/src/View.i3</A>
</UL>
<P>
<PRE>























</PRE>
</BODY>
</HTML>
