<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: juno-app/src/Drag.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>juno-app/src/Drag.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright (C) 1992, Digital Equipment Corporation                         
 All rights reserved.                                                      
 See the file COPYRIGHT for a full description.                            
                                                                           
 Last modified on Sun Oct 26 13:55:27 PST 1997 by heydon                   
      modified on Tue Feb  7 15:53:46 PDT 1995 by gnelson                  
<PRE>&lt;* PRAGMA LL *&gt;

MODULE <module><implements><A HREF="Drag.i3.html">Drag</A></implements></module>;

IMPORT <A HREF="#x1">View</A>, <A HREF="Drawing.i3.html">Drawing</A>, <A HREF="JunoUIImpl.i3.html">JunoUIImpl</A>, <A HREF="PSImpl.i3.html">PSImpl</A>, <A HREF="#x2">Source</A>, <A HREF="JunoPt.i3.html">JunoPt</A>, <A HREF="CurrCmd.i3.html">CurrCmd</A>;
IMPORT   <A HREF="ExternalProc.i3.html">ExternalProc</A>, <A HREF="JunoError.i3.html">JunoError</A>;
IMPORT <A HREF="../../juno-compiler/src/JunoAST.i3.html">JunoAST</A>, <A HREF="../../juno-compiler/src/JunoASTUtils.i3.html">JunoASTUtils</A>, <A HREF="../../juno-compiler/src/JunoScope.i3.html">JunoScope</A>, <A HREF="../../juno-compiler/src/JunoCompile.i3.html">JunoCompile</A>, <A HREF="../../juno-compiler/src/JunoCompileErr.i3.html">JunoCompileErr</A>;
IMPORT <A HREF="../../juno-machine/src/JunoRT.i3.html">JunoRT</A>, <A HREF="../../juno-machine/src/RTVal.i3.html">RTVal</A>, <A HREF="../../juno-machine/src/JunoArgs.i3.html">JunoArgs</A>, <A HREF="../../juno-machine/src/JunoRTError.i3.html">JunoRTError</A>;
IMPORT <A HREF="../../ui/src/vbt/VBT.i3.html">VBT</A>, <A HREF="../../ui/src/vbt/Cursor.i3.html">Cursor</A>, <A HREF="../../libm3/src/geometry/Point.i3.html">Point</A>, <A HREF="../../ui/src/vbt/PaintOp.i3.html">PaintOp</A>, <A HREF="../../ui/src/split/Filter.i3.html">Filter</A>, <A HREF="../../ui/src/split/DblBufferVBT.i3.html">DblBufferVBT</A>;
IMPORT <A HREF="../../libm3/src/atom/Atom.i3.html">Atom</A>, <A HREF="../../m3core/src/thread/Common/Thread.i3.html">Thread</A>, <A HREF="../../libm3/src/fmtlex/Fmt.i3.html">Fmt</A>;
</PRE> For debugging: 
<PRE>IMPORT <A HREF="../../libm3/src/rw/Wr.i3.html">Wr</A>, <A HREF="../../libm3/src/rw/Stdio.i3.html">Stdio</A>, <A HREF="../../m3core/src/time/Common/Time.i3.html">Time</A>;

REVEAL
  <A NAME="T">T</A> = Public BRANDED &quot;Drag.T&quot; OBJECT
    (* The following fields are used by the &quot;DuringDrag&quot;, &quot;PostDrag&quot;,
       &quot;RunDeltaCmd&quot;, and &quot;CallbackProc&quot; procedures. The first two of these
       procedures are invoked by a Trestle thread in response to a mouse
       motion or up-click, and the latter two are invoked by the thread that
       is forked to run the compiled drag command. *)
    firstTime: BOOLEAN;    (* unprotected; only accessed by drag loop thread *)
    mu: MUTEX := NIL;      (* protects the following fields *)
    c: Thread.Condition := NIL;
    lastReady: BOOLEAN;
    last: Point.T;
    deltaReady: BOOLEAN;
    delta: Point.T;	   (* read/written by &quot;DuringDrag&quot; *)
    done: BOOLEAN;         (* set by &quot;PostDrag&quot; *)
  END;

REVEAL
  <A NAME="ArgTool">ArgTool</A> = ArgToolPublic BRANDED &quot;Drag.ArgTool&quot; OBJECT OVERRIDES
    pre := PreNoop;
    post := PostNoop;
    during := DuringNoop;
  END;

VAR (*CONST*)
  DragColor := PaintOp.FromRGB(1.0, 0.0, 0.0);
</PRE> Default Pre, Post, During methods --------------------------------------- 

<P><PRE>PROCEDURE <A NAME="PreNoop"><procedure>PreNoop</procedure></A>(
    &lt;*UNUSED*&gt; tl: ArgTool;
    d: Drawing.T;
    &lt;*UNUSED*&gt; READONLY cd: VBT.MouseRec;
    &lt;*UNUSED*&gt; i: INTEGER) =
  VAR ch: Drawing.Child := Filter.Child(d); BEGIN
    DblBufferVBT.Save(ch);
    Drawing.PaintPoint(ch, d.dragName, DragColor, d.draggee)
  END PreNoop;

PROCEDURE <A NAME="DuringNoop"><procedure>DuringNoop</procedure></A>(
    &lt;*UNUSED*&gt; tl: ArgTool;
    d: Drawing.T;
    READONLY delta: Point.T;
    &lt;*UNUSED*&gt; i: INTEGER): Point.T RAISES {} =
  VAR
    ch: Drawing.Child := Filter.Child(d);
    new := Point.Add(d.draggee, ProjectMode(delta, d.dragMode));
  BEGIN
    DblBufferVBT.Restore(ch);
    Drawing.PaintPoint(ch, d.dragName, DragColor, new);
    Drawing.Sync(ch);
    RETURN new
  END DuringNoop;

PROCEDURE <A NAME="ProjectMode"><procedure>ProjectMode</procedure></A>(READONLY delta: Point.T; mode: DragMode): Point.T =
</PRE><BLOCKQUOTE><EM> Return the point <CODE>delta</CODE> projected to the horizontal or vertical axis
   according to the dragging mode <CODE>mode</CODE>. </EM></BLOCKQUOTE><PRE>
  BEGIN
    CASE mode OF
      DragMode.Unconstrained =&gt; RETURN delta
    | DragMode.Hor =&gt; RETURN Point.T{delta.h, 0}
    | DragMode.Ver =&gt; RETURN Point.T{0, delta.v}
    END
  END ProjectMode;

PROCEDURE <A NAME="PostNoop"><procedure>PostNoop</procedure></A>(
    &lt;*UNUSED*&gt; tl: ArgTool;
    &lt;*UNUSED*&gt; d: Drawing.T;
    &lt;*UNUSED*&gt; READONLY cd: VBT.MouseRec;
    &lt;*UNUSED*&gt; i: INTEGER) =
  BEGIN END PostNoop;
</PRE> New Dragging Tools ------------------------------------------------------ 

<P><PRE>TYPE
  DraggingTool = Drawing.ArgTool BRANDED &quot;Drag.DraggingTool&quot; OBJECT
    forkee: Thread.T;
    execRes: JunoRT.ExecRes
  OVERRIDES
    pre    := PreDrag;
    during := DuringDrag;
    post   := PostDrag;
    apply  := ApplyDrag
  END;
</PRE> A <CODE>DraggingTool</CODE> is the tool used to implement continuous solve dragging.
   It's <CODE>pre</CODE>, <CODE>during</CODE>, <CODE>post</CODE>, and <CODE>apply</CODE> methods work together. If <CODE>tl</CODE> is
   a <CODE>DraggingTool</CODE>, then <CODE>tl.pre</CODE> forks off a thread and sets <CODE>tl.forkee</CODE> to
   this thread. The thread is set running a compiled command (a loop) that
   gets new values for the dragged point by synchronizing with the <CODE>during</CODE>
   method, and determines when the user has stopped dragging by synchronizing
   with the <CODE>post</CODE> method. This command is contructed by the <CODE>CompileDrag</CODE>
   procedure below. When the thread finishes, <CODE>tl.execRes</CODE> is set to the
   execution result. 

<P><PRE>PROCEDURE <A NAME="NewTool"><procedure>NewTool</procedure></A>(): Drawing.ArgTool =
  VAR args := Drawing.NewArgArray(1, Drawing.ArgType.Drag); BEGIN
    RETURN NEW(DraggingTool, argType := args)
  END NewTool;
</PRE> Drag Tool Pre, Post, During, Apply methods ------------------------------ 

<P><PRE>TYPE
  Closure = Thread.Closure BRANDED &quot;Drag.Closure&quot; OBJECT
    d: Drawing.T;
    slot: CARDINAL;
  OVERRIDES
    apply := RunDeltaCmd
  END;

  (* A &quot;Drag.Closure&quot; is the closure used to fork the compiled drag command.
     If &quot;cl: Closure&quot;, &quot;cl.apply&quot; executes the bytestream in &quot;cl.slot&quot;, using
     the fields of &quot;cl.d&quot; (for example, &quot;cl.d.mu&quot;, &quot;cl.d.c&quot;, &quot;cl.d.firstTime&quot;,
     and &quot;cl.d.done&quot;) to synchronize with the drag command. *)

PROCEDURE <A NAME="RunDeltaCmd"><procedure>RunDeltaCmd</procedure></A>(cl: Closure): REFANY =
</PRE><BLOCKQUOTE><EM> The <CODE>apply</CODE> method of a <CODE>Drag.Closure</CODE>; i.e., this procedure is called by
   the thread that gets forked to run the compiled drag command. It runs the
   byte-code program in code table slot <CODE>cl.slot</CODE>. Returns a pointer to the
   resulting trapcode. </EM></BLOCKQUOTE><PRE>
  VAR res := NEW(REF JunoRT.ExecRes); BEGIN
    cl.d.firstTime := TRUE;
    res^ := JunoRT.ExecFromSlot(cl.slot, reset := TRUE);
    &lt;* ASSERT res^.trapCode = JunoRT.TrapCode.Error *&gt;
    (* If the current command failed, abort the dragging prematurely by
       setting &quot;cl.d.done&quot;. *)
    IF res.errorCode # JunoRTError.Code.Halt THEN
      LOCK cl.d.mu DO cl.d.done := TRUE END
    END;
    (* signal thread block in &quot;DuringDrag&quot; *)
    Thread.Signal(cl.d.c);
    RETURN res
  END RunDeltaCmd;

VAR
  debug := 0;
  start: Time.T;			 (* start of drag *)
  count: CARDINAL;			 (* number of drag frames *)

VAR (*CONST*)
  WatchCursor: Cursor.T;

PROCEDURE <A NAME="PreDrag"><procedure>PreDrag</procedure></A>(
    tl: DraggingTool;
    d: Drawing.T;
    READONLY cd: VBT.MouseRec;
    i: INTEGER)
    RAISES {Aborted} =
  &lt;* LL.sup = VBT.mu *&gt;
  VAR ch := Filter.Child(d); BEGIN
    DblBufferVBT.ClearSaved(ch);
start := Time.Now();
count := 0;
    IF d.mu = NIL THEN
      d.mu := NEW(MUTEX);
      d.c := NEW(Thread.Condition)
    END;
    LOCK d.mu DO
      d.last := d.draggee;
      d.lastReady := FALSE;
      d.deltaReady := FALSE;
      d.done := FALSE;
    END;
    (* hilight the selected point and display watch cursor *)
    Drawing.PaintPoint(ch, d.dragName, DragColor, d.draggee);
    VBT.SetCursor(ch, WatchCursor);
    Drawing.Sync(ch);
    (* initialize &quot;forkee&quot; in case &quot;CompileDrag&quot; raises an exception *)
    tl.forkee := NIL;
    TRY
      (* Fork off a new thread to run the compiled Juno command. *)
      tl.forkee := Thread.Fork(NEW(Closure, d := d,
        slot := CompileDrag(d, d.stack[i].name, cd.time)));
    FINALLY
      (* reset the cursor *)
      VBT.SetCursor(ch, Cursor.DontCare)
    END
  END PreDrag;

VAR (* CONST *)
  DragLoopAtom := Atom.FromText(&quot;Drag Loop&quot;);
  NewptAtom    := Atom.FromText(&quot;_newpt&quot;);
  OldptAtom    := Atom.FromText(&quot;_oldpt&quot;);

VAR (* CONST *)
  LoopMu := NEW(MUTEX);
  LoopHeader := NEW(JunoAST.ProcHeader, name := DragLoopAtom,
    ins := JunoAST.EmptyIdList, outs := JunoAST.EmptyIdList,
    inouts := JunoAST.EmptyIdList, bp := JunoAST.End);
  LoopDecl := NEW(JunoAST.ProcDecl, header := LoopHeader, bp := JunoAST.End);
  Newpt := NEW(JunoAST.QId, bp := JunoAST.End,
    id0 := JunoAST.NilId, id1 := NewptAtom);
  Oldpt := NEW(JunoAST.QId, bp := JunoAST.End,
    id0 := JunoAST.NilId, id1 := OldptAtom);
  NewGrd := NEW(JunoAST.Differs, bp := JunoAST.End,
    e1 := Newpt, e2 := JunoAST.NilVal);
  Locals := NEW(JunoAST.NearVarList, bp := JunoAST.End, size := 2, head :=
    NEW(JunoAST.NearVarLink, id := NewptAtom, hint := JunoAST.NilExpr, next :=
    NEW(JunoAST.NearVarLink, id := OldptAtom, hint := JunoAST.NilExpr)));
  IfCmd := NEW(JunoAST.If, bp := JunoAST.End);

PROCEDURE <A NAME="CompileDrag"><procedure>CompileDrag</procedure></A>(d: Drawing.T; p: JunoAST.QId; ts: VBT.TimeStamp):
    CARDINAL RAISES {Aborted} =
</PRE><BLOCKQUOTE><EM> Let <CODE>p</CODE> be the name of the dragged point, and let <CODE>r</CODE> denote the rest
   of the points in the current command. Let the current command C have the
   form:
<P>
<PRE>
          VAR nv IN P(nv) -&gt; A END
</PRE>
   and let P' be P conjoined with any equality constraints in <CODE>nv</CODE> (except for
   equality constraints on the dragged point <CODE>p</CODE>). Then create, compile,
   install, and return the slot for the following <CODE>drag</CODE> command:
<P>
<PRE>
          IF
          	VAR nv IN
          	  VAR _newpt, _oldpt IN
          	    _newpt := JunoUI._DRAG(p)
          	  ; DO _newpt # NIL -&gt;
          	      _oldpt := p
          	    ; p := _newpt                             ***
          	    ; { P'?(r) | P'?(p,r) | p := _oldpt }     ***
          	    ; IF A FI
          	    ; _newpt := JunoUI._DRAG(p)
          	    OD
          	  ; HALT
          	  END
          	END
          FI
</PRE>
   In the event that <CODE>d.dragMode</CODE> is <CODE>DragMode.Hor</CODE> or <CODE>DragMode.Ver</CODE>, we
   have to compile a slightly different command. In that case, the two lines
   with (**</EM></BLOCKQUOTE><PRE> beside them become:

|    DragMode.Hor:                    DragMode.Ver:
|      ; p := (CAR(p), CDR(_newpt))     ; p := (CAR(_newpt), CDR(p))
|      ; { PH'?(p,r) | p := _oldpt }    ; { PV'?(p,r) | p := _oldpt }

   where &quot;PH'&quot; is &quot;P' AND p HOR _newpt&quot; and &quot;PV'&quot; is &quot;P' AND p VER _newpt&quot;.

   Note how the drag command is cobbled together from syntactic parts of the
   current command, rather than from semantic parts. In particular, the drag
   command only solves for the variables listed at the outermost level of the
   current command, and it solves these variables only for the outermost
   predicate P. This technique fails to reflect the true semantics of the
   current command, for example, if A has the form:

|     VAR nv' IN Q(nv, nv') -&gt; A' END

   Now, &quot;Grd(C) = P(nv) AND Q(nv, nv') AND Grd(A')&quot;, and the solver solves the
   current command for the set of variables &quot;nv \union nv'&quot;. But, as previously
   mentioned, the drag command solves only for the variables &quot;nv&quot; such that
   &quot;P(nv)&quot;. To rectify this problem, we would have to inductively define the
   &quot;solve variables&quot; of a command and the &quot;non-guard&quot; part of a command. We
   could then compile a drag command that solved the &quot;SolveVars(C)&quot; for
   &quot;Grd(C)&quot;, and then executed &quot;NonGuard(C)&quot; in the drag loop. *)
  &lt;* LL.sup = VBT.mu *&gt;
  VAR
    ast := CurrCmd.GetAST(d.root.ccmd);
    locals := CurrCmd.GetVariables(ast);
    con := Conjoin(CurrCmd.GetConstraint(ast), EqConstraints(locals, p.id1));
    cmd := SkipifyBody(ast, d.root.skipify);
    allVars := MoveToFront(locals, p.id1);
    dragCall := NEW(JunoAST.ProcCall, bp := JunoAST.End,
      outs := JunoASTUtils.NewQIdList(Newpt),
      inouts := JunoAST.EmptyExprList,
      name := JunoASTUtils.QIdFromIds(JunoUIImpl.ModSym, JunoUIImpl.DragSym),
      ins := JunoASTUtils.NewExprList(p));
    innerSemi := NEW(JunoAST.Seq, bp := JunoAST.End,
      c1 := JunoASTUtils.NewAssign(Oldpt, p),
      c2 := NEW(JunoAST.Seq, bp := JunoAST.End,
        c1 := HintP(p, d.dragMode),
        c2 := NEW(JunoAST.Seq, bp := JunoAST.End,
          c1 := SolveCmd(p, con, allVars, d.dragMode),
          c2 := NEW(JunoAST.Seq, bp := JunoAST.End,
            c1 := cmd, c2 := dragCall))));
    do := NEW(JunoAST.Do, bp := JunoAST.End,
      body := NEW(JunoAST.Guard, bp := JunoAST.End,
      grd := NewGrd, body := innerSemi));
    outerSemi := NEW(JunoAST.Seq, bp := JunoAST.End, c1 := dragCall,
      c2 := NEW(JunoAST.Seq, bp := JunoAST.End, c1 := do,
        c2 := JunoAST.HaltVal));
    innerProj := NEW(JunoAST.Proj, bp := JunoAST.End,
      vars := Locals, body := outerSemi);
    outerProj := NEW(JunoAST.Proj, bp := JunoAST.End,
      vars := locals, body := innerProj);
    outerIf := NEW(JunoAST.If, bp := JunoAST.End, body := outerProj);
    proc: JunoScope.Proc;
  BEGIN
    LOCK LoopMu DO
      LoopDecl.body := outerIf;
      TRY
        proc := JunoScope.NewProc(LoopDecl, mod := NIL);
        EVAL JunoCompile.ProcDecl(DragLoopAtom, proc,
          CurrCmd.GetScope(d.root.ccmd))
      EXCEPT
        JunoCompileErr.Error (err) =&gt;
          Source.ShowError(d.root.source, ast, err, ts);
          RAISE Aborted
      END
    END;
    RETURN proc.index
  END CompileDrag;

PROCEDURE <A NAME="MoveToFront"><procedure>MoveToFront</procedure></A>(l: JunoAST.NearVarList; id: JunoAST.Id):
    JunoAST.NearVarList =
</PRE><BLOCKQUOTE><EM> Return a list like <CODE>l</CODE> such that all occurrences of variables named <CODE>id</CODE>
   appear first in the result. The order of the other variables in the result
   is undefined. Each variable in the result has its <CODE>evar</CODE> bit reset, is
   unhinted, and has its <CODE>frozen</CODE> bit set iff the corresponding variable in
   <CODE>l</CODE> was frozen or hinted. </EM></BLOCKQUOTE><PRE>
  VAR
    res := NEW(JunoAST.NearVarList, bp := l, size := l.size);
    curr := l.head; named: JunoAST.NearVarLink := NIL;
  BEGIN
    WHILE curr # NIL DO
      &lt;* ASSERT NOT curr.frozen OR curr.hint # JunoAST.NilExpr *&gt;
      IF curr.id = id THEN
        named := NEW(JunoAST.NearVarLink, id := curr.id, evar := FALSE,
          hint := JunoAST.NilExpr, frozen := (curr.hint # JunoAST.NilExpr),
          index := curr.index, next := named)
      ELSE
        res.head := NEW(JunoAST.NearVarLink, id := curr.id, evar := FALSE,
          hint := JunoAST.NilExpr, frozen := (curr.hint # JunoAST.NilExpr),
          index := curr.index, next := res.head)
      END;
      curr := curr.next
    END;
    IF named # NIL THEN
      (* ``res.head := Append(named, res.head)'' *)
      curr := named;
      WHILE curr.next # NIL DO curr := curr.next END;
      curr.next := res.head;
      res.head := named
    END;
    RETURN res
  END MoveToFront;

PROCEDURE <A NAME="EqConstraints"><procedure>EqConstraints</procedure></A>(l: JunoAST.NearVarList; id: JunoAST.Id):
    JunoAST.Formula =
</PRE><BLOCKQUOTE><EM> Return a conjunction containing a conjunct for each frozen variable in <CODE>l</CODE>
   other than <CODE>id</CODE>. Return NIL if there are no variables in <CODE>l</CODE> other than
   <CODE>id</CODE>. </EM></BLOCKQUOTE><PRE>
  VAR res: JunoAST.Formula := NIL; curr := l.head; eq: JunoAST.Equals; BEGIN
    WHILE curr # NIL DO
      &lt;* ASSERT NOT curr.frozen OR curr.hint # JunoAST.NilExpr *&gt;
      IF curr.id # id AND curr.frozen THEN
        eq := NEW(JunoAST.Equals, bp := JunoAST.End, near := FALSE,
          e1 := JunoASTUtils.QIdFromNearVar(curr), e2 := curr.hint);
        res := Conjoin(eq, res);
      END;
      curr := curr.next
    END;
    RETURN res
  END EqConstraints;

PROCEDURE <A NAME="Conjoin"><procedure>Conjoin</procedure></A>(f1, f2: JunoAST.Formula): JunoAST.Formula =
</PRE><BLOCKQUOTE><EM> If <CODE>f2 # NIL</CODE>, return the AST <CODE>f1 AND f2</CODE>; otherwise, return <CODE>f1</CODE> </EM></BLOCKQUOTE><PRE>
  BEGIN
    IF f2 = NIL THEN RETURN f1 ELSE
      RETURN NEW(JunoAST.And, bp := JunoAST.End, f1 := f1, f2 := f2)
    END
  END Conjoin;

PROCEDURE <A NAME="SkipNamed"><procedure>SkipNamed</procedure></A>(l: JunoAST.NearVarList; id: JunoAST.Id):
    JunoAST.NearVarList =
</PRE><BLOCKQUOTE><EM> Return a list whose head points to the element just after the longest
   prefix of <CODE>l</CODE> containing links named <CODE>id</CODE>. </EM></BLOCKQUOTE><PRE>
  VAR
    res := NEW(JunoAST.NearVarList, bp := l, size := l.size);
    curr := l.head;
  BEGIN
    WHILE curr # NIL AND curr.id = id DO
      DEC(res.size); curr := curr.next
    END;
    res.head := curr;
    RETURN res
  END SkipNamed;

PROCEDURE <A NAME="SkipifyBody"><procedure>SkipifyBody</procedure></A>(ast: JunoAST.Cmd; skipify: BOOLEAN): JunoAST.Cmd =
</PRE><BLOCKQUOTE><EM> If <CODE>skipify</CODE>, return <CODE>SKIP</CODE>. Otherwise, return <CODE>IF ast FI</CODE>. </EM></BLOCKQUOTE><PRE>
  BEGIN
    IF skipify
      THEN RETURN JunoAST.SkipVal
      ELSE IfCmd.body := CurrCmd.GetCmd(ast); RETURN IfCmd
    END
  END SkipifyBody;

PROCEDURE <A NAME="HintP"><procedure>HintP</procedure></A>(p: JunoAST.QId; mode: DragMode): JunoAST.Cmd =
</PRE><BLOCKQUOTE><EM> Return the assignment statement appropriate for the drag mode <CODE>mode</CODE>:
<PRE>
         DragMode.Unconstrained: &quot;p := _newpt&quot;
         DragMode.Hor: &quot;p := (CAR(p), CDR(_newpt))
         DragMode.Ver: &quot;p := (CAR(_newpt), CDR(p))
</EM></BLOCKQUOTE><PRE>
  PROCEDURE CarCdrPair(e1, e2: JunoAST.Expr): JunoAST.Pair =
  (* Return the expression &quot;(CAR(e1), CDR(e2))&quot;. *)
    BEGIN
      RETURN NEW(JunoAST.Pair, bp := JunoAST.End,
        e1 := NEW(JunoAST.Car, bp := JunoAST.End, e := e1),
        e2 := NEW(JunoAST.Cdr, bp := JunoAST.End, e := e2))
    END CarCdrPair;
  BEGIN
    CASE mode OF
      DragMode.Unconstrained =&gt; RETURN JunoASTUtils.NewAssign(p, Newpt)
    | DragMode.Hor =&gt; RETURN JunoASTUtils.NewAssign(p, CarCdrPair(p, Newpt))
    | DragMode.Ver =&gt; RETURN JunoASTUtils.NewAssign(p, CarCdrPair(Newpt, p))
    END
  END HintP;

PROCEDURE <A NAME="SolveCmd"><procedure>SolveCmd</procedure></A>(p: JunoAST.QId; con: JunoAST.Formula;
    vars: JunoAST.NearVarList; mode: DragMode): JunoAST.Cmd =
</PRE><BLOCKQUOTE><EM> Return the solve command for the point <CODE>p</CODE> given dragging mode <CODE>mode</CODE>.
   <CODE>con</CODE> is the constraint to solve, and <CODE>vars</CODE> is the list of unknowns (with
   <CODE>p</CODE> moved to the front for easy processing). The different cases for <CODE>mode</CODE>
   are:
<P>
<PRE>
         DragMode.Unconstrained: { P'?(r) | P'?(p,r) | p := _oldpt }
         DragMode.Hor: { ((p HOR _newpt) AND P')?(p,r) | p := _oldpt }
         DragMode.Ver: { ((p VER _newpt) AND P')?(p,r) | p := _oldpt }
</PRE>
   where P' is the set of constraints <CODE>con</CODE> and <CODE>r</CODE> is the set of variables
   <CODE>vars</CODE> minus the variable <CODE>p</CODE>. </EM></BLOCKQUOTE><PRE>
  VAR res: JunoAST.Cmd; BEGIN
    CASE mode OF
      DragMode.Hor =&gt;
        con := Conjoin(NEW(JunoAST.Hor, e1 := p, e2 := Newpt), con)
    | DragMode.Ver =&gt;
        con := Conjoin(NEW(JunoAST.Ver, e1 := p, e2 := Newpt), con)
    | DragMode.Unconstrained =&gt; (* SKIP *)
    END;
    res := NEW(JunoAST.Else, bp := JunoAST.End,
      c1 := NEW(JunoAST.Query, bp := JunoAST.End, f := con, vars := vars),
      c2 := JunoASTUtils.NewAssign(p, Oldpt));
    IF mode = DragMode.Unconstrained THEN
      res := NEW(JunoAST.Else, bp := JunoAST.End, c2 := res,
        c1 := NEW(JunoAST.Query, bp := JunoAST.End,
          f := con, vars := SkipNamed(vars, p.id1)))
    END;
    RETURN res
  END SolveCmd;

PROCEDURE <A NAME="CallbackProc"><procedure>CallbackProc</procedure></A>(cl: ExternalProc.Closure): BOOLEAN =
</PRE><BLOCKQUOTE><EM> This procedure is the implementation of the Juno procedure named by the
   global variable <CODE>JunoUIImpl.DragSym</CODE>, which is called inside the drag
   loop compiled by <CODE>CompileDrag</CODE>. Hence, it is called by the thread running
   the compiled Juno program, namely, the one forked in <CODE>PreDrag</CODE>.
<P>
   It is called with the current value of the point being dragged (as a Juno
   value representing a point in Juno coordinates). It sets <CODE>d.last</CODE> to the
   new point value (converted to Trestle coordinates), and signals the
   <CODE>DuringDrag</CODE> procedure that this value is ready.
<P>
   It then synchronizes with the <CODE>DuringDrag</CODE> procedure to get the delta
   for the difference between the mouse and the dragged point, adds this delta
   to the current value of the point, and returns (on the Juno stack) this
   value as the result. However, if <CODE>d.done</CODE>, then this procedure instead
   returns Juno <CODE>NIL</CODE>. </EM></BLOCKQUOTE><PRE>
  &lt;* LL.sup &lt; VBT.mu *&gt;
  VAR
    d := cl.rt.drawing;
    ch: Drawing.Child := Filter.Child(d);
    new: Point.T;
    res: RTVal.T;
  BEGIN
    &lt;* FATAL JunoPt.BadPt *&gt;
    VAR err := FALSE; pr: RTVal.Pair; BEGIN
      (* convert argument from Juno coords -&gt; Trestle coords *)
      pr := JunoArgs.ReadPair(1, err);
      &lt;* ASSERT NOT err *&gt;
      new := JunoPt.ToHV(JunoPt.FromValuePair(pr), ch.xform)
    END;

    IF d.firstTime THEN
      d.firstTime := FALSE;
    ELSE
      (* add final annotations to drawing *)
      Drawing.PaintPath(d);
      Drawing.PaintPoint(ch, d.dragName, DragColor, new);
      Drawing.Sync(ch);
      (* update shared data *)
      LOCK d.mu DO
        d.last := new;
        d.lastReady := TRUE;
        d.deltaReady := FALSE;
      END;
      (* signal &quot;DuringDrag&quot; thread *)
      Thread.Signal(d.c)
    END;
    LOCK d.mu DO
      (* wait for next call to &quot;DuringDrag&quot; (i.e., next mouse motion)
         or &quot;PostDrag&quot; (i.e., next up-click terminating drag) *)
      WHILE NOT d.deltaReady DO
        Thread.Wait(d.mu, d.c)
      END;
      IF d.done THEN res := RTVal.nil ELSE
        res := JunoPt.ToValuePair(JunoPt.FromHV(
          Point.Add(d.last, d.delta), ch.xform))
      END
    END;
    JunoArgs.WriteValue(2, res);
    (* if not done, restart a new drawing *)
    IF res # RTVal.nil THEN
      PSImpl.Reset(d);
      DblBufferVBT.Restore(ch);
    END;
    RETURN TRUE
  END CallbackProc;

PROCEDURE <A NAME="DuringDrag"><procedure>DuringDrag</procedure></A>(
    tl: DraggingTool;
    d: Drawing.T;
    &lt;*UNUSED*&gt; READONLY delta: Point.T;
    &lt;*UNUSED*&gt; i: INTEGER):
    Point.T RAISES {Aborted} =
</PRE><BLOCKQUOTE><EM> This is the procedure implementing the <CODE>during</CODE> method of the built-in
   <CODE>Drag</CODE> tool. This procedure is invoked by the <CODE>Drawing.Position</CODE> procedure
   each time the mouse moves.
<P>
   This procedure ignores its <CODE>delta</CODE> argument, and instead computes <CODE>d.delta</CODE>
   as the difference between the current mouse location <CODE>d.dragger</CODE> and the
   current value of the point being dragged <CODE>d.draggee</CODE>. In this way, we solve
   the system for the case where the dragged point is initially at the mouse
   cursor.
<P>
   If the thread running the compiled Juno command has set <CODE>d.done</CODE>, then that
   command hit a run-time error, so this procedure raises <CODE>Aborted</CODE> to abort
   the drag prematurely. </EM></BLOCKQUOTE><PRE>
  &lt;* LL.sup = VBT.mu *&gt;
  BEGIN
INC(count);
    IF tl.forkee = NIL THEN
      (* The drag was aborted during &quot;pre&quot; method due to compilation error,
         so just return the current location of the dragged point. *)
      RETURN d.draggee
    ELSE
      LOCK d.mu DO
    	d.delta := Point.Sub(d.dragger, d.draggee);
    	d.deltaReady := TRUE;
    	d.lastReady := FALSE
      END;
      (* signal &quot;CallbackProc&quot; thread *)
      Thread.Signal(d.c);
      LOCK d.mu DO
        (* wait for current frame to finish painting to get new value *)
    	WHILE NOT d.lastReady AND NOT d.done DO
    	  Thread.Wait(d.mu, d.c)
    	END;
    	IF d.done THEN RAISE Aborted END;
    	RETURN d.last
      END
    END
  END DuringDrag;

PROCEDURE <A NAME="PostDrag"><procedure>PostDrag</procedure></A>(
    tl: DraggingTool;
    d: Drawing.T;
    &lt;*UNUSED*&gt; READONLY cd: VBT.MouseRec;
    &lt;*UNUSED*&gt; i: INTEGER) =
  &lt;* LL.sup = VBT.mu *&gt;
  BEGIN
IF debug &gt; 0 THEN
  &lt;* FATAL Thread.Alerted, Wr.Failure *&gt;
  VAR
    delta := Time.Now() - start;
    usecs := ROUND(1.0d6 * delta) DIV count;
  BEGIN
    Wr.PutText(Stdio.stderr, &quot;Average time = &quot;
      &amp; Fmt.Pad(Fmt.Int(usecs), 8) &amp; &quot; usecs\n&quot;);
  END
END;
    IF tl.forkee # NIL THEN
      LOCK d.mu DO
        d.delta := Point.Origin;
        d.deltaReady := TRUE;
        d.lastReady := FALSE
      END;
      (* signal &quot;CallbackProc&quot; thread *)
      Thread.Signal(d.c);
      LOCK d.mu DO
        (* wait for current frame to finish painting *)
    	WHILE NOT d.lastReady AND NOT d.done DO
    	  Thread.Wait(d.mu, d.c)
    	END;
      END;
      (* now finish dragging *)
      LOCK d.mu DO
        d.done := TRUE;
        d.deltaReady := TRUE
      END;
      (* signal thread blocked in &quot;CallbackProc&quot; *)
      Thread.Signal(d.c);
      (* Wait for the thread running the compiled Juno command to terminate *)
      tl.execRes := NARROW(Thread.Join(tl.forkee), REF JunoRT.ExecRes)^
    END
  END PostDrag;

PROCEDURE <A NAME="ApplyDrag"><procedure>ApplyDrag</procedure></A>(
    tl: DraggingTool;
    d: Drawing.T;
    &lt;*UNUSED*&gt; READONLY arg: ARRAY OF Drawing.Arg) =
  VAR hintsChanged: BOOLEAN; BEGIN
    IF tl.forkee # NIL THEN
      &lt;* ASSERT tl.execRes.trapCode = JunoRT.TrapCode.Error *&gt;
      hintsChanged := CurrCmd.UpdateHints(d.root.ccmd);
      IF tl.execRes.errorCode # JunoRTError.Code.Halt THEN
        (* display an error message *)
        JunoError.Display(d, JunoRT.TrapMessage(tl.execRes))
      ELSE
        (* finish running current command if it ran up to final &quot;HALT&quot; *)
    	VAR execRes2 := JunoRT.Exec(); BEGIN
    	  &lt;* ASSERT execRes2.trapCode = JunoRT.TrapCode.NormalHalt *&gt;
    	END
      END;
      IF hintsChanged THEN
        Drawing.SourceUntrue(d, View.ModKind.ImplicitConsistent)
      END
    END;
    Drawing.Annotations(d)
  END ApplyDrag;

BEGIN
  WatchCursor := Cursor.FromName(ARRAY OF TEXT{&quot;XC_watch&quot;})
END Drag.
</PRE>
</inModule>
<HR>
<A NAME="x1">interface View is in:
</A><UL>
<LI><A HREF="View.i3.html#0TOP0">juno-app/src/View.i3</A>
<LI><A HREF="../../zeus/src/View.i3.html#0TOP0">zeus/src/View.i3</A>
</UL>
<P>
<HR>
<A NAME="x2">interface Source is in:
</A><UL>
<LI><A HREF="../../cm3ide/src/nodes/Source.i3.html#0TOP0">cm3ide/src/nodes/Source.i3</A>
<LI><A HREF="Source.i3.html#0TOP0">juno-app/src/Source.i3</A>
</UL>
<P>
<PRE>























</PRE>
</BODY>
</HTML>
