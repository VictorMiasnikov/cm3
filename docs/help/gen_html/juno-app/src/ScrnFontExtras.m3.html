<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: juno-app/src/ScrnFontExtras.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>juno-app/src/ScrnFontExtras.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright (C) 1992, Digital Equipment Corporation                         
 All rights reserved.                                                      
 See the file COPYRIGHT for a full description.                            
                                                                           
 Last modified on Mon Dec 15 13:59:24 PST 1997 by heydon                   

<P><PRE>MODULE <module><implements><A HREF="ScrnFontExtras.i3.html">ScrnFontExtras</A></implements></module>;

IMPORT <A HREF="../../ui/src/vbt/ScrnFont.i3.html">ScrnFont</A>, <A HREF="../../libm3/src/geometry/Rect.i3.html">Rect</A>, <A HREF="../../m3core/src/text/Text.i3.html">Text</A>;
</PRE> Most of this was copied from the implementation in <CODE>ScrnFont.m3</CODE>. 

<P><PRE>PROCEDURE <A NAME="TightBoundingBox"><procedure>TightBoundingBox</procedure></A>(txt: TEXT; fnt: ScrnFont.T): Rect.T =
  CONST BufSize = 64;
  VAR junk: BOOLEAN; len := Text.Length (txt); BEGIN
    IF (len &lt;= BufSize) THEN
      VAR buf: ARRAY [0..BufSize-1] OF CHAR; BEGIN
      	Text.SetChars(buf, txt);
      	RETURN TightBoundingBoxSubValid(SUBARRAY(buf, 0, len), fnt, junk)
      END
    ELSE
      VAR ref := NEW(REF ARRAY OF CHAR, len); BEGIN
      	Text.SetChars(ref^, txt);
      	RETURN TightBoundingBoxSubValid(ref^, fnt, junk)
      END
    END;
  END TightBoundingBox;

PROCEDURE <A NAME="TightBoundingBoxSub"><procedure>TightBoundingBoxSub</procedure></A>(
  READONLY txt: ARRAY OF CHAR;
  fnt: ScrnFont.T)
  : Rect.T =
  VAR junk: BOOLEAN; BEGIN
    RETURN TightBoundingBoxSubValid(txt, fnt, junk)
  END TightBoundingBoxSub;

PROCEDURE <A NAME="TightBoundingBoxSubValid"><procedure>TightBoundingBoxSubValid</procedure></A>(
  READONLY txt: ARRAY OF CHAR;
  fnt: ScrnFont.T;
  VAR (*OUT*) valid: BOOLEAN)
  : Rect.T =
  VAR res: Rect.T; len := NUMBER(txt); BEGIN
    valid := TRUE;
    IF len = 0 THEN RETURN Rect.Empty END;
    IF fnt = NIL OR fnt.metrics = NIL THEN RETURN Rect.FromSize(len, 1) END;
    WITH m = fnt.metrics DO
      &lt;* ASSERT m # NIL *&gt;
      IF m.charMetrics # NIL THEN
        (* This font has character metrics *)

        (* Compute join of bounding boxes *)
        res := Rect.Empty;
        VAR rp := 0; BEGIN
          FOR i := 0 TO LAST(txt) DO
            WITH cm = GetCM(m, txt[i], valid) DO
              res := Rect.Join(res, Rect.MoveH(cm.boundingBox, rp));
              INC(rp, cm.printWidth)
            END
          END
        END
      ELSE
        (* All characters have the same &quot;printWidth&quot; and &quot;boundingBox&quot;, which
           are stored in &quot;m.charMetrics.maxBounds&quot;. *)

        (* Set &quot;valid&quot; if some characters may be invalid, and set &quot;len&quot;
           to the number of valid characters. *)
        VAR fc, lc: INTEGER; BEGIN
          IF NOT AllCharsValid(m, fc, lc) THEN
            VAR validCnt := 0; ch: INTEGER; BEGIN
              FOR i := 0 TO len - 1 DO
          	ch := ORD(txt[i]);
          	IF fc &lt;= ch AND ch &lt;= lc THEN INC(validCnt) END
              END;
              valid := (len = validCnt);
              len := validCnt
            END
          END
        END;

        (* Compute the bounding box *)
        IF len = 0 THEN RETURN Rect.Empty END;
        WITH max = m.maxBounds, pw = max.printWidth, box = max.boundingBox DO
          res := box;
          IF len &gt; 1 THEN
            res := Rect.Join(res, Rect.MoveH(box, pw * (len - 1)))
          END
        END
      END;
      RETURN res
    END
  END TightBoundingBoxSubValid;

CONST EmptyCM = ScrnFont.CharMetric{
  boundingBox := Rect.Empty, printWidth := 0};

PROCEDURE <A NAME="GetCM"><procedure>GetCM</procedure></A>(
  m: ScrnFont.Metrics;
  ch: CHAR;
  VAR (*INOUT*) valid: BOOLEAN)
  : ScrnFont.CharMetric =
  CONST SpaceCode = ORD(' '); Char240 = 8_240;
  VAR c := ORD(ch); BEGIN
    WITH fc = m.firstChar, lc = m.lastChar DO
      IF c &lt; fc OR c &gt; lc THEN
    	c := m.defaultChar;
    	IF c &lt; fc OR c &gt; lc THEN
    	  valid := FALSE;
    	  RETURN EmptyCM
    	END
      END;
      IF c # SpaceCode AND c # Char240 THEN
        RETURN m.charMetrics[c - fc]
      ELSE
        (* The space character should have an empty bounding box, but the X
           font definition mistakenly gives it a width and height of 1. The
           same is true for character 8_240 in all fonts. *)
        RETURN ScrnFont.CharMetric{boundingBox := Rect.Empty,
          printWidth := m.charMetrics[c - fc].printWidth}
      END
    END
  END GetCM;

PROCEDURE <A NAME="AllCharsValid"><procedure>AllCharsValid</procedure></A>(m: ScrnFont.Metrics; VAR (*OUT*) fc,lc: INTEGER):
  BOOLEAN =
</PRE><BLOCKQUOTE><EM> Return <CODE>TRUE</CODE> iff all characters must be valid, that is, if <CODE>m</CODE> is defined
   on all characters, or if its default character is defined. This also has
   the side-effect of setting <CODE>fc</CODE> and <CODE>lc</CODE> to the codes of <CODE>m</CODE>'s first and
   last defined characters. </EM></BLOCKQUOTE><PRE>
  BEGIN
    fc := m.firstChar;
    lc := m.lastChar;
    WITH dc = m.defaultChar DO
      RETURN (fc &lt;= ORD(FIRST(CHAR)) AND ORD(LAST(CHAR)) &lt;= lc)
          OR (fc &lt;= dc AND dc &lt;= lc)
    END
  END AllCharsValid;

BEGIN
END ScrnFontExtras.
</PRE>
</inModule>
<PRE>























</PRE>
</BODY>
</HTML>
