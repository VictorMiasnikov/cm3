<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: juno-app/src/ToolBox.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>juno-app/src/ToolBox.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright (C) 1992, Digital Equipment Corporation                         
 All rights reserved.                                                      
 See the file COPYRIGHT for a full description.                            
                                                                           
 Last modified on Mon Dec 15 10:43:46 PST 1997 by heydon                   
      modified on Tue Feb  7 11:43:41 PST 1995 by gnelson                  
<PRE>&lt;* PRAGMA LL *&gt;
&lt;* PRAGMA SPEC *&gt;

MODULE <module><implements><A HREF="ToolBox.i3.html">ToolBox</A></implements></module>;

IMPORT <A HREF="#x1">View</A>, <A HREF="Drawing.i3.html">Drawing</A>, <A HREF="#x2">Editor</A>, <A HREF="#x3">Source</A>, <A HREF="CurrCmd.i3.html">CurrCmd</A>, <A HREF="JunoConfig.i3.html">JunoConfig</A>;
IMPORT <A HREF="../../juno-compiler/src/JunoAST.i3.html">JunoAST</A>, <A HREF="../../juno-compiler/src/JunoASTUtils.i3.html">JunoASTUtils</A>;
IMPORT <A HREF="../../ui/src/vbt/VBT.i3.html">VBT</A>, <A HREF="../../ui/src/split/TextVBT.i3.html">TextVBT</A>, <A HREF="../../ui/src/split/Filter.i3.html">Filter</A>, <A HREF="../../ui/src/vbt/PaintOp.i3.html">PaintOp</A>, <A HREF="../../ui/src/split/Split.i3.html">Split</A>, <A HREF="../../ui/src/split/BorderedVBT.i3.html">BorderedVBT</A>;
IMPORT   <A HREF="../../ui/src/split/ButtonVBT.i3.html">ButtonVBT</A>, <A HREF="../../ui/src/split/MenuBtnVBT.i3.html">MenuBtnVBT</A>, <A HREF="../../ui/src/split/AnchorBtnVBT.i3.html">AnchorBtnVBT</A>;
IMPORT <A HREF="../../libm3/src/atom/Atom.i3.html">Atom</A>;

REVEAL
  <A NAME="Button">Button</A> = ButtonPublic BRANDED &quot;ToolBox.Button&quot; OBJECT
    root: View.Root;
    tool: Drawing.ArgTool;
  OVERRIDES
    init := ButtonInit;
  END;

REVEAL
  <A NAME="SetButton">SetButton</A> = SetButtonPublic BRANDED &quot;ToolBox.SetButton&quot; OBJECT
    root: View.Root;
    tool: Drawing.SetTool;
  OVERRIDES
    init := SetButtonInit
  END;

TYPE
  PointButton = Button BRANDED &quot;ToolBox.PointButton&quot; OBJECT END;
  TextButton = Button BRANDED &quot;ToolBox.TextButton&quot; OBJECT END;
  SetAnchorBtn = AnchorBtnVBT.T BRANDED &quot;ToolBox.SetAnchorBtn&quot; OBJECT
    ed: Editor.T;
    nm: Atom.T;
  OVERRIDES
    pre := SetAnchorBtnPre
  END;
  TemplButton = Button BRANDED &quot;TemplButton&quot; OBJECT cmd: JunoAST.Cmd END;
</PRE> These are the types for the three kinds of buttons appearing in a
   toolbox. The first two are normal buttons for point tools and text tools,
   and the third is an anchor for a set tool menu. That menu will be a VSplit
   of <CODE>SetButton</CODE>'s.
<P>
   If <CODE>btn: SetAnchorBtn</CODE>, then <CODE>btn.ed</CODE> is the editor in which the procedure
   for this button appears, and <CODE>nm</CODE> is the name of the button's procedure.

<P><PRE>VAR (* CONST *)
  toolColor := ARRAY BOOLEAN OF PaintOp.ColorQuad{
    PaintOp.bgFg, PaintOp.MakeColorQuad(PaintOp.Fg, PaintOp.Bg)};

PROCEDURE <A NAME="ButtonText"><procedure>ButtonText</procedure></A>(name: TEXT): TextVBT.T =
</PRE><BLOCKQUOTE><EM> Return a <CODE>TextVBT.T</CODE> with the name <CODE>name</CODE> left-justified in the current
   tool font <CODE>JunoConfig.TextFont</CODE>. </EM></BLOCKQUOTE><PRE>
  BEGIN
    RETURN TextVBT.New(name, halign := 0.0, fnt := JunoConfig.textFont)
  END ButtonText;

PROCEDURE <A NAME="ButtonInit"><procedure>ButtonInit</procedure></A>(
    self: Button;
    root: View.Root;
    name: TEXT;
    tl: Drawing.ArgTool): Button =
</PRE><BLOCKQUOTE><EM> This is the implementation of the <CODE>Button.init</CODE> method. </EM></BLOCKQUOTE><PRE>
  BEGIN
    self.root := root;
    self.tool := tl;
    tl.label := name;
    EVAL ButtonVBT.T.init(self, ButtonText(name), DoButton);
    RETURN self
  END ButtonInit;

PROCEDURE <A NAME="DoButton"><procedure>DoButton</procedure></A>(self: ButtonVBT.T; READONLY cd: VBT.MouseRec) =
</PRE><BLOCKQUOTE><EM> This is the call-back procedure for a <CODE>Button</CODE>. It selects the tool
   associated with the button. If the tool has any arguments, this procedure
   also permanently highlights the button (and unhighlights the button of the
   current tool if one was selected). </EM></BLOCKQUOTE><PRE>
  &lt;* LL.sup = VBT.mu *&gt;
  VAR tb: Button := self; BEGIN
    TYPECASE self OF
      TemplButton (b) =&gt;
        CurrCmd.ChangeAST(b.root.ccmd, b.cmd);
        b.root.astTrue := TRUE;
        b.root.source.modified(View.ModKind.ImplicitConsistent);
        b.root.ccmd.codeValid := FALSE;
        b.root.dTrue := FALSE;
        IF NOT Source.Make(b.root.source, cd.time, b.root.skipify) THEN
          b.root.astTrue := FALSE;
          EVAL Source.Make(b.root.source, cd.time, b.root.skipify)
        END;
        RETURN
      ELSE (* SKIP *)
    END;
    Drawing.SelectTool(tb.root.drawing, tb.tool, cd.time);
    IF NUMBER(tb.tool.argType^) # 0 THEN
      HiliCurrButton(tb.root, hili := FALSE);
      tb.root.currButton := tb;
      HiliCurrButton(tb.root, hili := TRUE)
    END
  END DoButton;

PROCEDURE <A NAME="SetButtonInit"><procedure>SetButtonInit</procedure></A>(
    self: SetButton;
    root: View.Root;
    name: TEXT;
    tl: Drawing.SetTool): SetButton =
</PRE><BLOCKQUOTE><EM> This is the implementation of the <CODE>SetButton.init</CODE> method. </EM></BLOCKQUOTE><PRE>
  BEGIN
    self.root := root;
    self.tool := tl;
    tl.label := name;
    EVAL MenuBtnVBT.T.init(self, ButtonText(name), DoSetButton);
    RETURN self
  END SetButtonInit;

PROCEDURE <A NAME="DoSetButton"><procedure>DoSetButton</procedure></A>(self: ButtonVBT.T; READONLY cd: VBT.MouseRec) =
</PRE><BLOCKQUOTE><EM> This is the callback procedure for a <CODE>SetButton</CODE>. It selects the tool
   associated with the button when it was created. </EM></BLOCKQUOTE><PRE>
  &lt;* LL.sup = VBT.mu *&gt;
  VAR tb: SetButton := self; BEGIN
    Drawing.SelectTool(tb.root.drawing, tb.tool, cd.time)
  END DoSetButton;

PROCEDURE <A NAME="SetAnchorBtnPre"><procedure>SetAnchorBtnPre</procedure></A>(btn: SetAnchorBtn) =
</PRE><BLOCKQUOTE><EM> This is the implementation of the <CODE>pre</CODE> method for the <CODE>AnchorBtnVBT.T</CODE> of
   a set tool. It dynamically updates its menu from the menu stored in the
   button's editor <CODE>btn.ed</CODE> under the name <CODE>btn.nm</CODE>. </EM></BLOCKQUOTE><PRE>
  &lt;* LL.sup = VBT.mu *&gt;
  VAR menu := btn.ed.getMenu(btn.nm); BEGIN
    IF btn.menu = NIL THEN
      btn.menu := BorderedVBT.New(menu)
    ELSIF menu # Filter.Child(btn.menu) THEN
      EVAL Filter.Replace(btn.menu, menu)
    END;
    AnchorBtnVBT.T.pre(btn)
  END SetAnchorBtnPre;

PROCEDURE <A NAME="Update"><procedure>Update</procedure></A>(t: T; ed: Editor.T; rt: View.Root; n:=0; anon:=TRUE) =
  &lt;* LL.sup = VBT.mu *&gt;
  &lt;* FATAL Split.NotAChild *&gt;
  VAR
    ch: ButtonVBT.T := Split.Nth(t, n);
    mod := JunoAST.NilId;
    tr := Editor.Trees(ed);
  BEGIN
    (* The buttons in the toolbox preceeding &quot;ch&quot; correspond to the first &quot;n&quot;
       buttons plus the buttons for the trees preceeding &quot;tr&quot;. *)
    WHILE tr # NIL DO
      TYPECASE tr.tree OF
        JunoAST.Module(m) =&gt;
          IF NOT anon THEN mod := m.name END
      | JunoAST.PredDecl (p) =&gt;
          IF NOT p.private THEN
	    VAR hd := p.header; type: Atom.T; BEGIN
              IF ed.getToolType(hd.name, type) THEN
                &lt;* ASSERT type = Editor.PointToolSym *&gt;
                VAR nmMatch: BOOLEAN; BEGIN
                  IF PredMatch(mod, ch, hd, nmMatch)
                    THEN ch := Split.Succ(t, ch)
                    ELSE InsertBtn(t, ch, NewPredBtn(rt, mod, hd), nmMatch)
                  END
                END
              END
	    END
          END
      | JunoAST.FuncDecl (f) =&gt;
          IF NOT f.private THEN
	    VAR hd := f.header; type: Atom.T; BEGIN
              IF ed.getToolType(hd.name, type) THEN
                &lt;* ASSERT type = Editor.PointToolSym *&gt;
                VAR nmMatch: BOOLEAN; BEGIN
                  IF FuncMatch(mod, ch, hd, nmMatch)
                    THEN ch := Split.Succ(t, ch)
                    ELSE InsertBtn(t, ch, NewFuncBtn(rt, mod, hd), nmMatch)
                  END
                END
              END
	    END
          END
      | JunoAST.ProcDecl (p) =&gt;
          IF NOT p.private THEN
	    VAR hd := p.header; type: Atom.T; BEGIN
              IF ed.getToolType(hd.name, type) THEN
                VAR nmMatch: BOOLEAN; BEGIN
                  IF type # Editor.TemplToolSym AND
                     ProcMatch(mod, ch, hd, nmMatch) AND
                     BtnTypeMatches(ch, type) THEN
                    ch := Split.Succ(t, ch)
                  ELSE
                    InsertBtn(t, ch, NewProcBtn(rt, ed, mod, p, type),
                      nmMatch)
                  END
                END
	      END
	    END
          END
      ELSE (* SKIP *)
      END;
      tr := tr.next
    END;
    WHILE ch # NIL DO
      VAR nextCh := Split.Succ(t, ch); BEGIN
        Split.Delete(t, ch);
        ch := nextCh
      END
    END
  END Update;

PROCEDURE <A NAME="PredMatch"><procedure>PredMatch</procedure></A>(mod: JunoAST.Id; btn: ButtonVBT.T; sig: JunoAST.PredHeader;
    VAR (*OUT*) nameMatch: BOOLEAN): BOOLEAN =
</PRE><BLOCKQUOTE><EM> Returns <CODE>TRUE</CODE> iff both the <CODE>name</CODE> and <CODE>signature</CODE> of <CODE>btn</CODE> and <CODE>sig</CODE>
   match. If <CODE>FALSE</CODE> is returned, then <CODE>nameMatch</CODE> is set <CODE>TRUE</CODE> iff the
   <CODE>name</CODE> of <CODE>btn</CODE> matches that of <CODE>sig</CODE>. Otherwise, the value of <CODE>nameMatch</CODE>
   is undefined. </EM></BLOCKQUOTE><PRE>
  BEGIN
    TYPECASE btn OF
      NULL =&gt; (* SKIP *)
    | PointButton (b) =&gt;
      	TYPECASE b.tool OF
          Drawing.PredTool (tool) =&gt;
            nameMatch := (tool.name.id0 = mod AND tool.name.id1 = sig.name);
    	    RETURN nameMatch AND (tool.in_cnt = sig.ins.size)
        | Drawing.ArgTool (tool) =&gt;
            nameMatch := (tool.name.id0 = mod AND tool.name.id1 = sig.name);
            RETURN FALSE
      	END
    ELSE (* SKIP *)
    END;
    nameMatch := FALSE;
    RETURN FALSE
  END PredMatch;

PROCEDURE <A NAME="FuncMatch"><procedure>FuncMatch</procedure></A>(mod: JunoAST.Id; btn: ButtonVBT.T; sig: JunoAST.FuncHeader;
    VAR (*OUT*) nameMatch: BOOLEAN): BOOLEAN =
</PRE><BLOCKQUOTE><EM> Returns <CODE>TRUE</CODE> iff both the <CODE>name</CODE> and <CODE>signature</CODE> of <CODE>btn</CODE> and <CODE>sig</CODE>
   match. If <CODE>FALSE</CODE> is returned, then <CODE>nameMatch</CODE> is set <CODE>TRUE</CODE> iff the
   <CODE>name</CODE> of <CODE>btn</CODE> matches that of <CODE>sig</CODE>. Otherwise, the value of <CODE>nameMatch</CODE>
   is undefined. </EM></BLOCKQUOTE><PRE>
  BEGIN
    TYPECASE btn OF
      NULL =&gt; (* SKIP *)
    | PointButton (b) =&gt;
      	TYPECASE b.tool OF
          Drawing.FuncTool (tool) =&gt;
            nameMatch := (tool.name.id0 = mod AND tool.name.id1 = sig.name);
    	    RETURN nameMatch AND (tool.in_cnt = sig.ins.size)
        | Drawing.ArgTool (tool) =&gt;
            nameMatch := (tool.name.id0 = mod AND tool.name.id1 = sig.name);
            RETURN FALSE
      	END
    ELSE (* SKIP *)
    END;
    nameMatch := FALSE;
    RETURN FALSE
  END FuncMatch;

PROCEDURE <A NAME="ProcMatch"><procedure>ProcMatch</procedure></A>(mod: JunoAST.Id; btn: ButtonVBT.T; sig: JunoAST.ProcHeader;
    VAR (*OUT*) nameMatch: BOOLEAN): BOOLEAN =
</PRE><BLOCKQUOTE><EM> Returns <CODE>TRUE</CODE> iff both the <CODE>name</CODE> and <CODE>signature</CODE> of <CODE>btn</CODE> and <CODE>sig</CODE>
   match. If <CODE>FALSE</CODE> is returned, then <CODE>nameMatch</CODE> is set <CODE>TRUE</CODE> iff the
   <CODE>name</CODE> of <CODE>btn</CODE> matches that of <CODE>sig</CODE>. Otherwise, the value of <CODE>nameMatch</CODE>
   is undefined.
<P>
   In the case that <CODE>btn</CODE> is a <CODE>SetAnchorBtn</CODE>, which has no signature, the
   signatures of <CODE>btn</CODE> and <CODE>sig</CODE> are always considered to match. Otherwise,
   the signatures match if the signature of the tool associated with <CODE>btn</CODE>
   matches that of <CODE>sig</CODE>. </EM></BLOCKQUOTE><PRE>
  BEGIN
    TYPECASE btn OF &lt;* NOWARN *&gt;
      NULL =&gt; (* SKIP *)
    | SetAnchorBtn (b) =&gt; nameMatch := FALSE; RETURN b.nm = sig.name
    | Button (b) =&gt;
        TYPECASE b.tool OF
          Drawing.ProcTool (tool) =&gt;
            nameMatch := (tool.name.id0 = mod AND tool.name.id1 = sig.name);
            RETURN nameMatch AND (tool.in_cnt = sig.ins.size AND
              tool.out_cnt = sig.outs.size AND tool.inout_cnt=sig.inouts.size)
        | Drawing.ArgTool (tool) =&gt;
            nameMatch := (tool.name.id0 = mod AND tool.name.id1 = sig.name);
            RETURN FALSE
        END
    END;
    nameMatch := FALSE;
    RETURN FALSE
  END ProcMatch;

PROCEDURE <A NAME="BtnTypeMatches"><procedure>BtnTypeMatches</procedure></A>(btn: ButtonVBT.T; type: Atom.T): BOOLEAN =
</PRE><BLOCKQUOTE><EM> Return <CODE>TRUE</CODE> iff the type of <CODE>btn</CODE> matches the UI declaration type name
   <CODE>type</CODE>, which is one of <CODE>Editor.PointToolSym</CODE>, <CODE>Editor.TextToolSym</CODE>, or
   <CODE>Editor.SetToolSym</CODE>. </EM></BLOCKQUOTE><PRE>
  BEGIN
    TYPECASE btn OF &lt;* NOWARN *&gt;
      PointButton   =&gt; RETURN type = Editor.PointToolSym
    | TemplButton   =&gt; RETURN type = Editor.TemplToolSym
    | TextButton    =&gt; RETURN type = Editor.TextToolSym
    | SetAnchorBtn  =&gt; RETURN type = Editor.SetToolSym
    END
  END BtnTypeMatches;

PROCEDURE <A NAME="InsertBtn"><procedure>InsertBtn</procedure></A>(t: T; VAR (*INOUT*) ch: ButtonVBT.T; new: ButtonVBT.T;
    nameMatch: BOOLEAN) =
</PRE><BLOCKQUOTE><EM> If <CODE>nameMatch</CODE>, then replace <CODE>ch</CODE> by <CODE>new</CODE> in <CODE>t</CODE>, and set <CODE>ch</CODE> to the
   successor child of this new child. In this case, <CODE>ch</CODE> must be non-NIL.
   Otherwise, insert <CODE>new</CODE> before <CODE>ch</CODE> in <CODE>t</CODE>, and leave <CODE>ch</CODE> unchanged. </EM></BLOCKQUOTE><PRE>
  &lt;* FATAL Split.NotAChild *&gt;
  BEGIN
    IF nameMatch THEN
      &lt;* ASSERT ch # NIL *&gt;
      VAR curr := ch; BEGIN
        ch := Split.Succ(t, ch);
        Split.Delete(t, curr)
      END
    END;
    Split.Insert(t, Split.Pred(t, ch), new)
  END InsertBtn;

PROCEDURE <A NAME="NewPredBtn"><procedure>NewPredBtn</procedure></A>(
    rt: View.Root;
    mod: JunoAST.Id;
    hd: JunoAST.PredHeader): Button =
  BEGIN
    RETURN NEW(PointButton).init(rt, Atom.ToText(hd.name),
      Drawing.NewPredTool(JunoASTUtils.QIdFromIds(mod, hd.name), hd.ins.size))
  END NewPredBtn;

PROCEDURE <A NAME="NewFuncBtn"><procedure>NewFuncBtn</procedure></A>(
    rt: View.Root;
    mod: JunoAST.Id;
    hd: JunoAST.FuncHeader): Button =
  BEGIN
    RETURN NEW(PointButton).init(rt, Atom.ToText(hd.name),
      Drawing.NewFuncTool(JunoASTUtils.QIdFromIds(mod, hd.name), hd.ins.size))
  END NewFuncBtn;

PROCEDURE <A NAME="NewProcBtn"><procedure>NewProcBtn</procedure></A>(
    rt: View.Root;
    ed: Editor.T;
    mod: Atom.T;
    p: JunoAST.ProcDecl;
    type: Atom.T): ButtonVBT.T =
  VAR hd := p.header; txtName := Atom.ToText(hd.name);  BEGIN
    IF type = Editor.SetToolSym THEN
      RETURN NEW(SetAnchorBtn, ed := ed, nm := hd.name).init(
        ButtonText(txtName), menu := NIL, hfudge := 10.0, n := 999)
    ELSE
      VAR res: Button; BEGIN
        IF type = Editor.TextToolSym THEN
          res := NEW(TextButton)
        ELSIF type = Editor.PointToolSym THEN
          res := NEW(PointButton)
        ELSIF type = Editor.TemplToolSym THEN
          res := NEW(TemplButton, cmd := StripIFProj(p.body))
        ELSE
          &lt;* ASSERT FALSE *&gt;
        END;
        RETURN res.init(rt, txtName,
          Drawing.NewProcTool(JunoASTUtils.QIdFromIds(mod, hd.name),
            hd.ins.size, hd.outs.size, hd.inouts.size,
            isText := type = Editor.TextToolSym))
      END
    END
  END NewProcBtn;

PROCEDURE <A NAME="StripIFProj"><procedure>StripIFProj</procedure></A>(body: JunoAST.Cmd): JunoAST.Cmd =
  BEGIN
    TYPECASE body OF JunoAST.If (if) =&gt;
      IF ISTYPE(if.body, JunoAST.Proj)
        THEN RETURN if.body
        ELSE RETURN body
      END
    ELSE RETURN body
    END
  END StripIFProj;

&lt;* SPEC Unselect REQUIRES sup(LL) = VBT.mu *&gt;

PROCEDURE <A NAME="Unselect"><procedure>Unselect</procedure></A>(rt: View.Root) =
  BEGIN
    IF rt.drawing # NIL THEN
      Drawing.SelectTool(rt.drawing, NIL, time := 0)
    END;
    HiliCurrButton(rt, hili := FALSE);
    rt.currButton := NIL
  END Unselect;

PROCEDURE <A NAME="HiliCurrButton"><procedure>HiliCurrButton</procedure></A>(rt: View.Root; hili: BOOLEAN) =
</PRE><BLOCKQUOTE><EM> If the current button associated with <CODE>rt</CODE> is non-NIL, hilight or unhilight
   it as <CODE>hili</CODE> is <CODE>TRUE</CODE> OR <CODE>FALSE</CODE>. </EM></BLOCKQUOTE><PRE>
  BEGIN
    IF rt.currButton # NIL THEN
      TextVBT.SetFont(Filter.Child(rt.currButton),
        JunoConfig.textFont, toolColor[hili])
    END
  END HiliCurrButton;

PROCEDURE <A NAME="SwapButton"><procedure>SwapButton</procedure></A>(t: T; curr, new: Drawing.ArgTool; newLabel: TEXT) =
  &lt;* LL.sup = VBT.mu *&gt;
  &lt;* FATAL Split.NotAChild *&gt;
  VAR ch: Button := Split.Succ(t, NIL); BEGIN
    WHILE ch # NIL AND ch.tool # curr DO
      ch := Split.Succ(t, ch)
    END;
    &lt;* ASSERT ch # NIL *&gt;
    Split.Replace(t, ch, NEW(Button).init(ch.root, newLabel, new))
  END SwapButton;

BEGIN
END ToolBox.
</PRE>
</inModule>
<HR>
<A NAME="x1">interface View is in:
</A><UL>
<LI><A HREF="View.i3.html#0TOP0">juno-app/src/View.i3</A>
<LI><A HREF="../../zeus/src/View.i3.html#0TOP0">zeus/src/View.i3</A>
</UL>
<P>
<HR>
<A NAME="x2">interface Editor is in:
</A><UL>
<LI><A HREF="../../cm3ide/src/misc/Editor.i3.html#0TOP0">cm3ide/src/misc/Editor.i3</A>
<LI><A HREF="Editor.i3.html#0TOP0">juno-app/src/Editor.i3</A>
</UL>
<P>
<HR>
<A NAME="x3">interface Source is in:
</A><UL>
<LI><A HREF="../../cm3ide/src/nodes/Source.i3.html#0TOP0">cm3ide/src/nodes/Source.i3</A>
<LI><A HREF="Source.i3.html#0TOP0">juno-app/src/Source.i3</A>
</UL>
<P>
<PRE>























</PRE>
</BODY>
</HTML>
