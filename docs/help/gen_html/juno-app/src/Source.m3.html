<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: juno-app/src/Source.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>juno-app/src/Source.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright (C) 1992, Digital Equipment Corporation                         
 All rights reserved.                                                      
 See the file COPYRIGHT for a full description.                            
                                                                           
 Last modified on Sun Oct 26 14:01:00 PST 1997 by heydon                   
      modified on Wed Aug  2 16:17:14 PST 1995 by gnelson                  
      modified on Fri Aug  7 21:51:58 PDT 1992 by myers                    
<PRE>&lt;* PRAGMA LL *&gt;

MODULE <module><implements><A HREF="#x1">Source</A></implements></module>;

IMPORT <A HREF="CurrCmd.i3.html">CurrCmd</A>, <A HREF="JunoBuild.i3.html">JunoBuild</A>, <A HREF="#x2">View</A>, <A HREF="JunoError.i3.html">JunoError</A>, <A HREF="#x3">Editor</A>, <A HREF="Drawing.i3.html">Drawing</A>, <A HREF="JunoConfig.i3.html">JunoConfig</A>;
FROM <A HREF="JunoHandleLexErr.i3.html">JunoHandleLexErr</A> IMPORT HandleLexErr;
IMPORT <A HREF="../../juno-compiler/src/JunoAST.i3.html">JunoAST</A>, <A HREF="../../juno-compiler/src/JunoCompileErr.i3.html">JunoCompileErr</A>, <A HREF="../../juno-compiler/src/JunoToken.i3.html">JunoToken</A>, <A HREF="../../juno-compiler/src/JunoLex.i3.html">JunoLex</A>, <A HREF="../../juno-compiler/src/JunoParse.i3.html">JunoParse</A>, <A HREF="../../juno-compiler/src/JunoUnparse.i3.html">JunoUnparse</A>;
IMPORT <A HREF="../../vbtkit/src/etext/TextPort.i3.html">TextPort</A>, <A HREF="../../vbtkit/src/etext/TextEditVBT.i3.html">TextEditVBT</A>;
IMPORT <A HREF="../../ui/src/vbt/VBT.i3.html">VBT</A>, <A HREF="../../libm3/src/geometry/Axis.i3.html">Axis</A>, <A HREF="../../libm3/src/geometry/Rect.i3.html">Rect</A>;
IMPORT <A HREF="../../m3core/src/text/Text.i3.html">Text</A>, <A HREF="../../libm3/src/rw/Rd.i3.html">Rd</A>, <A HREF="../../libm3/src/rw/Wr.i3.html">Wr</A>, <A HREF="../../m3core/src/thread/Common/Thread.i3.html">Thread</A>, <A HREF="../../libm3/src/rw/TextRd.i3.html">TextRd</A>, <A HREF="../../libm3/src/rw/TextWr.i3.html">TextWr</A>, <A HREF="../../libm3/src/rw/Stdio.i3.html">Stdio</A>;

REVEAL
  <A NAME="T">T</A> = Public BRANDED &quot;Source.T&quot; OBJECT
    port: Port
  OVERRIDES
    init := Init;
    update := Update
  END;

TYPE
  Port = TextPort.T BRANDED &quot;Source.Port&quot; OBJECT
    src: T;				 (* the Source.T around this port *)
    width := -1;
    ignoreModifies := FALSE;
  OVERRIDES
    &lt;* LL.sup = VBT.mu *&gt;
    key      := Key;
    &lt;* LL.sup = VBT.mu.SELF *&gt;
    reshape  := Reshape;
    shape    := Shape;
    &lt;* LL.sup &lt; VBT.mu *&gt;
    modified := Modified
  END;
</PRE> A <CODE>Source.T</CODE>'s filter child is a <CODE>TextEditVBT.T</CODE> with a port of type
   <CODE>Port</CODE>. The <CODE>width</CODE> of a <CODE>Port</CODE> is the most recent width it was unparsed
   to, or <CODE>-1</CODE> if it was unparsed to a window with an empty domain. 

<P><PRE>PROCEDURE <A NAME="Init"><procedure>Init</procedure></A>(self: T; root: View.Root): T =
  VAR
    port := NEW(Port, src := self).init(
      wrap := FALSE, font := JunoConfig.codeFont);
    child := NEW(TextEditVBT.T, tp := port).init();
  BEGIN
    self.root := root;
    self.port := port;
    RETURN View.T.init(self, child);
  END Init;

PROCEDURE <A NAME="Update"><procedure>Update</procedure></A>(s: T) =
  &lt;* LL.sup &lt;= VBT.mu *&gt;
  VAR port := s.port; BEGIN
    IF Rect.IsEmpty(VBT.Domain(port))
      THEN port.width := -1
      ELSE UpdatePort(port, Editor.Width(port))
    END
  END Update;

PROCEDURE <A NAME="UpdatePort"><procedure>UpdatePort</procedure></A>(port: Port; width: INTEGER) =
  &lt;* LL.sup &lt;= VBT.mu *&gt;
  VAR v := port.src; BEGIN
    &lt;* ASSERT v.root.astTrue *&gt;
    IF (NOT v.root.sTrue) OR width # port.width THEN
      VAR
        wr := TextWr.New();
        cpos := TextPort.Index(port);
        ast := CurrCmd.GetAST(v.root.ccmd);
        &lt;* FATAL Thread.Alerted, Wr.Failure *&gt;
      BEGIN
        JunoUnparse.Cmd(wr, ast, LAST(CARDINAL),
          width := width, prec := JunoConfig.realPrec);
        SetTextPort(port, TextWr.ToText(wr));
        TextPort.Normalize(port, cpos);
        Wr.Close(wr);
        v.root.sTrue := TRUE;
        port.width := width
      END
    END
  END UpdatePort;

PROCEDURE <A NAME="SetTextPort"><procedure>SetTextPort</procedure></A>(port: Port; t: TEXT) =
  BEGIN
    port.ignoreModifies := TRUE;
    TextPort.SetText(port, t);
    port.ignoreModifies := FALSE
  END SetTextPort;
</PRE> If continuous unparsing is turned off, then it is possible that the AST
   is up-to-date, but the source does not reflect changes made through the
   drawing view. In this case, the user should not be able to type in the
   source window until it has been updated from the AST. 

<P><PRE>PROCEDURE <A NAME="Key"><procedure>Key</procedure></A>(self: Port; READONLY cd: VBT.KeyRec) =
  &lt;* LL.sup = VBT.mu *&gt;
  BEGIN
    WITH root = self.src.root DO
      IF root.astTrue AND NOT root.sTrue
    	THEN JunoError.Display(self, &quot;Oops! You forgot to click Run.&quot;)
    	ELSE TextPort.T.key(self, cd)
      END
    END
  END Key;

PROCEDURE <A NAME="Reshape"><procedure>Reshape</procedure></A>(port: Port; READONLY cd: VBT.ReshapeRec) =
</PRE><BLOCKQUOTE><EM> <CODE>Reshape</CODE> reformats its contents according to the new width (if any), then
   reshapes the textport normally. </EM></BLOCKQUOTE><PRE>
  &lt;* LL.sup = VBT.mu.port *&gt;
  BEGIN
    IF Rect.IsEmpty(cd.new) THEN
      port.width := -1
    ELSE
      IF port.src.root.astTrue THEN
        UpdatePort(port, Editor.Width(port))
      END
    END;
    TextPort.T.reshape(port, cd)
  END Reshape;

PROCEDURE <A NAME="Shape"><procedure>Shape</procedure></A>(port: Port; ax: Axis.T; n: CARDINAL): VBT.SizeRange =
  &lt;* LL.sup = VBT.mu.port *&gt;
  BEGIN
    IF ax = Axis.T.Hor THEN
      RETURN VBT.DefaultShape
    ELSE
      VAR res := TextPort.T.shape(port, ax, n); BEGIN
        res.lo := 0;
        res.hi := VBT.DefaultShape.hi;
        RETURN res
      END
    END
  END Shape;

PROCEDURE <A NAME="Modified"><procedure>Modified</procedure></A>(port: Port) =
</PRE><BLOCKQUOTE><EM> This procedure is called by the underlying TextPort when its text is
   modified. </EM></BLOCKQUOTE><PRE>
  &lt;* LL.sup &lt; VBT.mu *&gt;
  BEGIN
    TextPort.T.modified(port);
    IF port.ignoreModifies
      THEN TextPort.SetModified(port, FALSE)
      ELSE port.src.modified(how := View.ModKind.Explicit)
    END
  END Modified;

PROCEDURE <A NAME="ShowError"><procedure>ShowError</procedure></A>(
    s: T; ast: JunoAST.T;
    READONLY err: JunoCompileErr.ErrVal;
    ts: VBT.TimeStamp) =
  &lt;* FATAL Wr.Failure, Thread.Alerted *&gt;
  VAR txt: TEXT; start, finish: INTEGER; BEGIN
    VAR wr := TextWr.New(); BEGIN
      JunoUnparse.P(wr, ast, width := Editor.Width(s.port),
        prec := JunoConfig.realPrec, errast := err.ast);
      txt := TextWr.ToText(wr);
      Wr.Close(wr)
    END;
    start := Text.FindChar(txt, '\001');
    finish := Text.FindChar(txt, '\002');
    IF start &gt;= 0 AND finish &gt; start THEN
      txt := Text.Sub(txt, 0, start)
        &amp; Text.Sub(txt, start + 1, finish - start - 1)
        &amp; Text.Sub(txt, finish + 1);
      SetTextPort(s.port, txt);
      JunoError.P(s.port, err.msg, start, finish - 1, ts)
    ELSE
      Wr.PutText(Stdio.stderr, err.msg &amp; &quot;\n&quot;);
      Wr.PutText(Stdio.stderr, &quot;Error AST:\n&quot;);
      JunoUnparse.Debug(err.ast);
      Wr.PutText(Stdio.stderr, &quot;Original AST:\n&quot;);
      JunoUnparse.Debug(ast)
    END
  END ShowError;

PROCEDURE <A NAME="Parse"><procedure>Parse</procedure></A>(s: T; time: VBT.TimeStamp): JunoAST.Cmd =
  BEGIN
    (* use cached version if it is valid *)
    IF s.root.astTrue THEN RETURN CurrCmd.GetAST(s.root.ccmd) END;

    (* otherwise, parse the contents of the textport *)
    VAR res := ParseFromPort(s.port, time); BEGIN
      IF res # NIL THEN CurrCmd.ChangeAST(s.root.ccmd, res) END;
      RETURN res
    END
  END Parse;

PROCEDURE <A NAME="ParseFromPort"><procedure>ParseFromPort</procedure></A>(port: Port; time: VBT.TimeStamp): JunoAST.Cmd =
</PRE><BLOCKQUOTE><EM> Returns the parsed current command from <CODE>port</CODE>, or <CODE>NIL</CODE> if there
   was a lex or parse error. In the event of either error, the error is
   underlined using timestamp <CODE>time</CODE>, and an error window is displayed. </EM></BLOCKQUOTE><PRE>
  &lt;* FATAL Rd.Failure, Thread.Alerted, Wr.Failure *&gt;
  VAR
    res: JunoAST.Cmd;
    rd := TextRd.New(TextPort.GetText(port));
    tokens: CARDINAL;
  BEGIN
    TRY JunoParse.Command(rd, res, tokens) EXCEPT
      JunoLex.Error (err) =&gt;
        VAR wr := TextWr.New(); start, finish: INTEGER; BEGIN
  	  JunoUnparse.Cmd(wr, res, tokens,
            width := Editor.Width(port), prec := JunoConfig.realPrec);
  	  HandleLexErr(err, rd, wr, start, finish);
  	  SetTextPort(port, TextWr.ToText(wr));
          Wr.Close(wr);
  	  JunoError.P(port, JunoLex.ErrorText(err.kind), start, finish, time)
        END;
        res := NIL
    | JunoParse.Error (err) =&gt;
        IF tokens = 0 AND err.found.kind = JunoToken.Kind.EndMarker THEN
          res := JunoAST.SkipVal
        ELSE
          VAR wr := TextWr.New(); start, finish: CARDINAL; BEGIN
            JunoUnparse.Cmd(wr, res, tokens,
              width := Editor.Width(port), prec := JunoConfig.realPrec);
            Wr.PutChar(wr, '\n');
            start := Wr.Index(wr);
            Wr.PutText(wr, JunoToken.ToText(err.found));
            finish := Wr.Index(wr);
            Wr.PutChar(wr, ' ');
            Wr.PutText(wr, err.additional);
            Wr.PutText(wr, Rd.GetText(rd, LAST(CARDINAL)));
            SetTextPort(port, TextWr.ToText(wr));
            Wr.Close(wr);
            JunoError.P(port, &quot;Parse error&quot;, start, finish, time)
          END;
          res := NIL
        END
    END;
    Rd.Close(rd);
    RETURN res
  END ParseFromPort;

PROCEDURE <A NAME="Compile"><procedure>Compile</procedure></A>(s: T; time: VBT.TimeStamp; skipify: BOOLEAN): BOOLEAN =
  BEGIN
    IF s.root.astTrue AND s.root.ccmd.codeValid
      AND s.root.ccmd.skipify = skipify THEN
      RETURN TRUE
    END;
    RETURN Compile2(s, time, skipify)
  END Compile;

PROCEDURE <A NAME="Compile2"><procedure>Compile2</procedure></A>(s: T; time: VBT.TimeStamp; skipify: BOOLEAN): BOOLEAN =
  VAR port: Port := s.port; ast: JunoAST.Cmd := Parse(s, time); BEGIN
    (* check for parse error *)
    IF ast = NIL THEN RETURN FALSE END;
    (* recompile and update drawing if necessary *)
    WITH cc = s.root.ccmd DO
      IF NOT cc.codeValid OR skipify # cc.skipify THEN
        TRY
          VAR astp := ast; BEGIN
            IF skipify THEN astp := CurrCmd.Skipify(ast) END;
            cc.slot := JunoBuild.CurrCmd(astp,
              CurrCmd.GetScope(cc), checkTotal := TRUE);
            cc.codeValid := TRUE;
            cc.skipify := skipify
          END
        EXCEPT
          JunoCompileErr.Error (err) =&gt;
            ShowError(s, ast, err, time);
            RETURN FALSE
        END;
        TextPort.SetModified(port, FALSE);
        s.root.astTrue := TRUE
      END
    END;
    RETURN TRUE
  END Compile2;

PROCEDURE <A NAME="Make"><procedure>Make</procedure></A>(s: T; time: VBT.TimeStamp; skipify: BOOLEAN): BOOLEAN =
  BEGIN
    IF NOT Compile(s, time, skipify) THEN RETURN FALSE END;
    Drawing.Make(s.root.drawing, skipify);
    s.update();
    RETURN TRUE
  END Make;

PROCEDURE <A NAME="GetText"><procedure>GetText</procedure></A>(s: T): TEXT =
  BEGIN
    RETURN TextPort.GetText(s.port)
  END GetText;

PROCEDURE <A NAME="SetText"><procedure>SetText</procedure></A>(s:T; txt: TEXT) =
  BEGIN
    TextPort.SetText(s.port, txt)
  END SetText;

BEGIN
END Source.
</PRE>
</inModule>
<HR>
<A NAME="x1">interface Source is in:
</A><UL>
<LI><A HREF="../../cm3ide/src/nodes/Source.i3.html#0TOP0">cm3ide/src/nodes/Source.i3</A>
<LI><A HREF="Source.i3.html#0TOP0">juno-app/src/Source.i3</A>
</UL>
<P>
<HR>
<A NAME="x2">interface View is in:
</A><UL>
<LI><A HREF="View.i3.html#0TOP0">juno-app/src/View.i3</A>
<LI><A HREF="../../zeus/src/View.i3.html#0TOP0">zeus/src/View.i3</A>
</UL>
<P>
<HR>
<A NAME="x3">interface Editor is in:
</A><UL>
<LI><A HREF="../../cm3ide/src/misc/Editor.i3.html#0TOP0">cm3ide/src/misc/Editor.i3</A>
<LI><A HREF="Editor.i3.html#0TOP0">juno-app/src/Editor.i3</A>
</UL>
<P>
<PRE>























</PRE>
</BODY>
</HTML>
