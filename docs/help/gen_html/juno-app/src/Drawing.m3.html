<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: juno-app/src/Drawing.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>juno-app/src/Drawing.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright (C) 1992, Digital Equipment Corporation                         
 All rights reserved.                                                      
 See the file COPYRIGHT for a full description.                            
                                                                           
 Last modified on Sun Oct 26 13:40:18 PST 1997 by heydon                   
      modified on Sun Jun 11 17:26:27 PDT 1995 by gnelson                  
      modified on Sat Aug 22 22:10:45 PDT 1992 by myers                    
<PRE>&lt;* PRAGMA LL *&gt;
&lt;* PRAGMA SPEC *&gt;

MODULE <module><implements><A HREF="Drawing.i3.html">Drawing</A></implements></module>;

IMPORT <A HREF="CurrCmd.i3.html">CurrCmd</A>, <A HREF="#x1">View</A>, <A HREF="JunoError.i3.html">JunoError</A>, <A HREF="JunoBuild.i3.html">JunoBuild</A>, <A HREF="Drag.i3.html">Drag</A>, <A HREF="PSImpl.i3.html">PSImpl</A>, <A HREF="JunoPt.i3.html">JunoPt</A>;
IMPORT   <A HREF="#x2">Editor</A>, <A HREF="ToolBox.i3.html">ToolBox</A>, <A HREF="JunoConfig.i3.html">JunoConfig</A>;
IMPORT <A HREF="../../juno-compiler/src/JunoAST.i3.html">JunoAST</A>, <A HREF="../../juno-compiler/src/JunoASTUtils.i3.html">JunoASTUtils</A>, <A HREF="../../juno-compiler/src/JunoCompileErr.i3.html">JunoCompileErr</A>;
IMPORT <A HREF="#x3">JunoValue</A>, <A HREF="../../juno-machine/src/JunoRT.i3.html">JunoRT</A>;
IMPORT <A HREF="../../vbtkit/src/etext/TextEditVBT.i3.html">TextEditVBT</A>, <A HREF="../../vbtkit/src/etext/KeyTrans.i3.html">KeyTrans</A>;
IMPORT <A HREF="../../ui/src/vbt/VBT.i3.html">VBT</A>, <A HREF="../../ui/src/vbt/VBTClass.i3.html">VBTClass</A>, <A HREF="../../ui/src/vbt/PaintOp.i3.html">PaintOp</A>, <A HREF="../../libm3/src/geometry/Point.i3.html">Point</A>, <A HREF="../../libm3/src/geometry/Region.i3.html">Region</A>, <A HREF="../../libm3/src/geometry/Rect.i3.html">Rect</A>, <A HREF="../../libm3/src/geometry/Path.i3.html">Path</A>;
IMPORT   <A HREF="../../libm3/src/geometry/Axis.i3.html">Axis</A>, <A HREF="../../ui/src/split/Filter.i3.html">Filter</A>, <A HREF="../../ui/src/vbt/Pixmap.i3.html">Pixmap</A>, <A HREF="../../ui/src/split/DblBufferVBT.i3.html">DblBufferVBT</A>;
IMPORT <A HREF="../../ui/src/vbt/KeyboardKey.i3.html">KeyboardKey</A>, <A HREF="../../ui/src/vbt/Latin1Key.i3.html">Latin1Key</A>;
IMPORT <A HREF="../../libm3/src/atom/Atom.i3.html">Atom</A>, <A HREF="../../m3core/src/text/Text.i3.html">Text</A>, <A HREF="../../m3core/src/float/IEEE/Real.i3.html">Real</A>, <A HREF="../../libm3/src/fmtlex/Fmt.i3.html">Fmt</A>;
IMPORT <A HREF="../../m3core/src/runtime/common/RTCollector.i3.html">RTCollector</A>;

CONST
  DummyXY = JunoPt.T{0.0, 0.0};		 (* dummy point location *)

REVEAL
  View.<A NAME="Drawing">Drawing</A> = Drag.T BRANDED &quot;View.Drawing&quot; OBJECT
    (* General state *)
    tool: ArgTool := NIL;
    lastTool: Tool := NIL;
    usingTempTool := FALSE;
    freezeTool, dragTool: Tool := NIL; (* CONST after initialization *)
    labelStyle: [0..2] := 2;
    pointCount := 0;
    continuousUnparse := TRUE;
    hasMouseFocus: BOOLEAN;
    prevStackSize: CARDINAL := 0;
    (* &quot;Click&quot; args *)
    goodDownClick: BOOLEAN;
    (* &quot;Text&quot; arg *)
    txt: TEXT;        (* &quot;txt # NIL&quot; &lt;=&gt; user typing text argument *)
    (* Grid state *)
    toolBox: ToolBox.T;
    gridMode := FALSE;
    grid0, grid1: JunoPt.T;		 (* grid origin and unix X vector *)
    grid0nm, grid1nm: JunoAST.QId;
    gridOn, gridOff: Tool;
  OVERRIDES
    init := InitDrawing;
    update := Update;
  END;
</PRE> If <CODE>d</CODE> is a <CODE>Drawing.T</CODE>, then <CODE>d.tool</CODE> is the currently selected tool; it
   is <CODE>NIL</CODE> if no tool is currently selected. The UI supports freezing and
   dragging without having selected those tools directly (by using different
   mouse buttons). In the case that one of these tools is being applied in
   that way, <CODE>usingTempTool</CODE> is true and <CODE>lastTool</CODE> stores the tool that was
   in effect before the freeze or drag tool was used; they are FALSE and NIL,
   respectively, otherwise. <CODE>freezeTool</CODE> and <CODE>dragTool</CODE> are simply cached
   tools so new ones don't have to be allocated on each use.
<P>
   The values for <CODE>d.labelStyle</CODE> are: 0 for no labels, 1 for crosses, and 2
   for point-name labels.
<P>
   The value <CODE>d.pointCount</CODE> is the number of the next potential point. The
   drawing view maps cardinals into point names. When a new point is created,
   it searches for an available point name starting with the one designated by
   <CODE>d.pointCount</CODE>. Since point names can only be deleted by editing the source
   view, <CODE>d.pointCount</CODE> need only be reset whenever the drawing view is
   updated.
<P>
   The value <CODE>d.hasMouseFocus</CODE> is true if <CODE>d</CODE> was delivered a transition of
   type <CODE>FirstDown</CODE>, but not the matching transition of type <CODE>LastUp</CODE>.
   Drawings ignore transitions of type <CODE>LastUp</CODE> if they do not have the mouse
   focus.
<P>
   The field <CODE>d.goodDownClick</CODE> is relevant when the current argument of
   <CODE>d.tool</CODE> has type <CODE>ArgType.Click</CODE>. It is true if the most recent
   <CODE>FirstDown</CODE> mouse transition or down transition of the space key
   successfully selected an argument point in the drawing.
<P>
   The field <CODE>d.txt</CODE> is relevant when the current argument of <CODE>d.tool</CODE> has
   type <CODE>argType.Text</CODE>. In this case, <CODE>d.tool</CODE> must be a <CODE>ProcTool</CODE>. The field
   <CODE>d.txt</CODE> contains the text typed by the user so far (modulo backspaces), but
   not containing a trailing vertical bar character. 

<P><PRE>REVEAL
  <A NAME="ChildWriteOnly">ChildWriteOnly</A> = ChildPublic BRANDED &quot;Drawing.ChildWO&quot; OBJECT
    originVal: JunoConfig.Origin
  OVERRIDES
    init := InitWriteOnly;
    getOrigin := ChildGetOrigin;
    setOrigin := ChildSetOrigin;
    &lt;* LL.sup = VBT.mu.SELF *&gt;
    rescreen := RescreenWriteOnly;
    reshape := ReshapeWriteOnly;
  END;
  <A NAME="Child">Child</A> = ChildWriteOnly BRANDED &quot;Drawing.Child&quot; OBJECT
    hasFocus := FALSE
  OVERRIDES
    &lt;* LL.sup = VBT.mu *&gt;
    mouse := Mouse;
    position := Position;
    key := Key;
    misc := Misc;
    &lt;* LL.sup = VBT.mu.SELF *&gt;
    reshape := Reshape;
    repaint := Repaint;
  END;
</PRE> A <CODE>Child</CODE> is the leaf VBT in which painting for the drawing view is
   performed. A <CODE>Child</CODE> is installed as a child (in the tree of VBT's) of
   a <CODE>Drawing.T</CODE> (which is also both a <CODE>View.T</CODE> and a <CODE>Filter.T</CODE>). 

<P><PRE>REVEAL
  <A NAME="ArgTool">ArgTool</A> = Drag.ArgTool BRANDED &quot;Drawing.ArgTool&quot; OBJECT OVERRIDES
    setup := SetupNoop;
  END;

  <A NAME="PredTool">PredTool</A> = ArgTool BRANDED &quot;Drawing.PredTool&quot; OBJECT OVERRIDES
    apply := ApplyPred
  END;

  <A NAME="FuncTool">FuncTool</A> = ArgTool BRANDED &quot;Drawing.FuncTool&quot; OBJECT OVERRIDES
    apply := ApplyFunc
  END;

  <A NAME="ProcTool">ProcTool</A> = ProcToolPublic BRANDED &quot;Drawing.ProcTool&quot; OBJECT
    txt: JunoAST.Text := NIL;
    call: JunoAST.Save
  OVERRIDES
    setup := SetupProc;
    apply := ApplyProc;
    text := TextProc
  END;

  (* &quot;txt # NIL =&gt; call # NIL&quot;; &quot;txt&quot; will be the last argument of &quot;call&quot; *)

  <A NAME="SetTool">SetTool</A> = Tool BRANDED &quot;Drawing.SetTool&quot; OBJECT
    cmd: JunoAST.ProcCall
  OVERRIDES
    setup := SetupNoop;
    apply := ApplySetTool
  END;

PROCEDURE <A NAME="SetupNoop"><procedure>SetupNoop</procedure></A>(&lt;*UNUSED*&gt; tl: Tool; &lt;*UNUSED*&gt; d: T;
  &lt;*UNUSED*&gt; time: VBT.TimeStamp) =
  BEGIN END SetupNoop;

VAR (*CONST*)
  Red             := PaintOp.FromRGB(1.0, 0.0, 0.0);
  PathColor       := Red;
  SelectColor     := Red;
  FrozenColor     := PaintOp.FromRGB(0.0, 0.35, 1.0);
  LabelColor      := PaintOp.BgFg;

PROCEDURE <A NAME="InitDrawing"><procedure>InitDrawing</procedure></A>(d: T; ch: ChildPublic; root: View.Root): PSImpl.T =
  BEGIN
    EVAL PSImpl.T.init(d, ch, root);
    d.freezeTool := NewFreezeTool();
    d.dragTool := Drag.NewTool();
    RETURN d
  END InitDrawing;

PROCEDURE <A NAME="InitWriteOnly"><procedure>InitWriteOnly</procedure></A>(ch: ChildWriteOnly; origin: JunoConfig.Origin): Child =
  BEGIN
    ch.originVal := origin;
    RETURN ch
  END InitWriteOnly;

PROCEDURE <A NAME="SetLabelStyle"><procedure>SetLabelStyle</procedure></A>(d: T; style: [0..2]) =
  BEGIN
    IF style # d.labelStyle THEN
      d.labelStyle := style;
      d.root.dTrue := FALSE;
      d.update()
    END
  END SetLabelStyle;

PROCEDURE <A NAME="AcquireKBFocus"><procedure>AcquireKBFocus</procedure></A>(ch: Child; time: VBT.TimeStamp) =
</PRE><BLOCKQUOTE><EM> Acquire the keyboard focus if Juno doesn't have it already. </EM></BLOCKQUOTE><PRE>
  BEGIN
    IF NOT ch.hasFocus THEN
      TRY
    	VBT.Acquire(ch, VBT.KBFocus, time);
    	ch.hasFocus := TRUE
      EXCEPT VBT.Error =&gt; (* SKIP *)
      END
    END
  END AcquireKBFocus;

PROCEDURE <A NAME="Misc"><procedure>Misc</procedure></A>(ch: Child; READONLY cd: VBT.MiscRec) =
  &lt;* LL.sup = VBT.mu *&gt;
  BEGIN
    IF cd.type = VBT.Lost AND cd.selection = VBT.KBFocus THEN
      ch.hasFocus := FALSE;
      EVAL FinishTextTool(VBT.Parent(ch))
    END
  END Misc;

PROCEDURE <A NAME="DragModeFromModifiers"><procedure>DragModeFromModifiers</procedure></A>(mods: VBT.Modifiers): Drag.DragMode =
</PRE><BLOCKQUOTE><EM> Return the drag mode appropriate for the modifier keys <CODE>mods</CODE>. </EM></BLOCKQUOTE><PRE>
  BEGIN
    IF VBT.Modifier.Shift IN mods THEN
      RETURN Drag.DragMode.Hor
    ELSIF VBT.Modifier.Option IN mods OR VBT.Modifier.Control IN mods THEN
      RETURN Drag.DragMode.Ver
    ELSE
      RETURN Drag.DragMode.Unconstrained
    END
  END DragModeFromModifiers;

PROCEDURE <A NAME="Mouse"><procedure>Mouse</procedure></A>(ch: Child; READONLY cd: VBT.MouseRec) =
  &lt;* LL.sup = VBT.mu *&gt;
  VAR d: T := VBT.Parent(ch); BEGIN
    (* Ignore &quot;LastUp&quot; events if &quot;d&quot; does not have the mouse focus. *)
    IF NOT d.hasMouseFocus AND cd.clickType = VBT.ClickType.LastUp THEN
      RETURN
    END;
    IF cd.clickType = VBT.ClickType.FirstDown THEN
      AcquireKBFocus(ch, cd.time)
    END;
    (* Don't allow manipulations to drawing unless the AST is up to date
       and we are not in the process of entering text. *)
    IF NOT d.root.astTrue THEN
      JunoError.Display(ch, &quot;Oops! You forgot to click Run.&quot;);
      RETURN
    ELSIF d.txt # NIL THEN
      RETURN
    END;
    (* Check for use of &quot;freeze&quot; or &quot;drag&quot; tool shortcut *)
    IF cd.clickType = VBT.ClickType.FirstDown AND d.stackSize = 0 THEN
      CASE cd.whatChanged OF
        VBT.Modifier.MouseM =&gt;
          d.usingTempTool := TRUE; d.lastTool := d.tool; d.tool := d.dragTool
      | VBT.Modifier.MouseR =&gt;
          d.usingTempTool := TRUE; d.lastTool := d.tool; d.tool := d.freezeTool
      ELSE (* SKIP *)
      END
    END;
    (* Don't proceed  unless a tool has been selected; ignore errant
       middle- and right-mouse clicks. *)
    IF d.tool = NIL THEN
      JunoError.Display(ch, &quot;Oops! You need to choose a tool.&quot;);
      RETURN
    ELSIF cd.whatChanged # VBT.Modifier.MouseL AND NOT d.usingTempTool THEN
      RETURN
    END;
    (* Process the mouse click based on the type of the current argument for
       the current drawing tool. *)
    CASE d.tool.argType[d.stackSize] OF &lt;* NOWARN *&gt;
      ArgType.Click =&gt;
        CASE cd.clickType OF
          VBT.ClickType.FirstDown =&gt;
	    d.hasMouseFocus := TRUE;
	    VAR arg: Arg; BEGIN
	      IF VBT.Modifier.Shift IN cd.modifiers THEN
		arg := ClickNewPt(d, cd.cp.pt, ch.xform,
                  frozen := VBT.Modifier.Option IN cd.modifiers);
		SourceUntrue(d, View.ModKind.ImplicitConsistent);
	      ELSIF NOT FindArg(d, JunoPt.FromHV(cd.cp.pt, ch.xform), arg) THEN
		d.goodDownClick := FALSE;
		RETURN
	      END;
	      Push(d, arg);
	      PaintStackTop(d, select := TRUE)
	    END;
	    d.goodDownClick := TRUE;
        | VBT.ClickType.LastUp =&gt;
	    d.hasMouseFocus := FALSE;
	    IF d.goodDownClick THEN
	      PaintStackTop(d, select := FALSE);
	      ApplyIfReady(d, cd.time)
	    END
        ELSE
          RETURN
        END
    | ArgType.CreateClick =&gt;
        CASE cd.clickType OF
          VBT.ClickType.FirstDown =&gt;
	    d.hasMouseFocus := TRUE;
	    VAR arg: Arg; BEGIN
              arg := ClickNewPt(d, cd.cp.pt, ch.xform,
                frozen := VBT.Modifier.Option IN cd.modifiers);
	      SourceUntrue(d, View.ModKind.ImplicitConsistent);
	      Push(d, arg)
	    END;
	    PaintStackTop(d, select := TRUE)
        | VBT.ClickType.LastUp =&gt;
	    d.hasMouseFocus := FALSE;
	    PaintStackTop(d, select := FALSE);
	    ApplyIfReady(d, cd.time)
        ELSE
          RETURN
        END
    | ArgType.Drag =&gt;
        CASE cd.clickType OF
          VBT.ClickType.FirstDown =&gt;
	    d.hasMouseFocus := TRUE;
	    WITH arg = d.stack[d.stackSize] DO
	      IF FindArg(d, JunoPt.FromHV(cd.cp.pt, ch.xform), arg) THEN
		VBT.SetCage(ch, VBT.CageFromPosition(cd.cp));
		d.dragging := TRUE;
		d.dragger := cd.cp.pt;
		d.draggee := JunoPt.ToHV(arg.loc, ch.xform);
		d.dragName := Atom.ToText(arg.name.id1);
                d.dragMode := DragModeFromModifiers(cd.modifiers);
		(* d.root.marquee.putArg(d.dragName); *)
		TRY d.tool.pre(d, cd, d.stackSize) EXCEPT Drag.Aborted =&gt;
                  (* perform same actions as mouse up-click *)
                  UpDrag(d, ch, VBT.MouseRec{whatChanged:=VBT.Modifier.MouseL,
                    time := cd.time, cp := cd.cp, modifiers := cd.modifiers,
                    clickType := VBT.ClickType.LastUp, clickCount := 1});
                  (* set cage so &quot;Position&quot; won't be called again this drag *)
                  VBT.SetCage(ch, VBT.EverywhereCage)
                END
	      END
	    END
        | VBT.ClickType.LastUp =&gt;
            (* drag completed successfully *)
	    d.hasMouseFocus := FALSE;
	    IF d.dragging THEN UpDrag(d, ch, cd) END
        ELSE
          RETURN
        END
    END;
    (* undo the &quot;freeze&quot; or &quot;drag&quot; tool shortcut *)
    IF d.usingTempTool AND cd.clickType = VBT.ClickType.LastUp THEN
      d.tool := d.lastTool; d.lastTool := NIL; d.usingTempTool := FALSE
    END;
    Sync(ch)
  END Mouse;

PROCEDURE <A NAME="Position"><procedure>Position</procedure></A>(ch: Child; READONLY cd: VBT.PositionRec) =
  &lt;* LL.sup = VBT.mu *&gt;
  VAR d: T := VBT.Parent(ch); BEGIN
    IF NOT d.dragging THEN
      VBT.SetCage(ch, VBT.EverywhereCage)
    ELSIF cd.cp.gone THEN
      VBT.SetCage(ch, VBT.GoneCage)
    ELSE
      TRY
    	VAR delta := Point.Sub(cd.cp.pt, d.dragger); BEGIN
    	  d.dragger := cd.cp.pt;
    	  d.draggee := d.tool.during(d, delta, d.stackSize);
    	END;
    	VBT.SetCage(ch, VBT.CageFromPosition(cd.cp))
      EXCEPT
    	Drag.Aborted =&gt;
    	  (* perform same actions as mouse up-click *)
    	  UpDrag(d, ch, VBT.MouseRec{whatChanged := VBT.Modifier.MouseL,
    	    time := cd.time, cp := cd.cp, modifiers := cd.modifiers,
    	    clickType := VBT.ClickType.LastUp, clickCount := 1});
    	  (* set cage so &quot;Position&quot; won't be called again this drag *)
    	  VBT.SetCage(ch, VBT.EverywhereCage)
      END
    END
  END Position;

PROCEDURE <A NAME="UpDrag"><procedure>UpDrag</procedure></A>(d: T; ch: Child; READONLY cd: VBT.MouseRec) =
</PRE><BLOCKQUOTE><EM> Perform the action associated with up-clicking at the end of a drag arg.
   This can happen when the user explicitly up-clicks while dragging, if a
   compilation error occurred at the start of the drag, or if a Juno run-time
   error occurs while dragging. </EM></BLOCKQUOTE><PRE>
  &lt;* LL.sup = VBT.mu *&gt;
  BEGIN
    d.dragging := FALSE;
    d.stack[d.stackSize].locUp := JunoPt.FromHV(d.draggee, ch.xform);
    d.tool.post(d, cd, d.stackSize);
    ApplyIfReady(d, cd.time);
  END UpDrag;

TYPE ArgSet = SET OF ArgType;

VAR collecting := TRUE;

PROCEDURE <A NAME="Key"><procedure>Key</procedure></A>(ch: Child; READONLY cd: VBT.KeyRec) =
  &lt;* LL.sup = VBT.mu *&gt;
  CONST
    ClickArgs = ArgSet{ArgType.Click, ArgType.CreateClick};
    NoModifiers = VBT.Modifiers{};
  VAR d: T := VBT.Parent(ch);  BEGIN
    IF d.dragging THEN RETURN END;
    IF d.txt # NIL THEN
      (* user is typing a text argument *)
      WITH tool = NARROW(d.tool, ProcTool) DO
      	IF cd.wentDown THEN
      	  IF cd.whatChanged = KeyboardKey.BackSpace OR
      	     cd.whatChanged = KeyboardKey.Delete THEN
      	    IF Text.Length(d.txt) &gt; 0 THEN
      	      d.txt := Text.Sub(d.txt, 0, Text.Length(d.txt) - 1);
      	      tool.text(d, d.txt)
      	    END
      	  ELSIF cd.whatChanged = KeyboardKey.Return AND
      		cd.modifiers = NoModifiers THEN
      	    d.stack[d.stackSize].text := d.txt;
      	    d.txt := NIL;
      	    ApplyIfReady(d, cd.time)
      	  ELSE
      	    VAR ch := KeyTrans.TTY(cd); BEGIN
      	      IF ch # KeyTrans.NullKey THEN
      		d.txt := d.txt &amp; Text.FromChar(ch);
      		tool.text(d, d.txt)
      	      END
      	    END
      	  END;
      	END
      END
    ELSE
      (* user is not typing a text argument *)
      IF cd.whatChanged = Latin1Key.space THEN
	IF cd.wentDown THEN
	  IF d.tool = NIL OR (d.stackSize &gt;= d.prevStackSize) THEN
	    JunoError.Display(d, &quot;No previous argument to copy&quot;);
	    d.goodDownClick := FALSE;
	    RETURN
	  ELSIF d.tool.argType[d.stackSize] IN ClickArgs THEN
	    d.root.marquee.putArg(Atom.ToText(d.stack[d.stackSize].name.id1));
	    PaintStackTop(d, select := TRUE);
	    d.goodDownClick := TRUE
	  END
	ELSIF d.goodDownClick AND d.tool # NIL
            AND d.stackSize &lt; d.prevStackSize
	    AND d.tool.argType[d.stackSize] IN ClickArgs THEN
          PaintStackTop(d, select := FALSE);
          ApplyIfReady(d, cd.time)
	END
      ELSIF cd.wentDown THEN
	IF cd.whatChanged = Latin1Key.g OR cd.whatChanged = Latin1Key.G THEN
          (* do a garbage collection now *)
          RTCollector.Collect()
	ELSIF cd.whatChanged = Latin1Key.o OR cd.whatChanged = Latin1Key.O THEN
          (* toggle whether collection is on or off *)
          IF collecting
            THEN RTCollector.Disable()
            ELSE RTCollector.Enable()
          END;
          collecting := NOT collecting
	ELSIF cd.whatChanged = Latin1Key.u OR cd.whatChanged = Latin1Key.U THEN
	  IF d.stackSize &gt; 0 THEN
	    d.root.marquee.remArg();
	    DEC(d.stackSize)
	  END
	ELSIF cd.whatChanged = Latin1Key.c OR cd.whatChanged = Latin1Key.C THEN
	  d.continuousUnparse := NOT d.continuousUnparse;
	  IF d.continuousUnparse AND NOT d.root.sTrue THEN
	    d.root.source.update()
	  END
	END
      END
    END;
    Sync(ch)
  END Key;

PROCEDURE <A NAME="ClickNewPt"><procedure>ClickNewPt</procedure></A>(
    d: T;
    READONLY hvPt: Point.T;
    READONLY xform: JunoPt.Transform;
    frozen: BOOLEAN)
  : Arg =
</PRE><BLOCKQUOTE><EM> Create a new point in <CODE>d</CODE> at Trestle coordinate <CODE>hvPt</CODE> and update <CODE>d</CODE>'s
   source to contain it. The <CODE>frozen</CODE> parameter determines whether the new
   point is frozen or not. </EM></BLOCKQUOTE><PRE>
  &lt;* LL.sup = VBT.mu *&gt;
  VAR xyPt := JunoPt.FromHV(hvPt, xform); BEGIN
    RETURN CreatePoint(d, xyPt, frozen)
  END ClickNewPt;

PROCEDURE <A NAME="CreatePoint"><procedure>CreatePoint</procedure></A>(d: T; READONLY xyPt: JunoPt.T; frozen: BOOLEAN): Arg =
  &lt;* LL.sup = VBT.mu *&gt;
  VAR res: Arg; atom: Atom.T; BEGIN
    REPEAT
      VAR
        letter := VAL(ORD('a') + d.pointCount MOD 26, CHAR);
        digit := (d.pointCount DIV 26) - 1;
        name := Text.FromChar(letter);
      BEGIN
        IF digit &gt;= 0 THEN name := name &amp; Fmt.Int(digit) END;
        atom := Atom.FromText(name);
        INC(d.pointCount)
      END
    UNTIL CurrCmd.GetVariable(CurrCmd.GetAST(d.root.ccmd), atom) = NIL;
    VAR hint: JunoAST.Expr; BEGIN
      res.name := IdToQId(atom);
      IF GridActive(d) THEN
        VAR x, y: JunoValue.Real; BEGIN
          EVAL JunoPt.RelVal(xyPt.x, xyPt.y, d.grid0.x, d.grid0.y,
            d.grid1.x, d.grid1.y, x, y);
          x := FLOAT(ROUND(x), JunoValue.Real);
          y := FLOAT(ROUND(y), JunoValue.Real);
          hint := NEW(JunoAST.Rel,
            e1 := JunoASTUtils.NewPoint(x, y),
            e2 := NEW(JunoAST.Pair, e1 := d.grid0nm, e2 := d.grid1nm));
          res.loc := EvalRel(x, y, d.grid0, d.grid1);
          CurrCmd.AddVariable(d.root.ccmd, atom, res.loc, hint, frozen := TRUE)
        END
      ELSE
        res.loc := xyPt;
        hint := JunoPt.ToASTPair(xyPt);
        CurrCmd.AddVariable(d.root.ccmd, atom, xyPt, hint, frozen := frozen);
      END;
      res.locUp := DummyXY;
      RETURN res
    END
  END CreatePoint;

PROCEDURE <A NAME="FindArg"><procedure>FindArg</procedure></A>(d: T; READONLY pt: JunoPt.T; VAR (*OUT*) arg: Arg): BOOLEAN =
  VAR nm := FindPoint(d, pt); BEGIN
    IF nm = NIL THEN RETURN FALSE END;
    VAR px, py: JunoValue.Real; BEGIN
      EVAL CurrCmd.PointLocation(d.root.ccmd, nm, px, py);
      arg := Arg{IdToQId(nm), JunoPt.T{px, py}, DummyXY};
      RETURN TRUE
    END
  END FindArg;

PROCEDURE <A NAME="FindPoint"><procedure>FindPoint</procedure></A>(d: T; READONLY pt0: JunoPt.T): JunoAST.Id =
  &lt;* LL.sup &lt;= VBT.mu *&gt;
  VAR
    best := Real.MaxFinite;
    bestatom: Atom.T := NIL;
  PROCEDURE P(n: Atom.T; READONLY pt1: JunoPt.T) =
    VAR dx := pt1.x - pt0.x; dy := pt1.y - pt0.y; d := dx*dx + dy*dy; BEGIN
      IF d &lt; best THEN best := d; bestatom := n END
    END P;
  BEGIN
    CurrCmd.ForAllPoints(d.root.ccmd, P);
    RETURN bestatom
  END FindPoint;

PROCEDURE <A NAME="Push"><procedure>Push</procedure></A>(m: T; READONLY arg: Arg) =
  BEGIN
    m.stack[m.stackSize] := arg
    (* ; m.root.marquee.putArg(Atom.ToText(arg.name.id1)) *)
  END Push;

&lt;* SPEC ApplyIfReady REQUIRES sup(LL) = VBT.mu *&gt;

PROCEDURE <A NAME="ApplyIfReady"><procedure>ApplyIfReady</procedure></A>(d: T; time: VBT.TimeStamp) =
</PRE><BLOCKQUOTE><EM> Increment the stack pointer, and apply <CODE>d.tool</CODE> if there are enough
   arguments. If the most recent argument was not the last one and the current
   argument has type <CODE>ArgType.Text</CODE>, initialize state in <CODE>d</CODE> for the next
   argument.
<P>
   This procedure changes the drawing without doing a <CODE>Sync</CODE>. </EM></BLOCKQUOTE><PRE>
  BEGIN
    INC(d.stackSize);
    IF d.stackSize = NUMBER(d.tool.argType^) THEN
      d.tool.apply(d, SUBARRAY(d.stack, 0, d.stackSize));
      (* d.root.marquee.newline(); *)
      ClearStack(d, time)
    ELSIF d.tool.argType[d.stackSize] = ArgType.Text THEN
      d.txt := &quot;&quot;;
      NARROW(d.tool, ProcTool).text(d, d.txt)
    END;
  END ApplyIfReady;

PROCEDURE <A NAME="ClearStack"><procedure>ClearStack</procedure></A>(d: T; time: VBT.TimeStamp; tl: Tool := NIL) =
</PRE><BLOCKQUOTE><EM> Clear <CODE>d</CODE>'s stack, and call the <CODE>setup</CODE> method of the tool <CODE>tl</CODE>. If <CODE>tl</CODE> is
   defaulted, then use the current <CODE>ArgTool</CODE> of <CODE>d</CODE> if one is selected. </EM></BLOCKQUOTE><PRE>
  BEGIN
    IF d.stackSize # 0 THEN d.prevStackSize := d.stackSize END;
    d.stackSize := 0;
    (* d.root.marquee.erase(); *)
    IF tl = NIL THEN tl := d.tool END;
    IF tl # NIL THEN
      tl.setup(d, time);
      (* ; d.root.marquee.putName(tl.label) *)
      (* invoke &quot;text&quot; method of 1-arg text tool *)
      TYPECASE tl OF
        ProcTool (pt) =&gt;
       	  IF NUMBER(pt.argType^) = 1 AND pt.argType[0] = ArgType.Text THEN
    	    d.txt := &quot;&quot;;      (* indicate we are typing a text arg *)
    	    pt.text(d, d.txt);
    	    AcquireKBFocus(Filter.Child(d), time)
       	  END
      ELSE (*SKIP*)
      END
    END
  END ClearStack;

PROCEDURE <A NAME="RescreenWriteOnly"><procedure>RescreenWriteOnly</procedure></A>(ch: ChildWriteOnly; READONLY cd: VBT.RescreenRec) =
  &lt;* LL.sup = VBT.mu.ch *&gt;
  CONST MMPerPoint = 25.4(*mm/in*) /  72.0(*pt/in*); BEGIN
    IF cd.st # NIL THEN
      ch.xform.xScale := VBT.MMToPixels(ch, MMPerPoint, Axis.T.Hor);
      ch.xform.yScale := VBT.MMToPixels(ch, MMPerPoint, Axis.T.Ver);
      ch.xform.widthScale := (ch.xform.xScale + ch.xform.yScale) / 2.0
    END
  END RescreenWriteOnly;

PROCEDURE <A NAME="ReshapeWriteOnly"><procedure>ReshapeWriteOnly</procedure></A>(ch: ChildWriteOnly; READONLY cd: VBT.ReshapeRec) =
  &lt;* LL.sup = VBT.mu.ch *&gt;
  BEGIN
    IF cd.new # Rect.Empty THEN
      CASE ch.originVal OF
        JunoConfig.Origin.SW =&gt; ch.xform.origin := Rect.SouthWest(cd.new)
      | JunoConfig.Origin.Center =&gt; ch.xform.origin := Rect.Middle(cd.new)
      END;
      ch.repaint(Region.Full)
    END
  END ReshapeWriteOnly;

PROCEDURE <A NAME="Reshape"><procedure>Reshape</procedure></A>(ch: Child; READONLY cd: VBT.ReshapeRec) =
  &lt;* LL.sup = VBT.mu.ch *&gt;
  BEGIN
    IF cd.new # Rect.Empty THEN
      VAR d: T := VBT.Parent(ch); BEGIN
  	d.root.dTrue := FALSE
      END
    END;
    ChildWriteOnly.reshape(ch, cd)
  END Reshape;

PROCEDURE <A NAME="Repaint"><procedure>Repaint</procedure></A>(ch: Child; &lt;*UNUSED*&gt; READONLY rgn: Region.T) =
  &lt;* LL.sup = VBT.mu.ch *&gt;
  VAR d: T := VBT.Parent(ch); BEGIN
    DisplayError(d, Exec(d));
    Sync(ch)
  END Repaint;

PROCEDURE <A NAME="Exec"><procedure>Exec</procedure></A>(d: T): TEXT =
  VAR res: TEXT := NIL; BEGIN
    IF NOT d.root.dTrue THEN
      res := ExecCurrCmd(d, d.root.skipify); Annotations(d);
      IF res = NIL THEN d.root.dTrue := TRUE END
    END;
    RETURN res
  END Exec;

PROCEDURE <A NAME="ExecCurrCmd"><procedure>ExecCurrCmd</procedure></A>(d: T; skipify: BOOLEAN): TEXT =
  VAR ch: Child := Filter.Child(d); BEGIN
    (* prepare drawing *)
    PSImpl.Reset(d, inExec := FALSE);
    VBT.PaintTint(ch, Rect.Full, PaintOp.Bg);
    DblBufferVBT.ClearSaved(ch);
    (* run current command *)
    TRY
      IF CurrCmd.Run(d.root.ccmd, skipify) THEN
        SourceUntrue(d, View.ModKind.ImplicitConsistent)
      END
    EXCEPT
      CurrCmd.CompileError (msg) =&gt; RETURN msg
    | CurrCmd.RuntimeError (rec) =&gt; RETURN rec.errorMsg
    END;
    RETURN NIL
  END ExecCurrCmd;

PROCEDURE <A NAME="Annotations"><procedure>Annotations</procedure></A>(d: T) =
</PRE><BLOCKQUOTE><EM> Draw annotations after running the current command </EM></BLOCKQUOTE><PRE>
  BEGIN
    PaintPath(d);
    PaintGrid(d);
    PaintBBox(d);
    PaintPoints(d);
  END Annotations;

PROCEDURE <A NAME="Sync"><procedure>Sync</procedure></A>(v: VBT.T) =
  BEGIN VBT.Sync(v) END Sync;

PROCEDURE <A NAME="PaintPath"><procedure>PaintPath</procedure></A>(d: T) =
  VAR ch: Child := Filter.Child(d); BEGIN
    WITH path = d.ps.path DO
      VBT.Stroke(ch, Rect.Full, path, width := 3, end := VBT.EndStyle.Square,
        join := VBT.JoinStyle.Bevel, op := PaintOp.Bg);
      VBT.Stroke(ch, Rect.Full, path, op := PathColor)
    END
  END PaintPath;

PROCEDURE <A NAME="GridActive"><procedure>GridActive</procedure></A>(d: T): BOOLEAN =
  BEGIN
    RETURN d.gridMode AND
      1.0 &lt;= ABS(d.grid1.x - d.grid0.x) + ABS(d.grid1.y - d.grid0.y)
  END GridActive;

PROCEDURE <A NAME="AdjustGridPoints"><procedure>AdjustGridPoints</procedure></A>(d: T) =
</PRE><BLOCKQUOTE><EM> Update the values of <CODE>d.grid0</CODE> and <CODE>d.grid1</CODE> from the current command. If
   either of the points <CODE>d.grid0nm</CODE> or <CODE>d.grid1nm</CODE> is not defined in the
   current command, then reset <CODE>d.gridMode</CODE>. </EM></BLOCKQUOTE><PRE>
  PROCEDURE UpdatePoint(nm: JunoAST.Id; VAR (*OUT*) pt: JunoPt.T) =
    VAR x, y: JunoValue.Real; BEGIN
      IF CurrCmd.PointLocation(d.root.ccmd, nm, x, y)
        THEN pt := JunoPt.T{x, y}
        ELSE d.gridMode := FALSE
      END
    END UpdatePoint;
  BEGIN
    IF d.gridMode THEN
      UpdatePoint(d.grid0nm.id1, d.grid0);
      UpdatePoint(d.grid1nm.id1, d.grid1);
      (* turn off grid tool if grid is no longer active *)
      IF NOT d.gridMode THEN
        ToolBox.SwapButton(d.toolBox, d.gridOff, d.gridOn, &quot;Grid On&quot;)
      END
    END
  END AdjustGridPoints;

PROCEDURE <A NAME="ToGridCoords"><procedure>ToGridCoords</procedure></A>(d: T; ch: Child; hvPt: Point.T): JunoPt.T =
  VAR res: JunoPt.T; xyPt := JunoPt.FromHV(hvPt, ch.xform); BEGIN
    EVAL JunoPt.RelVal(xyPt.x, xyPt.y, d.grid0.x, d.grid0.y,
      d.grid1.x, d.grid1.y, res.x, res.y);
    RETURN res
  END ToGridCoords;

PROCEDURE <A NAME="PaintGrid"><procedure>PaintGrid</procedure></A>(d: T) =
  BEGIN
    AdjustGridPoints(d);
    IF GridActive(d) THEN
      VAR
	ch: Child := Filter.Child(d);
	dom := VBT.Domain(d);
	nw := ToGridCoords(d, ch, Rect.NorthWest(dom));
	ne := ToGridCoords(d, ch, Rect.NorthEast(dom));
	se := ToGridCoords(d, ch, Rect.SouthEast(dom));
	sw := ToGridCoords(d, ch, Rect.SouthWest(dom));
	mini := MIN(MIN(FLOOR(nw.x), FLOOR(ne.x)),
		    MIN(FLOOR(se.x), FLOOR(sw.x)));
	maxi := MAX(MAX(CEILING(nw.x), CEILING(ne.x)),
		    MAX(CEILING(se.x), CEILING(sw.x)));
	minj := MIN(MIN(FLOOR(nw.y), FLOOR(ne.y)),
		    MIN(FLOOR(se.y), FLOOR(sw.y)));
	maxj := MAX(MAX(CEILING(nw.y), CEILING(ne.y)),
		    MAX(CEILING(se.y), CEILING(sw.y)));
      BEGIN
	FOR i := mini TO maxi DO
	  FOR j := minj TO maxj DO
	    VBT.PaintTint(ch, Rect.FromPoint(
	      JunoPt.ToHV(
                EvalRel(
                  FLOAT(i, JunoValue.Real), FLOAT(j, JunoValue.Real),
		  d.grid0, d.grid1),
		ch.xform)),
	      PaintOp.Fg)
	  END
	END
      END
    END
  END PaintGrid;

PROCEDURE <A NAME="PaintBBox"><procedure>PaintBBox</procedure></A>(d: T) =
  VAR ch: Child := Filter.Child(d);
  PROCEDURE HVPoint(x, y: JunoValue.Real): Point.T =
    BEGIN RETURN JunoPt.ToHV(JunoPt.T{x, y}, ch.xform) END HVPoint;
  VAR path := NEW(Path.T); BEGIN
    WITH bbox = d.ps.bbox DO
      Path.MoveTo(path, HVPoint(bbox.west, bbox.south));
      Path.LineTo(path, HVPoint(bbox.east, bbox.south));
      Path.LineTo(path, HVPoint(bbox.east, bbox.north));
      Path.LineTo(path, HVPoint(bbox.west, bbox.north));
      Path.Close(path)
    END;
    VBT.Stroke(ch, Rect.Full, path, width := 1, end := VBT.EndStyle.Butt,
      join := VBT.JoinStyle.Miter, src := Pixmap.Gray)
  END PaintBBox;

PROCEDURE <A NAME="PaintPoints"><procedure>PaintPoints</procedure></A>(d: T) =
  VAR
    ccmd := d.root.ccmd;
    ch: Child := Filter.Child(d);
  PROCEDURE P(atom: Atom.T; READONLY pt: JunoPt.T) =
    VAR op: PaintOp.T; BEGIN
      IF CurrCmd.IsFrozen(ccmd, atom)
        THEN op := FrozenColor
        ELSE op := LabelColor
      END;
      PaintPoint(ch, Atom.ToText(atom), op,
        pt := JunoPt.ToHV(pt, ch.xform))
    END P;
  BEGIN
    IF d.labelStyle &gt; 0 THEN
      CurrCmd.ForAllPoints(ccmd, P)
    END
  END PaintPoints;

PROCEDURE <A NAME="PaintStackTop"><procedure>PaintStackTop</procedure></A>(d: T; select: BOOLEAN) =
</PRE><BLOCKQUOTE><EM> Paint the label on the top of <CODE>d</CODE>'s stack. If <CODE>select</CODE> is true, then paint
   the label in <CODE>SelectColor</CODE>; otherwise, paint it in <CODE>LabelColor</CODE> or
   <CODE>FrozenColor</CODE> based on whether the point is frozen or not. </EM></BLOCKQUOTE><PRE>
  VAR
    arg := d.stack[d.stackSize];
    ch: Child := Filter.Child(d);
    op: PaintOp.T;
  BEGIN
    IF select THEN
      op := SelectColor
    ELSE
      IF CurrCmd.IsFrozen(d.root.ccmd, arg.name.id1)
        THEN op := FrozenColor
        ELSE op := LabelColor
      END;
    END;
    PaintPoint(ch, Atom.ToText(arg.name.id1), op := op,
      pt := JunoPt.ToHV(arg.loc, ch.xform))
  END PaintStackTop;

PROCEDURE <A NAME="PaintPoint"><procedure>PaintPoint</procedure></A>(ch: Child; name: TEXT; op: PaintOp.T; pt: Point.T) =
  VAR m: T := VBT.Parent(ch); BEGIN
    CASE m.labelStyle OF
      0 =&gt; (* SKIP *)
    | 1 =&gt;
        VAR delta := Point.Sub(pt, Rect.Middle(JunoConfig.cross.r)); BEGIN
          JunoConfig.crossBdry := Region.Add(JunoConfig.crossBdry, delta);
          JunoConfig.cross := Region.Add(JunoConfig.cross, delta);
          VBT.PaintRegion(ch, JunoConfig.crossBdry, PaintOp.Bg);
          VBT.PaintRegion(ch, JunoConfig.cross, op)
        END
    | 2 =&gt;
        VAR
          delta := Point.Sub(pt, Rect.Middle(JunoConfig.dot.r));
          rect := VBT.BoundingBox(ch, name, JunoConfig.labelFont);
        BEGIN
	  JunoConfig.dot := Region.Add(JunoConfig.dot, delta);
	  VBT.PaintText(ch, t := name, op := op, fnt := JunoConfig.labelFont,
	    pt := Point.Sub(pt, Rect.NorthEast(rect)), clip := Rect.Full);
	  VBT.PaintRegion(ch, JunoConfig.dot, op)
        END
    END
  END PaintPoint;

PROCEDURE <A NAME="Update"><procedure>Update</procedure></A>(d: T) =
  &lt;* LL.sup &lt;= VBT.mu *&gt;
  BEGIN
    d.pointCount := 0;
    d.txt := NIL;
    ClearStack(d, 0);
    DisplayError(d, Exec(d));
    Sync(Filter.Child(d))
  END Update;

PROCEDURE <A NAME="ChildGetOrigin"><procedure>ChildGetOrigin</procedure></A>(ch: ChildWriteOnly): JunoConfig.Origin =
  BEGIN RETURN ch.originVal END ChildGetOrigin;

PROCEDURE <A NAME="ChildSetOrigin"><procedure>ChildSetOrigin</procedure></A>(ch: ChildWriteOnly; origin: JunoConfig.Origin) =
  BEGIN ch.originVal := origin END ChildSetOrigin;

PROCEDURE <A NAME="FinishTextTool"><procedure>FinishTextTool</procedure></A>(d: T): BOOLEAN =
  VAR res := d.txt # NIL; BEGIN
    IF res THEN ToolBox.Unselect(d.root) END;
    RETURN res
  END FinishTextTool;

PROCEDURE <A NAME="Make"><procedure>Make</procedure></A>(d: T; skipify: BOOLEAN) =
  BEGIN
    d.pointCount := 0;
    d.txt := NIL;
    ClearStack(d, 0);
    VAR err := ExecCurrCmd(d, skipify); BEGIN
      Annotations(d);
      IF err = NIL
        THEN d.root.dTrue := TRUE
        ELSE DisplayError(d, err)
      END
    END;
    Sync(Filter.Child(d))
  END Make;

PROCEDURE <A NAME="ArrayToList"><procedure>ArrayToList</procedure></A>(READONLY arg: ARRAY OF Arg; byVal := FALSE):
    JunoAST.ExprList =
</PRE><BLOCKQUOTE><EM> Return a list of expressions in <CODE>arg</CODE>, where the <CODE>i</CODE>th expression in the
   list corresponds to <CODE>arg[i]</CODE>. If <CODE>arg[i].text # NIL</CODE>, then the <CODE>i</CODE>th
   expression is the text string <CODE>arg[i].text</CODE>. Otherwise, if <CODE>byVal</CODE>, then
   the <CODE>i</CODE>th expression is the value <CODE>arg[i].loc</CODE>; if not, then it is the name
   of the point <CODE>arg[i].name</CODE>. </EM></BLOCKQUOTE><PRE>
  VAR res: JunoAST.ExprList; expr: JunoAST.Expr; BEGIN
    IF byVal
      THEN res := NEW(JunoAST.ExprList, bp := JunoAST.End)
      ELSE res := NEW(JunoAST.QIdList, bp := JunoAST.End)
    END;
    FOR i := LAST(arg) TO FIRST(arg) BY -1 DO
      IF arg[i].text # NIL THEN
        expr := ToASTText(arg[i].text)
      ELSIF byVal THEN
        expr := JunoPt.ToASTPair(arg[i].loc)
      ELSE
        &lt;* ASSERT arg[i].name # NIL *&gt;
        expr := arg[i].name
      END;
      res.head := NEW(JunoAST.ExprLink, expr := expr, next := res.head);
      INC(res.size)
    END;
    RETURN res
  END ArrayToList;

PROCEDURE <A NAME="FindBreak"><procedure>FindBreak</procedure></A>(t: TEXT): CARDINAL =
</PRE><BLOCKQUOTE><EM> Return the index of the character after the last run of newlines not
   counting a run of newlines that terminates <CODE>t</CODE>, or 0 if <CODE>t</CODE> contains no
   non-terminal newlines. </EM></BLOCKQUOTE><PRE>
  VAR i := Text.Length(t) - 1; BEGIN
    WHILE i &gt;= 0 AND Text.GetChar(t, i) = '\n' DO DEC(i) END;
    RETURN Text.FindCharR(t, '\n', start := i) + 1
  END FindBreak;

PROCEDURE <A NAME="ToASTText"><procedure>ToASTText</procedure></A>(t: TEXT): JunoAST.Expr =
</PRE><BLOCKQUOTE><EM> Return an expression equivalent to the text <CODE>t</CODE>. If <CODE>t</CODE> contains newline
   characters, the result will be a concatenation of text literals. </EM></BLOCKQUOTE><PRE>
  VAR i := FindBreak(t); res: JunoAST.Expr; BEGIN
    res := NEW(JunoAST.Text, val := Text.Sub(t, i), bp := JunoAST.End);
    IF i # 0 THEN
      res := NEW(JunoAST.Concat, bp := JunoAST.End,
        e1 := ToASTText(Text.Sub(t, 0, i)), e2 := res)
    END;
    RETURN res
  END ToASTText;

PROCEDURE <A NAME="MakeConstraint"><procedure>MakeConstraint</procedure></A>(tl: PredTool; READONLY arg: ARRAY OF Arg):
    JunoAST.Formula =
  BEGIN
    IF tl.name.id0 = JunoAST.NilId THEN
      IF tl.name.id1 = CongToolSym THEN
	RETURN NEW(JunoAST.Cong, bp := JunoAST.End,
	  e1 := MakePair(arg[0].name, arg[1].name),
	  e2 := MakePair(arg[2].name, arg[3].name))
      ELSIF tl.name.id1 = ParaToolSym THEN
	RETURN NEW(JunoAST.Para, bp := JunoAST.End,
	  e1 := MakePair(arg[0].name, arg[1].name),
	  e2 := MakePair(arg[2].name, arg[3].name))
      ELSIF tl.name.id1 = HorToolSym THEN
	RETURN NEW(JunoAST.Hor, bp := JunoAST.End,
          e1 := arg[0].name, e2 := arg[1].name)
      ELSIF tl.name.id1 = VerToolSym THEN
	RETURN NEW(JunoAST.Ver, bp := JunoAST.End,
          e1 := arg[0].name, e2 := arg[1].name)
      END
    END;
    RETURN NEW(JunoAST.Call, inouts := JunoAST.EmptyExprList,
      name := tl.name, ins := ArrayToList(arg), bp := JunoAST.End)
  END MakeConstraint;

PROCEDURE <A NAME="MakeEqConstraint"><procedure>MakeEqConstraint</procedure></A>(tl: FuncTool; READONLY arg: ARRAY OF Arg):
    JunoAST.Formula =
  BEGIN
    RETURN NEW(JunoAST.Equals, bp := JunoAST.End, e1 := arg[0].name,
      e2 := NEW(JunoAST.Call, bp := JunoAST.End,
        inouts := JunoAST.EmptyExprList, name := tl.name,
        ins := ArrayToList(SUBARRAY(arg, 1, tl.in_cnt))))
  END MakeEqConstraint;

PROCEDURE <A NAME="MakeCommand"><procedure>MakeCommand</procedure></A>(
    tl: ProcTool;
    READONLY arg: ARRAY OF Arg;
    byVal := FALSE): JunoAST.ProcCall =
</PRE><BLOCKQUOTE><EM> Return a procedure call to the procedure <CODE>tl.name</CODE> with the arguments
   stored in <CODE>arg</CODE>. </EM></BLOCKQUOTE><PRE>
  BEGIN
    &lt;* ASSERT tl.out_cnt + tl.inout_cnt + tl.in_cnt = NUMBER(arg) *&gt;
    WITH
      outArgs = SUBARRAY(arg, 0, tl.out_cnt),
      inoutArgs = SUBARRAY(arg, tl.out_cnt, tl.inout_cnt),
      inArgs = SUBARRAY(arg, tl.out_cnt + tl.inout_cnt, tl.in_cnt)
    DO
      RETURN NEW(JunoAST.ProcCall, bp := JunoAST.End, name := tl.name,
        outs := ArrayToList(outArgs, byVal := FALSE),
        inouts := ArrayToList(inoutArgs, byVal := FALSE),
        inout_parens := NUMBER(inoutArgs) &gt; 1,
        ins := ArrayToList(inArgs, byVal))
    END
  END MakeCommand;

PROCEDURE <A NAME="MakePair"><procedure>MakePair</procedure></A>(id1, id2: JunoAST.QId): JunoAST.Pair =
  BEGIN
    RETURN NEW(JunoAST.Pair, e1 := id1, e2 := id2, bp := JunoAST.End)
  END MakePair;

PROCEDURE <A NAME="EvalRel"><procedure>EvalRel</procedure></A>(x, y: JunoValue.Real; p0, p1: JunoPt.T): JunoPt.T =
  VAR res: JunoPt.T; dx := p1.x - p0.x; dy := p1.y - p0.y; BEGIN
    res.x := p0.x + dx * x - dy * y;
    res.y := p0.y + dx * y + dy * x;
    RETURN res
  END EvalRel;

&lt;* INLINE *&gt;
PROCEDURE <A NAME="IdToQId"><procedure>IdToQId</procedure></A>(a: JunoAST.Id): JunoAST.QId =
  BEGIN
    RETURN NEW(JunoAST.QId, bp := JunoAST.End, id0 := JunoAST.NilId, id1 := a)
  END IdToQId;

PROCEDURE <A NAME="SelectTool"><procedure>SelectTool</procedure></A>(d: T; t: Tool; time: VBT.TimeStamp) =
  BEGIN
    (* IF d.tool = NIL THEN PopupMarquee(d) END; *)
    IF d.txt # NIL THEN
      IF Text.Length(d.txt) &gt; 0 THEN
        (* In the middle of entering text; simulate &lt;RETURN&gt; *)
        d.stack[d.stackSize].text := d.txt;
        d.txt := NIL;
        ApplyIfReady(d, time);
        (* NOTE: If the current tool is a text tool taking a single argument,
           its &quot;setup&quot; method will have set &quot;d.txt&quot; to the empty string. *)
      END;
      IF d.txt # NIL THEN
        &lt;* ASSERT Text.Length(d.txt) = 0 *&gt;
        (* erase the vertical bar for the empty text arg *)
        DblBufferVBT.Restore(Filter.Child(d));
        d.txt := NIL
      END;
      Sync(Filter.Child(d))
    END;
    IF t # NIL AND NUMBER(t.argType^) = 0 THEN
      (* attempt to apply a tool with 0 user-supplied arguments;
         this includes set tools *)
      IF d.root.astTrue THEN (* AST must be up-to-date *)
        ClearStack(d, time, tl := t);
        t.apply(d, ARRAY OF Arg{});
      	ClearStack(d, time);
      	Sync(Filter.Child(d));
</PRE><BLOCKQUOTE><EM><P>
      	d.root.marquee.erase();
      	d.root.marquee.putName(t.label);
      	d.root.marquee.newline()
</EM></BLOCKQUOTE><PRE>
      ELSE
        JunoError.Display(Filter.Child(d), &quot;Oops! You forgot to click Run.&quot;);
      END
    ELSE
      (* deselect tool or select tool with &gt; 0 arguments *)
      d.tool := t;
      ClearStack(d, time)
    END
  END SelectTool;

PROCEDURE <A NAME="NewArgArray"><procedure>NewArgArray</procedure></A>(n: CARDINAL; type: ArgType): REF ARRAY OF ArgType =
  VAR res := NEW(REF ARRAY OF ArgType, n); BEGIN
    FOR i := 0 TO LAST(res^) DO res[i] := type END;
    RETURN res
  END NewArgArray;

PROCEDURE <A NAME="NewCreateTool"><procedure>NewCreateTool</procedure></A>(): ArgTool =
  BEGIN
    RETURN NEW(ArgTool, argType := NewArgArray(1, ArgType.CreateClick),
      apply := ApplyPoint)
  END NewCreateTool;

&lt;* SPEC ApplyPoint REQUIRES sup(LL) = VBT.mu *&gt;

PROCEDURE <A NAME="ApplyPoint"><procedure>ApplyPoint</procedure></A>(
  &lt;*UNUSED*&gt; tl: Tool;
  &lt;*UNUSED*&gt; d: T;
  &lt;*UNUSED*&gt; READONLY arg: ARRAY OF Arg) =
  BEGIN
    (*SKIP*)
  END ApplyPoint;

PROCEDURE <A NAME="NewPredTool"><procedure>NewPredTool</procedure></A>(name: JunoAST.QId; in_cnt: CARDINAL): ArgTool =
  BEGIN
    RETURN NEW(PredTool, name := name, in_cnt := in_cnt,
      argType := NewArgArray(in_cnt, ArgType.Click))
  END NewPredTool;

&lt;* SPEC ApplyPred REQUIRES sup(LL) = VBT.mu *&gt;

PROCEDURE <A NAME="ApplyPred"><procedure>ApplyPred</procedure></A>(tl: PredTool; d: T; READONLY arg: ARRAY OF Arg) =
</PRE><BLOCKQUOTE><EM> This is the default implementation of the <CODE>apply</CODE> method for predicate
   tools. </EM></BLOCKQUOTE><PRE>
  BEGIN
    &lt;* ASSERT d.root.astTrue *&gt;
    CurrCmd.AddConstraint(d.root.ccmd, MakeConstraint(tl, arg));
    d.root.dTrue := FALSE;		 (* indicate implicit modification *)
    SourceUntrue(d, View.ModKind.ImplicitOutOfDate)
  END ApplyPred;

PROCEDURE <A NAME="NewFuncTool"><procedure>NewFuncTool</procedure></A>(name: JunoAST.QId; in_cnt: CARDINAL): ArgTool =
  BEGIN
    RETURN NEW(FuncTool, name := name, in_cnt := in_cnt,
      argType := NewArgArray(1 + in_cnt, ArgType.Click))
  END NewFuncTool;

&lt;* SPEC ApplyFunc REQUIRES sup(LL) = VBT.mu *&gt;

PROCEDURE <A NAME="ApplyFunc"><procedure>ApplyFunc</procedure></A>(tl: FuncTool; d: T; READONLY arg: ARRAY OF Arg) =
</PRE><BLOCKQUOTE><EM> This is the default implementation of the <CODE>apply</CODE> method for function
   tools. </EM></BLOCKQUOTE><PRE>
  BEGIN
    &lt;* ASSERT d.root.astTrue *&gt;
    CurrCmd.AddConstraint(d.root.ccmd, MakeEqConstraint(tl, arg));
    d.root.dTrue := FALSE;		 (* indicate implicit modification *)
    SourceUntrue(d, View.ModKind.ImplicitOutOfDate)
  END ApplyFunc;

PROCEDURE <A NAME="NewProcTool"><procedure>NewProcTool</procedure></A>(name: JunoAST.QId; in_cnt: CARDINAL;
  out_cnt, inout_cnt: CARDINAL := 0; isText: BOOLEAN): ArgTool =
  VAR
    num := in_cnt + out_cnt + inout_cnt;
    args := NewArgArray(num, ArgType.Click);
  BEGIN
    IF isText THEN args[num-1] := ArgType.Text END;
    RETURN NEW(ProcTool, name := name, argType := args,
      out_cnt := out_cnt, inout_cnt := inout_cnt, in_cnt := in_cnt)
  END NewProcTool;

PROCEDURE <A NAME="SetupProc"><procedure>SetupProc</procedure></A>(tl: ProcTool; &lt;*UNUSED*&gt; d: T;
    &lt;*UNUSED*&gt; time: VBT.TimeStamp) =
  BEGIN
    (* initialize &quot;ProcTool&quot; fields *)
    tl.txt := NIL; tl.call := NIL;
  END SetupProc;

&lt;* SPEC ApplyProc REQUIRES sup(LL) = VBT.mu *&gt;

PROCEDURE <A NAME="ApplyProc"><procedure>ApplyProc</procedure></A>(tl: ProcTool; d: T; READONLY arg: ARRAY OF Arg) =
</PRE><BLOCKQUOTE><EM> This is the default implementation of the <CODE>apply</CODE> method for procedure
   tools. </EM></BLOCKQUOTE><PRE>
  BEGIN
    &lt;* ASSERT d.root.astTrue *&gt;
    IF tl.txt # NIL THEN
      (* This is a procedure whose last argument is a text *)
      tl.txt.val := d.stack[d.stackSize-1].text;
      DblBufferVBT.Restore(Filter.Child(d));
      tl.txt := NIL; tl.call := NIL
    END;
    IncrRunCmd(d, MakeCommand(tl, arg, byVal := TRUE));
    CurrCmd.AddCommand(d.root.ccmd, MakeCommand(tl, arg, byVal := FALSE));
    SourceUntrue(d, View.ModKind.ImplicitConsistent)
  END ApplyProc;

PROCEDURE <A NAME="IncrRunCmd"><procedure>IncrRunCmd</procedure></A>(d: T; cmd: JunoAST.Cmd) =
</PRE><BLOCKQUOTE><EM> Incrementally compile and run <CODE>cmd</CODE>, then annotate the current path and the
   point labels. Requires that <CODE>cmd</CODE> compiles without any errors. </EM></BLOCKQUOTE><PRE>
  &lt;* FATAL JunoCompileErr.Error *&gt;
  VAR
    slot := JunoBuild.Cmd(cmd, CurrCmd.GetScope(d.root.ccmd));
    execRes: JunoRT.ExecRes;
  BEGIN
    execRes := JunoRT.ExecFromSlot(slot);
    IF execRes.trapCode # JunoRT.TrapCode.NormalHalt THEN
      JunoError.Display(d, JunoRT.TrapMessage(execRes))
    END;
    PaintPath(d);
    PaintPoints(d)
  END IncrRunCmd;

PROCEDURE <A NAME="TextProc"><procedure>TextProc</procedure></A>(t: ProcTool; d: T; txt: TEXT) =
  &lt;* LL.sup = VBT.mu *&gt;
  VAR txtArg := txt &amp; &quot;|&quot;; ch := Filter.Child(d); BEGIN
    IF t.txt = NIL THEN
      (* save partial arg on stack for subsequent call to &quot;MakeCommand&quot; *)
      d.stack[d.stackSize].text := txtArg;
      WITH args = SUBARRAY(d.stack, 0, d.stackSize+1) DO
        t.call := NEW(JunoAST.Save, bp := JunoAST.End,
          nm := JunoASTUtils.QIdFromText(&quot;PS&quot;),
          body := MakeCommand(t, args, byVal := TRUE))
      END;
      t.txt := GetLastArg(t.call.body);
      DblBufferVBT.Save(ch);
    ELSE
      DblBufferVBT.Restore(ch);
      t.txt.val := txtArg   (* destructively change &quot;t.call&quot; *)
    END;
    IncrRunCmd(d, t.call);
    Sync(ch)
  END TextProc;

PROCEDURE <A NAME="GetLastArg"><procedure>GetLastArg</procedure></A>(c: JunoAST.ProcCall): JunoAST.Expr =
</PRE><BLOCKQUOTE><EM> Return the last <CODE>IN</CODE> argument of <CODE>c</CODE>. Requires that <CODE>c</CODE> has at least one
   <CODE>IN</CODE> parameter. </EM></BLOCKQUOTE><PRE>
  VAR curr := c.ins.head; BEGIN
    WHILE curr.next # NIL DO curr := curr.next END;
    RETURN curr.expr
  END GetLastArg;

PROCEDURE <A NAME="NewRelTool"><procedure>NewRelTool</procedure></A>(): ArgTool =
  BEGIN
    RETURN NEW(ArgTool, argType := NewArgArray(3, ArgType.Click),
      apply := ApplyRel)
  END NewRelTool;

&lt;* SPEC ApplyRel REQUIRES sup(LL) = VBT.mu *&gt;

PROCEDURE <A NAME="ApplyRel"><procedure>ApplyRel</procedure></A>(&lt;*UNUSED*&gt; tl: Tool; m: T; READONLY arg: ARRAY OF Arg) =
  BEGIN
    CurrCmd.DoRel(m.root.ccmd, arg[0].name.id1,
      arg[1].name.id1, arg[2].name.id1);
    SourceUntrue(m, View.ModKind.ImplicitConsistent)
  END ApplyRel;

PROCEDURE <A NAME="NewRel1Tool"><procedure>NewRel1Tool</procedure></A>(): ArgTool =
  BEGIN
    RETURN NEW(ArgTool, argType := NewArgArray(2, ArgType.Click),
      apply := ApplyRel1)
  END NewRel1Tool;

&lt;* SPEC ApplyRel1 REQUIRES sup(LL) = VBT.mu *&gt;

PROCEDURE <A NAME="ApplyRel1"><procedure>ApplyRel1</procedure></A>(&lt;*UNUSED*&gt; tl: Tool; m: T; READONLY arg: ARRAY OF Arg) =
  BEGIN
    CurrCmd.DoRel1(m.root.ccmd, arg[0].name.id1,
      arg[1].name.id1);
    SourceUntrue(m, View.ModKind.ImplicitConsistent)
  END ApplyRel1;

PROCEDURE <A NAME="NewFreezeTool"><procedure>NewFreezeTool</procedure></A>(): ArgTool =
  BEGIN
    RETURN NEW(ArgTool, argType := NewArgArray(1, ArgType.Click),
      apply := ApplyFreeze)
  END NewFreezeTool;

&lt;* SPEC ApplyFreeze REQUIRES sup(LL) = VBT.mu *&gt;

PROCEDURE <A NAME="ApplyFreeze"><procedure>ApplyFreeze</procedure></A>(&lt;*UNUSED*&gt; tl: Tool; m: T; READONLY arg: ARRAY OF Arg) =
  BEGIN
    CurrCmd.FreezePoint(m.root.ccmd, arg[0].name.id1);
    SourceUntrue(m, View.ModKind.ImplicitConsistent);
    PaintPoints(m)
  END ApplyFreeze;

PROCEDURE <A NAME="NewAdjustTool"><procedure>NewAdjustTool</procedure></A>(): ArgTool =
  BEGIN
    RETURN NEW(ArgTool, argType := NewArgArray(1, ArgType.Drag),
      apply := ApplyAdjust);
  END NewAdjustTool;

PROCEDURE <A NAME="RoundToGrid"><procedure>RoundToGrid</procedure></A>(d: T; p: JunoPt.T): JunoPt.T =
</PRE><BLOCKQUOTE><EM> If <CODE>d</CODE>'s grid is not active, return <CODE>p</CODE>. Otherwise, return the grid point
   nearest to <CODE>p</CODE>. </EM></BLOCKQUOTE><PRE>
  VAR x, y: JunoValue.Real; BEGIN
    IF NOT GridActive(d) THEN RETURN p END;
    EVAL JunoPt.RelVal(p.x, p.y, d.grid0.x, d.grid0.y,
      d.grid1.x, d.grid1.y, x, y);
    x := FLOAT(ROUND(x), JunoValue.Real);
    y := FLOAT(ROUND(y), JunoValue.Real);
    RETURN EvalRel(x, y, d.grid0, d.grid1)
  END RoundToGrid;

&lt;* SPEC ApplyAdjust REQUIRES sup(LL) = VBT.mu *&gt;

PROCEDURE <A NAME="ApplyAdjust"><procedure>ApplyAdjust</procedure></A>(&lt;*UNUSED*&gt; tl: Tool; d: T; READONLY arg: ARRAY OF Arg) =
  VAR newLoc := RoundToGrid(d, arg[0].locUp); BEGIN
    Adjust(d, arg[0].name.id1, newLoc)
  END ApplyAdjust;

PROCEDURE <A NAME="Adjust"><procedure>Adjust</procedure></A>(d: T; ptName: JunoAST.Id; READONLY ptLoc: JunoPt.T) =
</PRE><BLOCKQUOTE><EM> Adjusts the point named <CODE>ptName</CODE> in the drawing <CODE>d</CODE> to the new location
   <CODE>ptLoc</CODE>. If the point is currently unfrozen, Juno temporarily freezes the
   point and runs the current command. If that does not succeed, the point is
   unfrozen again, and the current command is re-tried.
<P>
   This procedure calls <CODE>SourceUntrue(d)</CODE>, and displays any run-time error
   message. It also automatically adjusts the drawing's grid if the grid is
   active and <CODE>ptName</CODE> is one of the grid control points. </EM></BLOCKQUOTE><PRE>
  VAR isFrozen := CurrCmd.IsFrozen(d.root.ccmd, ptName); res: TEXT; BEGIN
    IF NOT isFrozen THEN
      (* temporarily freeze the point *)
      CurrCmd.FreezePoint(d.root.ccmd, ptName)
    END;
    CurrCmd.MovePoint(d.root.ccmd, ptName, ptLoc.x, ptLoc.y);
    res := ExecCurrCmd(d, d.root.skipify);
    IF NOT isFrozen THEN
      (* point was originally unfrozen; unfreeze the point *)
      CurrCmd.FreezePoint(d.root.ccmd, ptName);
      (* run again if the command failed with the point frozen *)
      IF res # NIL THEN
        res := ExecCurrCmd(d, d.root.skipify)
      END
    END;
    Annotations(d);
    SourceUntrue(d, View.ModKind.ImplicitConsistent);
    DisplayError(d, res)
  END Adjust;

PROCEDURE <A NAME="NewGridTools"><procedure>NewGridTools</procedure></A>(tb: VBT.Split; d: T): ArgTool =
  BEGIN
    d.toolBox := tb;
    d.gridOn := NEW(ArgTool, argType := NewArgArray(2, ArgType.Click),
      apply := ApplyGridOn);
    d.gridOff := NEW(ArgTool, argType := NewArgArray(0, ArgType.Click),
      apply := ApplyGridOff);
    RETURN d.gridOn
  END NewGridTools;

&lt;* SPEC ApplyGridOn REQUIRES sup(LL) = VBT.mu *&gt;

PROCEDURE <A NAME="ApplyGridOn"><procedure>ApplyGridOn</procedure></A>(&lt;*UNUSED*&gt; tl: Tool; d: T; READONLY arg: ARRAY OF Arg) =
  BEGIN
    d.gridMode := TRUE;
    d.grid0 := arg[0].loc; d.grid0nm := arg[0].name;
    d.grid1 := arg[1].loc; d.grid1nm := arg[1].name;
    ToolBox.Unselect(d.root);
    ToolBox.SwapButton(d.toolBox, d.gridOn, d.gridOff, &quot;Grid Off&quot;);
    PaintGrid(d);
    PaintBBox(d);
    PaintPoints(d);
    Sync(Filter.Child(d))
  END ApplyGridOn;

&lt;* SPEC ApplyGridOff REQUIRES sup(LL) = VBT.mu *&gt;

PROCEDURE <A NAME="ApplyGridOff"><procedure>ApplyGridOff</procedure></A>(
    &lt;*UNUSED*&gt; tl: Tool;
    d: T;
    &lt;*UNUSED*&gt; READONLY arg: ARRAY OF Arg) =
  BEGIN
    ToolBox.SwapButton(d.toolBox, d.gridOff, d.gridOn, &quot;Grid On&quot;);
    d.gridMode := FALSE;
    d.root.dTrue := FALSE; (* force exec to repaint drawing *)
    DisplayError(d, Exec(d));
    Sync(Filter.Child(d))
  END ApplyGridOff;

PROCEDURE <A NAME="NewSetTool"><procedure>NewSetTool</procedure></A>(procNm: JunoAST.QId; arg: JunoAST.Expr): SetTool =
  VAR res := NEW(SetTool, argType := NEW(REF ARRAY OF ArgType, 0)); BEGIN
    res.cmd := NEW(JunoAST.ProcCall, bp := JunoAST.End,
      outs := JunoAST.EmptyQIdList, inouts := JunoAST.EmptyQIdList,
      name := procNm, ins := JunoASTUtils.NewExprList(arg));
    RETURN res
  END NewSetTool;

&lt;* SPEC ApplySetTool REQUIRES sup(LL) = VBT.mu *&gt;

PROCEDURE <A NAME="ApplySetTool"><procedure>ApplySetTool</procedure></A>(tl: SetTool; d: T;
    &lt;*UNUSED*&gt; READONLY arg: ARRAY OF Arg) =
  VAR cmd := Unqualify(tl.cmd, Editor.ModuleName(d.root.editor)); BEGIN
    CurrCmd.AddCommand(d.root.ccmd, cmd);
    IncrRunCmd(d, cmd);
    SourceUntrue(d, View.ModKind.ImplicitConsistent)
  END ApplySetTool;

PROCEDURE <A NAME="Unqualify"><procedure>Unqualify</procedure></A>(call: JunoAST.ProcCall; mod: Atom.T): JunoAST.Cmd =
</PRE><BLOCKQUOTE><EM> Requires <CODE>call</CODE> to have one argument. This unqualifies any top-level
   qualified identifier in <CODE>call</CODE> whose qualifier is <CODE>mod</CODE>. </EM></BLOCKQUOTE><PRE>
  BEGIN
    RETURN NEW(JunoAST.ProcCall, bp := JunoAST.End,
      outs := call.outs, inouts := call.inouts,
      name := UnqualifyQId(call.name, mod),
      ins := JunoASTUtils.NewExprList(UnqualifyQId(call.ins.head.expr, mod)))
  END Unqualify;

PROCEDURE <A NAME="UnqualifyQId"><procedure>UnqualifyQId</procedure></A>(ex: JunoAST.Expr; mod: Atom.T): JunoAST.Expr =
  BEGIN
    TYPECASE ex OF
      JunoAST.QId (qid) =&gt;
        IF qid.id0 # JunoAST.NilId AND qid.id0 = mod THEN
          RETURN NEW(JunoAST.QId, bp := JunoAST.End,
            id0 := JunoAST.NilId, id1 := qid.id1)
        END
    ELSE (* SKIP *)
    END;
    RETURN ex
  END UnqualifyQId;
</PRE><P>
PROCEDURE PopupMarquee(m: T) =
  BEGIN
    IF VBT.Parent(m.root.marquee) = NIL THEN
      VAR b := BorderedVBT.New(
        BorderedVBT.New(m.root.marquee, size := 3.0, op := PaintOp.Bg)); BEGIN
        VBTClass.Rescreen(b, VBT.ScreenTypeOf(m.root.source));
        VAR
          sh := VBTClass.GetShapes(b);
          se := Rect.SouthEast(VBT.Domain(m.root.source));
        BEGIN
          ZSplit.InsertAt(VBT.Parent(m.root.source), b,
            Point.MoveHV(se,
              -sh[Axis.T.Hor].pref - 10,
              -sh[Axis.T.Ver].pref - 10));
          ZSplit.SetReshapeControl(b, ZSplit.ESChains)
        END
      END
    END
  END PopupMarquee;


<P><PRE>PROCEDURE <A NAME="SourceUntrue"><procedure>SourceUntrue</procedure></A>(d: T; how: View.ModKind) =
  BEGIN
    d.root.source.modified(how);
    IF d.continuousUnparse THEN d.root.source.update() END
  END SourceUntrue;

PROCEDURE <A NAME="DisplayError"><procedure>DisplayError</procedure></A>(d: T; errmsg: TEXT) =
  &lt;* LL.sup = VBT.mu *&gt;
  BEGIN
    IF errmsg # NIL THEN
      VAR ch := Filter.Child(d.root.source); BEGIN
        JunoError.P(NARROW(ch, TextEditVBT.T).tp, errmsg)
      END
    END
  END DisplayError;

BEGIN
  HorToolSym  := Atom.FromText(&quot;_HOR&quot;);
  VerToolSym  := Atom.FromText(&quot;_VER&quot;);
  CongToolSym := Atom.FromText(&quot;_CONG&quot;);
  ParaToolSym := Atom.FromText(&quot;_PARA&quot;);
END Drawing.
</PRE>
</inModule>
<HR>
<A NAME="x1">interface View is in:
</A><UL>
<LI><A HREF="View.i3.html#0TOP0">juno-app/src/View.i3</A>
<LI><A HREF="../../zeus/src/View.i3.html#0TOP0">zeus/src/View.i3</A>
</UL>
<P>
<HR>
<A NAME="x2">interface Editor is in:
</A><UL>
<LI><A HREF="../../cm3ide/src/misc/Editor.i3.html#0TOP0">cm3ide/src/misc/Editor.i3</A>
<LI><A HREF="Editor.i3.html#0TOP0">juno-app/src/Editor.i3</A>
</UL>
<P>
<HR>
<A NAME="x3">interface JunoValue is in:
</A><UL>
<LI><A HREF="../../juno-machine/src/JunoValue.i3.html#0TOP0">juno-machine/src/JunoValue.i3</A>
<LI><A HREF="../../pkl-fonts/src/JunoValue.i3.html#0TOP0">pkl-fonts/src/JunoValue.i3</A>
</UL>
<P>
<PRE>























</PRE>
</BODY>
</HTML>
