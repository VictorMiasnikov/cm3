<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: showheap/src/ShowHeap.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>showheap/src/ShowHeap.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright (C) 1992, Digital Equipment Corporation       
 All rights reserved.                                    
 See the file COPYRIGHT for a full description.          
                                                         
 Last modified on Tue Jan 31 10:34:50 PST 1995 by kalsow 
      modified on Fri Jan 29 19:07:08 PST 1993 by jdd    
      modified on Fri Jan 15 12:20:22 PST 1993 by mhb    
      modified on Thu Apr 23 18:57:36 PDT 1992 by muller 

<P><PRE>UNSAFE MODULE <module>ShowHeap</module> EXPORTS <A HREF="../../m3core/src/main/Main.i3.html"><implements>Main</A></implements>;

IMPORT <A HREF="../../libm3/src/geometry/Axis.i3.html">Axis</A>, <A HREF="../../ui/src/split/ButtonVBT.i3.html">ButtonVBT</A>, <A HREF="../../vbtkit/src/color/ColorName.i3.html">ColorName</A>, <A HREF="../../vbtkit/src/color/Color.i3.html">Color</A>, <A HREF="../../libm3/src/fmtlex/Fmt.i3.html">Fmt</A>, <A HREF="../../ui/src/split/HVSplit.i3.html">HVSplit</A>, <A HREF="../../ui/src/vbt/PaintOp.i3.html">PaintOp</A>, <A HREF="../../libm3/src/geometry/Point.i3.html">Point</A>;
IMPORT <A HREF="../../libm3/src/os/Common/Process.i3.html">Process</A>, <A HREF="../../libm3/src/rw/Rd.i3.html">Rd</A>, <A HREF="../../libm3/src/geometry/Rect.i3.html">Rect</A>, <A HREF="../../libm3/src/geometry/Region.i3.html">Region</A>, <A HREF="../../ui/src/split/Split.i3.html">Split</A>, <A HREF="../../libm3/src/rw/Stdio.i3.html">Stdio</A>, <A HREF="../../m3core/src/text/Text.i3.html">Text</A>, <A HREF="../../ui/src/split/TextVBT.i3.html">TextVBT</A>, <A HREF="../../ui/src/vbt/Trestle.i3.html">Trestle</A>;
IMPORT <A HREF="../../ui/src/vbt/VBT.i3.html">VBT</A>, <A HREF="../../libm3/src/rw/Wr.i3.html">Wr</A>;

IMPORT <A HREF="../../m3core/src/runtime/common/RTHeapEvent.i3.html">RTHeapEvent</A>, <A HREF="../../m3core/src/runtime/common/RTHeapRep.i3.html">RTHeapRep</A>;

FROM <A HREF="../../m3core/src/runtime/common/RTHeapRep.i3.html">RTHeapRep</A> IMPORT Generation, Note, Page, Space;

TYPE
  Desc = RECORD
           space     : BITS 2 FOR Space;
           generation: BITS 1 FOR Generation;
           pure      : BITS 1 FOR BOOLEAN;
           note      : BITS 3 FOR Note;
           gray      : BITS 1 FOR BOOLEAN;
           clean     : BITS 1 FOR BOOLEAN;
           continued : BITS 1 FOR BOOLEAN := FALSE;
  END;

&lt;*FATAL ANY*&gt;
</PRE>------------------------------------------------------------ heap state ---

<P><PRE>VAR
  collections: INTEGER := 0;
  firstPage  : Page    := 1;
  lastPage   : Page    := 0;
  desc                 := NEW(UNTRACED REF ARRAY OF Desc, 0);

TYPE Counter = {None, New, Copied, Immobile, Older};

VAR
  count := ARRAY Counter OF CARDINAL{0, ..};
  countVBT, countTextVBT: ARRAY Counter OF VBT.T;

PROCEDURE <A NAME="CounterOf"><procedure>CounterOf</procedure></A> (d: Desc): Counter =
  BEGIN
    RETURN counterOf[
             d.space, d.generation, d.pure, d.note, d.gray, d.clean];
  END CounterOf;
</PRE>---------------------------------------------------------------- colors ---

<P> Each interesting page state has a bright color and a somber color.
   If the page state can be <CODE>gray</CODE> or not (in the GC sense), the
   bright color is used for the gray state and the somber for the
   normal, non-gray.  The somber color is generated by averaging the
   bright color with a gray of the same intensity.
<P>
<PRE>
      state       color   intensity  bright RGB         somber RGB
      
      free        white    1.0       1.000 1.000 1.000
      new         blue     0.75      0.730 0.730 0.730
      immobile    green    0.55      0.000 0.812 0.000  0.275 0.676 0.275
      copied      red      0.5       1.000 0.343 0.343  0.750 0.421 0.421
      older       magenta  0.45      1.000 0.198 1.000  0.725 0.324 0.725
      previous    gray     0.25      0.250 0.250 0.250
      unallocated black    0.0       0.000 0.000 0.000
</PRE>


<P>
<P><PRE>VAR
  rgb: ARRAY Space, Generation, BOOLEAN (* pure *), Note,
         BOOLEAN (* gray *), BOOLEAN (* clean *) OF
         Color.T;
  tint: ARRAY Space, Generation, BOOLEAN (* pure *), Note,
          BOOLEAN (* gray *), BOOLEAN (* clean *) OF
          PaintOp.T;
  counterOf: ARRAY Space, Generation, BOOLEAN (* pure *), Note,
          BOOLEAN (* gray *), BOOLEAN (* clean *) OF
          Counter;
  mapBackGround := ComputeColor(&quot;LightLightGray&quot;);
  red           := ComputeColor(&quot;Red&quot;);
  black         := ComputeColor(&quot;Black&quot;);
  white         := ComputeColor(&quot;White&quot;);

  gcOnQuad  := PaintOp.MakeColorQuad(black, red);
  gcOffQuad := PaintOp.MakeColorQuad(white, black);

PROCEDURE <A NAME="ComputeColor"><procedure>ComputeColor</procedure></A> (name: Text.T): PaintOp.T =
  VAR t: Color.T;
  BEGIN
    t := ColorName.ToRGB(name);
    RETURN PaintOp.FromRGB(t.r, t.g, t.b);
  END ComputeColor;

PROCEDURE <A NAME="InitColors"><procedure>InitColors</procedure></A> () =
  BEGIN
    FOR space := FIRST(Space) TO LAST(Space) DO
      FOR generation := FIRST(Generation) TO LAST(Generation) DO
        FOR pure := FIRST(BOOLEAN) TO LAST(BOOLEAN) DO
          FOR note := FIRST(Note) TO LAST(Note) DO
            FOR gray := FIRST(BOOLEAN) TO LAST(BOOLEAN) DO
              FOR clean := FIRST(BOOLEAN) TO LAST(BOOLEAN) DO
                CASE space OF
                | Space.Unallocated =&gt;
                    rgb[space, generation, pure, note, gray, clean] :=
                      Color.T{0.0, 0.0, 0.0};
                    counterOf[
                      space, generation, pure, note, gray, clean] :=
                      Counter.None;
                | Space.Free =&gt;
                    rgb[space, generation, pure, note, gray, clean] :=
                      Color.T{1.0, 1.0, 1.0};
                    counterOf[
                      space, generation, pure, note, gray, clean] :=
                      Counter.None;
                | Space.Previous =&gt;
                    rgb[space, generation, pure, note, gray, clean] :=
                      Color.T{0.25, 0.25, 0.25};
                    counterOf[
                      space, generation, pure, note, gray, clean] :=
                      Counter.None;
                | Space.Current =&gt;
                    CASE note OF
                    | Note.Allocated =&gt;
                        rgb[space, generation, pure, note, gray, clean] :=
                          Color.T{0.730, 0.730, 1.0};
                        counterOf[
                          space, generation, pure, note, gray, clean] :=
                          Counter.New;
                    | Note.Copied, Note.Large =&gt;
                        IF gray THEN
                          rgb[
                            space, generation, pure, note, gray, clean] :=
                            Color.T{1.0, 0.343, 0.343};
                        ELSE
                          rgb[
                            space, generation, pure, note, gray, clean] :=
                            Color.T{0.75, 0.421, 0.421};
                        END;
                        counterOf[
                          space, generation, pure, note, gray, clean] :=
                          Counter.Copied;
                    | Note.AmbiguousRoot =&gt;
                        IF gray THEN
                          rgb[
                            space, generation, pure, note, gray, clean] :=
                            Color.T{0.0, 0.812, 0.0};
                        ELSE
                          rgb[
                            space, generation, pure, note, gray, clean] :=
                            Color.T{0.275, 0.676, 0.275};
                        END;
                        counterOf[
                          space, generation, pure, note, gray, clean] :=
                          Counter.Immobile;
                    | Note.Frozen =&gt;
                        IF gray THEN
                          rgb[
                            space, generation, pure, note, gray, clean] :=
                            Color.T{0.0, 0.812, 0.0};
                        ELSE
                          rgb[
                            space, generation, pure, note, gray, clean] :=
                            Color.T{0.275, 0.676, 0.275};
                        END;
                        counterOf[
                          space, generation, pure, note, gray, clean] :=
                          Counter.Immobile;
                    | Note.OlderGeneration =&gt;
                        IF gray THEN
                          rgb[
                            space, generation, pure, note, gray, clean] :=
                            Color.T{1.0, 0.198, 1.0};
                        ELSE
                          rgb[
                            space, generation, pure, note, gray, clean] :=
                            Color.T{0.725, 0.225, 0.725};
                        END;
                        counterOf[
                          space, generation, pure, note, gray, clean] :=
                          Counter.Older;
                    END;
                END;
                WITH rgb = rgb[space, generation, pure, note, gray, clean] DO
                  tint[space, generation, pure, note, gray, clean] :=
                    PaintOp.FromRGB(rgb.r, rgb.g, rgb.b);
                END;
              END;
            END;
          END;
        END;
      END;
    END;
    VAR
      rgb := Color.T{0.730, 0.730, 1.0};
      quad := PaintOp.MakeColorQuad(
                PaintOp.FromRGB(rgb.r, rgb.g, rgb.b), PaintOp.Fg);
    BEGIN
      countVBT[Counter.New] := TextVBT.New(&quot;&quot;, bgFg := quad);
      countTextVBT[Counter.New] := TextVBT.New(&quot;new&quot;, bgFg := quad);
    END;
    VAR
      rgb := Color.T{0.75, 0.421, 0.421};
      quad := PaintOp.MakeColorQuad(
                PaintOp.FromRGB(rgb.r, rgb.g, rgb.b), PaintOp.Bg);
    BEGIN
      countVBT[Counter.Copied] := TextVBT.New(&quot;&quot;, bgFg := quad);
      countTextVBT[Counter.Copied] := TextVBT.New(&quot;copied&quot;, bgFg := quad);
    END;
    VAR
      rgb := Color.T{0.275, 0.676, 0.275};
      quad := PaintOp.MakeColorQuad(
                PaintOp.FromRGB(rgb.r, rgb.g, rgb.b), PaintOp.Fg);
    BEGIN
      countVBT[Counter.Immobile] := TextVBT.New(&quot;&quot;, bgFg := quad);
      countTextVBT[Counter.Immobile] :=
        TextVBT.New(&quot;immobile&quot;, bgFg := quad);
    END;
    VAR
      rgb := Color.T{0.725, 0.225, 0.725};
      quad := PaintOp.MakeColorQuad(
                PaintOp.FromRGB(rgb.r, rgb.g, rgb.b), PaintOp.Bg);
    BEGIN
      countVBT[Counter.Older] := TextVBT.New(&quot;&quot;, bgFg := quad);
      countTextVBT[Counter.Older] := TextVBT.New(&quot;older&quot;, bgFg := quad);
    END;
  END InitColors;
</PRE>------------------------------------------------------------ Heap map ---

<P><PRE>TYPE
  HeapMapVBT = VBT.Leaf OBJECT
                 rect                    := Rect.T{0, 1, 0, 1};
                 side        : INTEGER;
                 nCols, nRows: INTEGER   := 1;
                 firstSquare : Rect.T;
                 displayedTints: REF ARRAY OF PaintOp.T := NIL;
               OVERRIDES
                 repaint := RepaintHeapMap;
                 reshape := ReshapeHeapMap;
                 shape   := ShapeHeapMap;
               END;

PROCEDURE <A NAME="LayoutHeapMap"><procedure>LayoutHeapMap</procedure></A> (self: HeapMapVBT) = (* Given the rectangle to be
                                                occupied by the heap map
                                                and the number of pages to
                                                display, compute the size
                                                of each square *)
  VAR
    tryLarger              := TRUE;
    p                      := MAX(lastPage - firstPage + 1, 1);
    width, height: INTEGER;
  BEGIN                          (* Recompute the layout of the map *)
    width := self.rect.east - self.rect.west;
    height := self.rect.south - self.rect.north;
    self.side := 1;
    self.nCols := width;
    self.nRows := height;

    WHILE tryLarger DO
      WITH largerSide = self.side + 1,
           largerCols = width DIV largerSide,
           largerRows = height DIV largerSide DO

        IF p &lt;= largerCols * largerRows THEN (* ok *)
          self.side := largerSide;
          self.nCols := largerCols;
          self.nRows := largerRows;
        ELSE
          tryLarger := FALSE;
        END;
      END;
    END;

    self.firstSquare :=
      Rect.FromCorner(
        Point.MoveHV(Rect.NorthWest(self.rect),
                     (width - self.side * self.nCols) DIV 2,
                     (height - self.side * self.nRows) DIV 2), self.side,
        self.side);

  END LayoutHeapMap;

PROCEDURE <A NAME="RepaintHeapMap"><procedure>RepaintHeapMap</procedure></A> (                    self: HeapMapVBT;
                          &lt;*UNUSED*&gt; READONLY rgn : Region.T    ) =
  VAR
    p       := 0;
    nbPages := lastPage - firstPage + 1;
    square  := self.firstSquare;
  BEGIN
    (* Fill the map with the background color *)
    VBT.PaintTint(self, self.rect, mapBackGround);

    (* redisplay each page *)
    FOR y := 0 TO self.nRows - 1 DO
      FOR x := 0 TO self.nCols - 1 DO
        IF p &lt; nbPages THEN
          VAR
            d  := desc[p];
            sq := square;
          BEGIN
            INC(sq.north, 1);
            DEC(sq.south, 1);
            IF NOT d.continued THEN INC(sq.west, 2); END;
            VBT.PaintTint(self, square, white);
            VBT.PaintTint(self, sq, tint[d.space, d.generation, d.pure,
                                         d.note, d.gray, d.clean]);
          END;
        END;
        INC(p);
        INC(square.east, self.side);
        INC(square.west, self.side);
      END;
      square.east := self.firstSquare.east;
      square.west := self.firstSquare.west;
      INC(square.north, self.side);
      INC(square.south, self.side);
    END;
  END RepaintHeapMap;

PROCEDURE <A NAME="RepaintOnePage"><procedure>RepaintOnePage</procedure></A> (self: HeapMapVBT; page: Page) =
  VAR
    p     := page - firstPage;
    row   := p DIV MAX(self.nCols, 1);
    col   := p - row * self.nCols;
    west  := self.firstSquare.west + col * self.side;
    east  := west + self.side;
    north := self.firstSquare.north + row * self.side;
    south := north + self.side;
    square := Rect.T{
                west := west, east := east, north := north, south := south};
  BEGIN
    VBT.PaintTint(self, square, white);
    VAR
      d := desc[p];
      t := tint[d.space, d.generation, d.pure, d.note, d.gray, d.clean];
      sq := square;
    BEGIN
      INC(sq.north, 1);
      DEC(sq.south, 1);
      IF NOT d.continued THEN INC(sq.west, 2); END;
      VBT.PaintTint(self, sq, t);
    END;
  END RepaintOnePage;

PROCEDURE <A NAME="ReshapeHeapMap"><procedure>ReshapeHeapMap</procedure></A> (self: HeapMapVBT; READONLY cd: VBT.ReshapeRec) =
  BEGIN
    self.rect := cd.new;
    LayoutHeapMap(self);
    RepaintHeapMap(self, Region.T{r := cd.new});
  END ReshapeHeapMap;

PROCEDURE <A NAME="ShapeHeapMap"><procedure>ShapeHeapMap</procedure></A> (&lt;*UNUSED*&gt; self: HeapMapVBT;
                                   ax  : Axis.T;
                        &lt;*UNUSED*&gt; n   : CARDINAL    ): VBT.SizeRange =
  BEGIN
    IF ax = Axis.T.Hor THEN
      RETURN (VBT.SizeRange{lo := 200, pref := 300, hi := 100 * 1000});
    ELSE
      RETURN (VBT.SizeRange{lo := 200, pref := 200, hi := 100 * 1000});
    END;
  END ShapeHeapMap;
</PRE>---------------------------------------------------------- various VBTs ---

<P><PRE>PROCEDURE <A NAME="ShowValueVBT"><procedure>ShowValueVBT</procedure></A> (name: Text.T; value: VBT.T): VBT.T =
  BEGIN
    RETURN HVSplit.Cons(Axis.T.Hor, TextVBT.New(name, 0.0), value);
  END ShowValueVBT;

TYPE A = REF RECORD p: PROCEDURE ();  END;

PROCEDURE <A NAME="ActionVBT"><procedure>ActionVBT</procedure></A> (name: Text.T; action: PROCEDURE ()): VBT.T =
  BEGIN
    RETURN
      ButtonVBT.New(TextVBT.New(name), DoActionVBT, NEW(A, p := action));
  END ActionVBT;

PROCEDURE <A NAME="DoActionVBT"><procedure>DoActionVBT</procedure></A> (                    self: ButtonVBT.T;
                       &lt;*UNUSED*&gt; READONLY cd  : VBT.MouseRec ) =
  BEGIN
    NARROW(VBT.GetProp(self, TYPECODE(A)), A).p();
  END DoActionVBT;
</PRE>------------------------------------------------------- Number Displays ---

<P><PRE>VAR
  gcs := TextVBT.New(&quot;&quot;);
  off := TextVBT.New(&quot;&quot;);
</PRE>-------------------------------------------------------------- controls ---

<P><PRE>VAR
  root, control: VBT.T;
  map          : HeapMapVBT;

PROCEDURE <A NAME="StartAction"><procedure>StartAction</procedure></A> () =
  BEGIN
    Wr.PutChar(Stdio.stdout, 'g');
    Wr.Flush(Stdio.stdout);
  END StartAction;

PROCEDURE <A NAME="QuitAction"><procedure>QuitAction</procedure></A> () =
  BEGIN
    Trestle.Delete(root);
    Process.Exit(0);
  END QuitAction;

PROCEDURE <A NAME="SetupVBT"><procedure>SetupVBT</procedure></A> () =
  BEGIN
    control := HVSplit.New(Axis.T.Ver);
    Split.AddChild(
      control, countVBT[Counter.New], countTextVBT[Counter.New],
      countVBT[Counter.Copied], countTextVBT[Counter.Copied],
      countVBT[Counter.Immobile], countTextVBT[Counter.Immobile],
      countVBT[Counter.Older], countTextVBT[Counter.Older]);
    Split.AddChild(
      control, ShowValueVBT(&quot;gcs = &quot;, gcs), ShowValueVBT(&quot;off = &quot;, off));
    Split.AddChild(control, ActionVBT(&quot;start&quot;, StartAction),
                   ActionVBT(&quot;quit&quot;, QuitAction));
    map := NEW(HeapMapVBT);
    root := HVSplit.Cons(Axis.T.Hor, control, map);
    Trestle.Install(root);
  END SetupVBT;
</PRE>---------------------------------------------------------------------------

<P><PRE>TYPE
  Evt = RTHeapEvent.T;

CONST
  EvtSize = (BITSIZE (Evt) + BITSIZE (CHAR) - 1) DIV BITSIZE (CHAR);

TYPE
  EvtChars = ARRAY [0..EvtSize-1] OF CHAR;

PROCEDURE <A NAME="GetEvent"><procedure>GetEvent</procedure></A> (): Evt =
  VAR e: Evt;
  BEGIN
    EVAL Rd.GetSub (Stdio.stdin, LOOPHOLE (e, EvtChars));
    RETURN e;
  END GetEvent;
</PRE>---------------------------------------------------------------------------

<P><PRE>PROCEDURE <A NAME="Run"><procedure>Run</procedure></A> () =
  BEGIN
    LOOP
      VAR e := GetEvent();
      BEGIN
        CASE e.kind OF
        | RTHeapEvent.Kind.Begin =&gt;
            INC(collections);
            TextVBT.SetFont(gcs, TextVBT.GetFont(gcs), gcOnQuad);
            TextVBT.Put(gcs, Fmt.Int(collections));
        | RTHeapEvent.Kind.Flip =&gt;
        | RTHeapEvent.Kind.Roots =&gt;
        | RTHeapEvent.Kind.End =&gt;
            TextVBT.SetFont(gcs, TextVBT.GetFont(gcs), gcOffQuad);
        | RTHeapEvent.Kind.Grow =&gt;
            VAR
              newFirstPage, newLastPage: Page;
              newDesc                  : UNTRACED REF ARRAY OF Desc;
            BEGIN
              IF firstPage = 1 AND lastPage = 0 THEN
                newFirstPage := e.first;
                newLastPage := e.first + e.nb - 1;
              ELSE
                newFirstPage := MIN(e.first, firstPage);
                newLastPage := MAX(e.first + e.nb - 1, lastPage);
              END;
              newDesc := NEW(UNTRACED REF ARRAY OF Desc,
                             newLastPage - newFirstPage + 1);
              FOR p := e.first TO e.first + e.nb - 1 DO
                newDesc[p - newFirstPage].space := Space.Free;
                newDesc[p - newFirstPage].pure := TRUE;
                newDesc[p - newFirstPage].continued := FALSE;
              END;
              IF NOT (firstPage = 1 AND lastPage = 0) THEN
                SUBARRAY(newDesc^, firstPage - newFirstPage,
                         lastPage - firstPage + 1) := desc^;
                FOR p := e.first + e.nb TO firstPage - 1 DO
                  newDesc[p - newFirstPage].space := Space.Unallocated;
                END;
                FOR p := lastPage + 1 TO e.first - 1 DO
                  newDesc[p - newFirstPage].space := Space.Unallocated;
                END;
              END;
              desc := newDesc;
              firstPage := newFirstPage;
              lastPage := newLastPage;
            END;
            LayoutHeapMap(map);
            RepaintHeapMap(map, Region.T{r := map.rect});
        | RTHeapEvent.Kind.Change =&gt;
            VAR
              edesc := Desc{space      := e.desc.space,
                            generation := e.desc.generation,
                            pure       := e.desc.pure,
                            note       := e.desc.note,
                            gray       := e.desc.gray,
                            clean      := e.desc.clean};
              new := CounterOf(edesc);
            BEGIN
              VAR old := CounterOf(desc[e.first - firstPage]);
              BEGIN
                desc[e.first - firstPage] := edesc;
                IF new # old THEN
                  IF old # Counter.None THEN
                    DEC(count[old]);
                    TextVBT.Put(countVBT[old], Fmt.Int(count[old]));
                  END;
                  IF new # Counter.None THEN
                    INC(count[new]);
                    TextVBT.Put(countVBT[new], Fmt.Int(count[new]));
                  END;
                END;
              END;
              edesc.continued := TRUE;
              FOR p := e.first + 1 TO e.first + e.nb - 1 DO
                VAR old := CounterOf(desc[p - firstPage]);
                BEGIN
                  desc[p - firstPage] := edesc;
                  IF new # old THEN
                    IF old # Counter.None THEN
                      DEC(count[old]);
                      TextVBT.Put(countVBT[old], Fmt.Int(count[old]));
                    END;
                    IF new # Counter.None THEN
                      INC(count[new]);
                      TextVBT.Put(countVBT[new], Fmt.Int(count[new]));
                    END;
                  END;
                END;
              END;
            END;
            FOR p := e.first TO e.first + e.nb - 1 DO
              RepaintOnePage(map, p);
            END;
        | RTHeapEvent.Kind.Bye =&gt; EXIT;
        | RTHeapEvent.Kind.Off =&gt; TextVBT.Put(off, Fmt.Int(e.nb));
        | RTHeapEvent.Kind.CollectNow, RTHeapEvent.Kind.GCOff,
            RTHeapEvent.Kind.GCOn =&gt; &lt;* ASSERT FALSE *&gt;
        END;
      END;
    END;
  END Run;

BEGIN
  InitColors();
  SetupVBT();
  Run();
  Trestle.AwaitDelete(root);
END ShowHeap.
</PRE>
</inModule>
<PRE>























</PRE>
</BODY>
</HTML>
