<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: rehearsecode/src/RehearseCode.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>rehearsecode/src/RehearseCode.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright (C) 1993, Digital Equipment Corporation                         
 All rights reserved.                                                      
 See the file COPYRIGHT for a full description.                            
                                                                           
 Last modified on Tue Jan 31 11:45:16 PST 1995 by kalsow                   
      modified on Sun Jun  5 14:23:16 PDT 1994 by mhb                      

<P><PRE>MODULE <module>RehearseCode</module> EXPORTS <A HREF="../../m3core/src/main/Main.i3.html"><implements>Main</A></implements>;

IMPORT <A HREF="../../vbtkit/src/vbtkitutils/AutoRepeat.i3.html">AutoRepeat</A>, <A HREF="../../libm3/src/geometry/Axis.i3.html">Axis</A>, <A HREF="../../codeview/src/CodeView.i3.html">CodeView</A>, <A HREF="../../libm3/src/rw/FileRd.i3.html">FileRd</A>, <A HREF="../../libm3/src/fmtlex/Fmt.i3.html">Fmt</A>, <A HREF="../../formsvbt/src/FormsVBT.i3.html">FormsVBT</A>, <A HREF="../../ui/src/split/HVBar.i3.html">HVBar</A>,
       <A HREF="../../ui/src/split/HVSplit.i3.html">HVSplit</A>, <A HREF="../../vbtkit/src/lego/ListVBT.i3.html">ListVBT</A>, <A HREF="../../libm3/src/os/Common/OSError.i3.html">OSError</A>, <A HREF="../../libm3/src/params/Params.i3.html">Params</A>, <A HREF="../../libm3/src/rw/Rd.i3.html">Rd</A>, <A HREF="../../libm3/derived/RefList.i3.html">RefList</A>,
       <A HREF="../derived/RehearseCodeBundle.i3.html">RehearseCodeBundle</A>, <A HREF="../../vbtkit/src/vbtkitutils/Rsrc.i3.html">Rsrc</A>, <A HREF="../../libm3/derived/SortedIntRefTbl.i3.html">SortedIntRefTbl</A>,
       <A HREF="../../libm3/derived/SortedTextRefTbl.i3.html">SortedTextRefTbl</A>, <A HREF="../../ui/src/split/Split.i3.html">Split</A>, <A HREF="../../libm3/src/rw/Stdio.i3.html">Stdio</A>, <A HREF="../../m3core/src/text/Text.i3.html">Text</A>, <A HREF="../../vbtkit/src/etext/TextEditVBT.i3.html">TextEditVBT</A>,
       <A HREF="../../vbtkit/src/etext/TextPort.i3.html">TextPort</A>, <A HREF="../../m3core/src/thread/Common/Thread.i3.html">Thread</A>, <A HREF="../../ui/src/vbt/Trestle.i3.html">Trestle</A>, <A HREF="../../ui/src/vbt/TrestleComm.i3.html">TrestleComm</A>, <A HREF="../../ui/src/vbt/VBT.i3.html">VBT</A>, <A HREF="../../libm3/src/rw/Wr.i3.html">Wr</A>, <A HREF="../../libm3/src/rw/WrClass.i3.html">WrClass</A>;

&lt;* FATAL Rsrc.NotFound, Rd.Failure, Wr.Failure, Thread.Alerted *&gt;
&lt;* FATAL Split.NotAChild, TrestleComm.Failure *&gt;
&lt;* FATAL FormsVBT.Error *&gt;

TYPE
  View = REF RECORD
               filename: TEXT         := NIL;
               codeview: CodeView.T;
             END;

  Writer = Wr.T OBJECT
             typescript: TextEditVBT.T;
           OVERRIDES
             seek  := Seek;
             flush := Flush;
           END;

  Repeater = AutoRepeat.T OBJECT OVERRIDES
               repeat := RepeatStep
             END;

VAR
  procNames   : RefList.T (* of TEXT *);
  regions     : RefList.T (* of REF INTEGER *);
  views       : RefList.T (* of View *);
  running                  := FALSE;
  currentProc : TEXT       := NIL;
  fv          : FormsVBT.T;
  typescriptWr: Writer;
  codeViews   : HVSplit.T;
  repeater                 := NEW (Repeater).init (0, 400);

PROCEDURE <A NAME="NewWriter"><procedure>NewWriter</procedure></A> (ts: TextEditVBT.T): Writer =
  CONST BufferSize = 100;
  BEGIN
    RETURN
      NEW (Writer, typescript := ts, lo := 0, cur := 0, hi := BufferSize,
           st := 0, buff := NEW (REF ARRAY OF CHAR, BufferSize),
           closed := FALSE, seekable := FALSE, buffered := FALSE);
  END NewWriter;

PROCEDURE <A NAME="Seek"><procedure>Seek</procedure></A> (wr: Writer; &lt;* UNUSED *&gt; n: CARDINAL)
  RAISES {Wr.Failure, Thread.Alerted} =
  BEGIN
    wr.flush ()
  END Seek;

PROCEDURE <A NAME="Flush"><procedure>Flush</procedure></A> (wr: Writer) RAISES {Thread.Alerted} =
  BEGIN
    TextPort.PutText (
      wr.typescript.tp,
      Text.FromChars (SUBARRAY (wr.buff^, 0, wr.cur - wr.lo)));
    wr.lo := wr.cur;
    wr.hi := wr.lo + NUMBER (wr.buff^);
    IF Thread.TestAlert () THEN RAISE Thread.Alerted END
  END Flush;

PROCEDURE <A NAME="PickAction"><procedure>PickAction</procedure></A> (             fv  : FormsVBT.T;
                      &lt;* UNUSED *&gt; name: Text.T;
                      &lt;* UNUSED *&gt; cl  : REFANY;
                      &lt;* UNUSED *&gt; time: VBT.TimeStamp) =
  BEGIN Pick(fv)
  END PickAction;

PROCEDURE <A NAME="Pick"><procedure>Pick</procedure></A> (fv: FormsVBT.T) =
  VAR
    list := views;
    browser : ListVBT.T := FormsVBT.GetVBT (fv, &quot;procedures&quot;);
    cell: ListVBT.Cell;
  BEGIN
    IF running THEN AutoRepeat.Stop (repeater); running := FALSE; END;
    IF NOT browser.getFirstSelected (cell) THEN RETURN; END;
    WITH name = RefList.Nth (procNames, cell) DO
      WHILE list # NIL DO
        WITH view = NARROW (list.head, View) DO
          view.codeview.exitAll ();
          view.codeview.enter (name, 0);
        END;
        list := list.tail
      END;
      regions := UnionOfRegions (name, views).tail;
      currentProc := name;
    END;
  END Pick;

PROCEDURE <A NAME="ReparseAction"><procedure>ReparseAction</procedure></A> (             fv  : FormsVBT.T;
                         &lt;* UNUSED *&gt; name: Text.T;
                         &lt;* UNUSED *&gt; cl  : REFANY;
                         &lt;* UNUSED *&gt; time: VBT.TimeStamp) =
  VAR list := views;
  BEGIN
    IF running THEN
      AutoRepeat.Stop(repeater);
      running := FALSE;
    END;
    WHILE list # NIL DO
      WITH view = NARROW(list.head, View) DO
        Wr.PutText(typescriptWr, Fmt.F(&quot;Reloading file %s ...\n&quot;,
                                       view.filename));
        TRY
          WITH new = CodeView.New(
                       FileRd.Open(view.filename), typescriptWr) DO
            Split.Replace(
              VBT.Parent(view.codeview), view.codeview, new);
            view.codeview := new;
          END;
        EXCEPT
          OSError.E =&gt;
            Wr.PutText(
              typescriptWr,
              Fmt.F(&quot;*** OSError.E on file %s\n&quot;, view.filename));
        END;
      END;
      list := list.tail;
    END;
    WITH view = NARROW(views.head, View) DO
      procNames := view.codeview.listNames();
    END;
    StuffBrowser(fv, procNames);
    regions := NIL;
    currentProc := NIL;
  END ReparseAction;

PROCEDURE <A NAME="StepAction"><procedure>StepAction</procedure></A> (&lt;* UNUSED *&gt; fv  : FormsVBT.T;
                      &lt;* UNUSED *&gt; name: Text.T;
                      &lt;* UNUSED *&gt; cl  : REFANY;
                      &lt;* UNUSED *&gt; time: VBT.TimeStamp) =
  BEGIN
    IF running THEN AutoRepeat.Stop (repeater); running := FALSE; END;
    IF (regions = NIL) AND (currentProc # NIL) THEN
      regions := UnionOfRegions (currentProc, views);
    END;
    IF regions # NIL THEN
      WITH region = NARROW (regions.head, REF INTEGER) DO
        At (region^, views);
      END;
      regions := regions.tail;
    END;
  END StepAction;

PROCEDURE <A NAME="RunAction"><procedure>RunAction</procedure></A> (&lt;* UNUSED *&gt; fv  : FormsVBT.T;
                     &lt;* UNUSED *&gt; name: Text.T;
                     &lt;* UNUSED *&gt; cl  : REFANY;
                     &lt;* UNUSED *&gt; time: VBT.TimeStamp) =
  BEGIN
    IF running THEN AutoRepeat.Stop (repeater); running := FALSE; RETURN; END;
    IF (regions = NIL) AND (currentProc # NIL) THEN
      regions := UnionOfRegions (currentProc, views);
    END;
    AutoRepeat.Start (repeater);
    running := TRUE;
  END RunAction;

PROCEDURE <A NAME="RepeatStep"><procedure>RepeatStep</procedure></A> (repeater: Repeater) =
  BEGIN
    IF regions = NIL THEN
      AutoRepeat.Stop (repeater);
      running := FALSE;
    ELSE
      WITH region = NARROW (regions.head, REF INTEGER) DO
        LOCK VBT.mu DO At (region^, views); END;
      END;
      regions := regions.tail;
    END;
  END RepeatStep;

PROCEDURE <A NAME="ExitAction"><procedure>ExitAction</procedure></A> (             fv  : FormsVBT.T;
                      &lt;* UNUSED *&gt; name: Text.T;
                      &lt;* UNUSED *&gt; cl  : REFANY;
                      &lt;* UNUSED *&gt; time: VBT.TimeStamp) =
  BEGIN
    IF running THEN AutoRepeat.Stop (repeater); running := FALSE; END;
    Trestle.Delete (codeViews);
    Trestle.Delete (fv);
  END ExitAction;

PROCEDURE <A NAME="At"><procedure>At</procedure></A> (line: INTEGER; viewList: RefList.T) =
  BEGIN
    WHILE viewList # NIL DO
      WITH view = NARROW (viewList.head, View) DO
        view.codeview.at (line, 0);
      END;
      viewList := viewList.tail;
    END;
  END At;

PROCEDURE <A NAME="StuffBrowser"><procedure>StuffBrowser</procedure></A> (fv: FormsVBT.T; names: RefList.T) =
  VAR browser: ListVBT.T := FormsVBT.GetVBT (fv, &quot;procedures&quot;);
      oldCount := browser.count();
      oldSelection := -1;
      newCount := RefList.Length(names);
  BEGIN
    EVAL browser.getFirstSelected(oldSelection);
    browser.selectNone();
    browser.removeCells (0, LAST(INTEGER));
    browser.insertCells (oldCount, newCount);
    IF newCount &gt; 0 THEN
      FOR j := 0 TO newCount - 1 DO
        browser.setValue (j, NARROW (names.head, TEXT));
        names := names.tail;
      END;
      IF oldCount = newCount AND oldSelection # -1 THEN
        browser.selectOnly(oldSelection)
      ELSE
        browser.selectOnly(0)
      END;
      Pick(fv)
    END;
  END StuffBrowser;

PROCEDURE <A NAME="CheckNames"><procedure>CheckNames</procedure></A> (names: RefList.T; viewList: RefList.T) =
  VAR nameList: RefList.T;
  BEGIN
    WHILE viewList # NIL DO
      WITH view = NARROW(viewList.head, View) DO
        nameList := names;
        WHILE nameList # NIL DO
          WITH name = NARROW(nameList.head, TEXT) DO
            IF NOT TextListMember(
                     name, view.codeview.listNames()) THEN
              Wr.PutText(
                typescriptWr,
                Fmt.F(&quot;procedure annotation %s not in file %s\n&quot;,
                      name, view.filename));
            END;
          END;
          nameList := nameList.tail
        END
      END;
      viewList := viewList.tail
    END
  END CheckNames;

PROCEDURE <A NAME="TextListMember"><procedure>TextListMember</procedure></A> (x: TEXT; l: RefList.T): BOOLEAN =
  BEGIN
    WHILE l # NIL DO
      IF Text.Equal(NARROW(l.head, TEXT), x) THEN
        RETURN TRUE
      END;
      l := l.tail;
    END;
    RETURN FALSE;
  END TextListMember;

PROCEDURE <A NAME="UnionOfNames"><procedure>UnionOfNames</procedure></A> (viewList: RefList.T): RefList.T =
  VAR
    list : RefList.T;
    name : TEXT;
    value: REFANY;
    tbl  : SortedTextRefTbl.T;
  BEGIN
    WHILE viewList # NIL DO
      WITH view = NARROW(viewList.head, View) DO
        list := RefList.Append(list, view.codeview.listNames());
      END;
      viewList := viewList.tail;
    END;
    (* build a SortedTextRefTbl of unique keys: *)
    tbl := NEW(SortedTextRefTbl.Default).init();
    WHILE list # NIL DO
      name := list.head;
      EVAL tbl.put(name, NIL);
      list := list.tail;
    END;
    (* build a new list with items in sorted order: *)
    WITH iter = tbl.iterateOrdered(FALSE) DO
      WHILE iter.next(name, value) DO
        list := RefList.Cons(name, list)
      END
    END;
    RETURN list
  END UnionOfNames;

PROCEDURE <A NAME="UnionOfRegions"><procedure>UnionOfRegions</procedure></A> (proc: TEXT; viewList: RefList.T):
  RefList.T =
  VAR
    list     : RefList.T;
    refRegion: REFANY;
    region   : INTEGER;
    tbl      : SortedIntRefTbl.T;
  BEGIN
    WHILE viewList # NIL DO
      WITH view = NARROW(viewList.head, View) DO
        list :=
          RefList.Append(list, view.codeview.listRegions(proc));
      END;
      viewList := viewList.tail;
    END;
    (* build a SortedIntRefTbl of unique keys: *)
    tbl := NEW(SortedIntRefTbl.Default).init();
    WHILE list # NIL DO
      refRegion := list.head;
      region := NARROW(refRegion, REF INTEGER)^;
      EVAL tbl.put(region, refRegion);
      list := list.tail;
    END;
    (* build a new list with items in sorted order: *)
    WITH iter = tbl.iterateOrdered(FALSE) DO
      WHILE iter.next(region, refRegion) DO
        list := RefList.Cons(refRegion, list)
      END
    END;
    RETURN list
  END UnionOfRegions;

PROCEDURE <A NAME="Main"><procedure>Main</procedure></A> () =
  VAR hsplit, vsplit: HVSplit.T;
  BEGIN
    fv := NEW(FormsVBT.T).initFromRsrc (
            &quot;RehearseCode.fv&quot;,
            Rsrc.BuildPath (&quot;$REHEARSECODE&quot;,  RehearseCodeBundle.Get()));
    FormsVBT.AttachProc (fv, &quot;reparse&quot;, ReparseAction);
    FormsVBT.AttachProc (fv, &quot;step&quot;, StepAction);
    FormsVBT.AttachProc (fv, &quot;run&quot;, RunAction);
    FormsVBT.AttachProc (fv, &quot;exit&quot;, ExitAction);
    FormsVBT.AttachProc (fv, &quot;procedures&quot;, PickAction);

    typescriptWr := NewWriter (FormsVBT.GetVBT (fv, &quot;typescript&quot;));

    IF (Params.Count &lt; 2) OR (Params.Count &gt; 5) THEN
      Wr.PutText (Stdio.stderr,
        &quot;usage: RehearseCode filename1 [... filename4]\n&quot;);
      RETURN
    END;

    FOR i := 1 TO Params.Count - 1 DO
      WITH source = Params.Get (i),
           view   = NEW (View)      DO
        TRY
          Wr.PutText (
            typescriptWr, Fmt.F (&quot;Loading file %s ...\n&quot;, source));
          view.filename := source;
          view.codeview :=
            CodeView.New (FileRd.Open (source), typescriptWr);
          views := RefList.Cons(view, views);
          IF vsplit = NIL THEN
            vsplit := HVSplit.Cons (Axis.T.Ver, view.codeview);
          ELSE
            Split.AddChild (vsplit, HVBar.New (1.5), view.codeview);
          END;
        EXCEPT
        |  OSError.E =&gt;
            Wr.PutText (
              Stdio.stderr,
              Fmt.F (&quot;RehearseCode: Error trying to open file %s\n&quot;, source));
            Wr.PutText (
              typescriptWr, Fmt.F (&quot;*** Error trying to open file %s\n&quot;, source));
        END;
      END;
    END;

    IF views = NIL THEN
      Wr.PutText (Stdio.stderr, &quot;RehearseCode: no source files found\n&quot;);
      RETURN
    END;

    IF hsplit = NIL THEN
      codeViews := vsplit;
    ELSE
      codeViews := hsplit;
    END;

    procNames := UnionOfNames (views);
    CheckNames (procNames, views);
    StuffBrowser (fv, procNames);
    Trestle.Install (
      codeViews, &quot;RehearseCode&quot;, NIL, &quot;RehearseCode Code Views&quot;);
    Trestle.Install (fv, &quot;RehearseCode&quot;, NIL, &quot;RehearseCode Controller&quot;);
    Trestle.AwaitDelete (fv);
  END Main;

BEGIN
  Main ();
END RehearseCode.
</PRE>
</inModule>
<PRE>























</PRE>
</BODY>
</HTML>
