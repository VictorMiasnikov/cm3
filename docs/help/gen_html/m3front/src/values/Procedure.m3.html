<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: m3front/src/values/Procedure.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>m3front/src/values/Procedure.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright (C) 1992, Digital Equipment Corporation           
 All rights reserved.                                        
 See the file COPYRIGHT for a full description.              

<P> File: Procedure.m3                                          
 Last Modified On Tue Jun 20 09:58:41 PDT 1995 by kalsow     
      Modified On Tue Jun 13 20:00:51 PDT 1995 by ericv      
      Modified On Thu Dec  5 17:21:10 PST 1991 by muller     

<P><PRE>MODULE <module><implements><A HREF="Procedure.i3.html">Procedure</A></implements></module>;

IMPORT <A HREF="../misc/M3.i3.html">M3</A>, <A HREF="#x1">M3ID</A>, <A HREF="../misc/CG.i3.html">CG</A>, <A HREF="#x2">Value</A>, <A HREF="ValueRep.i3.html">ValueRep</A>, <A HREF="#x3">Type</A>, <A HREF="../misc/Scope.i3.html">Scope</A>, <A HREF="../misc/Error.i3.html">Error</A>, <A HREF="../misc/Host.i3.html">Host</A>;
IMPORT <A HREF="../types/ProcType.i3.html">ProcType</A>, <A HREF="../stmts/Stmt.i3.html">Stmt</A>, <A HREF="../stmts/BlockStmt.i3.html">BlockStmt</A>, <A HREF="#x4">Marker</A>, <A HREF="../misc/Coverage.i3.html">Coverage</A>, <A HREF="../../../m3middle/src/M3RT.i3.html">M3RT</A>;
IMPORT <A HREF="../exprs/CallExpr.i3.html">CallExpr</A>, <A HREF="#x5">Token</A>, <A HREF="Variable.i3.html">Variable</A>, <A HREF="../exprs/ProcExpr.i3.html">ProcExpr</A>, <A HREF="../misc/Tracer.i3.html">Tracer</A>;
IMPORT <A HREF="../misc/Scanner.i3.html">Scanner</A>, <A HREF="#x6">Decl</A>, <A HREF="../misc/ESet.i3.html">ESet</A>, <A HREF="../misc/ProcBody.i3.html">ProcBody</A>, <A HREF="../../../m3middle/src/Target.i3.html">Target</A>, <A HREF="../exprs/Expr.i3.html">Expr</A>, <A HREF="Formal.i3.html">Formal</A>;
FROM <A HREF="../misc/Scanner.i3.html">Scanner</A> IMPORT GetToken, Match, MatchID, cur;

REVEAL
  <A NAME="T">T</A> = Value.T BRANDED OBJECT
        intf_peer    : T;
        impl_peer    : T;
        next_defn    : T;
        signature    : Type.T;
        syms         : Scope.T;
        block        : Stmt.T;
        body         : Body;
        result       : Variable.T;
        builtin      : BOOLEAN;
        predefined   : BOOLEAN;
        needs_raises : BOOLEAN;
        direct       : BOOLEAN;
        fails        : ESet.T;
        cg_proc      : CG.Proc;
        next_cg_proc : T;
        end_origin   : INTEGER;
      OVERRIDES
        typeCheck   := Check;
        set_globals := ValueRep.NoInit;
        load        := Load;
        declare     := Declarer;
        const_init  := ValueRep.NoInit;
        need_init   := NeedInit;
        lang_init   := LangInit;
        user_init   := ValueRep.NoInit;
        toExpr      := ToExpr;
        toType      := ValueRep.NoType;
        typeOf      := TypeOf;
        base        := ValueRep.Self;
        add_fp_tag  := AddFPTag;
        fp_type     := FPType;
      END;

TYPE
  Body = ProcBody.T OBJECT
    self: T;
  OVERRIDES
    gen_decl := EmitDecl;
    gen_body := EmitBody;
  END;

VAR (*CONST*)
  resultName : M3ID.T := M3ID.NoID;
  returnName : M3ID.T := M3ID.NoID;

VAR
  defined_procs: T := NIL;
  (* interface procedures that are implemented in the current compilation unit *)

VAR
  cg_procs: T := NIL;
  (* procedures that have M3CG attachments *)

PROCEDURE <A NAME="Reset"><procedure>Reset</procedure></A> () =
  VAR t, u: T;
  BEGIN
    (* disconnect the interface-&gt;implementation mappings *)
    t := defined_procs;
    WHILE (t # NIL) DO
      u := t;  t := t.next_defn;
      u.impl_peer := NIL;
      u.next_defn := NIL;
    END;
    defined_procs := NIL;

    (* release any M3CG objects that were created. *)
    t := cg_procs;
    WHILE (t # NIL) DO
      u := t;  t := t.next_cg_proc;
      u.cg_proc      := NIL;
      u.next_cg_proc := NIL;
      u.body         := NIL;
    END;
    cg_procs := NIL;
  END Reset;

PROCEDURE <A NAME="ParseDecl"><procedure>ParseDecl</procedure></A> (READONLY att: Decl.Attributes;
                     headerOnly: BOOLEAN := FALSE) =
  TYPE TK = Token.T;
  VAR t: T;  id, final_id: M3ID.T;  cc := att.callingConv;
  BEGIN
    Match (TK.tPROCEDURE);
    id := MatchID ();
    t := Create (id);
    t.unused    := att.isUnused;
    t.obsolete  := att.isObsolete;
    IF (att.isExternal) THEN
      t.external := TRUE;
      IF (att.alias = M3ID.NoID)
        THEN t.extName := t.name;
        ELSE t.extName := att.alias;
      END;
    END;
    IF (cc = NIL) THEN cc := Target.DefaultCall; END;
    t.signature := ProcType.ParseSignature (id, cc);
    Scope.Insert (t);
    IF (cur.token = TK.tEQUAL) THEN
      t.body := NEW (Body, self := t);
      ProcBody.Push (t.body);
      GetToken (); (* = *)
      IF (headerOnly) THEN
        Error.ID (id, &quot;procedure declaration cannot include a body&quot;);
      END;
      IF (att.isExternal) THEN
        Error.WarnID (2, id, &quot;external procedure cannot include a body&quot;);
        t.external := FALSE;
        t.extName  := M3ID.NoID;
      END;
      t.syms  := Scope.PushNew (TRUE, id);
      t.block := BlockStmt.Parse (FALSE);
      t.fails := BlockStmt.ExtractFails (t.block);
      t.end_origin := Scanner.offset;
      final_id := MatchID ();
      IF (final_id # id) THEN
        Error.ID (id, &quot;Initial name doesn\'t match final name&quot;);
      END;
      Scope.PopNew ();
      ProcBody.Pop ();
    ELSIF (headerOnly) OR (att.isExternal) THEN
      (* ok *)
    ELSIF (cur.token = TK.tSEMI) THEN
      t.body := NEW (Body, self := t);
      ProcBody.Push (t.body);
      (* try accepting the Modula-2 syntax *)
      Error.ID (id, &quot;expecting \'=\' before procedure body&quot;);
      GetToken (); (* ; *)
      t.syms  := Scope.PushNew (TRUE, id);
      t.block := BlockStmt.Parse (FALSE);
      t.fails := BlockStmt.ExtractFails (t.block);
      t.end_origin := Scanner.offset;
      final_id := MatchID ();
      IF (final_id # id) THEN
        Error.ID (id, &quot;Initial name doesn\'t match final name&quot;);
      END;
      Scope.PopNew ();
      ProcBody.Pop ();
    ELSE
      Error.ID (id, &quot;procedure declaration must include a body&quot;);
    END;
    Match (TK.tSEMI);
  END ParseDecl;

PROCEDURE <A NAME="IsEqual"><procedure>IsEqual</procedure></A> (a, b: Value.T): BOOLEAN =
  VAR ta, tb: T;
  BEGIN
    a := Value.Base (a);
    b := Value.Base (b);
    IF (a = b) THEN RETURN TRUE END;
    TYPECASE a OF
    | NULL =&gt; RETURN FALSE;
    | T(t) =&gt; ta := t;
    ELSE      RETURN FALSE;
    END;
    TYPECASE b OF
    | NULL =&gt; RETURN FALSE;
    | T(t) =&gt; tb := t;
    ELSE      RETURN FALSE;
    END;
    RETURN (ta.intf_peer = tb) OR (tb.intf_peer = ta)
        OR (ta.impl_peer = tb) OR (tb.impl_peer = ta);
  END IsEqual;

PROCEDURE <A NAME="Create"><procedure>Create</procedure></A> (name: M3ID.T): T =
  VAR t: T;
  BEGIN
    t := NEW (T);
    ValueRep.Init (t, name, Value.Class.Procedure);
    t.readonly     := TRUE;
    t.intf_peer    := NIL;
    t.impl_peer    := NIL;
    t.next_defn    := NIL;
    t.signature    := NIL;
    t.syms         := NIL;
    t.body         := NIL;
    t.block        := NIL;
    t.external     := FALSE;
    t.builtin      := FALSE;
    t.predefined   := FALSE;
    t.result       := NIL;
    t.extName      := M3ID.NoID;
    t.needs_raises := TRUE;
    t.fails        := NIL;
    t.cg_proc      := NIL;
    t.next_cg_proc := NIL;
    t.end_origin   := t.origin;
    RETURN t;
  END Create;

PROCEDURE <A NAME="Define"><procedure>Define</procedure></A> (name      : TEXT;
                  methods   : CallExpr.MethodList;
                  reserved  : BOOLEAN;
                  signature : Type.T := NIL) =
  VAR t: T;  s: M3ID.T;  sig: Type.T;
  BEGIN
    IF (signature = NIL)
      THEN sig := ProcType.New (NIL);
      ELSE sig := signature;
    END;
    ProcType.SetMethods (sig, methods);
    s := M3ID.Add (name);
    t := Create (s);
    t.signature  := sig;
    t.builtin    := (signature = NIL);
    t.predefined := TRUE;
    Scope.Insert (t);
    IF (reserved) THEN Scanner.NoteReserved (s, t) END;
  END Define;

PROCEDURE <A NAME="NoteExport"><procedure>NoteExport</procedure></A> (implv, intfv: Value.T) =
  VAR impl: T := Value.Base (implv);  intf: T := Value.Base (intfv);
  BEGIN
    IF (impl.intf_peer # NIL) OR (intf.impl_peer # NIL) THEN
      Redefined (impl, NIL(*intf*));
    ELSE
      IF NOT Type.IsAssignable (intf.signature, impl.signature) THEN
        Redefined (impl, NIL(*intf*));
      END;
      intf.next_defn := defined_procs;  defined_procs := intf;
      intf.impl_peer := impl;
      impl.intf_peer := intf;
      impl.scope     := intf.scope;  (* retain the exported module name *)
      impl.used      := TRUE;
    END;
  END NoteExport;

PROCEDURE <A NAME="TypeOf"><procedure>TypeOf</procedure></A> (p: T): Type.T =
  BEGIN
    RETURN p.signature;
  END TypeOf;

PROCEDURE <A NAME="Check"><procedure>Check</procedure></A> (p: T;  VAR cs: Value.CheckState) =
  BEGIN
    EVAL Type.Check (p.signature);
    (* NOTE: don't save the signature returned by Type.Check cause if
       you do, the formals will be reused by procedures with the
       same signature. *)

    Value.TypeCheck (p.intf_peer, cs);

    (* defer the rest to CheckBody *)
  END Check;

PROCEDURE <A NAME="CheckBody"><procedure>CheckBody</procedure></A> (p: T;  VAR cs: Value.CheckState) =
  VAR
    v         : Variable.T;
    formal    : Value.T;
    n_formals : INTEGER;
    result    : Type.T;
    zz        : Scope.T;
    raises    : ESet.T;
    save      : Value.CheckState;
    f_info    : Formal.Info;
    cconv     : CG.CallingConvention;
  BEGIN
    IF (p.body = NIL) THEN RETURN END;

    Coverage.NoteProcedure (p);
    zz := Scope.Push (p.syms);
    p.body.name := Value.GlobalName (p, dots := TRUE, with_module := FALSE);
    result := ProcType.Result (p.signature);
    cconv := ProcType.CallConv (p.signature);

      (* create a variable for the return result *)
      IF cconv.results_on_left THEN CheckResult (p, result); END;

      (* create local variables for each of the formals *)
      formal := ProcType.Formals (p.signature);  n_formals := 0;
      WHILE (formal # NIL) DO
        v := Variable.NewFormal (formal, formal.name);
        Scope.Insert (v);
        Formal.Split (formal, f_info);
        Variable.BindTrace (v, f_info.trace);
        formal.scope := v.scope;
          (* identify the full names of the formal &amp; its local variable *)
        formal := formal.next;  INC (n_formals);
      END;

      (* create a variable for the return result *)
      IF NOT cconv.results_on_left THEN CheckResult (p, result); END;

      raises := ProcType.Raises (p.signature);
      save := cs;
      cs.raises_others := FALSE;
      ESet.TypeCheck (p.fails);
      ESet.Push (cs, raises, p.fails, stop := TRUE);

      p.checked := TRUE;
      INC (Type.recursionDepth);
        Scope.TypeCheck (p.syms, cs);
        Marker.PushProcedure (result, p.result, cconv);
          Stmt.TypeCheck (p.block, cs);
        Marker.Pop ();
        Scope.WarnUnused (p.syms);
      DEC (Type.recursionDepth);

      p.needs_raises := cs.raises_others;
      cs.raises_others := save.raises_others;
      ESet.Pop (cs, raises, p.fails, stop := TRUE);

    Scope.Pop (zz);
  END CheckBody;

PROCEDURE <A NAME="CheckResult"><procedure>CheckResult</procedure></A> (p: T;  result: Type.T) =
  VAR v: Variable.T;  formal: Value.T;
  BEGIN
    IF (result = NIL) THEN RETURN; END;
    IF (resultName = M3ID.NoID) THEN resultName := M3ID.Add (&quot;_result&quot;); END;
    IF (returnName = M3ID.NoID) THEN returnName := M3ID.Add (&quot;_return&quot;); END;

    IF ProcType.LargeResult (result) THEN
      formal := Formal.New (Formal.Info {returnName, Formal.Mode.mVAR,
                                         -1, result, NIL, FALSE, NIL});
      formal.imported := FALSE;
      formal.origin := p.origin;
      formal.used := TRUE;
      Scope.Insert (formal);
      v := Variable.NewFormal (formal, resultName);
    ELSE
      v := Variable.New (resultName, TRUE);
      Variable.BindType (v, result, indirect := FALSE,
                         readonly := FALSE, needs_init := FALSE,
                         open_array_ok := FALSE);
    END;
    v.imported := FALSE;
    v.origin := p.origin;
    v.used := TRUE;
    Scope.Insert (v);
    p.result := v;
  END CheckResult;

PROCEDURE <A NAME="Load"><procedure>Load</procedure></A> (t: T) =
  BEGIN
    IF (t.builtin) THEN
      Error.ID (t.name, &quot;builtin operation is not a procedure&quot;);
    END;
    IF (t.impl_peer # NIL) THEN t := t.impl_peer; END;
    t.used := TRUE;
    Value.Declare (t);
    CG.Load_procedure (t.cg_proc);
  END Load;

PROCEDURE <A NAME="LoadStaticLink"><procedure>LoadStaticLink</procedure></A> (t: T) =
 BEGIN
    IF (t.builtin) THEN
      Error.ID (t.name, &quot;builtin operation is not a procedure&quot;);
    END;
    IF (t.impl_peer # NIL) THEN t := t.impl_peer; END;
    t.used := TRUE;
    Value.Declare (t);
    IF IsNested (t)
      THEN CG.Load_static_link (t.cg_proc);
      ELSE CG.Load_nil ();
    END;
 END LoadStaticLink;

PROCEDURE <A NAME="ImportProc"><procedure>ImportProc</procedure></A> (p: T;  name: TEXT;  n_formals: INTEGER;
                      cg_result: CG.Type;  cc: CG.CallingConvention) =
  VAR zz: Scope.T;  new: BOOLEAN;
  BEGIN
    &lt;*ASSERT p.cg_proc = NIL*&gt;
    p.next_cg_proc := cg_procs;  cg_procs := p;
    p.cg_proc := CG.Import_procedure (M3ID.Add (name), n_formals,
                                      cg_result, cc, new);
    IF (new) THEN
      (* declare the formals *)
      IF (p.syms # NIL) THEN
        zz := Scope.Push (p.syms);
        Scope.Enter (p.syms);
        Scope.Pop (zz);
      ELSE
        DeclareFormals (p, cc);
      END;
    END;
  END ImportProc;

PROCEDURE <A NAME="DeclareFormals"><procedure>DeclareFormals</procedure></A> (p: T;  cc: CG.CallingConvention) =
  VAR
    v       : Value.T;
    formals := ProcType.Formals (p.signature);
    result  := ProcType.Result (p.signature);
  BEGIN
    (* declare types for each of the formals *)
    v := formals;
    WHILE (v # NIL) DO
      Formal.EmitDeclaration (v, TRUE, TRUE);
      v := v.next;
    END;

    (* declare parameters for each of the formals *)
    IF cc.results_on_left THEN DeclareResult (result); END;
    v := formals;
    WHILE (v # NIL) DO
      Formal.EmitDeclaration (v, FALSE, TRUE);
      v := v.next;
    END;
    IF NOT cc.results_on_left THEN DeclareResult (result); END;
  END DeclareFormals;

PROCEDURE <A NAME="DeclareResult"><procedure>DeclareResult</procedure></A> (result: Type.T) =
  VAR result_id : INTEGER;
  BEGIN
    IF (result # NIL) AND ProcType.LargeResult (result) THEN
      IF (returnName = M3ID.NoID) THEN returnName := M3ID.Add (&quot;_return&quot;); END;
      result_id := CG.Declare_indirect (Type.GlobalUID (result));
      EVAL CG.Declare_param (returnName, Target.Address.size,
                Target.Address.align, CG.Type.Addr, result_id,
                in_memory := FALSE, up_level := FALSE, f := CG.Maybe);
    END;
  END DeclareResult;

PROCEDURE <A NAME="Declarer"><procedure>Declarer</procedure></A> (p: T): BOOLEAN =
  VAR
    zz: Scope.T;
    par: CG.Proc := NIL;
    cg_result: CG.Type;
    name := Value.GlobalName (p, dots := FALSE, with_module := TRUE);
    type: CG.TypeUID;
    sig := p.signature;
    n_formals: INTEGER;
    cconv: CG.CallingConvention;
  BEGIN
    IF (p.predefined) AND (p.body = NIL) THEN
      (* don't bother importing Word.* procedures or declaring their
         signatures, but do generate a version stamp dependency *)
      RETURN TRUE;
    END;

    IF (p.intf_peer # NIL) THEN
      sig := p.intf_peer.signature;
      Type.Compile (sig);
    END;
    Type.Compile (p.signature);
    type := Type.GlobalUID (p.signature);
    (* try to compile the imported type first... *)

    cg_result := ProcType.CGResult (p.signature);
    n_formals := ProcType.NFormals (p.signature);
    cconv     := ProcType.CallConv (p.signature);

    IF (p.body = NIL) THEN
      (* it's not a local procedure *)
      IF p.impl_peer # NIL THEN
        (* it's an interface procedure that's implemented in this module *)
        Value.Declare (p.impl_peer);
        RETURN FALSE;
      ELSE
        (* it's an imported procedure *)
        ImportProc (p, name, n_formals, cg_result, cconv);
        RETURN TRUE;
      END;
    END;

    IF (p.body.parent # NIL) THEN par := p.body.parent.cg_proc END;

    p.next_cg_proc := cg_procs;  cg_procs := p;
    p.cg_proc := CG.Declare_procedure (M3ID.Add (name),
                    n_formals, cg_result, p.body.level,  cconv,
                    exported := (p.exported OR p.imported),
                    parent := par);
    p.body.cg_proc := p.cg_proc;
    Scanner.offset := p.origin;
    IF (p.syms # NIL) THEN
      zz := Scope.Push (p.syms);
      Scope.Enter (p.syms);
      Scope.Pop (zz);
    END;
    RETURN TRUE;
  END Declarer;

PROCEDURE <A NAME="NeedInit"><procedure>NeedInit</procedure></A> (t: T): BOOLEAN =
  BEGIN
    RETURN (t.body # NIL) AND (t.body.level &gt; 0);
  END NeedInit;

PROCEDURE <A NAME="LangInit"><procedure>LangInit</procedure></A> (t: T) =
  VAR x: Body := t.body;
  BEGIN
    IF (x = NIL) OR (x.level &lt;= 0) THEN
      (* outer-level proc =&gt; don't do anything *)
    ELSIF (Host.inline_nested_procs) THEN
      GenBody (t);
    ELSE
      CG.Note_procedure_origin (t.cg_proc);
    END;
  END LangInit;

PROCEDURE <A NAME="ToExpr"><procedure>ToExpr</procedure></A> (t: T): Expr.T =
  BEGIN
    RETURN ProcExpr.New (t);
  END ToExpr;

PROCEDURE <A NAME="IsNested"><procedure>IsNested</procedure></A> (t: T): BOOLEAN =
  BEGIN
    RETURN (t # NIL) AND (t.body # NIL) AND (t.body.level # 0);
  END IsNested;

PROCEDURE <A NAME="StaticLevel"><procedure>StaticLevel</procedure></A> (t: T): INTEGER =
  BEGIN
    IF (t = NIL) OR (t.body = NIL)
      THEN RETURN 0;
      ELSE RETURN t.body.level;
    END;
  END StaticLevel;

PROCEDURE <A NAME="CGName"><procedure>CGName</procedure></A> (t: T): CG.Proc =
  BEGIN
    IF (t.impl_peer # NIL) THEN t := t.impl_peer; END;
    t.used := TRUE;
    Value.Declare (t);
    RETURN t.cg_proc;
  END CGName;

PROCEDURE <A NAME="EmitDecl"><procedure>EmitDecl</procedure></A> (x: Body) =
  BEGIN
    Value.Declare (x.self);
  END EmitDecl;

PROCEDURE <A NAME="EmitBody"><procedure>EmitBody</procedure></A> (x: Body) =
  VAR p: T := x.self;
  BEGIN
    IF (x.level &lt;= 0) OR (NOT Host.inline_nested_procs) THEN
      GenBody (p);
    END;
  END EmitBody;

PROCEDURE <A NAME="GenBody"><procedure>GenBody</procedure></A> (p: T) =
  VAR
    tresult  : Type.T;
    oc       : Stmt.Outcomes;
    zz       : Scope.T;
    fallThru : BOOLEAN;
    l        : CG.Label;
    frame    : CG.Var;
    cconv    : CG.CallingConvention;
  BEGIN
    IF (Host.inline_nested_procs)
      AND (p.body # NIL) AND (p.body.level &gt; 0) THEN
      (* make sure outer-level variable initializations are traced
         in the outer procedure, before we enter the nested one.*)
      Tracer.EmitPending ();
    END;

    Scanner.offset := p.origin;
    zz := Scope.Push (p.syms);
    tresult := ProcType.Result (p.signature);
    cconv   := ProcType.CallConv (p.signature);

    CG.Gen_location (p.origin);
    CG.Begin_procedure (p.cg_proc);
    Scope.Enter (p.syms);

    Marker.PushProcedure (tresult, p.result, cconv);
    Marker.AllocReturnTemp ();
      StartRaises (p, l, frame);
        Scope.InitValues (p.syms);
        Scanner.offset := BlockStmt.BodyOffset (p.block);
        Coverage.CountProcedure (p);
        oc := Stmt.Compile (p.block);
        fallThru := (Stmt.Outcome.FallThrough IN oc);
      EndRaises (p, l, frame, fallThru);
      IF (fallThru) THEN
        CG.Gen_location (p.end_origin);
        IF (tresult = NIL) THEN
          Marker.EmitReturn (NIL, fromFinally := FALSE);
        ELSE
          Error.WarnID (1, p.name, &quot;function may not return a value&quot;);
          IF Host.doReturnChk THEN
            CG.Abort (CG.RuntimeError.NoReturnValue);
            oc := oc - Stmt.Outcomes {Stmt.Outcome.FallThrough};
          END;
        END;
      END;
    Marker.Pop ();
    Scope.Exit (p.syms);

    CG.End_procedure (p.cg_proc);

    Scope.Pop (zz);
  END GenBody;

PROCEDURE <A NAME="StartRaises"><procedure>StartRaises</procedure></A> (t: T;  VAR(*OUT*)l: CG.Label; VAR(*OUT*)frame: CG.Var) =
  VAR raises: ESet.T;  eset: CG.Var;  eoffset: INTEGER;  frame_size : INTEGER;
  BEGIN
    IF (NOT Host.doRaisesChk) OR (NOT t.needs_raises) THEN RETURN END;
    raises := ProcType.Raises (t.signature);
    IF ESet.RaisesAny (raises) THEN RETURN END;

    IF Target.Has_stack_walker THEN
      frame_size := 0;
    ELSIF ESet.RaisesNone (raises) THEN
      frame_size := M3RT.EF_SIZE;
    ELSE
      frame_size := M3RT.EF3_SIZE;
    END;
    frame := NIL;
    IF frame_size # 0 THEN
      frame := CG.Declare_local (M3ID.NoID, frame_size, Target.Address.align,
                                 CG.Type.Struct, 0, in_memory := TRUE,
                                 up_level := FALSE, f := CG.Never);
    END;

    l := CG.Next_label (2);
    CG.Set_label (l, barrier := TRUE);
    Marker.PushRaises (l, l+1, raises, frame);

    IF Target.Has_stack_walker THEN
      Marker.SaveFrame ();
    ELSIF ESet.RaisesNone (raises) THEN
      Marker.PushFrame (frame, M3RT.HandlerClass.RaisesNone);
    ELSE
      ESet.GetAddress (raises, eset, eoffset);
      CG.Load_addr_of (eset, eoffset, Target.Address.align);
      CG.Store_addr (frame, M3RT.EF3_raises);
      Marker.PushFrame (frame, M3RT.HandlerClass.Raises);
    END;
  END StartRaises;

PROCEDURE <A NAME="EndRaises"><procedure>EndRaises</procedure></A> (t: T;  l: CG.Label;  frame: CG.Var;  fallThru: BOOLEAN) =
  VAR raises: ESet.T;
  BEGIN
    IF (NOT Host.doRaisesChk) OR (NOT t.needs_raises) THEN RETURN END;
    raises := ProcType.Raises (t.signature);
    IF ESet.RaisesAny (raises) THEN RETURN END;
    CG.Set_label (l+1, barrier := TRUE);
    Marker.Pop ();
    IF Target.Has_stack_walker THEN
      (* nothing to do on exit *)
    ELSIF fallThru THEN
      Marker.PopFrame (frame);
    END;
  END EndRaises;

PROCEDURE <A NAME="StartCall"><procedure>StartCall</procedure></A> (t: T) =
  VAR result := ProcType.CGResult (t.signature);
  BEGIN
    IF (t.impl_peer # NIL) THEN t := t.impl_peer; END;
    t.used := TRUE;
    Value.Declare (t);
    CG.Start_call_direct (t.cg_proc, StaticLevel (t), result);
  END StartCall;

PROCEDURE <A NAME="EmitValueCall"><procedure>EmitValueCall</procedure></A> (t: T): CG.Val =
  VAR result := ProcType.CGResult (t.signature);
  BEGIN
    IF (t.impl_peer # NIL) THEN t := t.impl_peer; END;
    CG.Call_direct (t.cg_proc, result);
    RETURN Marker.EmitExceptionTest (t.signature, need_value := TRUE);
  END EmitValueCall;

PROCEDURE <A NAME="EmitCall"><procedure>EmitCall</procedure></A> (t: T) =
  VAR result := ProcType.CGResult (t.signature);
  BEGIN
    IF (t.impl_peer # NIL) THEN t := t.impl_peer; END;
    CG.Call_direct (t.cg_proc, result);
    EVAL Marker.EmitExceptionTest (t.signature, need_value := FALSE);
  END EmitCall;

PROCEDURE <A NAME="Redefined"><procedure>Redefined</procedure></A> (t: T;  other: Value.T;) =
  VAR save: INTEGER;
  BEGIN
    save := Scanner.offset;
    IF (other = NIL)
      THEN Scanner.offset := t.origin;
      ELSE Scanner.offset := MIN (t.origin, other.origin);
    END;
    Error.ID (t.name, &quot;procedure redefined&quot;);
    Scanner.offset := save;
  END Redefined;

PROCEDURE <A NAME="Signature"><procedure>Signature</procedure></A> (t: T): Type.T =
  BEGIN
    IF (t = NIL) THEN RETURN NIL END;
    RETURN t.signature;
  END Signature;

PROCEDURE <A NAME="HasBody"><procedure>HasBody</procedure></A> (t: T): BOOLEAN =
  BEGIN
    RETURN (t # NIL) AND (t.body # NIL);
  END HasBody;

PROCEDURE <A NAME="AddFPTag"><procedure>AddFPTag</procedure></A> (t: T;  VAR x: M3.FPInfo): CARDINAL =
  BEGIN
    ValueRep.FPStart (t, x, &quot;PROCEDURE &quot;, 0, global := TRUE);
    RETURN 1;
  END AddFPTag;

PROCEDURE <A NAME="FPType"><procedure>FPType</procedure></A> (t: T): Type.T =
  BEGIN
    IF (t.intf_peer = NIL)
      THEN RETURN t.signature;
      ELSE RETURN t.intf_peer.signature;
    END;
  END FPType;

BEGIN
END Procedure.
</PRE>
</inModule>
<HR>
<A NAME="x1">interface M3ID is in:
</A><UL>
<LI><A HREF="../../../m3middle/src/M3ID.i3.html#0TOP0">m3middle/src/M3ID.i3</A>
<LI><A HREF="../../../m3scan/src/M3ID.i3.html#0TOP0">m3scan/src/M3ID.i3</A>
</UL>
<P>
<HR>
<A NAME="x2">interface Value is in:
</A><UL>
<LI><A HREF="Value.i3.html#0TOP0">m3front/src/values/Value.i3</A>
<LI><A HREF="../../../sharedobjgen/src/Value.i3.html#0TOP0">sharedobjgen/src/Value.i3</A>
<LI><A HREF="../../../stablegen/src/Value.i3.html#0TOP0">stablegen/src/Value.i3</A>
<LI><A HREF="../../../stubgen/src/Value.i3.html#0TOP0">stubgen/src/Value.i3</A>
</UL>
<P>
<HR>
<A NAME="x3">interface Type is in:
</A><UL>
<LI><A HREF="../../../cm3ide/src/nodes/Type.i3.html#0TOP0">cm3ide/src/nodes/Type.i3</A>
<LI><A HREF="../types/Type.i3.html#0TOP0">m3front/src/types/Type.i3</A>
<LI><A HREF="../../../sharedobjgen/src/Type.i3.html#0TOP0">sharedobjgen/src/Type.i3</A>
<LI><A HREF="../../../stablegen/src/Type.i3.html#0TOP0">stablegen/src/Type.i3</A>
<LI><A HREF="../../../stubgen/src/Type.i3.html#0TOP0">stubgen/src/Type.i3</A>
</UL>
<P>
<HR>
<A NAME="x4">interface Marker is in:
</A><UL>
<LI><A HREF="../../../cm3ide/src/markup/Marker.i3.html#0TOP0">cm3ide/src/markup/Marker.i3</A>
<LI><A HREF="../misc/Marker.i3.html#0TOP0">m3front/src/misc/Marker.i3</A>
</UL>
<P>
<HR>
<A NAME="x5">interface Token is in:
</A><UL>
<LI><A HREF="../misc/Token.i3.html#0TOP0">m3front/src/misc/Token.i3</A>
<LI><A HREF="../../../mentor/src/parse/Token.i3.html#0TOP0">mentor/src/parse/Token.i3</A>
</UL>
<P>
<HR>
<A NAME="x6">interface Decl is in:
</A><UL>
<LI><A HREF="../../../cm3ide/src/nodes/Decl.i3.html#0TOP0">cm3ide/src/nodes/Decl.i3</A>
<LI><A HREF="Decl.i3.html#0TOP0">m3front/src/values/Decl.i3</A>
</UL>
<P>
<PRE>























</PRE>
</BODY>
</HTML>
