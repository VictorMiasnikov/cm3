<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: m3front/src/values/Exceptionz.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>m3front/src/values/Exceptionz.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright (C) 1992, Digital Equipment Corporation           
 All rights reserved.                                        
 See the file COPYRIGHT for a full description.              

<P> File: Exceptionz.m3                                         
 Last Modified On Tue May 23 15:38:44 PDT 1995 by kalsow     
      Modified On Thu Dec  5 17:20:35 PST 1991 by muller     

<P><PRE>MODULE <module><implements><A HREF="Exceptionz.i3.html">Exceptionz</A></implements></module>;

IMPORT <A HREF="../misc/M3.i3.html">M3</A>, <A HREF="#x1">M3ID</A>, <A HREF="../../../m3middle/src/M3FP.i3.html">M3FP</A>, <A HREF="../misc/CG.i3.html">CG</A>, <A HREF="#x2">Value</A>, <A HREF="ValueRep.i3.html">ValueRep</A>, <A HREF="#x3">Type</A>, <A HREF="../misc/Scope.i3.html">Scope</A>, <A HREF="Formal.i3.html">Formal</A>, <A HREF="../../../m3middle/src/M3RT.i3.html">M3RT</A>;
IMPORT <A HREF="../misc/Error.i3.html">Error</A>, <A HREF="../exprs/Expr.i3.html">Expr</A>, <A HREF="#x4">Token</A>, <A HREF="#x5">Decl</A>, <A HREF="../../../m3core/src/text/Text.i3.html">Text</A>, <A HREF="Procedure.i3.html">Procedure</A>, <A HREF="../stmts/AssignStmt.i3.html">AssignStmt</A>, <A HREF="../builtinTypes/Addr.i3.html">Addr</A>, <A HREF="../misc/Scanner.i3.html">Scanner</A>;
IMPORT <A HREF="../../../m3middle/src/Target.i3.html">Target</A>, <A HREF="../types/RefType.i3.html">RefType</A>, <A HREF="../misc/ProcBody.i3.html">ProcBody</A>, <A HREF="Module.i3.html">Module</A>, <A HREF="../misc/RunTyme.i3.html">RunTyme</A>, <A HREF="#x6">Marker</A>, <A HREF="../../../m3middle/src/M3Buf.i3.html">M3Buf</A>, <A HREF="../builtinTypes/Int.i3.html">Int</A>;
FROM <A HREF="../misc/Scanner.i3.html">Scanner</A> IMPORT GetToken, Match, MatchID, cur;

TYPE
  T = Value.T BRANDED &quot;Exceptionz.T&quot; OBJECT
        tipe    : Type.T;
        refTipe : Type.T;
        offset  : INTEGER;
        coffset : INTEGER;
        uid     : INTEGER;
        implicit: BOOLEAN;
        next    : T;
      OVERRIDES
        typeCheck   := Check;
        set_globals := SetGlobals;
        load        := Load;
        declare     := Declarer;
        const_init  := ValueRep.NoInit;
        need_init   := ValueRep.Never;
        lang_init   := ValueRep.NoInit;
        user_init   := ValueRep.NoInit;
        toExpr      := ValueRep.NoExpr;
        toType      := ValueRep.NoType;
        typeOf      := ValueRep.TypeVoid;
        base        := ValueRep.Self;
        add_fp_tag  := AddFPTag;
        fp_type     := FPType;
      END;

TYPE
  Raiser = ProcBody.T OBJECT
    self   : T;
    arg    : CG.Var;
    module : CG.Var;
    line   : CG.Var;
  OVERRIDES
    gen_decl := EmitDecl;
    gen_body := EmitBody;
  END;

PROCEDURE <A NAME="ParseDecl"><procedure>ParseDecl</procedure></A> (READONLY att: Decl.Attributes) =
  TYPE TK = Token.T;
  VAR t: T; id: M3ID.T;
  BEGIN
    Match (TK.tEXCEPTION);
    WHILE (cur.token = TK.tIDENT) DO
      id := MatchID ();
      t := NEW (T);
      ValueRep.Init (t, id, Value.Class.Exception);
      t.readonly := TRUE;
      t.unused   := att.isUnused;
      t.obsolete := att.isObsolete;
      t.tipe     := NIL;
      t.refTipe  := NIL;
      t.offset   := 0;
      t.coffset  := 0;
      t.uid      := 0;
      t.implicit := att.isImplicit;
      IF (cur.token = TK.tLPAREN) THEN
        GetToken (); (* ( *)
        t.tipe := Type.Parse ();
        Match (TK.tRPAREN);
      END;
      Scope.Insert (t);
      Match (TK.tSEMI);
    END;
  END ParseDecl;

PROCEDURE <A NAME="EmitRaise"><procedure>EmitRaise</procedure></A> (v: Value.T;  arg: Expr.T) =
  VAR
    t: T := Value.Base (v);
    expr_type: Type.T;
    arg_type: CG.Type;
    info: Type.Info;
    p := RunTyme.LookUpProc (RunTyme.Hook.RaiseEx);
    tmp: CG.Val;
  BEGIN
    Value.Declare (t);
    &lt;*ASSERT t.offset # 0*&gt;

    IF (arg = NIL) THEN
      Procedure.StartCall (p);
      IF Target.DefaultCall.args_left_to_right THEN
        LoadSelf (t);
        CG.Pop_param (CG.Type.Addr);
        CG.Load_nil ();
        CG.Pop_param (CG.Type.Addr);
        PassLocation ();
      ELSE
        PassLocation ();
        CG.Load_nil ();
        CG.Pop_param (CG.Type.Addr);
        LoadSelf (t);
        CG.Pop_param (CG.Type.Addr);
      END;
      Procedure.EmitCall (p);
    ELSIF (t.refTipe = NIL) THEN
      expr_type := Expr.TypeOf (arg);
      expr_type := Type.CheckInfo (expr_type, info);
      arg_type := info.stk_type;
      Expr.Prep (arg);
      AssignStmt.DoEmitCheck (t.tipe, arg);
      tmp := CG.Pop ();
      Procedure.StartCall (p);
      IF Target.DefaultCall.args_left_to_right THEN
        LoadSelf (t);
        CG.Pop_param (CG.Type.Addr);
        CG.Push (tmp);
        IF (arg_type # CG.Type.Addr) THEN
          CG.Loophole (arg_type, CG.Type.Addr);
        END;
        CG.Pop_param (CG.Type.Addr);
        PassLocation ();
      ELSE
        PassLocation ();
        CG.Push (tmp);
        IF (arg_type # CG.Type.Addr) THEN
          CG.Loophole (arg_type, CG.Type.Addr);
        END;
        CG.Pop_param (CG.Type.Addr);
        LoadSelf (t);
        CG.Pop_param (CG.Type.Addr);
      END;
      Procedure.EmitCall (p);
      CG.Free (tmp);
    ELSE (* large argument =&gt; call the raise procedure *)
      expr_type := Expr.TypeOf (arg);
      expr_type := Type.CheckInfo (expr_type, info);
      arg_type := info.stk_type;
      Expr.Prep (arg);
      AssignStmt.DoEmitCheck (t.tipe, arg);
      tmp := CG.Pop ();
      CG.Start_call_indirect (CG.Type.Void, Target.DefaultCall);
      IF Target.DefaultCall.args_left_to_right THEN
        CG.Push (tmp);
        IF (arg_type # CG.Type.Addr) THEN Formal.GenCopy (expr_type) END;
        CG.Pop_param (CG.Type.Addr);
        PassLocation ();
      ELSE
        PassLocation ();
        CG.Push (tmp);
        IF (arg_type # CG.Type.Addr) THEN Formal.GenCopy (expr_type) END;
        CG.Pop_param (CG.Type.Addr);
      END;
      LoadSelf (t);
      CG.Add_offset (M3RT.ED_SIZE);
      CG.Boost_alignment (Target.Address.align);
      CG.Load_indirect (CG.Type.Addr, 0, Target.Address.size);
      CG.Gen_Call_indirect (CG.Type.Void, Target.DefaultCall);
      EVAL Marker.EmitExceptionTest (Procedure.Signature (p), need_value := FALSE);
      CG.Free (tmp);
    END;
  END EmitRaise;

PROCEDURE <A NAME="LoadSelf"><procedure>LoadSelf</procedure></A> (t: T) =
  BEGIN
    IF (t.imported) THEN
      Module.LoadGlobalAddr (Scope.ToUnit (t), t.offset, is_const := FALSE);
      CG.Load_indirect (CG.Type.Addr, 0, Target.Address.size);
      CG.Boost_alignment (Target.Address.align);
    ELSE
      Module.LoadGlobalAddr (Scope.ToUnit (t), t.coffset, is_const := TRUE);
    END;
  END LoadSelf;

PROCEDURE <A NAME="PassLocation"><procedure>PassLocation</procedure></A> () =
  (* push the &quot;module, linenumber&quot; parameters *)
  VAR
    globals   := Module.GlobalData (is_const := FALSE);
    this_file : TEXT;
    this_line : INTEGER;
  BEGIN
    Scanner.Here (this_file, this_line);
    IF Target.DefaultCall.args_left_to_right THEN
      CG.Load_addr_of (globals, 0, CG.Max_alignment);
      CG.Pop_param (CG.Type.Addr);
      CG.Load_intt (this_line);
      CG.Pop_param (Target.Integer.cg_type);
    ELSE
      CG.Load_intt (this_line);
      CG.Pop_param (Target.Integer.cg_type);
      CG.Load_addr_of (globals, 0, CG.Max_alignment);
      CG.Pop_param (CG.Type.Addr);
    END;
  END PassLocation;

PROCEDURE <A NAME="ArgByReference"><procedure>ArgByReference</procedure></A> (type: Type.T): BOOLEAN =
  VAR info: Type.Info;
  BEGIN
    EVAL Type.CheckInfo (type, info);
    RETURN (info.size &gt; Target.Address.size) OR Type.IsStructured (type);
  END ArgByReference;

PROCEDURE <A NAME="Check"><procedure>Check</procedure></A> (t: T;  &lt;*UNUSED*&gt; VAR cs: Value.CheckState) =
  VAR info: Type.Info;
  BEGIN
    IF (t.tipe # NIL) THEN
      t.tipe := Type.CheckInfo (t.tipe, info);
      IF (info.size &lt; 0) THEN
        Error.ID (t.name, &quot;argument type must have fixed length&quot;);
      END;
      IF ArgByReference (t.tipe) THEN
        t.refTipe := RefType.New (t.tipe, TRUE, NIL);
        t.refTipe := Type.Check (t.refTipe);
      END;
    END;
  END Check;

PROCEDURE <A NAME="ArgType"><procedure>ArgType</procedure></A> (v: Value.T): Type.T =
  BEGIN
    TYPECASE Value.Base (v) OF
    | NULL =&gt; RETURN NIL;
    | T(t) =&gt; RETURN t.tipe;
    ELSE      RETURN NIL;
    END;
  END ArgType;

PROCEDURE <A NAME="UID"><procedure>UID</procedure></A> (v: Value.T): INTEGER =
  BEGIN
    TYPECASE Value.Base (v) OF
    | NULL =&gt; RETURN 0;
    | T(t) =&gt; &lt;*ASSERT t.uid # 0*&gt; RETURN t.uid;
    ELSE      RETURN 0;
    END;
  END UID;

PROCEDURE <A NAME="IsImplicit"><procedure>IsImplicit</procedure></A> (v: Value.T): BOOLEAN =
  BEGIN
    TYPECASE Value.Base (v) OF
    | NULL =&gt; RETURN FALSE;
    | T(t) =&gt; RETURN t.implicit;
    ELSE      RETURN FALSE;
    END;
  END IsImplicit;

PROCEDURE <A NAME="Load"><procedure>Load</procedure></A> (t: T) =
  BEGIN
    Value.Declare (t);
    LoadSelf (t);
  END Load;

PROCEDURE <A NAME="SetGlobals"><procedure>SetGlobals</procedure></A> (t: T) =
  VAR name: TEXT;  size: INTEGER;
  BEGIN
    (* Type.SetGlobals (t.tipe); *)
    (* Type.SetGlobals (t.refTipe); *)
    IF (t.offset # 0) OR (t.external) THEN RETURN END;

    name := Value.GlobalName (t, dots := TRUE, with_module := TRUE);
    size := M3RT.ED_SIZE;
    IF (t.refTipe # NIL) THEN INC (size, Target.Address.size) END;
    INC (size, (Text.Length (name) + 1) * Target.Char.size);
    t.offset := Module.Allocate (Target.Address.size, Target.Address.align,
                                 FALSE, name);
    t.coffset := Module.Allocate (size, Target.Address.align, TRUE, name);
    t.uid := M3FP.ToInt (M3FP.FromText (name));
  END SetGlobals;

PROCEDURE <A NAME="Declarer"><procedure>Declarer</procedure></A> (t: T): BOOLEAN =
  VAR
    name: TEXT;
    globals, consts: CG.Var;
    arg_type: CG.TypeUID := 0;
    size: INTEGER;
    proc: CG.Proc;
  BEGIN
    Type.Compile (t.tipe);
    Type.Compile (t.refTipe);
    IF (t.tipe # NIL) THEN arg_type := Type.GlobalUID (t.tipe) END;

    IF t.external OR t.imported THEN
      (* don't bother to force the import *)
    ELSE (* locally declared *)
      SetGlobals (t);
      name := Value.GlobalName (t, dots := TRUE, with_module := TRUE);
      globals := Module.GlobalData (is_const := FALSE);
      consts  := Module.GlobalData (is_const := TRUE);
      size := M3RT.ED_SIZE;
      IF (t.refTipe # NIL) THEN
        INC (size, Target.Address.pack);
        proc := DeclareRaiseProc (t);
      END;
      CG.Declare_exception (t.name, arg_type, (t.refTipe#NIL), consts, t.coffset);
      CG.Declare_global_field (t.name, t.offset, Target.Address.size, 0, FALSE);
      CG.Declare_global_field (t.name, t.coffset, size, 0, TRUE);
      CG.Init_var  (t.offset, consts, t.coffset, FALSE);
      CG.Init_intt (t.coffset + M3RT.ED_uid, Target.Integer.size, t.uid, TRUE);
      CG.Init_var  (t.coffset + M3RT.ED_name, consts, t.coffset + size, TRUE);
      CG.Init_intt (t.coffset + M3RT.ED_implicit, Target.Integer.size,
                                                       ORD (t.implicit), TRUE);
      IF (t.refTipe # NIL) THEN
        CG.Init_proc (t.coffset + M3RT.ED_SIZE, proc, TRUE);
      END;
      CG.Init_chars (t.coffset + size, name, TRUE);
    END;
    RETURN TRUE;
  END Declarer;

PROCEDURE <A NAME="DeclareRaiseProc"><procedure>DeclareRaiseProc</procedure></A> (t: T): CG.Proc =
  VAR r := NEW (Raiser, self := t);
  BEGIN
    r.name    := Value.GlobalName (t, dots := FALSE, with_module := TRUE);
    r.name    := r.name &amp; &quot;_RAISE&quot;;
    r.cg_proc := CG.Declare_procedure (M3ID.Add (r.name), 3, CG.Type.Void,
                      lev := 0, cc := Target.DefaultCall, exported := FALSE,
                      parent := NIL);
    r.arg     := CG.Declare_param (M3ID.NoID, Target.Address.size,
                      Target.Address.align, CG.Type.Addr,
                      CG.Declare_indirect (Type.GlobalUID (t.tipe)),
                      in_memory := FALSE, up_level := FALSE, f := CG.Always);
    r.module  := CG.Declare_param (M3ID.NoID, Target.Address.size,
                      Target.Address.align, CG.Type.Addr,
                      Type.GlobalUID (Addr.T), in_memory := FALSE,
                      up_level := FALSE, f := CG.Always);
    r.line    := CG.Declare_param (M3ID.NoID, Target.Integer.size,
                      Target.Integer.align, Target.Integer.cg_type,
                      Type.GlobalUID (Int.T), in_memory := FALSE,
                      up_level := FALSE, f := CG.Always);
    ProcBody.Schedule (r);
    RETURN r.cg_proc;
  END DeclareRaiseProc;

PROCEDURE <A NAME="EmitDecl"><procedure>EmitDecl</procedure></A> (&lt;*UNUSED*&gt; x: Raiser) =
  BEGIN
  END EmitDecl;

PROCEDURE <A NAME="EmitBody"><procedure>EmitBody</procedure></A> (x: Raiser) =
  VAR
    t := x.self;
    ptr: CG.Val;
    align: INTEGER;
    proc: Procedure.T;
    info: Type.Info;
  BEGIN
    CG.Gen_location (t.origin);
    CG.Begin_procedure (x.cg_proc);
    EVAL Type.CheckInfo (t.tipe, info);
    align := info.alignment;

    (* ptr := NEW (REF t.type) *)
    proc := RunTyme.LookUpProc (RunTyme.Hook.NewTracedRef);
    Procedure.StartCall (proc);
    Type.LoadInfo (t.refTipe, -1);
    CG.Pop_param (CG.Type.Addr);
    ptr := Procedure.EmitValueCall (proc);

    (* ptr^ := arg^ *)
    CG.Push (ptr);
    CG.Boost_alignment (align);
    CG.Load_addr (x.arg);
    CG.Boost_alignment (align);
    CG.Copy (info.size, overlap := FALSE);

    (* RAISE (e, ptr) *)
    proc := RunTyme.LookUpProc (RunTyme.Hook.RaiseEx);
    Procedure.StartCall (proc);
    IF Target.DefaultCall.args_left_to_right THEN
      LoadSelf (t);
      CG.Pop_param (CG.Type.Addr);
      CG.Push (ptr);
      CG.Pop_param (CG.Type.Addr);
      CG.Load_addr (x.module);
      CG.Pop_param (CG.Type.Addr);
      CG.Load_int (Target.Integer.cg_type, x.line);
      CG.Pop_param (Target.Integer.cg_type);
    ELSE
      CG.Load_int (Target.Integer.cg_type, x.line);
      CG.Pop_param (Target.Integer.cg_type);
      CG.Load_addr (x.module);
      CG.Pop_param (CG.Type.Addr);
      CG.Push (ptr);
      CG.Pop_param (CG.Type.Addr);
      LoadSelf (t);
      CG.Pop_param (CG.Type.Addr);
    END;
    Procedure.EmitCall (proc);

    CG.Free (ptr);
    CG.End_procedure (x.cg_proc);
  END EmitBody;

PROCEDURE <A NAME="AddFPSetTag"><procedure>AddFPSetTag</procedure></A> (tt: Value.T;  VAR x: M3.FPInfo): CARDINAL =
  (* called for RAISES sets, doesn't include the interface record offset *)
  VAR t: T := Value.Base (tt);
  BEGIN
    M3Buf.PutChar (x.buf, '&lt;');
    ValueRep.FPStart (t, x, &quot;EXCEPT &quot;, 0, global := TRUE);
    M3Buf.PutChar (x.buf, '&gt;');
    RETURN ORD (t.tipe # NIL);
  END AddFPSetTag;

PROCEDURE <A NAME="AddFPTag"><procedure>AddFPTag</procedure></A> (t: T;  VAR x: M3.FPInfo): CARDINAL =
  VAR offset := t.offset DIV Target.Address.align;
  BEGIN
    ValueRep.FPStart (t, x, &quot;EXCEPT &quot;, offset, global := TRUE);
    RETURN ORD (t.tipe # NIL);
  END AddFPTag;

PROCEDURE <A NAME="FPType"><procedure>FPType</procedure></A> (t: T): Type.T =
  BEGIN
    RETURN t.tipe;
  END FPType;

BEGIN
END Exceptionz.
</PRE>
</inModule>
<HR>
<A NAME="x1">interface M3ID is in:
</A><UL>
<LI><A HREF="../../../m3middle/src/M3ID.i3.html#0TOP0">m3middle/src/M3ID.i3</A>
<LI><A HREF="../../../m3scan/src/M3ID.i3.html#0TOP0">m3scan/src/M3ID.i3</A>
</UL>
<P>
<HR>
<A NAME="x2">interface Value is in:
</A><UL>
<LI><A HREF="Value.i3.html#0TOP0">m3front/src/values/Value.i3</A>
<LI><A HREF="../../../sharedobjgen/src/Value.i3.html#0TOP0">sharedobjgen/src/Value.i3</A>
<LI><A HREF="../../../stablegen/src/Value.i3.html#0TOP0">stablegen/src/Value.i3</A>
<LI><A HREF="../../../stubgen/src/Value.i3.html#0TOP0">stubgen/src/Value.i3</A>
</UL>
<P>
<HR>
<A NAME="x3">interface Type is in:
</A><UL>
<LI><A HREF="../../../cm3ide/src/nodes/Type.i3.html#0TOP0">cm3ide/src/nodes/Type.i3</A>
<LI><A HREF="../types/Type.i3.html#0TOP0">m3front/src/types/Type.i3</A>
<LI><A HREF="../../../sharedobjgen/src/Type.i3.html#0TOP0">sharedobjgen/src/Type.i3</A>
<LI><A HREF="../../../stablegen/src/Type.i3.html#0TOP0">stablegen/src/Type.i3</A>
<LI><A HREF="../../../stubgen/src/Type.i3.html#0TOP0">stubgen/src/Type.i3</A>
</UL>
<P>
<HR>
<A NAME="x4">interface Token is in:
</A><UL>
<LI><A HREF="../misc/Token.i3.html#0TOP0">m3front/src/misc/Token.i3</A>
<LI><A HREF="../../../mentor/src/parse/Token.i3.html#0TOP0">mentor/src/parse/Token.i3</A>
</UL>
<P>
<HR>
<A NAME="x5">interface Decl is in:
</A><UL>
<LI><A HREF="../../../cm3ide/src/nodes/Decl.i3.html#0TOP0">cm3ide/src/nodes/Decl.i3</A>
<LI><A HREF="Decl.i3.html#0TOP0">m3front/src/values/Decl.i3</A>
</UL>
<P>
<HR>
<A NAME="x6">interface Marker is in:
</A><UL>
<LI><A HREF="../../../cm3ide/src/markup/Marker.i3.html#0TOP0">cm3ide/src/markup/Marker.i3</A>
<LI><A HREF="../misc/Marker.i3.html#0TOP0">m3front/src/misc/Marker.i3</A>
</UL>
<P>
<PRE>























</PRE>
</BODY>
</HTML>
