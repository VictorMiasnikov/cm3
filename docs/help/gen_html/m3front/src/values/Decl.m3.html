<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: m3front/src/values/Decl.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>m3front/src/values/Decl.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright (C) 1992, Digital Equipment Corporation           
 All rights reserved.                                        
 See the file COPYRIGHT for a full description.              

<P> File: Decl.m3                                               
 Last modified on Tue Dec 20 14:54:22 PST 1994 by kalsow     
      modified on Sat Mar 16 01:56:20 1991 by muller         

<P><PRE>MODULE <module><implements><A HREF="#x1">Decl</A></implements></module>;

IMPORT <A HREF="../misc/M3.i3.html">M3</A>, <A HREF="#x2">M3ID</A>, <A HREF="../misc/M3String.i3.html">M3String</A>, <A HREF="#x3">Token</A>, <A HREF="../misc/Error.i3.html">Error</A>, <A HREF="../misc/ESet.i3.html">ESet</A>, <A HREF="Module.i3.html">Module</A>, <A HREF="Exceptionz.i3.html">Exceptionz</A>;
IMPORT <A HREF="Constant.i3.html">Constant</A>, <A HREF="Tipe.i3.html">Tipe</A>, <A HREF="Variable.i3.html">Variable</A>, <A HREF="Procedure.i3.html">Procedure</A>, <A HREF="Revelation.i3.html">Revelation</A>, <A HREF="../misc/CG.i3.html">CG</A>, <A HREF="../../../m3middle/src/Target.i3.html">Target</A>;
FROM <A HREF="../misc/Scanner.i3.html">Scanner</A> IMPORT GetToken, Match, cur;

TYPE
  TK = Token.T;

TYPE
  Attr    = { External, Inline, Implicit, CallConv, Obsolete, Unused,
              LazyAligned };
  AttrSet = SET OF Attr;
CONST
  RevealOK    = AttrSet { };
  ConstantOK  = AttrSet { Attr.Obsolete, Attr.Unused, Attr.LazyAligned };
  TypeOK      = AttrSet { Attr.Obsolete, Attr.Unused, Attr.LazyAligned };
  ExceptionOK = AttrSet { Attr.Obsolete, Attr.Unused, Attr.Implicit };
  VariableOK  = AttrSet { Attr.Obsolete, Attr.Unused, Attr.External,
                          Attr.LazyAligned };
  ProcedureOK = AttrSet { Attr.Obsolete, Attr.Unused, Attr.External,
                          Attr.Inline, Attr.CallConv };

PROCEDURE <A NAME="Parse"><procedure>Parse</procedure></A> (interface, top_level: BOOLEAN;  VAR fails: M3.ExSet) =
  VAR att: Attributes;  got_cc := FALSE;
  BEGIN
    att.isInline    := FALSE;
    att.isExternal  := FALSE;
    att.isUnused    := FALSE;
    att.isObsolete  := FALSE;
    att.isImplicit  := FALSE;
    att.alias       := M3ID.NoID;
    att.callingConv := NIL;
    att.isLazyAligned := Module.LazyAlignmentOn ();
    LOOP
      CASE cur.token OF
      | TK.tEXTERNAL =&gt;
          IF NOT Module.IsInterface () THEN
            Error.Msg (&quot;External declarations only allowed in interfaces&quot;);
          END;
          ParseExternalPragma (att.alias, att.callingConv, got_cc);
          att.isExternal := TRUE;
      | TK.tINLINE   =&gt;
          att.isInline := TRUE;
          GetToken (); (* INLINE *)
          Match (TK.tENDPRAGMA);
      | TK.tUNUSED   =&gt;
          att.isUnused := TRUE;
          GetToken (); (* UNUSED *)
          Match (TK.tENDPRAGMA);
      | TK.tOBSOLETE =&gt;
          att.isObsolete := TRUE;
          GetToken (); (* OBSOLETE *)
          Match (TK.tENDPRAGMA);
      | TK.tIMPLICIT =&gt;
          att.isImplicit := TRUE;
          GetToken (); (* IMPLICIT *)
          Match (TK.tENDPRAGMA);
      | TK.tCALLCONV   =&gt;
          att.callingConv := Target.FindConvention (M3ID.ToText (cur.id));
          got_cc := TRUE;
          GetToken (); (* convention name *)
          Match (TK.tENDPRAGMA);
      | TK.tLAZYALIGN =&gt;
          att.isLazyAligned := TRUE;
          Module.SetLazyAlignment (TRUE);
          GetToken (); (* LAZYALIGN *)
          Match (TK.tENDPRAGMA);
      | TK.tSTRICTALIGN =&gt;
          att.isLazyAligned := FALSE;
          Module.SetLazyAlignment (FALSE);
          GetToken (); (* LAZYALIGN *)
          Match (TK.tENDPRAGMA);
      ELSE EXIT;
      END;
    END;

    CASE cur.token OF
    | TK.tCONST =&gt;
        CheckAttrs (att, got_cc, ConstantOK, &quot;constant&quot;);
        Constant.ParseDecl (att);
    | TK.tTYPE =&gt;
        CheckAttrs (att, got_cc, TypeOK, &quot;type&quot;);
        Tipe.Parse (att);
    | TK.tVAR =&gt;
        att.isExternal := att.isExternal OR Module.IsExternal ();
        CheckAttrs (att, got_cc, VariableOK, &quot;variable&quot;);
        Variable.ParseDecl (att);
    | TK.tPROCEDURE =&gt;
        att.isExternal := att.isExternal OR Module.IsExternal ();
        CheckAttrs (att, got_cc, ProcedureOK, &quot;procedure&quot;);
        Procedure.ParseDecl (att, interface);
    | TK.tREVEAL =&gt;
        IF (NOT top_level) THEN
          Error.Msg (&quot;revelations in nested scopes are not allowed&quot;);
        END;
        CheckAttrs (att, got_cc, RevealOK, &quot;revelation&quot;);
        Revelation.Parse (att);
    | TK.tEXCEPTION =&gt;
        IF (NOT top_level) THEN
          Error.Msg (&quot;exception declarations in nested scopes are not allowed&quot;);
        END;
        CheckAttrs (att, got_cc, ExceptionOK, &quot;exception&quot;);
        Exceptionz.ParseDecl (att);
    | TK.tFATAL =&gt;
        fails := ESet.ParseFails (fails);
    ELSE
        IF att.isInline OR att.isExternal OR att.isUnused
           OR att.isObsolete OR att.isImplicit THEN
          Error.Msg (&quot;declaration pragma not followed by a declaration&quot;);
        END;
    END;
  END Parse;

PROCEDURE <A NAME="ParseExternalPragma"><procedure>ParseExternalPragma</procedure></A> (VAR alias  : M3ID.T;
                               VAR cc     : CG.CallingConvention;
                               VAR got_cc : BOOLEAN) =
  VAR txt: TEXT;
  BEGIN
    &lt;* ASSERT cur.token = TK.tEXTERNAL *&gt;
    GetToken (); (* EXTERNAL *)

    alias  := M3ID.NoID;  (* default =&gt; use the Modula-3 name *)
    cc     := Target.DefaultCall;
    got_cc := FALSE;

    IF (cur.token = TK.tIDENT) OR (cur.token = TK.tTEXTCONST) THEN
      IF (cur.token = TK.tIDENT)
        THEN alias := cur.id;
        ELSE alias := M3ID.Add (M3String.ToText (cur.str));
      END;
      GetToken (); (* IDENT, TEXTCONST *)

      IF (cur.token = TK.tCOLON) THEN
        GetToken (); (* : *)
        IF    (cur.token = TK.tIDENT)     THEN  txt := M3ID.ToText (cur.id);
        ELSIF (cur.token = TK.tTEXTCONST) THEN  txt := M3String.ToText (cur.str);
        ELSE                                    txt := NIL;
        END;
        IF (txt # NIL) THEN
          cc := Target.FindConvention (txt);
          got_cc := TRUE;
          IF (cc = NIL) THEN
            Error.ID (cur.id, &quot;unsupported language or calling convention&quot;);
            cc := Target.DefaultCall;
          END;
          GetToken (); (* IDENT/TEXTCONST *)
        ELSE
          Error.Msg (&quot;Missing language for &lt;*EXTERNAL*&gt; pragma&quot;);
        END;
      END;

    END;

    Match (TK.tENDPRAGMA);
  END ParseExternalPragma;

PROCEDURE <A NAME="CheckAttrs"><procedure>CheckAttrs</procedure></A> (VAR att: Attributes;  got_cc: BOOLEAN;
                      allowed: AttrSet;  tag: TEXT) =
  BEGIN
    IF att.isUnused AND NOT Attr.Unused IN allowed THEN
      Error.Msg (tag &amp; &quot;s cannot be unused&quot;);
      att.isUnused := FALSE;
    END;
    IF att.isObsolete AND NOT Attr.Obsolete IN allowed THEN
      Error.Msg (tag &amp; &quot;s cannot be obsolete&quot;);
      att.isObsolete := FALSE;
    END;
    IF att.isInline AND NOT Attr.Inline IN allowed THEN
      Error.Msg (tag &amp; &quot;s cannot be inline&quot;);
      att.isInline := FALSE;
    END;
    IF att.isImplicit AND NOT Attr.Implicit IN allowed THEN
      Error.Msg (tag &amp; &quot;s cannot be declared &lt;*IMPLICIT*&gt;&quot;);
      att.isImplicit := FALSE;
    END;
    IF att.isExternal AND NOT Attr.External IN allowed THEN
      Error.Msg (tag &amp; &quot;s cannot be external&quot;);
      att.isExternal  := FALSE;
      att.callingConv := NIL;
      att.alias       := M3ID.NoID;
    ELSIF got_cc AND att.callingConv # NIL AND NOT Attr.CallConv IN allowed THEN
      Error.Msg (tag &amp; &quot;s do not have calling conventions&quot;);
      att.callingConv := NIL;
    END;
  END CheckAttrs;

BEGIN
END Decl.
</PRE>
</inModule>
<HR>
<A NAME="x1">interface Decl is in:
</A><UL>
<LI><A HREF="../../../cm3ide/src/nodes/Decl.i3.html#0TOP0">cm3ide/src/nodes/Decl.i3</A>
<LI><A HREF="Decl.i3.html#0TOP0">m3front/src/values/Decl.i3</A>
</UL>
<P>
<HR>
<A NAME="x2">interface M3ID is in:
</A><UL>
<LI><A HREF="../../../m3middle/src/M3ID.i3.html#0TOP0">m3middle/src/M3ID.i3</A>
<LI><A HREF="../../../m3scan/src/M3ID.i3.html#0TOP0">m3scan/src/M3ID.i3</A>
</UL>
<P>
<HR>
<A NAME="x3">interface Token is in:
</A><UL>
<LI><A HREF="../misc/Token.i3.html#0TOP0">m3front/src/misc/Token.i3</A>
<LI><A HREF="../../../mentor/src/parse/Token.i3.html#0TOP0">mentor/src/parse/Token.i3</A>
</UL>
<P>
<PRE>























</PRE>
</BODY>
</HTML>
