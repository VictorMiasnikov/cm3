<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: m3front/src/types/Brand.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>m3front/src/types/Brand.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright 1996-2000, Critical Mass, Inc.  All rights reserved. 
 See file COPYRIGHT-CMASS for details. 

<P><PRE>MODULE <module><implements><A HREF="Brand.i3.html">Brand</A></implements></module>;

IMPORT <A HREF="../misc/M3.i3.html">M3</A>, <A HREF="../exprs/Expr.i3.html">Expr</A>, <A HREF="../misc/M3String.i3.html">M3String</A>, <A HREF="../misc/M3WString.i3.html">M3WString</A>, <A HREF="../../../m3middle/src/M3Buf.i3.html">M3Buf</A>, <A HREF="../misc/Scanner.i3.html">Scanner</A>, <A HREF="../values/Module.i3.html">Module</A>;
IMPORT <A HREF="../exprs/TextExpr.i3.html">TextExpr</A>, <A HREF="../../../m3middle/src/Target.i3.html">Target</A>, <A HREF="#x1">Type</A>, <A HREF="#x2">Token</A>, <A HREF="#x3">M3ID</A>, <A HREF="../misc/Error.i3.html">Error</A>, <A HREF="../misc/CG.i3.html">CG</A>, <A HREF="../../../m3core/src/word/Word.i3.html">Word</A>;

REVEAL
  <A NAME="T">T</A> = BRANDED &quot;Brand.T&quot; REF RECORD
    origin: INTEGER     := 0;
    type  : Type.T      := NIL;
    expr  : Expr.T      := NIL;
    val8  : M3String.T  := NIL;
    val16 : M3WString.T := NIL;
    next  : T           := NIL;
    error : BOOLEAN     := FALSE;
  END;

VAR brand_buf  : M3Buf.T := NIL;
VAR all_brands : ARRAY [0..97] OF T;

PROCEDURE <A NAME="Reset"><procedure>Reset</procedure></A> () =
  VAR t, u: T;
  BEGIN
    FOR i := FIRST (all_brands) TO LAST (all_brands) DO
      t := all_brands[i];
      WHILE (t # NIL) DO
        u := t.next;
        t.next  := NIL;
        t.error := FALSE;
        t := u;
      END;
      all_brands[i] := NIL;
    END;
  END Reset;

PROCEDURE <A NAME="Parse"><procedure>Parse</procedure></A> (): T =
  VAR t: T := NIL;
  BEGIN
    IF (Scanner.cur.token = Token.T.tBRANDED) THEN
      t := NEW (T, origin := Scanner.offset);
      Scanner.GetToken (); (* BRANDED *)
      IF (Scanner.cur.token IN Token.ExprStart)
        THEN t.expr := Expr.Parse ();
        ELSE t.expr := GenerateBrand ();
      END;
    END;
    RETURN t;
  END Parse;

PROCEDURE <A NAME="New"><procedure>New</procedure></A> (txt: TEXT): T =
  VAR t := NEW (T);
  BEGIN
    t.origin := Scanner.offset;
    t.expr   := TextExpr.New8 (M3String.Add (txt));
    RETURN t;
  END New;

PROCEDURE <A NAME="GenerateBrand"><procedure>GenerateBrand</procedure></A> (): Expr.T =
  CONST Suffix = ARRAY BOOLEAN OF CHAR { 'M', 'I' };
  VAR counter: ARRAY [0..4] OF CHAR;
  BEGIN
    Module.GetNextCounter (counter);

    (* build the string *)
    IF (brand_buf = NIL) THEN brand_buf := M3Buf.New (); END;
    M3ID.Put      (brand_buf, Module.Name (NIL));
    M3Buf.PutText (brand_buf, &quot; # AuTo-BrAnD # &quot;);
    M3Buf.PutSub  (brand_buf, counter);
    M3Buf.PutChar (brand_buf, Suffix [Module.IsInterface ()]);

    RETURN TextExpr.New8 (M3String.Add (M3Buf.ToText (brand_buf)));
  END GenerateBrand;

PROCEDURE <A NAME="Check"><procedure>Check</procedure></A> (t: T;  holder: Type.T;
                 VAR hash: INTEGER;  VAR cs: Expr.CheckState) =
  VAR e: Expr.T;  xx: INTEGER;
  BEGIN
    IF (t = NIL) THEN RETURN; END;
    t.type := holder;
    Expr.TypeCheck (t.expr, cs);
    e := Expr.ConstValue (t.expr);
    IF (e = NIL) THEN
      Error.Msg (&quot;brand is not a constant&quot;);
    ELSIF TextExpr.Split8 (e, t.val8) THEN
      t.expr := e;
      xx := M3String.Hash (t.val8);
      hash := Word.Plus (Word.Times (hash, 37), xx);
      CheckDuplicate (t, xx);
    ELSIF TextExpr.Split16 (e, t.val16) THEN
      t.expr := e;
      xx := M3WString.Hash (t.val16);
      hash := Word.Plus (Word.Times (hash, 37), xx);
      CheckDuplicate (t, xx);
    ELSE
      Error.Msg (&quot;brand is not a TEXT constant&quot;);
    END;
  END Check;

PROCEDURE <A NAME="CheckDuplicate"><procedure>CheckDuplicate</procedure></A> (t: T;  hash: INTEGER) =
  VAR cell : INTEGER := hash MOD NUMBER (all_brands);
  VAR node : T       := all_brands[cell];
  BEGIN
    LOOP
      IF (node = NIL) THEN
        (* add an entry to the table *)
        t.next := all_brands[cell];
        all_brands[cell] := t;
        RETURN;
      ELSIF (node = t) OR (node.type = t.type) THEN
        (* ok, this is a repeated check of an existing brand *)
        RETURN;
      ELSIF (node.val8 = t.val8) AND (node.val16 = t.val16) THEN
        IF (node.type.origin # t.type.origin) THEN
          (* error, duplicate brand *)
          DuplicateError (t);
          DuplicateError (node);
        END;
        RETURN;
      END;
      node := node.next;
    END;
  END CheckDuplicate;

PROCEDURE <A NAME="DuplicateError"><procedure>DuplicateError</procedure></A> (t: T) =
  VAR save := Scanner.offset;
  BEGIN
    IF NOT t.error THEN
      t.error := TRUE;
      Scanner.offset := t.origin;
      Error.Txt (ToText (t), &quot;duplicate brand&quot;);
      Scanner.offset := save;
    END;
  END DuplicateError;

PROCEDURE <A NAME="Compile"><procedure>Compile</procedure></A> (t: T): INTEGER =
  VAR offset := -1;  len, n_bytes: INTEGER;
  BEGIN
    IF (t = NIL) THEN
      (* no brand *)
    ELSIF (t.val8 # NIL) THEN
      len := Target.Char.size * (M3String.Length (t.val8) + 1);
      n_bytes := (len - Target.Char.size) DIV Target.Int8.size;
      offset := Module.Allocate (Target.Integer.size + len,
                                 Target.Integer.align, TRUE, &quot;brand&quot;);
      CG.Init_intt (offset, Target.Integer.size, n_bytes, TRUE);
      M3String.Init_chars (offset + Target.Integer.size, t.val8, TRUE);
    ELSIF (t.val16 # NIL) THEN
      len := Target.Int16.size * (M3WString.Length (t.val16) + 1);
      n_bytes := (len - Target.Int16.size) DIV Target.Int8.size;
      offset := Module.Allocate (Target.Integer.size + len,
                                 Target.Integer.align, TRUE, &quot;brand&quot;);
      CG.Init_intt (offset, Target.Integer.size, n_bytes, TRUE);
      M3WString.Init_chars (offset + Target.Integer.size, t.val16, TRUE);
    END;
    RETURN offset;
  END Compile;

PROCEDURE <A NAME="GenFPrint"><procedure>GenFPrint</procedure></A> (t: T;  VAR x: M3.FPInfo) =
  BEGIN
    IF (t = NIL) THEN
      (* no brand info *)
    ELSIF (t.val8 # NIL) THEN
      M3Buf.PutText (x.buf, &quot;-BRAND8 &quot;);
      M3Buf.PutInt  (x.buf, M3String.Length (t.val8));
      M3Buf.PutChar (x.buf, ' ');
      M3String.Put  (x.buf, t.val8);
    ELSIF (t.val16 # NIL) THEN
      M3Buf.PutText (x.buf, &quot;-BRAND16 &quot;);
      M3Buf.PutInt  (x.buf, M3WString.Length (t.val16));
      M3Buf.PutChar (x.buf, ' ');
      M3WString.PutLiteral (x.buf, t.val16);
    END;
  END GenFPrint;

PROCEDURE <A NAME="Equal"><procedure>Equal</procedure></A> (a, b: T): BOOLEAN =
  BEGIN
    IF    (a = NIL)       OR  (b = NIL)       THEN  RETURN (a = b);
    ELSIF (a.val8  # NIL) AND (b.val8 # NIL)  THEN  RETURN (a.val8 = b.val8);
    ELSIF (a.val16 # NIL) AND (b.val16 # NIL) THEN  RETURN (a.val16 = b.val16);
    ELSIF (a.val8  # NIL) AND (b.val16 # NIL) THEN  RETURN FALSE;
    ELSIF (a.val16 # NIL) AND (b.val8 # NIL)  THEN  RETURN FALSE;
    ELSE  RETURN Expr.IsEqual (a.expr, b.expr, NIL);
    END;
  END Equal;

PROCEDURE <A NAME="ToText"><procedure>ToText</procedure></A> (t: T): TEXT =
  VAR txt: TEXT := NIL;
  BEGIN
    IF    (t = NIL)       THEN txt := NIL;
    ELSIF (t.val8  # NIL) THEN txt := M3String.ToText (t.val8);
    ELSIF (t.val16 # NIL) THEN txt := M3WString.ToLiteral (t.val16);
    END;
    RETURN txt;
  END ToText;

BEGIN
END Brand.
</PRE>
</inModule>
<HR>
<A NAME="x1">interface Type is in:
</A><UL>
<LI><A HREF="../../../cm3ide/src/nodes/Type.i3.html#0TOP0">cm3ide/src/nodes/Type.i3</A>
<LI><A HREF="Type.i3.html#0TOP0">m3front/src/types/Type.i3</A>
<LI><A HREF="../../../sharedobjgen/src/Type.i3.html#0TOP0">sharedobjgen/src/Type.i3</A>
<LI><A HREF="../../../stablegen/src/Type.i3.html#0TOP0">stablegen/src/Type.i3</A>
<LI><A HREF="../../../stubgen/src/Type.i3.html#0TOP0">stubgen/src/Type.i3</A>
</UL>
<P>
<HR>
<A NAME="x2">interface Token is in:
</A><UL>
<LI><A HREF="../misc/Token.i3.html#0TOP0">m3front/src/misc/Token.i3</A>
<LI><A HREF="../../../mentor/src/parse/Token.i3.html#0TOP0">mentor/src/parse/Token.i3</A>
</UL>
<P>
<HR>
<A NAME="x3">interface M3ID is in:
</A><UL>
<LI><A HREF="../../../m3middle/src/M3ID.i3.html#0TOP0">m3middle/src/M3ID.i3</A>
<LI><A HREF="../../../m3scan/src/M3ID.i3.html#0TOP0">m3scan/src/M3ID.i3</A>
</UL>
<P>
<PRE>























</PRE>
</BODY>
</HTML>
