<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: arithmetic/src/misc/stochastic/RandomParkMiller.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>arithmetic/src/misc/stochastic/RandomParkMiller.m3</H2></A><HR>
<inModule>
<PRE>MODULE <module><implements><A HREF="RandomParkMiller.i3.html">RandomParkMiller</A></implements></module>;
</PRE><BLOCKQUOTE><EM> Arithmetic for Modula-3, see doc for details
<P>
   Abstract: &lt;describe&gt;
<P>
   1/1/96 &lt;name&gt; Initial version </EM></BLOCKQUOTE><PRE>

IMPORT <A HREF="../../../derived/LongRealBasic.i3.html">LongRealBasic</A> AS R;

FROM <A HREF="RandomBasic.i3.html">RandomBasic</A> IMPORT Min, Max;
IMPORT <A HREF="RandomRep.i3.html">RandomRep</A>, <A HREF="RandomBasic.i3.html">RandomBasic</A>;

CONST Module = &quot;RandomParkMiller.&quot;;

CONST TableSize = 32;            (* for Bayes-Durham shuffle *)

REVEAL
  <A NAME="T">T</A> = TPublic BRANDED OBJECT
        z1, table_z: INTEGER;
        table      : ARRAY [0 .. TableSize - 1] OF INTEGER;
      OVERRIDES
        init   := Init;
        engine := Engine;
      END;

CONST
  m       = 2147483647;
  m_recip = R.One / FLOAT(m, R.T);
  a       = 48271;
  q       = 44488;
  r       = 3399;

PROCEDURE <A NAME="Init"><procedure>Init</procedure></A> (SELF: T; seed: [1 .. LAST(INTEGER)] := 1; ): T =
  VAR z1, tmp: INTEGER;
  BEGIN
    SELF.z1 := seed;
    z1 := SELF.z1;
    FOR i := FIRST(SELF.table) TO LAST(SELF.table) DO
      tmp := z1 DIV q;
      z1 := a * (z1 - tmp * q) - r * tmp;
      IF z1 &lt; 0 THEN INC(z1, m) END;
      SELF.table[i] := z1;
    END;
    SELF.z1 := z1;
    SELF.table_z := SELF.table[2];
    RETURN SELF;
  END Init;

PROCEDURE <A NAME="Engine"><procedure>Engine</procedure></A> (SELF: T; ): R.T =
  &lt;* UNUSED *&gt;
  CONST
    ftn = Module &amp; &quot;Engine&quot;;

  VAR
    z1, table_z, tmp, ndx: INTEGER;
    result               : R.T;
  BEGIN
    (*---get the raw result---*)
    z1 := SELF.z1;
    tmp := z1 DIV q;
    z1 := a * (z1 - tmp * q) - r * tmp;
    IF z1 &lt; 0 THEN INC(z1, m) END;

    (*---do the shuffle---*)
    table_z := SELF.table_z;
    ndx := table_z - (table_z DIV TableSize) * TableSize;
    table_z := SELF.table[ndx];
    SELF.table[ndx] := z1;

    (*---convert and check for out of bounds---*)
    result := FLOAT(table_z, R.T) * m_recip;
    IF result &lt; Min THEN
      result := Min;
    ELSIF result &gt; Max THEN
      result := Max;
    END;

    (*---save data and close---*)
    SELF.z1 := z1;
    SELF.table_z := table_z;
    RETURN result;
  END Engine;

BEGIN
END RandomParkMiller.
</PRE>
</inModule>
<PRE>























</PRE>
</BODY>
</HTML>
