<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: arithmetic/src/linearalgebra/vector/VectorFast.mg</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>arithmetic/src/linearalgebra/vector/VectorFast.mg</H2></A><HR>
<inModule>
<PRE>GENERIC MODULE <genericModule><A HREF="VectorFast.ig.html">VectorFast</A></genericModule>(R, V);
</PRE><BLOCKQUOTE><EM> Arithmetic for Modula-3, see doc for details </EM></BLOCKQUOTE><PRE>
</PRE> VR is needed to reveal the structure of V.T 

<P><PRE>&lt;* UNUSED *&gt;
CONST
  Module = &quot;VectorFast.&quot;;

&lt;* INLINE *&gt;
PROCEDURE <A NAME="AssertEqualSize"><procedure>AssertEqualSize</procedure></A> (READONLY x, y: T; ) =
  BEGIN
    &lt;* ASSERT NUMBER(x) = NUMBER(y), &quot;Vectors must have the same size.&quot; *&gt;
  END AssertEqualSize;

PROCEDURE <A NAME="IsZero"><procedure>IsZero</procedure></A> (READONLY x: T; ): BOOLEAN =
  BEGIN
    FOR i := FIRST(x) TO LAST(x) DO
      IF x[i] # R.Zero THEN RETURN FALSE; END
    END;
    RETURN TRUE;
  END IsZero;

PROCEDURE <A NAME="Equal"><procedure>Equal</procedure></A> (READONLY x, y: T; ): BOOLEAN =
  BEGIN
    AssertEqualSize(x, y);
    FOR i := FIRST(x) TO LAST(x) DO
      IF x[i] # y[i] THEN RETURN FALSE; END
    END;
    RETURN TRUE;
  END Equal;

PROCEDURE <A NAME="Add"><procedure>Add</procedure></A> (READONLY x, y: T; ): V.T =
  VAR z := NEW(V.T, NUMBER(x));
  BEGIN
    AssertEqualSize(x, y);
    FOR i := FIRST(x) TO LAST(x) DO z[i] := x[i] + y[i]; END;
    RETURN z;
  END Add;

PROCEDURE <A NAME="Sub"><procedure>Sub</procedure></A> (READONLY x, y: T; ): V.T =
  VAR z := NEW(V.T, NUMBER(x));
  BEGIN
    AssertEqualSize(x, y);
    FOR i := FIRST(x) TO LAST(x) DO z[i] := x[i] - y[i]; END;
    RETURN z;
  END Sub;

PROCEDURE <A NAME="Neg"><procedure>Neg</procedure></A> (READONLY x: T; ): V.T =
  VAR z := NEW(V.T, NUMBER(x));
  BEGIN
    FOR i := FIRST(x) TO LAST(x) DO z[i] := -x[i]; END;
    RETURN z;
  END Neg;

PROCEDURE <A NAME="Scale"><procedure>Scale</procedure></A> (READONLY x: T; y: R.T; ): V.T =
  VAR z := NEW(V.T, NUMBER(x));
  BEGIN
    FOR i := FIRST(x) TO LAST(x) DO z[i] := x[i] * y; END;
    RETURN z;
  END Scale;

PROCEDURE <A NAME="Inner"><procedure>Inner</procedure></A> (READONLY x, y: T; ): R.T =
  VAR sum := R.Zero;
  BEGIN
    AssertEqualSize(x, y);
    FOR i := FIRST(x) TO LAST(x) DO sum := sum + x[i] * y[i]; END;
    RETURN sum;
  END Inner;

PROCEDURE <A NAME="Sum"><procedure>Sum</procedure></A> (READONLY x: T; ): R.T =
  VAR sum := R.Zero;
  BEGIN
    FOR i := FIRST(x) TO LAST(x) DO sum := sum + x[i]; END;
    RETURN sum;
  END Sum;

PROCEDURE <A NAME="Max"><procedure>Max</procedure></A> (READONLY x: T; ): R.T =
  VAR max := R.NegInf;
  BEGIN
    FOR i := FIRST(x) TO LAST(x) DO max := MAX(max, x[i]); END;
    RETURN max;
  END Max;

PROCEDURE <A NAME="Min"><procedure>Min</procedure></A> (READONLY x: T; ): R.T =
  VAR min := R.PosInf;
  BEGIN
    FOR i := FIRST(x) TO LAST(x) DO min := MIN(min, x[i]); END;
    RETURN min;
  END Min;

PROCEDURE <A NAME="ArithSeq"><procedure>ArithSeq</procedure></A> (num: CARDINAL; from: R.T; by: R.T; ): V.T =
  VAR x := NEW(V.T, num);
  BEGIN
    IF num &gt; 0 THEN
      x[0] := from;
      FOR j := 1 TO num - 1 DO from := from + by; x[j] := from; END;
    END;
    RETURN x;
  END ArithSeq;

PROCEDURE <A NAME="GeomSeq"><procedure>GeomSeq</procedure></A> (num: CARDINAL; from: R.T; by: R.T; ): V.T =
  VAR x := NEW(V.T, num);
  BEGIN
    IF num &gt; 0 THEN
      x[0] := from;
      FOR j := 1 TO num - 1 DO from := from * by; x[j] := from; END;
    END;
    RETURN x;
  END GeomSeq;

BEGIN
END VectorFast.
</PRE>
</inModule>
<PRE>























</PRE>
</BODY>
</HTML>
