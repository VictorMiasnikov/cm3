<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: arithmetic/src/linearalgebra/matrix/CharPolynomial.mg</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>arithmetic/src/linearalgebra/matrix/CharPolynomial.mg</H2></A><HR>
<inModule>
<PRE>GENERIC MODULE <genericModule><A HREF="CharPolynomial.ig.html">CharPolynomial</A></genericModule>(R, Rt, M);
</PRE><BLOCKQUOTE><EM> Arithmetic for Modula-3, see doc for details </EM></BLOCKQUOTE><PRE>

IMPORT <A HREF="../../Arithmetic.i3.html">Arithmetic</A> AS Arith;

&lt;* UNUSED *&gt;
CONST
  Module = &quot;CharPolynomial.&quot;;
</PRE> it is trace(x^n) = lambda[1]^n+...+lambda[n]^n thus we get sequence of
   power sums if we compute the trace of successive powers of x 
<PRE>PROCEDURE <A NAME="CharPolynomial"><procedure>CharPolynomial</procedure></A> (x: M.T; ): Rt.T =
  BEGIN
    &lt;* ASSERT NUMBER(x^) = NUMBER(x[0]), &quot;Matrix must have square form!&quot; *&gt;
    VAR
      p   := NEW(REF Rt.PowerSumSeq, NUMBER(x^));
      pow := x;
    &lt;* FATAL Arith.Error *&gt;      (* Rt.FromPowerSumSeq can't fail, all
                                    divisions can be performed because the
                                    polynomial coefficients are in the same
                                    ring as the matrix coefficients. *)
    BEGIN
      p[0] := M.Trace(pow);
      FOR j := 1 TO LAST(p^) DO
        pow := M.Mul(pow, x);
        p[j] := M.Trace(pow);
      END;
      RETURN Rt.FromPowerSumSeq(p^);
    END;
  END CharPolynomial;

PROCEDURE <A NAME="CompanionMatrix"><procedure>CompanionMatrix</procedure></A> (x: Rt.T; ): M.T =
  BEGIN
    &lt;* ASSERT R.Equal(x[LAST(x^)], R.One),
                &quot;The leading coefficient of the polynomial must be one.&quot; *&gt;
    WITH y = M.NewZero(LAST(x^), LAST(x^)) DO
      FOR j := 0 TO LAST(y^) - 1 DO y[j, j + 1] := R.One; END;
      FOR j := 0 TO LAST(y[0]) DO y[LAST(y^), j] := R.Neg(x[j]); END;
      RETURN y;
    END;
  END CompanionMatrix;

BEGIN
END CharPolynomial.
</PRE>
</inModule>
<PRE>























</PRE>
</BODY>
</HTML>
