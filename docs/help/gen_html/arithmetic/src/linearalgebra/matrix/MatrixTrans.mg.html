<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: arithmetic/src/linearalgebra/matrix/MatrixTrans.mg</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>arithmetic/src/linearalgebra/matrix/MatrixTrans.mg</H2></A><HR>
<inModule>
<PRE>GENERIC MODULE <genericModule><A HREF="MatrixTrans.ig.html">MatrixTrans</A></genericModule>(R, RT, CT, CVR, M, Eigen);
</PRE><BLOCKQUOTE><EM> Arithmetic for Modula-3, see doc for details </EM></BLOCKQUOTE><PRE>

FROM <A HREF="../../Arithmetic.i3.html">Arithmetic</A> IMPORT Error;

&lt;* UNUSED *&gt;
CONST
  Module = &quot;MatrixTrans.&quot;;

PROCEDURE <A NAME="Norm1"><procedure>Norm1</procedure></A> (x: T; ): R.T =
  VAR max := R.Zero;
  BEGIN
    FOR j := FIRST(x[0]) TO LAST(x[0]) DO
      VAR sum := R.Zero;
      BEGIN
        FOR i := FIRST(x^) TO LAST(x^) DO
          sum := R.Add(sum, CT.Abs(x[i, j]));
        END;
        max := MAX(max, sum);
      END;
    END;
    RETURN max;
  END Norm1;

PROCEDURE <A NAME="Norm2"><procedure>Norm2</procedure></A> (x: T; ): R.T RAISES {Error} =
  BEGIN
    RETURN RT.SqRt(Norm2Sqr(x));
  END Norm2;

PROCEDURE <A NAME="Norm2Sqr"><procedure>Norm2Sqr</procedure></A> (x: T; ): R.T RAISES {Error} =
  VAR xax: T;
  BEGIN
    IF NUMBER(x^) &lt; NUMBER(x[0]) THEN
      xax := M.MulMMA(x);
    ELSE
      xax := M.MulMAM(x);
    END;
    RETURN Eigen.SquareMethod(xax).value;
  END Norm2Sqr;

PROCEDURE <A NAME="NormFrob"><procedure>NormFrob</procedure></A> (x: T; ): R.T =
  BEGIN
    RETURN RT.SqRt(NormFrobSqr(x));
  END NormFrob;

PROCEDURE <A NAME="NormFrobSqr"><procedure>NormFrobSqr</procedure></A> (x: T; ): R.T =
  VAR sum := R.Zero;
  BEGIN
    FOR i := FIRST(x^) TO LAST(x^) DO
      sum := R.Add(sum, CVR.Inner(x[i], x[i]));
    END;
    RETURN sum;
  END NormFrobSqr;

PROCEDURE <A NAME="NormInf"><procedure>NormInf</procedure></A> (x: T; ): R.T =
  VAR max := R.Zero;
  BEGIN
    FOR i := FIRST(x^) TO LAST(x^) DO
      VAR sum := R.Zero;
      BEGIN
        FOR j := FIRST(x[0]) TO LAST(x[0]) DO
          sum := R.Add(sum, CT.Abs(x[i, j]));
        END;
        max := MAX(max, sum);
      END;
    END;
    RETURN max;
  END NormInf;

BEGIN
END MatrixTrans.
</PRE>
</inModule>
<PRE>























</PRE>
</BODY>
</HTML>
