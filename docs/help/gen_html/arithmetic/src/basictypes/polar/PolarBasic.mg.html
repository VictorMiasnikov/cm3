<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: arithmetic/src/basictypes/polar/PolarBasic.mg</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>arithmetic/src/basictypes/polar/PolarBasic.mg</H2></A><HR>
<inModule>
<PRE>GENERIC MODULE <genericModule><A HREF="PolarBasic.ig.html">PolarBasic</A></genericModule>(R, RT, C, CT);
</PRE><BLOCKQUOTE><EM> Arithmetic for Modula-3, see doc for details
<P>
   Abstract: Complex numbers in polar coordinates </EM></BLOCKQUOTE><PRE>

FROM <A HREF="../../Arithmetic.i3.html">Arithmetic</A> IMPORT Error;

&lt;* UNUSED *&gt;
CONST
  Module = &quot;PolarBasic.&quot;;

PROCEDURE <A NAME="IsZero"><procedure>IsZero</procedure></A> (READONLY x: T; ): BOOLEAN =
  BEGIN
    RETURN R.IsZero(x.radius);
  END IsZero;

PROCEDURE <A NAME="Equal"><procedure>Equal</procedure></A> (READONLY x, y: T; ): BOOLEAN =
  BEGIN
    RETURN R.Equal(x.radius, y.radius) AND R.Equal(x.angle, y.angle);
  END Equal;

PROCEDURE <A NAME="FromComplex"><procedure>FromComplex</procedure></A> (READONLY c: C.T; ): T =
  VAR x: T;
  BEGIN
    x.radius := CT.Abs(c);
    x.angle := RT.ArcTan2(c.im, c.re);
    RETURN x;
  END FromComplex;

PROCEDURE <A NAME="ToComplex"><procedure>ToComplex</procedure></A> (READONLY x: T; ): C.T =
  BEGIN
    RETURN
      C.Scale(C.T{re := RT.Cos(x.angle), im := RT.Sin(x.angle)}, x.radius);
  END ToComplex;

PROCEDURE <A NAME="NormalizeAngle"><procedure>NormalizeAngle</procedure></A> (READONLY x: T; ): T =
  VAR angle := x.angle;
  BEGIN
    (*---normalize to -pi..+pi---*)
    (* if it was normalized before, the loops should run at most one
       cycle *)
    WHILE R.Compare(angle, RT.Pi) &gt; 0 DO
      angle := R.Sub(angle, RT.TwoPi);
    END;
    WHILE R.Compare(angle, R.Neg(RT.Pi)) &lt; 0 DO
      angle := R.Add(angle, RT.TwoPi);
    END;
    RETURN T{x.radius, angle};
  END NormalizeAngle;

PROCEDURE <A NAME="Mul"><procedure>Mul</procedure></A> (READONLY x, y: T; ): T =
  VAR z: T;
  BEGIN
    z.radius := R.Mul(x.radius, y.radius);
    z.angle := R.Add(x.angle, y.angle);
    RETURN NormalizeAngle(z);
  END Mul;

PROCEDURE <A NAME="Div"><procedure>Div</procedure></A> (READONLY x, y: T; ): T RAISES {Error} =
  VAR z: T;
  BEGIN
    z.radius := R.Div(x.radius, y.radius);
    z.angle := R.Sub(x.angle, y.angle);
    RETURN NormalizeAngle(z);
  END Div;

BEGIN
END PolarBasic.
</PRE>
</inModule>
<PRE>























</PRE>
</BODY>
</HTML>
