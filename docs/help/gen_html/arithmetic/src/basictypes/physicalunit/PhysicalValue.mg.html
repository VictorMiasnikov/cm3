<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: arithmetic/src/basictypes/physicalunit/PhysicalValue.mg</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>arithmetic/src/basictypes/physicalunit/PhysicalValue.mg</H2></A><HR>
<inModule>
<PRE>GENERIC MODULE <genericModule><A HREF="PhysicalValue.ig.html">PhysicalValue</A></genericModule>(R);
</PRE><BLOCKQUOTE><EM> Arithmetic for Modula-3, see doc for details </EM></BLOCKQUOTE><PRE>

IMPORT <A HREF="PhysicalUnit.i3.html">PhysicalUnit</A> AS U;

IMPORT <A HREF="../../Arithmetic.i3.html">Arithmetic</A> AS Arith;

&lt;* UNUSED *&gt;
CONST
  Module = &quot;PhysicalValue.&quot;;

PROCEDURE <A NAME="Add"><procedure>Add</procedure></A> (READONLY x, y: T; ): T RAISES {Arith.Error} =
  BEGIN
    (* if the value is zero, different units don't matter*)
    IF R.IsZero(y.val) THEN
      RETURN x;
    ELSIF R.IsZero(x.val) THEN
      RETURN y;
    ELSE
      IF NOT U.Equal(x.unit, y.unit) THEN
        RAISE Arith.Error(NEW(Arith.ErrorUnitMismatch).init());
      END;
      RETURN T{R.Add(x.val, y.val), x.unit};
    END;
  END Add;

PROCEDURE <A NAME="Sub"><procedure>Sub</procedure></A> (READONLY x, y: T; ): T RAISES {Arith.Error} =
  BEGIN
    (* if the value is zero, different units don't matter*)
    IF R.IsZero(y.val) THEN
      RETURN x;
    ELSIF R.IsZero(x.val) THEN
      RETURN Neg(y);
    ELSE
      IF NOT U.Equal(x.unit, y.unit) THEN
        RAISE Arith.Error(NEW(Arith.ErrorUnitMismatch).init());
      END;
      RETURN T{R.Sub(x.val, y.val), x.unit};
    END;
  END Sub;

PROCEDURE <A NAME="Neg"><procedure>Neg</procedure></A> (READONLY x: T; ): T =
  BEGIN
    RETURN T{R.Neg(x.val), x.unit};
  END Neg;

PROCEDURE <A NAME="Conj"><procedure>Conj</procedure></A> (READONLY x: T; ): T =
  BEGIN
    RETURN T{R.Conj(x.val), x.unit};
  END Conj;

PROCEDURE <A NAME="IsZero"><procedure>IsZero</procedure></A> (READONLY x: T; ): BOOLEAN =
  BEGIN
    RETURN R.IsZero(x.val);
  END IsZero;

PROCEDURE <A NAME="IsScalar"><procedure>IsScalar</procedure></A> (READONLY x: T; ): BOOLEAN =
  BEGIN
    RETURN U.IsZero(x.unit) OR R.IsZero(x.val);
  END IsScalar;

PROCEDURE <A NAME="Equal"><procedure>Equal</procedure></A> (READONLY x, y: T; ): BOOLEAN =
  BEGIN
    RETURN R.Equal(x.val, y.val) AND U.Equal(x.unit, y.unit);
  END Equal;

PROCEDURE <A NAME="Mul"><procedure>Mul</procedure></A> (READONLY x, y: T; ): T =
  BEGIN
    RETURN T{R.Mul(x.val, y.val), U.Add(x.unit, y.unit)};
  END Mul;

PROCEDURE <A NAME="Div"><procedure>Div</procedure></A> (READONLY x, y: T; ): T RAISES {Arith.Error} =
  BEGIN
    RETURN T{R.Div(x.val, y.val), U.Sub(x.unit, y.unit)};
  END Div;

PROCEDURE <A NAME="Rec"><procedure>Rec</procedure></A> (READONLY x: T; ): T RAISES {Arith.Error} =
  BEGIN
    RETURN T{R.Rec(x.val), U.Neg(x.unit)};
  END Rec;

PROCEDURE <A NAME="Mod"><procedure>Mod</procedure></A> (READONLY x, y: T; ): T RAISES {Arith.Error} =
  BEGIN
    RETURN T{R.Mod(x.val, y.val), x.unit};
  END Mod;

PROCEDURE <A NAME="DivMod"><procedure>DivMod</procedure></A> (READONLY x, y: T; ): QuotRem RAISES {Arith.Error} =
  VAR qr := R.DivMod(x.val, y.val);
  BEGIN
    RETURN QuotRem{T{qr.quot, U.Sub(x.unit, y.unit)}, T{qr.rem, x.unit}};
  END DivMod;

PROCEDURE <A NAME="Square"><procedure>Square</procedure></A> (READONLY x: T; ): T =
  BEGIN
    (* RETURN T{R.Square(x.val),U.Scale(x.unit,2)}; *)
    RETURN T{R.Mul(x.val, x.val), U.Scale(x.unit, 2)};
  END Square;

PROCEDURE <A NAME="Scale"><procedure>Scale</procedure></A> (READONLY x: T; y: R.T; ): T =
  BEGIN
    RETURN T{R.Mul(x.val, y), x.unit};
  END Scale;

BEGIN
  Zero := T{val := R.Zero, unit := U.New()};
  One := T{val := R.One, unit := Zero.unit};
END PhysicalValue.
</PRE>
</inModule>
<PRE>























</PRE>
</BODY>
</HTML>
