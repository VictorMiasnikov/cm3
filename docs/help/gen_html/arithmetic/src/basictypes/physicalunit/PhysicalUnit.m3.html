<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: arithmetic/src/basictypes/physicalunit/PhysicalUnit.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>arithmetic/src/basictypes/physicalunit/PhysicalUnit.m3</H2></A><HR>
<inModule>
<PRE>MODULE <module><implements><A HREF="PhysicalUnit.i3.html">PhysicalUnit</A></implements></module>;
</PRE><BLOCKQUOTE><EM> Arithmetic for Modula-3, see doc for details </EM></BLOCKQUOTE><PRE>

IMPORT                           (*IntIntTbl AS Tbl,*)
  <A HREF="../../../derived/Integer32Basic.i3.html">Integer32Basic</A> AS I,
  <A HREF="../../../derived/RealBasic.i3.html">RealBasic</A>      AS R,
  <A HREF="../../../derived/RealTrans.i3.html">RealTrans</A>      AS RT;

IMPORT <A HREF="../../Arithmetic.i3.html">Arithmetic</A> AS Arith;

&lt;* UNUSED *&gt;
CONST
  Module = &quot;PhysicalUnit.&quot;;

PROCEDURE <A NAME="New"><procedure>New</procedure></A> (): T =
  BEGIN
    RETURN NEW(T).init(sizeHint := 10);
  END New;

PROCEDURE <A NAME="FromArray"><procedure>FromArray</procedure></A> (READONLY x: ARRAY OF ExpType; ): T =
  VAR
    y                 := New();
    replaced: BOOLEAN;
  BEGIN
    FOR j := 0 TO LAST(x) DO
      IF x[j] # 0 THEN
        replaced := y.put(j, x[j]);
        &lt;* ASSERT NOT replaced *&gt;
        (* resist writing &lt;*ASSERT NOT y.put(j,x[j])*&gt; because ASSERT
           statements may be removed for final executables *)
      END;
    END;
    RETURN y;
  END FromArray;

PROCEDURE <A NAME="Copy"><procedure>Copy</procedure></A> (x: T; ): T =
  VAR
    y                  := New();
    it                 := x.iterate();
    unit, exp: INTEGER;
    replaced : BOOLEAN;
  BEGIN
    WHILE it.next(unit, exp) DO
      replaced := y.put(unit, exp);
      &lt;* ASSERT NOT replaced *&gt;
    END;
    RETURN y;
  END Copy;

PROCEDURE <A NAME="Equal"><procedure>Equal</procedure></A> (x, y: T; ): BOOLEAN =
  VAR
    it                        := x.iterate();
    unit, xexp, yexp: ExpType;
  BEGIN
    WHILE it.next(unit, xexp) DO
      IF NOT (y.get(unit, yexp) AND xexp = yexp) THEN RETURN FALSE; END;
    END;
    it := y.iterate();
    WHILE it.next(unit, yexp) DO
      IF NOT x.get(unit, xexp) THEN RETURN FALSE; END;
    END;
    RETURN TRUE;
  END Equal;

PROCEDURE <A NAME="IsZero"><procedure>IsZero</procedure></A> (x: T; ): BOOLEAN =
  BEGIN
    RETURN x.size() = 0;
  END IsZero;

PROCEDURE <A NAME="Put"><procedure>Put</procedure></A> (x: T; unit: INTEGER; exp: ExpType; ) =
  BEGIN
    IF exp # 0 THEN
      VAR replaced := x.put(unit, exp);
      BEGIN
        &lt;* ASSERT NOT replaced *&gt;
      END;
    END;
  END Put;

PROCEDURE <A NAME="Add"><procedure>Add</procedure></A> (x, y: T; ): T =
  VAR
    z                         := New();
    it                        := x.iterate();
    unit, xexp, yexp: ExpType;
  BEGIN
    WHILE it.next(unit, xexp) DO
      yexp := 0;
      EVAL y.get(unit, yexp);
      Put(z, unit, xexp + yexp);
    END;
    it := y.iterate();
    WHILE it.next(unit, yexp) DO
      IF NOT x.get(unit, xexp) THEN Put(z, unit, yexp); END;
    END;
    RETURN z;
  END Add;

PROCEDURE <A NAME="Sub"><procedure>Sub</procedure></A> (x, y: T; ): T =
  VAR
    z                         := New();
    it                        := x.iterate();
    unit, xexp, yexp: ExpType;
  BEGIN
    WHILE it.next(unit, xexp) DO
      yexp := 0;
      EVAL y.get(unit, yexp);
      Put(z, unit, xexp - yexp);
    END;
    it := y.iterate();
    WHILE it.next(unit, yexp) DO
      IF NOT x.get(unit, xexp) THEN Put(z, unit, -yexp); END;
    END;
    RETURN z;
  END Sub;

PROCEDURE <A NAME="Neg"><procedure>Neg</procedure></A> (x: T; ): T =
  VAR
    y                  := New();
    it                 := x.iterate();
    unit, exp: ExpType;
    replaced : BOOLEAN;
  BEGIN
    WHILE it.next(unit, exp) DO
      replaced := y.put(unit, -exp);
      &lt;* ASSERT NOT replaced *&gt;
    END;
    RETURN y;
  END Neg;

PROCEDURE <A NAME="Scale"><procedure>Scale</procedure></A> (x: T; y: ExpType; ): T =
  VAR
    z                  := New();
    it                 := x.iterate();
    unit, exp: ExpType;
    replaced : BOOLEAN;
  BEGIN
    IF y # 0 THEN
      WHILE it.next(unit, exp) DO
        replaced := z.put(unit, exp * y);
        &lt;* ASSERT NOT replaced *&gt;
      END;
    END;
    RETURN z;
  END Scale;

PROCEDURE <A NAME="ScaleDiv"><procedure>ScaleDiv</procedure></A> (x: T; y: ExpType; ): T RAISES {Arith.Error} =
  VAR
    z                  := New();
    it                 := x.iterate();
    unit, exp: ExpType;
    replaced : BOOLEAN;
  BEGIN
    WHILE it.next(unit, exp) DO
      replaced := z.put(unit, I.Div(exp, y));
      &lt;* ASSERT NOT replaced *&gt;
    END;
    RETURN z;
  END ScaleDiv;

PROCEDURE <A NAME="ScaleReal"><procedure>ScaleReal</procedure></A> (x: T; y: R.T; ): T RAISES {Arith.Error} =
  VAR
    z                         := New();
    it                        := x.iterate();
    unit, xexp, zexp: ExpType;
    zexpr           : R.T;
  BEGIN
    WHILE it.next(unit, xexp) DO
      zexpr := FLOAT(xexp, R.T) * y;
      zexp := ROUND(zexpr);
      IF ABS(FLOAT(zexp, R.T) - zexpr) &gt; RT.Eps THEN
        RAISE Arith.Error(NEW(Arith.ErrorIndivisible).init());
      END;
      Put(z, unit, zexp);
    END;
    RETURN z;
  END ScaleReal;

PROCEDURE <A NAME="Norm1"><procedure>Norm1</procedure></A> (x: T; ): ExpType =
  VAR
    it                 := x.iterate();
    unit, exp: ExpType;
    sum      : ExpType := 0;
  BEGIN
    WHILE it.next(unit, exp) DO sum := sum + ABS(exp); END;
    RETURN sum;
  END Norm1;

PROCEDURE <A NAME="NormInf"><procedure>NormInf</procedure></A> (x: T; ): ExpType =
  VAR
    it                 := x.iterate();
    unit, exp: ExpType;
    max      : ExpType := 0;
  BEGIN
    WHILE it.next(unit, exp) DO max := MAX(max, ABS(exp)); END;
    RETURN max;
  END NormInf;

BEGIN
END PhysicalUnit.
</PRE>
</inModule>
<PRE>























</PRE>
</BODY>
</HTML>
