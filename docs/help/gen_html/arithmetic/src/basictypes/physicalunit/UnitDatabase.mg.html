<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: arithmetic/src/basictypes/physicalunit/UnitDatabase.mg</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>arithmetic/src/basictypes/physicalunit/UnitDatabase.mg</H2></A><HR>
<inModule>
<PRE>GENERIC MODULE <genericModule><A HREF="UnitDatabase.ig.html">UnitDatabase</A></genericModule>(UU, UUList, CU, CUList);

IMPORT <A HREF="PhysicalUnit.i3.html">PhysicalUnit</A> AS U;
</PRE><P>
IMPORT IO, Fmt,
       PhysicalUnitFmtLex AS UF;


<P> should be part of the List module 
<PRE>PROCEDURE <A NAME="RemoveFirst"><procedure>RemoveFirst</procedure></A> (VAR l: CUList.T; ): CUList.T =
  VAR fst: CUList.T;
  BEGIN
    fst := l;
    l := l.tail;
    fst.tail := NIL;
    RETURN fst;
  END RemoveFirst;

PROCEDURE <A NAME="AddUnit"><procedure>AddUnit</procedure></A> (VAR      db    : T;
                   READONLY unit  : ARRAY OF U.ExpType;
                            flags : UU.FlagSet;
                   READONLY scales: ARRAY OF ScaledUnitInit; ) =
  VAR
    newScales: REF ARRAY OF UU.ScaledUnit;
    defScale : CARDINAL;
  BEGIN
    VAR cnt: CARDINAL := 0;
    BEGIN
      FOR j := 0 TO LAST(scales) DO
        IF ScaledUnitFlags.IsUnit IN scales[j].flags THEN INC(cnt); END;
      END;
      (* add no entry if none of the entries is usable as unit *)
      IF cnt = 0 THEN RETURN; END;
      newScales := NEW(REF ARRAY OF UU.ScaledUnit, cnt);
    END;
    VAR k: CARDINAL := 0;
    BEGIN
      FOR j := 0 TO LAST(scales) DO
        IF ScaledUnitFlags.IsUnit IN scales[j].flags THEN
          newScales[k].mag := scales[j].mag;
          newScales[k].symbol := scales[j].symbol;
          IF ScaledUnitFlags.Default IN scales[j].flags THEN
            defScale := k;
          END;
          INC(k);
        END;
      END;
    END;

    (* add new item to the list *)
    db.first :=
      UUList.Cons(UU.T{unit := U.FromArray(unit), flags := flags, scales :=
                       newScales, defScale := defScale}, db.first);
  END AddUnit;
</PRE> approximate a scaling for which the source unit matches the target unit
   best return the approximation error 
<PRE>PROCEDURE <A NAME="Approx"><procedure>Approx</procedure></A> (target, source: U.T; VAR exp: U.ExpType; ): U.ExpType =
  VAR
    zeroDiff                  := U.Norm1(target);
    diff, lastDiff: U.ExpType;
  BEGIN
    &lt;* ASSERT NOT U.IsZero(source) *&gt;
    (* the mapping x-&gt;norm(target-x*source) is a norm along an affine
       function and thus this mapping is convex and has at most one
       interval where it is minimal, of all those x where
       norm(target-x*source) is minimal, choose the one with least absolute
       value *)
    diff := U.Norm1(U.Sub(target, source));
    IF zeroDiff &gt; diff THEN
      exp := 1;
      REPEAT
        lastDiff := diff;
        INC(exp);
        diff := U.Norm1(U.Sub(target, U.Scale(source, exp)));
      UNTIL lastDiff &lt;= diff;
      DEC(exp);
      RETURN lastDiff;
    ELSIF zeroDiff &lt; diff THEN
      diff := zeroDiff;
      exp := 0;
      REPEAT
        lastDiff := diff;
        DEC(exp);
        diff := U.Norm1(U.Sub(target, U.Scale(source, exp)));
      UNTIL lastDiff &lt;= diff;
      INC(exp);
      RETURN lastDiff;
    ELSE
      exp := 0;
      RETURN diff;
    END;
  END Approx;
</PRE> find the usual unit incl.  exponent which matches best to the given
   unit 
<PRE>PROCEDURE <A NAME="FindBestUU"><procedure>FindBestUU</procedure></A> (READONLY db: T; remain: U.T; isFirst: BOOLEAN; ):
  CU.T =
  VAR
    bestUU   : UUList.T  := NIL;
    uu                   := db.first;
    exp, diff: U.ExpType;
    minDiff  : U.ExpType := LAST(U.ExpType);
    minExp   : U.ExpType := LAST(U.ExpType);
  BEGIN
    (*
    IO.Put(&quot;find unit closest to &quot; &amp; UF.Fmt(remain) &amp; &quot;\n&quot;);
    *)
    WHILE uu # NIL DO
      (*
      IO.Put(UF.Fmt(uu.head.unit)&amp;&quot;  &quot;);
      *)
      diff := Approx(remain, uu.head.unit, exp);
      (*
      IO.Put(&quot;   exp &quot; &amp; Fmt.Int(exp));
      IO.Put(&quot;   diff &quot; &amp; Fmt.Int(diff));
      IO.Put(&quot;   indep &quot; &amp; Fmt.Bool(UU.Flags.independent IN uu.head.flags));
      IO.Put(&quot;   isFirst &quot; &amp; Fmt.Bool(isFirst) &amp; &quot;\n&quot;);
      *)
      IF (diff &lt; minDiff) AND (NOT UU.Flags.Independent IN uu.head.flags
                                 OR (isFirst AND diff = 0)) THEN
        minDiff := diff;
        bestUU := uu;
        minExp := exp;
      END;
      uu := uu.tail;
    END;

    RETURN CU.T{uu := bestUU, exp := minExp};
  END FindBestUU;

PROCEDURE <A NAME="DecomposeUnit"><procedure>DecomposeUnit</procedure></A> (READONLY db: T; unit: U.T; ): CUList.T =
  VAR
    ucList    : CUList.T  := NIL;
    remainNorm            := U.Norm1(unit);
    newNorm   : U.ExpType;
    remain                := U.Copy(unit);

  BEGIN
    WHILE NOT U.IsZero(remain) DO
      (* prepend the new unit *)
      ucList := CUList.Cons(FindBestUU(db, remain, ucList = NIL), ucList);

      (* extract the found usual unit from the given one *)
      (*
      IO.Put(&quot;best unit &quot; &amp; UF.Fmt(ucList.head.uu.head.unit) &amp; &quot;\n&quot;);
      IO.Put(&quot;scaled by &quot; &amp; Fmt.Int(ucList.head.exp) &amp; &quot;: &quot; &amp; UF.Fmt(U.Scale(ucList.head.uu.head.unit,ucList.head.exp)) &amp; &quot;\n&quot;);
      IO.Put(&quot;remain before Sub &quot; &amp; UF.Fmt(remain) &amp; &quot;\n&quot;);
      *)
      remain :=
        U.Sub(remain, U.Scale(ucList.head.uu.head.unit, ucList.head.exp));
      (*
      IO.Put(&quot;remain after Sub &quot; &amp; UF.Fmt(remain) &amp; &quot;\n&quot;);
      *)
      newNorm := U.Norm1(remain);
      (* the database must contain all unit vectors so that every composed
         unit can be decomposed into unit vectors from the database *)
      &lt;* ASSERT newNorm &lt; remainNorm *&gt;
      remainNorm := newNorm;
    END;

    (* reverse order and sort usual units with negative exponent to the
       end *)
    VAR
      ucPos, ucNeg: CUList.T := NIL;
      ucFirst     : CUList.T := NIL;
    BEGIN
      WHILE ucList # NIL DO
        ucFirst := RemoveFirst(ucList);
        IF ucFirst.head.exp &lt; 0 THEN
          ucNeg := CUList.AppendD(ucFirst, ucNeg);
        ELSE
          ucPos := CUList.AppendD(ucFirst, ucPos);
        END;
      END;
      ucList := CUList.AppendD(ucPos, ucNeg);
    END;
    &lt;* ASSERT U.Equal(ComposeUnit(ucList), unit) *&gt;
    RETURN ucList;
  END DecomposeUnit;

PROCEDURE <A NAME="ComposeUnit"><procedure>ComposeUnit</procedure></A> (cu: CUList.T; ): U.T =
  VAR unit := U.New();
  BEGIN
    WHILE cu # NIL DO
      unit := U.Add(unit, U.Scale(cu.head.uu.head.unit, cu.head.exp));
      cu := cu.tail;
    END;
    RETURN unit;
  END ComposeUnit;

BEGIN
END UnitDatabase.
</PRE>
</inModule>
<PRE>























</PRE>
</BODY>
</HTML>
