<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: arithmetic/src/basictypes/fraction/FractionBasic.mg</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>arithmetic/src/basictypes/fraction/FractionBasic.mg</H2></A><HR>
<inModule>
<PRE>GENERIC MODULE <genericModule><A HREF="FractionBasic.ig.html">FractionBasic</A></genericModule>(R, GCD);
</PRE><BLOCKQUOTE><EM> Arithmetic for Modula-3, see doc for details
<P>
   Abstract: Fraction numbers and basic operations </EM></BLOCKQUOTE><PRE>

IMPORT <A HREF="../../Arithmetic.i3.html">Arithmetic</A> AS Arith;

&lt;* UNUSED *&gt;
CONST
  Module = &quot;FractionBasic.&quot;;

PROCEDURE <A NAME="Cancel"><procedure>Cancel</procedure></A> (READONLY x: T; ): T RAISES {Arith.Error} =
  VAR gcd := GCD.GCD(x.n, x.d);
  BEGIN
    RETURN T{R.Div(x.n, gcd), R.Div(x.d, gcd)};
  END Cancel;

PROCEDURE <A NAME="Add"><procedure>Add</procedure></A> (READONLY x, y: T; ): T =
  VAR
    gcd := GCD.GCD(x.d, y.d);
    xdc := R.Div(x.d, gcd);
    ydc := R.Div(y.d, gcd);
    z := T{n := R.Add(R.Mul(x.n, ydc), R.Mul(y.n, xdc)), d :=
           R.Mul(xdc, y.d) (* least common multiple*)};
  &lt;* FATAL Arith.Error *&gt;        (* Division will always succeed*)
  BEGIN
    RETURN Cancel(z);            (* final cancellation is necessary as the
                                    example 1/2+1/2 shows, if the
                                    denominators are different it may be
                                    unnecessary*)
  END Add;

PROCEDURE <A NAME="Sub"><procedure>Sub</procedure></A> (READONLY x, y: T; ): T =
  VAR
    gcd := GCD.GCD(x.d, y.d);
    xdc := R.Div(x.d, gcd);
    ydc := R.Div(y.d, gcd);
    z := T{n := R.Sub(R.Mul(x.n, ydc), R.Mul(y.n, xdc)), d :=
           R.Mul(xdc, y.d) (* least common multiple*)};
  &lt;* FATAL Arith.Error *&gt;        (* Division will always succeed*)
  BEGIN
    RETURN Cancel(z);
  END Sub;

PROCEDURE <A NAME="Neg"><procedure>Neg</procedure></A> (READONLY x: T; ): T =
  BEGIN
    RETURN T{R.Neg(x.n), x.d};
  END Neg;

PROCEDURE <A NAME="Conj"><procedure>Conj</procedure></A> (READONLY x: T; ): T =
  BEGIN
    RETURN x;
  END Conj;

PROCEDURE <A NAME="IsZero"><procedure>IsZero</procedure></A> (READONLY x: T; ): BOOLEAN =
  BEGIN
    RETURN R.IsZero(x.n);
  END IsZero;

PROCEDURE <A NAME="Equal"><procedure>Equal</procedure></A> (READONLY x, y: T; ): BOOLEAN =
  BEGIN
    (* comparing component-wise may fail if the field has more than one
       unit (say e.g.  -1), in this case the fraction representation is not
       unique!*)
    RETURN R.Equal(R.Mul(x.n, y.d), R.Mul(y.n, x.d));
  END Equal;

PROCEDURE <A NAME="Compare"><procedure>Compare</procedure></A> (READONLY x, y: T; ): [-1 .. 1] =
  BEGIN
    RETURN R.Compare(R.Mul(x.n, y.d), R.Mul(y.n, x.d));
  END Compare;

PROCEDURE <A NAME="Mul"><procedure>Mul</procedure></A> (READONLY x, y: T; ): T =
  VAR
    gcd    := GCD.GCD(x.n, y.d);
    z  : T;
  &lt;* FATAL Arith.Error *&gt;        (* Division will always succeed*)
  BEGIN
    z.n := R.Div(x.n, gcd);
    z.d := R.Div(y.d, gcd);

    gcd := GCD.GCD(y.n, x.d);
    z.n := R.Mul(z.n, R.Div(y.n, gcd));
    z.d := R.Mul(z.d, R.Div(x.d, gcd));
    RETURN z;
  END Mul;

PROCEDURE <A NAME="Div"><procedure>Div</procedure></A> (READONLY x, y: T; ): T RAISES {Arith.Error} =
  VAR
    gcd    := GCD.GCD(x.n, y.n);
    z  : T;
  BEGIN
    z.n := R.Div(x.n, gcd);
    z.d := R.Div(y.n, gcd);

    gcd := GCD.GCD(y.d, x.d);
    z.n := R.Mul(z.n, R.Div(y.d, gcd));
    z.d := R.Mul(z.d, R.Div(x.d, gcd));

    RETURN z;
  END Div;

PROCEDURE <A NAME="Rec"><procedure>Rec</procedure></A> (READONLY x: T; ): T RAISES {Arith.Error} =
  BEGIN
    IF R.IsZero(x.n) THEN
      RAISE Arith.Error(NEW(Arith.ErrorDivisionByZero).init());
    END;
    RETURN T{x.d, x.n};
  END Rec;

PROCEDURE <A NAME="Mod"><procedure>Mod</procedure></A> (&lt;* UNUSED *&gt; READONLY x: T; READONLY y: T; ): T
  RAISES {Arith.Error} (* return x mod y*) =
  BEGIN
    IF R.IsZero(y.n) THEN
      RAISE Arith.Error(NEW(Arith.ErrorDivisionByZero).init());
    END;
    RETURN Zero;
  END Mod;

PROCEDURE <A NAME="DivMod"><procedure>DivMod</procedure></A> (x, y: T; ): QuotRem RAISES {Arith.Error} =
  BEGIN
    RETURN QuotRem{Div(x, y), Zero};
  END DivMod;

PROCEDURE <A NAME="IntMod"><procedure>IntMod</procedure></A> (READONLY x, y: T; ): T RAISES {Arith.Error} =
  VAR
    gcd := GCD.GCD(x.d, y.d);
    xdc := R.Div(x.d, gcd);
    ydc := R.Div(y.d, gcd);
    z := T{n := R.Mod(R.Mul(x.n, ydc), R.Mul(y.n, xdc)), d :=
           R.Mul(xdc, y.d) (* least common multiple*)};
  BEGIN
    RETURN Cancel(z);
  END IntMod;

PROCEDURE <A NAME="Square"><procedure>Square</procedure></A> (READONLY x: T; ): T =
  BEGIN
    RETURN T{R.Mul(x.n, x.n), R.Mul(x.d, x.d)};
    (*RETURN T{R.Square(x.n),R.Square(x.d)};*)
  END Square;

PROCEDURE <A NAME="Scale"><procedure>Scale</procedure></A> (READONLY x: T; y: R.T; ): T =
  &lt;* FATAL Arith.Error *&gt;
  BEGIN
    RETURN Cancel(T{R.Mul(x.n, y), x.d});
    (*RETURN T{R.Scale(x.n,y),x.d};*)
  END Scale;

BEGIN
  Zero := T{n := R.Zero, d := R.One};
  One := T{n := R.One, d := R.One};
  (* MinusOne := T{n:=R.MinusOne, d:=R.One}; *)
END FractionBasic.
</PRE>
</inModule>
<PRE>























</PRE>
</BODY>
</HTML>
