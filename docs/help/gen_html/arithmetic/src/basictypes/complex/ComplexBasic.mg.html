<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: arithmetic/src/basictypes/complex/ComplexBasic.mg</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>arithmetic/src/basictypes/complex/ComplexBasic.mg</H2></A><HR>
<inModule>
<PRE>GENERIC MODULE <genericModule><A HREF="ComplexBasic.ig.html">ComplexBasic</A></genericModule>(R);
</PRE><BLOCKQUOTE><EM> Arithmetic for Modula-3, see doc for details
<P>
   Abstract: Complex numbers and basic operations </EM></BLOCKQUOTE><PRE>

FROM <A HREF="../../Arithmetic.i3.html">Arithmetic</A> IMPORT Error;

&lt;* UNUSED *&gt;
CONST
  Module = &quot;ComplexBasic.&quot;;

PROCEDURE <A NAME="Add"><procedure>Add</procedure></A> (READONLY x, y: T; ): T =
  VAR z: T;
  BEGIN
    z.re := R.Add(x.re, y.re);
    z.im := R.Add(x.im, y.im);
    RETURN z;
  END Add;

PROCEDURE <A NAME="Sub"><procedure>Sub</procedure></A> (READONLY x, y: T; ): T =
  VAR z: T;
  BEGIN
    z.re := R.Sub(x.re, y.re);
    z.im := R.Sub(x.im, y.im);
    RETURN z;
  END Sub;

PROCEDURE <A NAME="Neg"><procedure>Neg</procedure></A> (READONLY x: T; ): T =
  VAR z: T;
  BEGIN
    z.re := R.Neg(x.re);
    z.im := R.Neg(x.im);
    RETURN z;
  END Neg;

PROCEDURE <A NAME="Conj"><procedure>Conj</procedure></A> (READONLY x: T; ): T =
  VAR z: T;
  BEGIN
    z.re := x.re;
    z.im := R.Neg(x.im);
    RETURN z;
  END Conj;

PROCEDURE <A NAME="IsZero"><procedure>IsZero</procedure></A> (READONLY x: T; ): BOOLEAN =
  BEGIN
    RETURN R.IsZero(x.re) AND R.IsZero(x.im);
  END IsZero;

PROCEDURE <A NAME="Equal"><procedure>Equal</procedure></A> (READONLY x, y: T; ): BOOLEAN =
  BEGIN
    RETURN R.Equal(x.re, y.re) AND R.Equal(x.im, y.im);
  END Equal;

PROCEDURE <A NAME="Mul"><procedure>Mul</procedure></A> (READONLY x, y: T; ): T =
  VAR z: T;
  BEGIN
    z.re := R.Sub(R.Mul(x.re, y.re), R.Mul(x.im, y.im));
    z.im := R.Add(R.Mul(x.im, y.re), R.Mul(x.re, y.im));
    RETURN z;
  END Mul;
</PRE> multiply x by the conjugate of y 
<PRE>PROCEDURE <A NAME="MulConj"><procedure>MulConj</procedure></A> (READONLY x, y: T; ): T =
  VAR z: T;
  BEGIN
    z.re := R.Add(R.Mul(x.re, y.re), R.Mul(x.im, y.im));
    z.im := R.Sub(R.Mul(x.im, y.re), R.Mul(x.re, y.im));
    RETURN z;
  END MulConj;
</PRE><P>
Use Karatsuba's trick
tend to produce truncation errors
<P>
xr yr - xi yi
xr yi + xi yr
<P>
(xr - xi) (yr + yi) - xr yi + xi yr

<PRE>&lt;* UNUSED *&gt;
PROCEDURE <A NAME="FastMul"><procedure>FastMul</procedure></A> (READONLY x, y: T; ): T =
  VAR
    xyri    := R.Mul(R.Sub(x.re, x.im), R.Add(y.re, y.im));
    xryi    := R.Mul(x.re, y.im);
    xiyr    := R.Mul(x.im, y.re);
    z   : T;
  BEGIN
    z.re := R.Add(R.Sub(xyri, xryi), xiyr);
    z.im := R.Add(xryi, xiyr);
    RETURN z;
  END FastMul;

PROCEDURE <A NAME="DivScale"><procedure>DivScale</procedure></A> (READONLY x, y: T; ): T RAISES {Error} =
  VAR denom := R.Add(R.Mul(y.re, y.re), R.Mul(y.im, y.im));
  BEGIN
    (* Err.divide_by_zero will be thrown by Div *)
    RETURN T{R.Div(x.re, denom), R.Div(x.im, denom)};
  END DivScale;

PROCEDURE <A NAME="Div"><procedure>Div</procedure></A> (READONLY x, y: T; ): T RAISES {Error} =
  BEGIN
    RETURN DivScale(Mul(x, Conj(y)), y);
  END Div;

PROCEDURE <A NAME="Rec"><procedure>Rec</procedure></A> (READONLY x: T; ): T RAISES {Error} =
  BEGIN
    RETURN DivScale(Conj(x), x);
  END Rec;
</PRE> I have not found a method which a) is generic (works without
   comparisons) b) matches the needs of the Euclidean algorithm for
   determining the greatest common divisor (that is, there must be a
   magnitude indicator which is reduced by a modulo operation).  It work
   for some Gaussian numbers though, but not for all, e.g.
   GCD(-3-3i,-1-4i) (the order is important, the opposite would work!).
   The last example lead to a cycle: -3-3i,-1-4i,1-4i,3-3i,4-i,4+i,3+3i,...
   In this case the Euclidean algorithm may be rewritten to process the
   pair (x*u,y) for several units u in parallel. 
<PRE>PROCEDURE <A NAME="Mod"><procedure>Mod</procedure></A> (READONLY x, y: T; ): T RAISES {Error} =
  VAR
    denom := R.Add(R.Mul(y.re, y.re), R.Mul(y.im, y.im));
    (* Err.divide_by_zero will be thrown by Mod *)
    xbig    := MulConj(x, y);
    r   : T;
  BEGIN
    r.re := R.Mod(xbig.re, denom);
    r.im := R.Mod(xbig.im, denom);
    r := Mul(r, y);              (* in fact, r is now AbsSqr(y) as big as
                                    before *)
    r.re := R.Div(r.re, denom);  (* is always divisible *)
    r.im := R.Div(r.im, denom);
    RETURN r;
  END Mod;

PROCEDURE <A NAME="DivMod"><procedure>DivMod</procedure></A> (READONLY x, y: T; ): QuotRem RAISES {Error} =
  VAR
    denom := R.Add(R.Mul(y.re, y.re), R.Mul(y.im, y.im));
    (* Err.divide_by_zero will be thrown by Div *)
    xbig := MulConj(x, y);
    re   := R.DivMod(xbig.re, denom);
    im   := R.DivMod(xbig.im, denom);
    r := Mul(T{re.rem, im.rem}, y); (* in fact, r is now AbsSqr(y) as big
                                       as before *)
  BEGIN
    r.re := R.Div(r.re, denom);  (* is always divisible *)
    r.im := R.Div(r.im, denom);
    &lt;* ASSERT Equal(r, Mod(x, y)) *&gt;
    RETURN QuotRem{T{re.quot, im.quot}, r};
  END DivMod;

PROCEDURE <A NAME="Square"><procedure>Square</procedure></A> (READONLY x: T; ): T =
  VAR
    xri: R.T;
    z  : T;
  BEGIN
    z.re := R.Sub(R.Mul(x.re, x.re), R.Mul(x.im, x.im));
    xri := R.Mul(x.re, x.im);
    z.im := R.Add(xri, xri);     (* double *)
    RETURN z;
  END Square;

PROCEDURE <A NAME="Scale"><procedure>Scale</procedure></A> (READONLY x: T; y: R.T; ): T =
  VAR z: T;
  BEGIN
    z.re := R.Mul(x.re, y);
    z.im := R.Mul(x.im, y);
    RETURN z;
  END Scale;

BEGIN
  Zero := T{re := R.Zero, im := R.Zero};
  One := T{re := R.One, im := R.Zero};
  I := T{re := R.Zero, im := R.One};
  MinusOne := T{re := R.MinusOne, im := R.Zero};
END ComplexBasic.
</PRE>
</inModule>
<PRE>























</PRE>
</BODY>
</HTML>
