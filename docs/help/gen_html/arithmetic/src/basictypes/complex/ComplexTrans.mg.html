<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: arithmetic/src/basictypes/complex/ComplexTrans.mg</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>arithmetic/src/basictypes/complex/ComplexTrans.mg</H2></A><HR>
<inModule>
<PRE>GENERIC MODULE <genericModule><A HREF="ComplexTrans.ig.html">ComplexTrans</A></genericModule>(R, RT, C);
</PRE><BLOCKQUOTE><EM> Arithmetic for Modula-3, see doc for details
<P>
   Abstract: Transcendental functions of complex numbers. </EM></BLOCKQUOTE><PRE>

IMPORT <A HREF="../../Arithmetic.i3.html">Arithmetic</A> AS Arith;
IMPORT <A HREF="../../../../m3core/src/float/IEEE-default/FloatMode.i3.html">FloatMode</A>;

&lt;* UNUSED *&gt;
CONST
  Module = &quot;ComplexTrans.&quot;;

PROCEDURE <A NAME="Arg"><procedure>Arg</procedure></A> (READONLY x: T; ): R.T =
  BEGIN
    RETURN RT.ArcTan2(x.im, x.re);
  END Arg;

PROCEDURE <A NAME="PowR"><procedure>PowR</procedure></A> (READONLY x: T; y: R.T; ): T =
  VAR
    arg := Arg(x);
    abs := Abs(x);
  BEGIN
    RETURN C.Scale(Exp(T{R.Zero, R.Mul(arg, y)}), RT.Pow(abs, y));
  END PowR;

PROCEDURE <A NAME="Pow"><procedure>Pow</procedure></A> (x, y: T; ): T =
  VAR z := Ln(x);
  BEGIN
    z := C.Mul(y, z);
    z := Exp(z);
    RETURN z;
  END Pow;
</PRE> This function would definitely benefit of an implementation of SinCos 
<PRE>PROCEDURE <A NAME="Exp"><procedure>Exp</procedure></A> (READONLY x: T; ): T =
  BEGIN
    RETURN C.Scale(T{RT.Cos(x.im), RT.Sin(x.im)}, RT.Exp(x.re));
  END Exp;
</PRE> This function would definitely benefit of an implementation of SinCos 
<PRE>PROCEDURE <A NAME="ExpI"><procedure>ExpI</procedure></A> (x: R.T; ): T =
  BEGIN
    RETURN T{RT.Cos(x), RT.Sin(x)};
  END ExpI;

PROCEDURE <A NAME="Ln"><procedure>Ln</procedure></A> (READONLY x: T; ): T =
  VAR z: T;
  BEGIN
    (*z.re:= R.Div(RT.Ln(AbsSqr(x)),R.Two);*)
    z.re := RT.Ln(AbsSqr(x)) / R.Two;
    z.im := Arg(x);
    RETURN z;
  END Ln;

PROCEDURE <A NAME="Cos"><procedure>Cos</procedure></A> (READONLY x: T; ): T RAISES {Arith.Error} =
  VAR z: T;
  BEGIN
    IF ABS(x.re) &gt; FLOAT(18.0D0, R.T) OR ABS(x.im) &gt; FLOAT(18.0D0, R.T) THEN
      RAISE Arith.Error(NEW(Arith.ErrorOutOfRange).init());
    END;
    z.re := +RT.Cos(x.re) * RT.CosH(x.im);
    z.im := -RT.Sin(x.re) * RT.SinH(x.im);
    RETURN z;
  END Cos;

PROCEDURE <A NAME="Sin"><procedure>Sin</procedure></A> (READONLY x: T; ): T RAISES {Arith.Error} =
  VAR z: T;
  BEGIN
    IF ABS(x.re) &gt; FLOAT(18.0D0, R.T) OR ABS(x.im) &gt; FLOAT(18.0D0, R.T) THEN
      RAISE Arith.Error(NEW(Arith.ErrorOutOfRange).init());
    END;
    z.re := +RT.Sin(x.re) * RT.CosH(x.im);
    z.im := +RT.Cos(x.re) * RT.SinH(x.im);
    RETURN z;
  END Sin;

PROCEDURE <A NAME="Tan"><procedure>Tan</procedure></A> (READONLY x: T; ): T RAISES {Arith.Error} =
  BEGIN
    RETURN C.Div(Sin(x), Cos(x));
  END Tan;

PROCEDURE <A NAME="CosH"><procedure>CosH</procedure></A> (READONLY x: T; ): T RAISES {Arith.Error} =
  BEGIN
    RETURN Cos(T{re := -x.im, im := +x.re});
  END CosH;

PROCEDURE <A NAME="SinH"><procedure>SinH</procedure></A> (READONLY x: T; ): T RAISES {Arith.Error} =
  VAR
    (*z.re = -i*i*z.im = z.im*)
    (*z.im = -i*z.re* = -z.re*)
    z := Sin(T{-x.im, x.re});
  BEGIN
    RETURN T{z.im, -z.re};
  END SinH;

PROCEDURE <A NAME="TanH"><procedure>TanH</procedure></A> (READONLY x: T; ): T RAISES {Arith.Error} =
  BEGIN
    RETURN C.Div(SinH(x), CosH(x));
  END TanH;

PROCEDURE <A NAME="Norm1"><procedure>Norm1</procedure></A> (READONLY x: T; ): R.T =
  BEGIN
    RETURN ABS(x.re) + ABS(x.im);
  END Norm1;

PROCEDURE <A NAME="NormInf"><procedure>NormInf</procedure></A> (READONLY x: T; ): R.T =
  BEGIN
    RETURN MAX(ABS(x.re), ABS(x.im));
  END NormInf;
</PRE> Lemming's stuff 

<P><PRE>PROCEDURE <A NAME="ArcSin"><procedure>ArcSin</procedure></A> (READONLY x: T; ): T =
  VAR
    ix := T{R.Neg(x.im), x.re};
    y  := SqRt(C.Add(C.Square(ix), C.One));
  BEGIN
    (* arcsin x = -i ln (ix ± sqrt (1-x²)) *)
    (*ix := C.Mul(x,C.I);*)
    RETURN C.Neg(C.Mul(C.I, Ln(C.Add(ix, y))));
  END ArcSin;

PROCEDURE <A NAME="ArcCos"><procedure>ArcCos</procedure></A> (READONLY x: T; ): T =
  VAR y := SqRt(C.Sub(C.Square(x), C.One));
  BEGIN
    (* arccos x = -i ln (x ± sqrt (x²-1)) *)
    RETURN C.Neg(C.Mul(C.I, Ln(C.Add(x, y))));
  END ArcCos;

PROCEDURE <A NAME="ArcTan"><procedure>ArcTan</procedure></A> (READONLY x: T; ): T RAISES {Arith.Error} =
  VAR y := C.Div(C.Sub(C.I, x), C.Add(C.I, x));
  BEGIN
    (* arctan x := 1/2i ln ((i-x)/(i+x)) *)
    RETURN C.Mul(Ln(y), T{R.Zero, RT.Half});
  END ArcTan;

PROCEDURE <A NAME="Abs"><procedure>Abs</procedure></A> (READONLY x0: T; ): R.T =
  VAR
    x := C.Normalize(x0);
    y := RT.SqRt(AbsSqr(x.val));
  &lt;* FATAL FloatMode.Trap *&gt;
  BEGIN
    (* a workaround to prevent NaNs and Zeros *)
    RETURN R.Scalb(y, x.exp);
  END Abs;
</PRE><P>
PROCEDURE Abs (READONLY x:T;) : R.T =
BEGIN
  RETURN RT.SqRt(AbsSqr(x));
END Abs;


<P><PRE>PROCEDURE <A NAME="AbsSqr"><procedure>AbsSqr</procedure></A> (READONLY x: T; ): R.T =
  BEGIN
    (* RETURN C.Mul(x,C.Conj(x)); it's a real number, but the type is still
       T *)
    RETURN x.re * x.re + x.im * x.im;
  END AbsSqr;

PROCEDURE <A NAME="SqRt"><procedure>SqRt</procedure></A> (READONLY x: T; ): T =
  VAR
    r    := Abs(x);
    z: T;
  BEGIN
    z.re := R.Add(r, x.re);
    IF R.Compare(z.re, R.Zero) &lt; 0 THEN (* mathematically impossible, can
                                           be caused by rounding *)
      z.re := R.Zero;
    ELSE
      z.re := RT.SqRt(R.Mul(z.re, RT.Half));
    END;

    z.im := R.Sub(r, x.re);
    IF R.Compare(z.im, R.Zero) &lt; 0 THEN (* mathematically impossible, can
                                           be caused by rounding *)
      z.im := R.Zero;
    ELSE
      z.im := RT.SqRt(R.Mul(z.im, RT.Half));
      IF R.Compare(x.im, R.Zero) &lt; 0 THEN (* instead of using the Sgn
                                             function *)
        z.im := R.Neg(z.im);
      END;
    END;
    (* Root is on the same side as the radicand with respect to the real
       axis. *)
    RETURN z;
  END SqRt;

BEGIN
END ComplexTrans.
</PRE>
</inModule>
<PRE>























</PRE>
</BODY>
</HTML>
