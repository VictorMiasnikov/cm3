<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: arithmetic/src/basictypes/integer/IntegerBasic.mg</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>arithmetic/src/basictypes/integer/IntegerBasic.mg</H2></A><HR>
<inModule>
<PRE>GENERIC MODULE <genericModule><A HREF="IntegerBasic.ig.html">IntegerBasic</A></genericModule>();
</PRE><BLOCKQUOTE><EM> Arithmetic for Modula-3, see doc for details</EM></BLOCKQUOTE><PRE>

IMPORT <A HREF="../../../../m3core/src/word/Word.i3.html">Word</A> AS W, <A HREF="../../Arithmetic.i3.html">Arithmetic</A> AS Arith;

&lt;* UNUSED *&gt;
CONST
  Module = &quot;IntegerBasic.&quot;;

PROCEDURE <A NAME="Add"><procedure>Add</procedure></A> (x, y: T; ): T =
  BEGIN
    RETURN x + y
  END Add;

PROCEDURE <A NAME="Sub"><procedure>Sub</procedure></A> (x, y: T; ): T =
  BEGIN
    RETURN x - y
  END Sub;

PROCEDURE <A NAME="Neg"><procedure>Neg</procedure></A> (x: T; ): T =
  BEGIN
    RETURN -x
  END Neg;

PROCEDURE <A NAME="Conj"><procedure>Conj</procedure></A> (x: T; ): T =
  BEGIN
    RETURN x
  END Conj;

PROCEDURE <A NAME="IsZero"><procedure>IsZero</procedure></A> (x: T; ): BOOLEAN =
  BEGIN
    RETURN x = Zero;
  END IsZero;

PROCEDURE <A NAME="Mul"><procedure>Mul</procedure></A> (x, y: T; ): T =
  BEGIN
    RETURN x * y
  END Mul;

PROCEDURE <A NAME="CheckDivisor"><procedure>CheckDivisor</procedure></A> (x: T; ) RAISES {Arith.Error} =
  BEGIN
    IF x = 0 THEN
      RAISE Arith.Error(NEW(Arith.ErrorDivisionByZero).init())
    END;
  END CheckDivisor;

PROCEDURE <A NAME="Div"><procedure>Div</procedure></A> (x, y: T; ): T RAISES {Arith.Error} =
  BEGIN
    CheckDivisor(y);
    IF x MOD y # 0 THEN
      RAISE Arith.Error(NEW(Arith.ErrorIndivisible).init())
    END;
    RETURN x DIV y;
  END Div;

PROCEDURE <A NAME="Rec"><procedure>Rec</procedure></A> (x: T; ): T RAISES {Arith.Error} =
  BEGIN
    CASE x OF
    | 0 =&gt; RAISE Arith.Error(NEW(Arith.ErrorDivisionByZero).init());
    | 1 =&gt; RETURN 1;
    ELSE
      RAISE Arith.Error(NEW(Arith.ErrorIndivisible).init());
    END;
  END Rec;

PROCEDURE <A NAME="Mod"><procedure>Mod</procedure></A> (x, y: T; ): T RAISES {Arith.Error} =
  BEGIN
    CheckDivisor(y);
    RETURN x MOD y;
  END Mod;

PROCEDURE <A NAME="DivMod"><procedure>DivMod</procedure></A> (x, y: T; ): QuotRem RAISES {Arith.Error} =
  BEGIN
    CheckDivisor(y);
    RETURN QuotRem{x MOD y, x DIV y};
  END DivMod;
</PRE> Kapil Hari Paranjape: Some lectures on number theory, elliptic curves
   and cryptology, chapter 2: Greatest common divisor,
   http://www.imsc.ernet.in/~kapil/crypto/notes/node8.html
<PRE>PROCEDURE <A NAME="GCD"><procedure>GCD</procedure></A> (x, y: T; ): T =
  VAR
    xt, yt: [0 .. BITSIZE(T)] := 0;
    z     : T                 := One;
  BEGIN
    IF x = 0 THEN RETURN y; END;
    IF y = 0 THEN RETURN x; END;
    (* This will be optimized to bit shift operations I hope*)
    (* count the factor 2*)
    WHILE x MOD 2 = 0 DO x := x DIV 2; INC(xt); END;
    WHILE y MOD 2 = 0 DO y := y DIV 2; INC(yt); END;
    WHILE x # y DO
      IF x &lt;= y THEN z := y - x; ELSE z := x - y; x := y; END;
      (* both x and y are odd, thus there difference is even*)
      WHILE z MOD 2 = 0 DO z := z DIV 2; END;
      y := z;
    END;
    RETURN W.LeftShift(x, MIN(xt, yt));
  END GCD;

BEGIN
END IntegerBasic.
</PRE>
</inModule>
<PRE>























</PRE>
</BODY>
</HTML>
