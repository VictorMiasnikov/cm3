<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: arithmetic/src/algebra/chebyshev/ChebyPolynomialFast.mg</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>arithmetic/src/algebra/chebyshev/ChebyPolynomialFast.mg</H2></A><HR>
<inModule>
<PRE>GENERIC MODULE <genericModule><A HREF="ChebyPolynomialFast.ig.html">ChebyPolynomialFast</A></genericModule>(R, RT);
</PRE><BLOCKQUOTE><EM> Arithmetic for Modula-3, see doc for details
<P>
   Abstract: Implementation of Modula-3 version of NR92, ch 5. </EM></BLOCKQUOTE><PRE>
IMPORT <A HREF="../../Arithmetic.i3.html">Arithmetic</A> AS Arith;
FROM RT IMPORT Cos, Pi;

CONST Module = &quot;ChebyPolynomialFast.&quot;;

PROCEDURE <A NAME="Expand"><procedure>Expand</procedure></A> (func: Ftn; n: CARDINAL; ): T =
  VAR
    nr := FLOAT(n, R.T);
    f  := NEW(T, n + 1);         (* we skip f[0] *)
    x, factor, sum, jr, kr: R.T;
    z                           := NEW(T, n);
  BEGIN
    (*---load up the function values---*)
    FOR k := 1 TO n DO
      kr := FLOAT(k, R.T);
      x := Cos(Pi * (kr - RT.Half) / nr);
      f[k] := func(x);
    END;

    (*---compute coeffs---*)
    factor := R.Two / nr;
    FOR j := 0 TO n - 1 DO
      jr := FLOAT(j, R.T);
      sum := R.Zero;
      FOR k := 1 TO n DO
        kr := FLOAT(k, R.T);
        sum := sum + f[k] * Cos(Pi * jr * (kr - RT.Half) / nr);
      END;
      z[j] := factor * sum;
    END;
    RETURN z;
  END Expand;

PROCEDURE <A NAME="FindUpperExp"><procedure>FindUpperExp</procedure></A> (x: T; prec: R.T; ): CARDINAL =
  &lt;* UNUSED *&gt;
  CONST
    ftn = Module &amp; &quot;FindUpperExp&quot;;
  VAR error: R.T := R.Zero;
  BEGIN
    FOR i := LAST(x^) TO 0 BY -1 DO
      error := error + ABS(x[i]);
      IF error &gt; prec THEN RETURN i + 1; END;
    END;
    RETURN 0;
  END FindUpperExp;

PROCEDURE <A NAME="Abort"><procedure>Abort</procedure></A> (x: T;           (* abort the expansion *)
                 m: CARDINAL;    (* before the m-th term *)
  ): T =
  VAR z := NEW(T, m);
  BEGIN
    z^ := SUBARRAY(x^, 0, m);
    RETURN z;
  END Abort;

PROCEDURE <A NAME="Eval"><procedure>Eval</procedure></A> (x: T; xi: R.T; ): R.T RAISES {Arith.Error} =
  &lt;* UNUSED *&gt;
  CONST
    ftn = Module &amp; &quot;Eval&quot;;
  VAR
    dj   := R.Zero;
    djp1 := R.Zero;
    djp2 := R.Zero;
  BEGIN
    IF xi &lt; -R.One OR xi &gt; R.One THEN
      (* need -1&lt;x&lt;+1 *)
      RAISE Arith.Error(NEW(Arith.ErrorOutOfRange).init());
    END;
    FOR j := LAST(x^) TO 1 BY -1 DO
      dj := R.Two * xi * djp1 - djp2 + x[j];
      djp2 := djp1;
      djp1 := dj;
    END;
    RETURN xi * djp1 - djp2 + RT.Half * x[0];
  END Eval;

PROCEDURE <A NAME="Derive"><procedure>Derive</procedure></A> (x: T; ): T =
  VAR
    n := NUMBER(x^);
    z := NEW(T, n - 1);
  BEGIN
    IF n &gt;= 2 THEN
      z[n - 2] := FLOAT(2 * (n - 1), R.T) * x[n - 1];
      IF n &gt;= 3 THEN
        z[n - 3] := FLOAT(2 * (n - 2), R.T) * x[n - 2];
        FOR j := n - 4 TO 0 BY -1 DO
          z[j] := z[j + 2] + FLOAT(2 * (j + 1), R.T) * x[j + 1];
        END;
      END;
    END;
    RETURN z;
  END Derive;

PROCEDURE <A NAME="Integrate"><procedure>Integrate</procedure></A> (x: T; ): T =
  VAR
    n := NUMBER(x^);
    z := NEW(T, n + 1);
  BEGIN
    IF 0 &lt;= n THEN
      z[0] := R.Zero;
      FOR j := 1 TO n - 2 DO
        z[j] := (x[j - 1] - x[j + 1]) / FLOAT(2 * j, R.T);
      END;
      IF 2 &lt;= n THEN z[n - 1] := x[n - 2] / FLOAT(2 * (n - 1), R.T); END;
      IF 1 &lt;= n THEN z[n] := x[n - 1] / FLOAT(2 * (n), R.T); END;
    END;
    RETURN z;
  END Integrate;

BEGIN
END ChebyPolynomialFast.
</PRE>
</inModule>
<PRE>























</PRE>
</BODY>
</HTML>
