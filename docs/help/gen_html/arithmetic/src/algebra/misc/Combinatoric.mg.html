<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: arithmetic/src/algebra/misc/Combinatoric.mg</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>arithmetic/src/algebra/misc/Combinatoric.mg</H2></A><HR>
<inModule>
<PRE>GENERIC MODULE <genericModule><A HREF="Combinatoric.ig.html">Combinatoric</A></genericModule>(R);
</PRE><BLOCKQUOTE><EM> Arithmetic for Modula-3, see doc for details
<P>
   Abstract: Combinatoric operations </EM></BLOCKQUOTE><PRE>

FROM <A HREF="../../Arithmetic.i3.html">Arithmetic</A> IMPORT Error;

&lt;* UNUSED *&gt;
CONST
  Module = &quot;Combinatoric.&quot;;
</PRE> Factorial of n 
<PRE>PROCEDURE <A NAME="Factorial"><procedure>Factorial</procedure></A> (n: T; ): T =
  VAR num := R.One;

  BEGIN
    WHILE NOT R.IsZero(n) DO
      num := R.Mul(num, n);
      n := R.Sub(n, R.One);
    END;
    RETURN num;
  END Factorial;
</PRE> Permutations 
<PRE>PROCEDURE <A NAME="Permutations"><procedure>Permutations</procedure></A> (READONLY n: ARRAY OF T; ): T =
  (**
  possible optimizations:
  1. start with the biggest number n[j],
     the computation time for the corresponding term can be saved completely
  2. calculating binomial coefficients might be sped up by
     keeping a list of machine size integer factors
  *)
  VAR
    num       := R.One;
    k, div: T;

  &lt;* FATAL Error *&gt;
  BEGIN
    IF NUMBER(n) = 0 THEN RETURN R.One; END;
    k := n[0];
    FOR j := 1 TO LAST(n) DO
      div := R.Zero;
      WHILE NOT R.Equal(div, n[j]) DO
        k := R.Add(k, R.One);
        div := R.Add(div, R.One);
        num := R.Div(R.Mul(num, k), div);
      END;
    END;
    RETURN num;
  END Permutations;
</PRE> Variationen ohne Wiederholung von n zur Klasse k 
<PRE>PROCEDURE <A NAME="Arrangements"><procedure>Arrangements</procedure></A> (n, k: T; ): T =
  VAR num := R.One;

  BEGIN
    WHILE NOT R.IsZero(k) DO
      num := R.Mul(num, n);
      n := R.Sub(n, R.One);
      k := R.Sub(k, R.One);
    END;
    RETURN num;
  END Arrangements;
</PRE> Variationen mit Wiederholung von n zur Klasse k 
 similar to the routine IntegerPower
<PRE>PROCEDURE <A NAME="ArrangementsR"><procedure>ArrangementsR</procedure></A> (n, k: T; ): T =
  VAR
    num := R.One;
    qr  := R.QuotRem{k, R.Zero};

  &lt;* FATAL Error *&gt;
  BEGIN
    WHILE NOT R.IsZero(qr.quot) DO
      qr := R.DivMod(qr.quot, R.Two);
      IF NOT R.IsZero(qr.rem) THEN num := R.Mul(num, n); END;
      n := R.Mul(n, n);
    END;
    RETURN num;
  END ArrangementsR;
</PRE> Kombinationen ohne Wiederholung von n zur Klasse k 
<PRE>PROCEDURE <A NAME="Combinations"><procedure>Combinations</procedure></A> (n, k: T; ): T =
  BEGIN
    RETURN Permutations(ARRAY OF T{k, R.Sub(n, k)});
  END Combinations;
</PRE> Kombinationen mit Wiederholung von n zur Klasse k 
<PRE>PROCEDURE <A NAME="CombinationsR"><procedure>CombinationsR</procedure></A> (n, k: T; ): T =
  BEGIN
    (*
    RETURN Combinations (n+k-1, k);
    RETURN Permutations (ARRAY OF T{n-1, k});
    *)
    RETURN Permutations(ARRAY OF T{R.Sub(n, R.One), k});
  END CombinationsR;

BEGIN
END Combinatoric.
</PRE>
</inModule>
<PRE>























</PRE>
</BODY>
</HTML>
