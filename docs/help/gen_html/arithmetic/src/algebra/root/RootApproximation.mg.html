<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: arithmetic/src/algebra/root/RootApproximation.mg</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>arithmetic/src/algebra/root/RootApproximation.mg</H2></A><HR>
<inModule>
<PRE>GENERIC MODULE <genericModule><A HREF="RootApproximation.ig.html">RootApproximation</A></genericModule>(R, RT, RRt, C, CT, CP, CRt);
</PRE><BLOCKQUOTE><EM> Arithmetic for Modula-3, see doc for details </EM></BLOCKQUOTE><PRE>

FROM <A HREF="../../Arithmetic.i3.html">Arithmetic</A> IMPORT Error;
</PRE>*
IMPORT LongRealComplexVectorFmtLex AS VF,
       LongRealComplexFmtLex AS CF,
       LongRealFmtLex AS RF,
       IO;
*

<P><PRE>&lt;* UNUSED *&gt;
CONST
  Module = &quot;RootApproximation.&quot;;
</PRE> Quadratics 

<P>
<P><PRE>PROCEDURE <A NAME="RealQuadratic"><procedure>RealQuadratic</procedure></A> (READONLY x: RealPolynomial2; ): RootArray2 =
  (* Given a*t^2+b*t+c=0, solve for t. *)
  VAR
    a                    := x[2];
    b                    := x[1];
    c                    := x[0];
    disc, q, q1, q2: R.T;
  BEGIN
    disc := b * b - FLOAT(4.0, R.T) * a * c;
    IF disc &lt;= R.Zero THEN
      q1 := -b / a * RT.Half;
      q2 := RT.SqRt(-disc) / a * RT.Half;
      RETURN RootArray2{C.T{re := q1, im := q2}, C.T{re := q1, im := -q2}};
    ELSE
      (* avoid cancelation *)
      IF b &lt; R.Zero THEN
        q := RT.Half * (-b + RT.SqRt(disc));
      ELSE
        q := RT.Half * (-b - RT.SqRt(disc));
      END;
      (* fails because Sgn(0)=0 :
         q:=-RT.Half*(b+RT.Sgn(b)*RT.SqRt(disc)); *)
      q1 := q / a;
      q2 := c / q;
      RETURN RootArray2{
               C.T{re := q1, im := R.Zero}, C.T{re := q2, im := R.Zero}};
    END;
  END RealQuadratic;

PROCEDURE <A NAME="ComplexQuadratic"><procedure>ComplexQuadratic</procedure></A> (READONLY x: ComplexPolynomial2; ): RootArray2
  RAISES {Error} =
  (* Given a*t^2+b*t+c=0, solve for t. *)
  CONST c4 = FLOAT(4.0, R.T);
  VAR
    a                       := x[2];
    b                       := x[1];
    c                       := x[0];
    disc, disc_sqrt, q: C.T;
  BEGIN
    disc := C.Sub(C.Mul(b, b), C.Scale(C.Mul(a, c), c4));
    disc_sqrt := CT.SqRt(disc);

    (*---set sign of sqrt via NR92 eqn 5.6.6---*)
    IF C.Mul(C.Conj(b), disc_sqrt).re &gt; R.Zero THEN
      disc_sqrt := C.Neg(disc_sqrt);
    END;

    (*---calculate per NR92 eqn 5.6.4, 5.6.5.---*)
    q := C.Scale(C.Sub(disc_sqrt, b), RT.Half);
    RETURN RootArray2{C.Div(q, a), C.Div(c, q)};
  END ComplexQuadratic;

PROCEDURE <A NAME="RealNewtonMaehli"><procedure>RealNewtonMaehli</procedure></A> (x: RRt.T; ): REF CRt.RootArray RAISES {Error} =
  VAR xc := NEW(CRt.T, NUMBER(x^));
  BEGIN
    FOR j := 0 TO LAST(xc^) DO xc[j] := C.T{x[j], R.Zero}; END;
    RETURN ComplexNewtonMaehli(xc);
  END RealNewtonMaehli;
</PRE>*
  calculates all zeros simultanously
  calculation is made with complex numbers to catch all zeros
  iteration is aborted if polynomial value at these points is small enough
*
<PRE>PROCEDURE <A NAME="ComplexNewtonMaehli"><procedure>ComplexNewtonMaehli</procedure></A> (x: CRt.T; ): REF CRt.RootArray
  RAISES {Error} =
  CONST
    maxArgError = RT.Eps * FLOAT(10.0, R.T); (* error in the argument r *)
  &lt;* UNUSED *&gt;
  CONST
    maxValError = RT.Eps * FLOAT(100.0, R.T); (* error in the value p(r) *)
  VAR
    z       := NEW(REF CRt.RootArray, LAST(x^));
    maxIter := 100;
  BEGIN
    VAR jr, nr: R.T;
    BEGIN
      nr := FLOAT(NUMBER(z^), R.T);
      FOR j := 0 TO LAST(z^) DO
        jr := R.One - FLOAT(j, R.T) / nr * R.Two;
        z[j] := C.T{jr, jr};
      END;
    END;

    VAR iterating: BOOLEAN;
    BEGIN
      REPEAT
        (*
        IO.Put(VF.Fmt(z) &amp; &quot;\n&quot;);
        *)
        iterating := FALSE;
        FOR j := 0 TO LAST(z^) DO
          VAR
            y      := CP.EvalDerivative(x, z[j], 2);
            sumRec := C.Zero;
          BEGIN
            (* iterating := iterating OR (norm(y) &gt; maxValError);*)

            FOR k := 0 TO LAST(z^) DO
              IF j # k THEN
                sumRec := C.Add(sumRec, C.Rec(C.Sub(z[j], z[k])));
              END;
            END;

            VAR dz := C.Div(y[0], (C.Sub(y[1], C.Mul(y[0], sumRec))));
            BEGIN
              z[j] := C.Sub(z[j], dz);
              (**
              IO.Put(&quot;z &quot;&amp;CF.Fmt(z[j])&amp;&quot;, dz &quot;&amp;CF.Fmt(dz)&amp;&quot;\n&quot;);
              IO.Put(&quot;Abs2(z) &quot;&amp;RF.Fmt(CT.AbsSqr(z[j]))&amp;&quot;, Abs2(dz) &quot;&amp;RF.Fmt(CT.AbsSqr(dz))&amp;
                      &quot;, maxArgError &quot;&amp;RF.Fmt(maxArgError)&amp;&quot;\n&quot;);
              **)
              (**
                I'm not sure if using the relative change is the right criterion
                but it works even if one zero is at 6e11 and another is at 0.2
                this may occur if the coefficent of the highest power of z is close to zero
              **)
              iterating :=
                iterating OR (CT.AbsSqr(dz) &gt; CT.AbsSqr(z[j]) * maxArgError
                                                * maxArgError);
            END;
          END;
        END;
        DEC(maxIter);
        iterating := iterating AND maxIter &gt; 0;
      UNTIL NOT iterating;
    END;
    RETURN z;
  END ComplexNewtonMaehli;

BEGIN
END RootApproximation.
</PRE>
</inModule>
<PRE>























</PRE>
</BODY>
</HTML>
