<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: mentor/src/stringsearch/KMP.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>mentor/src/stringsearch/KMP.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright 1992 Digital Equipment Corporation. 
 Distributed only by permission. 
 Last modified on Thu Jan  5 23:50:15 PST 1995 by najork  
      modified on Sat Oct 24 03:25:10 PDT 1992 by broder  
      modified on Thu Sep 24 13:14:29 PDT 1992 by mhb     
      modified on Fri Aug  7 16:28:58 PDT 1992 by guarino 

<P><PRE>MODULE <module><implements><A HREF="KMP.i3.html">KMP</A></implements></module>;

IMPORT <A HREF="../../../zeus/src/Algorithm.i3.html">Algorithm</A>, <A HREF="../../derived/StringSearchAlgClass.i3.html">StringSearchAlgClass</A>, <A HREF="../../derived/StringSearchIE.i3.html">StringSearchIE</A>, <A HREF="../../../m3core/src/text/Text.i3.html">Text</A>,
       <A HREF="../../../m3core/src/thread/Common/Thread.i3.html">Thread</A>, <A HREF="../../../zeus/src/ZeusPanel.i3.html">ZeusPanel</A>, <A HREF="AlgsBase.i3.html">AlgsBase</A>;

REVEAL
  <A NAME="T">T</A> = StringSearchAlgClass.T BRANDED OBJECT OVERRIDES run := Run; END;

PROCEDURE <A NAME="Run"><procedure>Run</procedure></A> (alg: T) RAISES {Thread.Alerted} =
  VAR
    p, s: TEXT;                 (* pattern and string *)
    m, n: CARDINAL;             (* their length *)
    next: REF ARRAY OF INTEGER;
    i   : CARDINAL             := 0; (* index in pattern *)
  BEGIN
    AlgsBase.GetData(alg, p, s);
    m := Text.Length(p);
    n := Text.Length(s);
    IF m = 0 OR n = 0 THEN RETURN; END;
    next := InitNext(alg, p);
    ZeusPanel.Pause(alg, &quot;KMP precomputing done&quot;);
    StringSearchIE.Setup(alg, p, s);

    (**
        This is the basic algorithm i := 0;
        FOR j := 0 TO n - m DO
        (* Invariant invalidated by INC(j) *)
          WHILE (i &gt; 0) AND (p[i] # s[j]) DO i := next[i]; END;
          IF p[i] = s[j] THEN INC(i) END;
        (* Invariant: p[0..i-1] is the longest prefix of p that is a
          suffix of s[0..j] *)
          IF i = m THEN (*Match*) i := next[i] END;
        END;
    **)

    FOR j := 0 TO n - m DO
      WHILE (i &gt; 0) AND (Text.GetChar(p, i) # Text.GetChar(s, j)) DO
        StringSearchIE.Probe(alg, i, j);
        StringSearchIE.Result(alg, FALSE);
        StringSearchIE.PartialMatchClear(alg);
        i := next[i];
        StringSearchIE.SlideTo(alg, j - i);
        StringSearchIE.PartialMatch(alg, 0, j - i, i);
      END;
      StringSearchIE.Probe(alg, i, j);
      IF Text.GetChar(p, i) = Text.GetChar(s, j) THEN
        StringSearchIE.Result(alg, TRUE);
        INC(i);
        StringSearchIE.PartialMatch(alg, 0, j - i + 1, i);
      ELSE                      (* i = 0 *)
        StringSearchIE.Result(alg, FALSE);
        StringSearchIE.PartialMatchClear(alg);
        StringSearchIE.SlideTo(alg, j + 1);
      END;
      IF i = m THEN
        StringSearchIE.CompleteMatch(alg, j - i + 1);
        i := next[i];
        StringSearchIE.SlideTo(alg, j - i + 1);
        StringSearchIE.PartialMatch(alg, 0, j - i + 1, i);
      END;
    END;
  END Run;
</PRE> Compute the next function: next[i] == the length of the maximum proper
   suffix of p[0..i-1] that is a prefix of p 

<P>
<P><PRE>PROCEDURE <A NAME="InitNext"><procedure>InitNext</procedure></A> (alg: T; p: TEXT): REF ARRAY OF INTEGER
  RAISES {Thread.Alerted} =
  VAR
    m    := Text.Length(p);
    next := NEW(REF ARRAY OF INTEGER, m + 1);
    i    := 0;
  BEGIN
    StringSearchIE.KMPSetup(alg, p); (* Must do before setup! *)
    StringSearchIE.Setup(alg, p, p);
    StringSearchIE.SlideTo(alg, 1);
    next[0] := 0;
    (* AddEdge(0,0) not needed *)
    next[1] := 0;
    StringSearchIE.AddEdge(alg, 1, 0);

    FOR j := 1 TO m - 1 DO
      WHILE (i &gt; 0) AND (Text.GetChar(p, i) # Text.GetChar(p, j)) DO
        StringSearchIE.Probe(alg, i, j);
        StringSearchIE.Result(alg, FALSE);
        StringSearchIE.PartialMatchClear(alg);
        i := next[i];
        StringSearchIE.SlideTo(alg, j - i);
        StringSearchIE.PartialMatch(alg, 0, j - i, i);
      END;

      StringSearchIE.Probe(alg, i, j);
      IF Text.GetChar(p, i) = Text.GetChar(p, j) THEN
        StringSearchIE.Result(alg, TRUE);
        INC(i);
        StringSearchIE.PartialMatch(alg, 0, j - i + 1, i);
        next[j + 1] := i;
        StringSearchIE.AddEdge(alg, j + 1, i);
      ELSE                      (* i = 0 *)
        StringSearchIE.Result(alg, FALSE);
        StringSearchIE.PartialMatchClear(alg);
        next[j + 1] := i;
        StringSearchIE.AddEdge(alg, j + 1, i);
        StringSearchIE.SlideTo(alg, j + 1);
      END;
    END;
    StringSearchIE.PartialMatchClear(alg);
    RETURN (next);
  END InitNext;

PROCEDURE <A NAME="New"><procedure>New</procedure></A> (): Algorithm.T =
  BEGIN
    RETURN
      NEW(
        T, data := ZeusPanel.NewForm(&quot;stringsearchinput.fv&quot;)).init();
  END New;

BEGIN
  ZeusPanel.RegisterAlg(New, &quot;KnuthMorrisPratt&quot;, &quot;StringSearch&quot;);
END KMP.
</PRE>
</inModule>
<PRE>























</PRE>
</BODY>
</HTML>
