<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: mentor/src/unionfind/AlgCompress.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>mentor/src/unionfind/AlgCompress.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright (C) 1994, Digital Equipment Corporation                         
 All rights reserved.                                                      
 See the file COPYRIGHT for a full description.                            
                                                                           
 Last modified on Thu Aug 18 14:19:09 PDT 1994 by heydon                   
      modified on Wed Jun 15 14:18:35 PDT 1994 by shillner                 

<P><PRE>MODULE <module><implements><A HREF="AlgCompress.i3.html">AlgCompress</A></implements></module>;

IMPORT <A HREF="../../derived/UnionFindAlgClass.i3.html">UnionFindAlgClass</A>, <A HREF="../../derived/UnionFindIE.i3.html">UnionFindIE</A>;
IMPORT <A HREF="UFInput.i3.html">UFInput</A>, <A HREF="TreeNode.i3.html">TreeNode</A>;
IMPORT <A HREF="../../../zeus/src/Algorithm.i3.html">Algorithm</A>, <A HREF="../../../zeus/src/ZeusPanel.i3.html">ZeusPanel</A>;
IMPORT <A HREF="../../../ui/src/vbt/VBT.i3.html">VBT</A>;
IMPORT <A HREF="../../../m3core/src/thread/Common/Thread.i3.html">Thread</A>, <A HREF="../../../libm3/src/fmtlex/Fmt.i3.html">Fmt</A>;

TYPE
  T = UnionFindAlgClass.T BRANDED OBJECT OVERRIDES
    run := Run
  END;
  Nodes = REF ARRAY OF TreeNode.T;

PROCEDURE <A NAME="Run"><procedure>Run</procedure></A>(alg: T) RAISES {Thread.Alerted} =
  VAR ufin: UFInput.T; nodes: Nodes; cmd: UFInput.Cmd; BEGIN
    TRY
      ufin := UFInput.New(alg.data);
      IF ufin # NIL THEN
        nodes := Setup(alg, ufin);
    	WHILE ufin.next(cmd) DO
    	  TYPECASE cmd OF &lt;* NOWARN *&gt;
    	    UFInput.FindCmd (find) =&gt;    EVAL Find(alg, nodes, find)
    	  | UFInput.UnionCmd (union) =&gt;  EVAL Union(alg, nodes, union)
    	  END
    	END
      END
    EXCEPT
      UFInput.Error (txt) =&gt;
        LOCK VBT.mu DO
          ZeusPanel.ReportError(&quot;Bad input: &quot; &amp; txt &amp; &quot;\n&quot;)
        END
    END
  END Run;

PROCEDURE <A NAME="Setup"><procedure>Setup</procedure></A>(alg: T; ufin: UFInput.T): Nodes
    RAISES {UFInput.Error, Thread.Alerted} =
  VAR numSets := 0; cmd: UFInput.Cmd; res: Nodes; BEGIN
    UnionFindIE.Setup(alg);
    WHILE ufin.next(cmd) DO
      TYPECASE cmd OF &lt;* NOWARN *&gt;
        UFInput.NewSetCmd (newSet) =&gt;
          UnionFindIE.NewSet(alg, newSet.arg1); INC(numSets)
      | UFInput.FinishedSetsCmd =&gt;
          UnionFindIE.FinishedSets(alg, numSets, usesRanks := FALSE); EXIT
      END
    END;
    res := NEW(Nodes, numSets);
    FOR i := 0 TO numSets - 1 DO
      res[i] := NEW(TreeNode.T, id := i)
    END;
    RETURN res
  END Setup;

PROCEDURE <A NAME="Find"><procedure>Find</procedure></A>(alg: T; nodes: Nodes; cmd: UFInput.FindCmd): TreeNode.T
    RAISES {UFInput.Error, Thread.Alerted} =
  VAR root: TreeNode.T; BEGIN
    IF cmd.arg1 &gt; LAST(nodes^) THEN
      RAISE UFInput.Error(&quot;Find: set &quot; &amp;Fmt.Int(cmd.arg1)&amp; &quot; does not exist&quot;)
    END;
    WITH node = nodes[cmd.arg1] DO
      UnionFindIE.StartFind(alg, node.id);
      root := DoFind(alg, node);
    END;
    UnionFindIE.EndFind(alg, root.id);
    RETURN root
  END Find;

PROCEDURE <A NAME="Union"><procedure>Union</procedure></A>(alg: T; nodes: Nodes; cmd: UFInput.UnionCmd): TreeNode.T
    RAISES {UFInput.Error, Thread.Alerted} =
  BEGIN
    IF cmd.arg1 &gt; LAST(nodes^) THEN
      RAISE UFInput.Error(&quot;Union: set &quot; &amp;Fmt.Int(cmd.arg1)&amp; &quot; does not exist&quot;)
    ELSIF cmd.arg2 &gt; LAST(nodes^) THEN
      RAISE UFInput.Error(&quot;Union: set &quot; &amp;Fmt.Int(cmd.arg2)&amp; &quot; does not exist&quot;)
    END;
    VAR node1 := nodes[cmd.arg1]; node2 := nodes[cmd.arg2]; BEGIN
      UnionFindIE.StartUnion(alg, node1.id, node2.id, cmd.bothRoots);
      IF NOT cmd.bothRoots THEN
      	node1 := DoFind(alg, node1);
      	UnionFindIE.FoundFirst(alg, node1.id);
      	node2 := DoFind(alg, node2)
      END;
      IF node1 # node2 THEN
        node2.up := node1;
        UnionFindIE.Unite(alg, node2.id, node1.id, pRank := -1)
      END;
      UnionFindIE.EndUnion(alg);
      RETURN node2
    END
  END Union;

PROCEDURE <A NAME="DoFind"><procedure>DoFind</procedure></A>(alg: T; node: TreeNode.T): TreeNode.T
    RAISES {Thread.Alerted} =
  VAR root: TreeNode.T;
  BEGIN
    UnionFindIE.StartDoFind(alg, node.id);
    root := DoFind2(alg, node);
    UnionFindIE.EndDoFind(alg, node.id);
    RETURN root
  END DoFind;

PROCEDURE <A NAME="DoFind2"><procedure>DoFind2</procedure></A>(alg: T; node: TreeNode.T): TreeNode.T
    RAISES {Thread.Alerted} =
  BEGIN
    IF node.up # NIL THEN
      UnionFindIE.StepUp(alg, node.id, node.up.id);
      VAR root := DoFind2(alg, node.up); BEGIN
        UnionFindIE.StepDown(alg, node.id, node.up.id);
        IF node.up # root THEN
          UnionFindIE.ChangeParent(alg, node.id, node.up.id, root.id)
        END;
        node.up := root;
        RETURN root;
      END
    ELSE
      UnionFindIE.Found(alg, node.id);
      RETURN node
    END
  END DoFind2;

PROCEDURE <A NAME="New"><procedure>New</procedure></A>(): Algorithm.T =
  BEGIN
    RETURN NEW(T, data := ZeusPanel.NewForm(&quot;UnionFind.fv&quot;)).init()
  END New;

BEGIN
  ZeusPanel.RegisterAlg(New, &quot;Tree, Path Compression&quot;, &quot;UnionFind&quot;)
END AlgCompress.
</PRE>
</inModule>
<PRE>























</PRE>
</BODY>
</HTML>
