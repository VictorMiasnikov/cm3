<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: mentor/src/pktroute/ASP.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>mentor/src/pktroute/ASP.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright 1993 Digital Equipment Corporation.                             
 Distributed only by permission.                                           
                                                                           
 Last modified on Mon May  2 16:15:53 PDT 1994 by najork                   
      modified on Fri Aug 13 12:14:07 PDT 1993 by heydon                   

<P><PRE>MODULE <module><implements><A HREF="ASP.i3.html">ASP</A></implements></module>;

IMPORT <A HREF="../../../libm3/derived/IntPQ.i3.html">IntPQ</A>, <A HREF="../../derived/RealPQ.i3.html">RealPQ</A>, <A HREF="../../../m3core/src/float/Common/IEEESpecial.i3.html">IEEESpecial</A>, <A HREF="../../../libm3/derived/IntRefTbl.i3.html">IntRefTbl</A>, <A HREF="Graph.i3.html">Graph</A>, <A HREF="GraphRep.i3.html">GraphRep</A>;

&lt;* FATAL IntPQ.Empty,  IntPQ.NotInQueue *&gt;
&lt;* FATAL RealPQ.Empty, RealPQ.NotInQueue *&gt;

REVEAL
  <A NAME="Unweighted">Unweighted</A> = UnweightedPub BRANDED OBJECT
    min: REF ARRAY OF ARRAY OF INTEGER;
  OVERRIDES
    dist := UnweightedDist;
  END;

  <A NAME="Weighted">Weighted</A> = WeightedPub BRANDED OBJECT
    min: REF ARRAY OF ARRAY OF REAL;
  OVERRIDES
    dist := WeightedDist;
  END;

TYPE
  IntElt = IntPQ.Elt BRANDED OBJECT value: INTEGER END;
  RealElt = RealPQ.Elt BRANDED OBJECT value: INTEGER END;

PROCEDURE <A NAME="UnweightedDist"><procedure>UnweightedDist</procedure></A>(uw: Unweighted; READONLY from, to: CARDINAL): INTEGER=
  BEGIN RETURN uw.min[from, to] END UnweightedDist;

PROCEDURE <A NAME="WeightedDist"><procedure>WeightedDist</procedure></A>(w: Weighted; READONLY from, to: CARDINAL): REAL =
  BEGIN RETURN w.min[from, to] END WeightedDist;

PROCEDURE <A NAME="UnweightedFromGraph"><procedure>UnweightedFromGraph</procedure></A>(g: Graph.Sparse): Unweighted =
  VAR
    res := NEW(Unweighted, min := NEW(REF ARRAY OF ARRAY OF INTEGER,
      g.nodeCnt, g.nodeCnt));
    pq := NEW(IntPQ.Default);
    tbl := NEW(IntRefTbl.Default);	 (* node id -&gt; IntElt map *)

  (* Invariant: tbl.get(node, elt) =&gt; elt.value = node *)

  PROCEDURE FromNode(
      VAR (*INOUT*) min: ARRAY OF INTEGER;
      src: CARDINAL) =
  (* Sets &quot;min[i]&quot; to contain the length of the shortest path from node &quot;src&quot;
     to node &quot;i&quot;. If there is no path from &quot;src&quot; to &quot;i&quot;, then &quot;min[i] = -1&quot;.
     Requires that &quot;min[i]&quot; is initially -1 for &quot;i&quot; in the interval
     &quot;[FIRST(min)..LAST(min)]&quot;. *)

    PROCEDURE Update(adjNode: CARDINAL; newP: INTEGER) =
    (* If the node &quot;adjNode&quot; is in &quot;tbl&quot;, then set its priority to &quot;newP&quot; if
       &quot;newP&quot; is a &quot;higher&quot; priority than its current one; otherwise, add
       &quot;adjNode&quot; to &quot;tbl&quot; and insert it in &quot;pq&quot; with priority &quot;newP&quot;. *)
      VAR ref: REFANY; BEGIN
        IF tbl.get(adjNode, ref) THEN
          (* &quot;adjNode&quot; is already in the Frontier *)
          VAR adjElt: IntElt := ref; BEGIN
            (* change priority of &quot;adjElt&quot; if it is better than current one *)
            IF pq.pCompare(newP, adjElt.priority) = -1 THEN
              pq.change(adjElt, newP)
            END
          END
        ELSE
          (* &quot;adjNode&quot; currently Unvisited; add it to the Frontier *)
          VAR elt := NEW(IntElt, priority := newP, value := adjNode); BEGIN
            pq.insert(elt);
            EVAL tbl.put(adjNode, elt)
          END
        END
      END Update;

    BEGIN
      EVAL pq.init(sizeHint := g.nodeCnt);
      EVAL tbl.init(sizeHint := g.nodeCnt);
      VAR elt := NEW(IntElt, priority := 0, value := src); BEGIN
        pq.insert(elt);
        EVAL tbl.put(src, elt)
      END;
      WHILE pq.size() # 0 DO
        VAR elt: IntElt := pq.deleteMin(); BEGIN
          min[elt.value] := elt.priority;
          VAR curr := g.adj[elt.value]; BEGIN
            WHILE curr # NIL DO
              IF min[curr.node] = -1 THEN (* node in Fringe or Unvisited set *)
                Update(curr.node, elt.priority + 1)
              END;
              curr := curr.next
            END
          END
        END
      END
    END FromNode;

  BEGIN
    FOR i := 0 TO g.nodeCnt - 1 DO
      FOR j := 0 TO g.nodeCnt - 1 DO
        res.min[i, j] := -1
      END;
      FromNode(res.min[i], i)
    END;
    RETURN res
  END UnweightedFromGraph;

PROCEDURE <A NAME="WeightedFromGraph"><procedure>WeightedFromGraph</procedure></A>(g: Graph.Sparse): Weighted =
  VAR
    res := NEW(Weighted, min := NEW(REF ARRAY OF ARRAY OF REAL,
      g.nodeCnt, g.nodeCnt));
    pq := NEW(RealPQ.Default);
    tbl := NEW(IntRefTbl.Default);	 (* node id -&gt; RealElt map *)

  (* Invariant: tbl.get(node, elt) =&gt; elt.value = node *)

  PROCEDURE FromNode(
      VAR (*INOUT*) min: ARRAY OF REAL;
      src: CARDINAL) =
  (* Sets &quot;min[i]&quot; to contain the length of the shortest path from node &quot;src&quot;
     to node &quot;i&quot;. If there is no path from &quot;src&quot; to &quot;i&quot;, then &quot;min[i] =
     IEEESpecial.RealPosInf&quot;. Requires that &quot;min[i]&quot; is initially
     &quot;IEEESpecial.RealPosInf&quot; for &quot;i&quot; in the interval
     &quot;[FIRST(min)..LAST(min)]&quot;. *)

    PROCEDURE Update(adjNode: CARDINAL; newP: REAL) =
    (* If the node &quot;adjNode&quot; is in &quot;tbl&quot;, then set its priority to &quot;newP&quot; if
       &quot;newP&quot; is a &quot;higher&quot; priority than its current one; otherwise, add
       &quot;adjNode&quot; to &quot;tbl&quot; and insert it in &quot;pq&quot; with priority &quot;newP&quot;. *)
      VAR ref: REFANY; BEGIN
        IF tbl.get(adjNode, ref) THEN
          (* &quot;adjNode&quot; is already in the Frontier *)
          VAR adjElt: RealElt := ref; BEGIN
            (* change priority of &quot;adjElt&quot; if it is better than current one *)
            IF pq.pCompare(newP, adjElt.priority) = -1 THEN
              pq.change(adjElt, newP)
            END
          END
        ELSE
          (* &quot;adjNode&quot; currently Unvisited; add it to the Frontier *)
          VAR elt := NEW(RealElt, priority := newP, value := adjNode); BEGIN
            pq.insert(elt);
            EVAL tbl.put(adjNode, elt)
          END
        END
      END Update;

    BEGIN
      EVAL pq.init(sizeHint := g.nodeCnt);
      EVAL tbl.init(sizeHint := g.nodeCnt);
      VAR elt := NEW(RealElt, priority := 0.0, value := src); BEGIN
        pq.insert(elt);
        EVAL tbl.put(src, elt)
      END;
      WHILE pq.size() # 0 DO
        VAR elt: RealElt := pq.deleteMin(); BEGIN
          min[elt.value] := elt.priority;
          VAR curr := g.adj[elt.value]; BEGIN
            WHILE curr # NIL DO
              IF min[curr.node] = IEEESpecial.RealPosInf THEN
                Update(curr.node, elt.priority + curr.weight)
              END;
              curr := curr.next
            END
          END
        END
      END
    END FromNode;

  BEGIN
    FOR i := 0 TO g.nodeCnt - 1 DO
      FOR j := 0 TO g.nodeCnt - 1 DO
        res.min[i, j] := IEEESpecial.RealPosInf
      END;
      FromNode(res.min[i], i)
    END;
    RETURN res
  END WeightedFromGraph;

BEGIN
END ASP.
</PRE>
</inModule>
<PRE>























</PRE>
</BODY>
</HTML>
