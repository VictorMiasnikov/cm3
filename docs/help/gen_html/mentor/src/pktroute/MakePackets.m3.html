<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: mentor/src/pktroute/MakePackets.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>mentor/src/pktroute/MakePackets.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright 1993 Digital Equipment Corporation.                             
 Distributed only by permission.                                           
                                                                           
 Last modified on Tue Jan 31 15:40:31 PST 1995 by kalsow                   
      modified on Sun Aug  8 15:56:33 PDT 1993 by heydon                   

<P><PRE>MODULE <module><implements><A HREF="MakePackets.i3.html">MakePackets</A></implements></module>;

IMPORT <A HREF="AlgGreedy.i3.html">AlgGreedy</A>, <A HREF="Packet.i3.html">Packet</A>;
IMPORT <A HREF="../../derived/PktRouteIE.i3.html">PktRouteIE</A>;
IMPORT <A HREF="../../../libm3/src/rw/Rd.i3.html">Rd</A>, <A HREF="../../../m3core/src/thread/Common/Thread.i3.html">Thread</A>, <A HREF="../../../m3core/src/text/Text.i3.html">Text</A>, <A HREF="../../../libm3/src/sx/Sx.i3.html">Sx</A>, <A HREF="../../../libm3/derived/RefList.i3.html">RefList</A>;

PROCEDURE <A NAME="SourceFromName"><procedure>SourceFromName</procedure></A>(nm: TEXT): Source =
  BEGIN
    IF    Text.Equal(nm, &quot;randomSrc&quot;)    THEN RETURN Source.Random
    ELSIF Text.Equal(nm, &quot;constantSrc&quot;)  THEN RETURN Source.Constant
    ELSIF Text.Equal(nm, &quot;pktsFromFile&quot;) THEN RETURN Source.FromFile
    ELSE &lt;* ASSERT FALSE *&gt;
    END
  END SourceFromName;

PROCEDURE <A NAME="RandomPkts"><procedure>RandomPkts</procedure></A>(alg: AlgGreedy.T; total: CARDINAL): Packet.Array
    RAISES {Thread.Alerted} =
  VAR res: Packet.Array; numNodes := alg.graph.numNodes(); BEGIN
    PktRouteIE.StartPackets(alg, total);
    res := NEW(Packet.Array, total);
    VAR src, dest, hops: INTEGER; BEGIN
      FOR i := 0 TO LAST(res^) DO
        REPEAT
          REPEAT src := alg.random.integer(min := 0, max := numNodes - 1)
          UNTIL alg.maxQSz = -1 OR alg.qSz[src] &lt; alg.maxQSz;
          dest := alg.random.integer(min := 0, max := numNodes - 1);
          hops := alg.unweighted.dist(src, dest)
        UNTIL hops &gt; 0 OR numNodes = 1;
        res[i] := alg.newPkt().init(src, dest);
        INC(alg.qSz[src]);
        res[i].id := i;
        PktRouteIE.NewPacket(alg, i, src, dest, hops,
          alg.graph.nodeName(dest));
      END
    END;
    PktRouteIE.EndPackets(alg);
    RETURN res
  END RandomPkts;

PROCEDURE <A NAME="ConstantPkts"><procedure>ConstantPkts</procedure></A>(alg: AlgGreedy.T; num: CARDINAL): Packet.Array
    RAISES {Thread.Alerted} =
  VAR
    numNodes := alg.graph.numNodes();
    tot := num * numNodes;
    res := NEW(Packet.Array, tot);
    pkt, dest, hops: INTEGER;
  BEGIN
    PktRouteIE.StartPackets(alg, tot);
    FOR i := 0 TO numNodes - 1 DO
      FOR j := 0 TO num - 1 DO
        pkt := (i * num) + j;
        REPEAT
          dest := alg.random.integer(min := 0, max := numNodes - 1);
          hops := alg.unweighted.dist(i, dest)
        UNTIL hops &gt; 0 OR numNodes = 1;
        res[pkt] := alg.newPkt().init(i, dest);
        res[pkt].id := pkt;
        INC(alg.qSz[i]);
        PktRouteIE.NewPacket(alg, pkt, i, dest, hops,
          alg.graph.nodeName(dest));
      END
    END;
    PktRouteIE.EndPackets(alg);
    RETURN res
  END ConstantPkts;

PROCEDURE <A NAME="FromFilePkts"><procedure>FromFilePkts</procedure></A>(alg: AlgGreedy.T; rd: Rd.T): Packet.Array
    RAISES {Packet.BadPkts, Thread.Alerted} =
  VAR res: Packet.Array; n := alg.graph.numNodes(); pktId := 0;
  PROCEDURE ReadPacket(l: RefList.T) RAISES {Packet.BadPkts, Thread.Alerted} =
    VAR from, to: INTEGER; BEGIN
      TYPECASE l.head OF REF INTEGER (i) =&gt; from := i^ ELSE
        RAISE Packet.BadPkts(&quot;Packet 'from' value not an integer&quot;)
      END;
      l := l.tail;
      TYPECASE l.head OF REF INTEGER (i) =&gt; to := i^ ELSE
        RAISE Packet.BadPkts(&quot;Packet 'to' value not an integer&quot;)
      END;
      IF NOT (0 &lt;= from AND from &lt; n AND 0 &lt;= to AND to &lt; n) THEN
        RAISE Packet.BadPkts(&quot;Node id out of range in packet specification&quot;)
      END;
      res[pktId] := alg.newPkt().init(from, to);
      res[pktId].id := pktId;
      INC(alg.qSz[from]);
      PktRouteIE.NewPacket(alg, pktId, from, to,
        alg.unweighted.dist(from, to), alg.graph.nodeName(to))
    END ReadPacket;
  VAR sx: Sx.T; BEGIN
    TRY sx := Sx.Read(rd) EXCEPT
      Rd.EndOfFile =&gt; RAISE Packet.BadPkts(&quot;Encountered end-of-file&quot;)
    | Sx.ReadError (msg) =&gt; RAISE Packet.BadPkts(msg)
    END;
    VAR num := RefList.Length(sx); BEGIN
      PktRouteIE.StartPackets(alg, num);
      res := NEW(Packet.Array, num)
    END;
    TYPECASE sx OF RefList.T (curr) =&gt;
      WHILE curr # NIL DO
        TYPECASE curr.head OF RefList.T (r) =&gt; ReadPacket(r) ELSE
          RAISE Packet.BadPkts(&quot;Bad packet specification&quot;)
        END;
        INC(pktId);
        curr := curr.tail
      END
    ELSE RAISE Packet.BadPkts(&quot;Top-level packet specification is not a list&quot;)
    END;
    PktRouteIE.EndPackets(alg);
    RETURN res
  END FromFilePkts;

BEGIN
END MakePackets.
</PRE>
</inModule>
<PRE>























</PRE>
</BODY>
</HTML>
