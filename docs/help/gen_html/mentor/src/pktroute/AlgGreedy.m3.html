<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: mentor/src/pktroute/AlgGreedy.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>mentor/src/pktroute/AlgGreedy.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright 1993 Digital Equipment Corporation.                             
 Distributed only by permission.                                           
                                                                           
 Last modified on Tue Jan 31 14:47:28 PST 1995 by kalsow                   
      modified on Wed Aug 11 01:43:37 PDT 1993 by heydon                   

<P><PRE>MODULE <module><implements><A HREF="AlgGreedy.i3.html">AlgGreedy</A></implements></module>;

IMPORT <A HREF="Graph.i3.html">Graph</A>, <A HREF="Topology.i3.html">Topology</A>, <A HREF="ASP.i3.html">ASP</A>, <A HREF="Packet.i3.html">Packet</A>, <A HREF="MakePackets.i3.html">MakePackets</A>;
IMPORT <A HREF="../../../zeus/src/ZeusPanel.i3.html">ZeusPanel</A>, <A HREF="../../../zeus/src/Algorithm.i3.html">Algorithm</A>, <A HREF="../../derived/PktRouteIE.i3.html">PktRouteIE</A>;
IMPORT <A HREF="../../../formsvbt/src/FormsVBT.i3.html">FormsVBT</A>;
IMPORT <A HREF="../../../ui/src/vbt/VBT.i3.html">VBT</A>;
IMPORT <A HREF="../../../m3core/src/thread/Common/Thread.i3.html">Thread</A>, <A HREF="../../../libm3/src/os/Common/OSError.i3.html">OSError</A>, <A HREF="../../../libm3/src/rw/FileRd.i3.html">FileRd</A>, <A HREF="../../../libm3/derived/IntRefTbl.i3.html">IntRefTbl</A>, <A HREF="../../../m3core/src/text/Text.i3.html">Text</A>, <A HREF="../../../libm3/src/random/Common/Random.i3.html">Random</A>, <A HREF="../../../libm3/src/rw/Stdio.i3.html">Stdio</A>, <A HREF="../../../libm3/src/rw/Wr.i3.html">Wr</A>;

&lt;* FATAL FormsVBT.Error, FormsVBT.Unimplemented, Wr.Failure *&gt;

TYPE
  TieBreak = { FirstMatch, Random };
  Contention = { FurthestFirst, Random };

REVEAL
  <A NAME="T">T</A> = TPub BRANDED OBJECT
    pkt: Packet.Array;			 (* packet data *)
    activePkts: CARDINAL;		 (* active are in [0..activePkts-1] *)
    tieBreak: TieBreak;			 (* tie breaking heuristic *)
    contention: Contention;		 (* contention resolution heuristic *)
    outTbl: IntRefTbl.Default;		 (* Int -&gt; EdgeRec table *)
    inTbl: IntRefTbl.Default;		 (* Int -&gt; EdgeRec table *)
    minNbr: REF ARRAY OF CARDINAL;	 (* closest neighbors array *)
  OVERRIDES
    run := Run;
    newPkt := NewPkt;
  END;
</PRE> For <CODE>alg: T</CODE>, An entry <CODE>(i, l)</CODE> in <CODE>alg.outTbl</CODE> means that <CODE>l</CODE> is the list
   of packets leaving node <CODE>i</CODE>. In this list, there is only one entry for each
   of <CODE>i</CODE>'s neighbors <CODE>n</CODE>. If <CODE>e: EdgeRec</CODE> in <CODE>l</CODE>, then <CODE>e.neighbor</CODE> is the
   index of the neighbor <CODE>n</CODE>, and <CODE>e.head</CODE> is a list of packets scheduled to
   leave on the edge from <CODE>i</CODE> to <CODE>e.neighbor</CODE>. In the case of the
   furthest-first contention heuristic, the list of packets for any particular
   neighbor has exactly one packet, namely, the packet with furthest distance
   to travel.
<P>
   <CODE>alg.inTbl</CODE> is only used if <CODE>alg.maxQSz # -1</CODE> (i.e., if the node queues
   have bounded size). An entry <CODE>(i, l)</CODE> in <CODE>alg.inTbl</CODE> means that <CODE>l</CODE> is the
   list of packets entering node <CODE>i</CODE>. In this list, there is only one entry
   for each of <CODE>i</CODE>'s neighbors <CODE>n</CODE>. The <CODE>EdgeRec</CODE>'s in <CODE>l</CODE> have the same
   meaning as those in <CODE>outTbl</CODE>. Note, however, that there will be exactly
   *one* packet associated with each <CODE>EdgeRec</CODE> in <CODE>alg.inTbl</CODE>, since at most
   one packet flows along any edge at each step. 

<P><PRE>PROCEDURE <A NAME="New"><procedure>New</procedure></A>(): Algorithm.T =
  BEGIN
    RETURN NEW(T, data := ZeusPanel.NewForm(&quot;AlgGreedy.fv&quot;)).init()
  END New;

PROCEDURE <A NAME="CreateGraph"><procedure>CreateGraph</procedure></A>(alg: T): Graph.T
    RAISES {Topology.BadGraph, Thread.Alerted} =
</PRE><BLOCKQUOTE><EM> Create a new graph according to the form values in <CODE>alg</CODE>. Sets the value
   <CODE>alg.maxQSz</CODE>. Raises <CODE>BadGraph</CODE> if the graph cannot be created according to
   the given form values. </EM></BLOCKQUOTE><PRE>
  VAR
    topology: TEXT;			 (* name of topology choice *)
    bnd: BOOLEAN;			 (* is queue size bounded? *)
    maxQSz: INTEGER;			 (* max size of queue at each node *)
    res: Graph.T;
  BEGIN
    LOCK VBT.mu DO
      topology := FormsVBT.GetChoice(alg.data, &quot;topology&quot;);
      bnd := FormsVBT.GetBoolean(alg.data, &quot;bounded&quot;);
      maxQSz := FormsVBT.GetInteger(alg.data, &quot;maxQueueSize&quot;)
    END;
    IF bnd THEN alg.maxQSz := maxQSz ELSE alg.maxQSz := -1 END;
    (* generate the graph *)
    CASE Topology.FromName(topology) OF
      Topology.Kind.Grid =&gt;
        VAR w, h: INTEGER; BEGIN
          LOCK VBT.mu DO
            w := FormsVBT.GetInteger(alg.data, &quot;gWidth&quot;);
            h := FormsVBT.GetInteger(alg.data, &quot;gHeight&quot;);
          END;
          res := Topology.NewGrid(alg, w, h, maxQSz, bnd)
        END
    | Topology.Kind.Torus =&gt;
        VAR w, h: INTEGER; BEGIN
          LOCK VBT.mu DO
            w := FormsVBT.GetInteger(alg.data, &quot;tWidth&quot;);
            h := FormsVBT.GetInteger(alg.data, &quot;tHeight&quot;);
          END;
          res := Topology.NewTorus(alg, w, h, maxQSz, bnd)
        END
    | Topology.Kind.Butterfly =&gt;
        VAR dim: INTEGER; BEGIN
          LOCK VBT.mu DO dim := FormsVBT.GetInteger(alg.data, &quot;butDim&quot;) END;
          res := Topology.NewButterfly(alg, dim, maxQSz, bnd)
        END
    | Topology.Kind.FromFile =&gt;
        VAR fname: TEXT; BEGIN
          LOCK VBT.mu DO
            fname := FormsVBT.GetText(alg.data, &quot;networkFilename&quot;)
          END;
          TRY
            res := Topology.NewFromFile(alg, FileRd.Open(fname), maxQSz, bnd)
          EXCEPT
            OSError.E =&gt;
              RAISE Topology.BadGraph(&quot;Could not open file \&quot;&quot; &amp; fname &amp; &quot;\&quot;&quot;)
          END
        END
    END;
    IF res.numNodes() &lt; 2 THEN
      RAISE Topology.BadGraph(&quot;Graph must have at least 2 nodes&quot;)
    END;
    RETURN res
  END CreateGraph;

PROCEDURE <A NAME="InitQueueSz"><procedure>InitQueueSz</procedure></A>(alg: T) =
</PRE><BLOCKQUOTE><EM> Initialize <CODE>alg.qSz</CODE> to an array of 0 packets at each node. </EM></BLOCKQUOTE><PRE>
  VAR n := alg.graph.numNodes(); BEGIN
    alg.qSz := NEW(QueueSz, n);
    FOR i := 0 TO n - 1 DO alg.qSz[i] := 0 END;
  END InitQueueSz;

PROCEDURE <A NAME="CreatePkts"><procedure>CreatePkts</procedure></A>(alg: T): Packet.Array
    RAISES {Packet.BadPkts, Thread.Alerted} =
</PRE><BLOCKQUOTE><EM> Create and return a new set of packets according to the form values in
   <CODE>alg</CODE>. Raises <CODE>Packet.BadPkts</CODE> if the packets cannot be created according
   to the given form values. </EM></BLOCKQUOTE><PRE>
  VAR choice: TEXT; BEGIN
    LOCK VBT.mu DO choice := FormsVBT.GetChoice(alg.data, &quot;pktSources&quot;) END;
    CASE MakePackets.SourceFromName(choice) OF &lt;* NOWARN *&gt;
      MakePackets.Source.Random =&gt;
        VAR total: INTEGER; BEGIN
          LOCK VBT.mu DO
            total := FormsVBT.GetInteger(alg.data, &quot;pktsTotal&quot;)
          END;
          RETURN MakePackets.RandomPkts(alg, total)
        END
    | MakePackets.Source.Constant =&gt;
        VAR num: INTEGER; BEGIN
          LOCK VBT.mu DO
            num := FormsVBT.GetInteger(alg.data, &quot;pktsPerNode&quot;)
          END;
          RETURN MakePackets.ConstantPkts(alg, num)
        END
    | MakePackets.Source.FromFile =&gt;
        VAR fname: TEXT; BEGIN
          LOCK VBT.mu DO
            fname := FormsVBT.GetText(alg.data, &quot;pktsFilename&quot;)
          END;
          TRY RETURN MakePackets.FromFilePkts(alg, FileRd.Open(fname)) EXCEPT
            OSError.E =&gt;
              RAISE Packet.BadPkts(&quot;Could not open file \&quot;&quot; &amp; fname &amp; &quot;\&quot;&quot;)
          END
        END
    END
  END CreatePkts;

TYPE
  PktList = REF RECORD
    index: CARDINAL;			 (* index into alg.pkt[] array *)
    dist: INTEGER;
    next: PktList := NIL;
  END;

  EdgeRec = REF RECORD
    neighbor: CARDINAL;
    head: PktList := NIL;
    next: EdgeRec
  END;

PROCEDURE <A NAME="MovePkts"><procedure>MovePkts</procedure></A>(alg: T) RAISES {Thread.Alerted} =
</PRE><BLOCKQUOTE><EM> Move packets and generate the corresponding events until all packets have
   reached their final destinations. </EM></BLOCKQUOTE><PRE>
  BEGIN
    WHILE alg.activePkts &gt; 0 DO
      EVAL alg.outTbl.init(sizeHint := alg.graph.numNodes());
      FOR i := 0 TO alg.activePkts - 1 DO
        MovePktOut(alg, i, SelectNeighbor(alg, alg.pkt[i]))
      END;
      IF alg.contention = Contention.Random THEN
        SelectPktOut(alg)
      END;
      IF alg.maxQSz = -1 THEN
        MoveEligiblePktsOut(alg)
      ELSE
        MovePktsIn(alg);
        SelectPktsIn(alg);
        MoveEligiblePktsIn(alg)
      END;
      PktRouteIE.QueueSizes(alg, alg.qSz);
      DetectNonActive(alg);
      PktRouteIE.Step(alg)
    END
  END MovePkts;

PROCEDURE <A NAME="SelectNeighbor"><procedure>SelectNeighbor</procedure></A>(alg: T; READONLY pkt: Packet.T): CARDINAL =
</PRE><BLOCKQUOTE><EM> Return the index of the node adjacent to <CODE>pkt</CODE>'s current node for the
   packet <CODE>pkt</CODE> to move to next. </EM></BLOCKQUOTE><PRE>
  VAR nextNbr: CARDINAL := 0;
  PROCEDURE AddMinNbr(n: CARDINAL) =
    BEGIN
      IF nextNbr = NUMBER(alg.minNbr^) THEN
	VAR new := NEW(REF ARRAY OF CARDINAL, 2 * nextNbr); BEGIN
	  SUBARRAY(new^, 0, nextNbr) := alg.minNbr^;
	  alg.minNbr := new
	END
      END;
      alg.minNbr[nextNbr] := n;
      INC(nextNbr)
    END AddMinNbr;
  VAR
    it := alg.graph.neighbors(pkt.curr());
    n: CARDINAL; wt: REAL;
    dist := alg.unweighted.dist(pkt.curr(), pkt.dest());
  BEGIN
    WHILE it.next(n, wt) DO
      IF 1 + alg.unweighted.dist(n, pkt.dest()) = dist THEN
        CASE alg.tieBreak OF
          TieBreak.FirstMatch =&gt; RETURN n
        | TieBreak.Random =&gt; AddMinNbr(n)
        END
      END
    END;
    CASE alg.tieBreak OF &lt;* NOWARN *&gt;
      TieBreak.Random =&gt;
        &lt;* ASSERT nextNbr &gt; 0 *&gt;
        RETURN alg.minNbr[alg.random.integer(min := 0, max := nextNbr - 1)]
    END
  END SelectNeighbor;

PROCEDURE <A NAME="MovePktOut"><procedure>MovePktOut</procedure></A>(alg: T; pktIndex, neighbor: CARDINAL) =
</PRE><BLOCKQUOTE><EM> Assert that the packet <CODE>alg.pkt[pktIndex]</CODE> is destined for the neighbor
   node with id <CODE>neighbor</CODE>.
<P>
   If <CODE>alg.contention = Contention.FurthestFirst</CODE>, then only one packet is
   stored per neighbor. Otherwise, all packets destined for each neighbor are
   stored. </EM></BLOCKQUOTE><PRE>
  VAR
    ref: REFANY := NIL;
    outEdge: EdgeRec;
    pkt := alg.pkt[pktIndex];
    dist := alg.unweighted.dist(pkt.curr(), pkt.dest());
  BEGIN
    IF alg.outTbl.get(pkt.curr(), ref) THEN
      VAR curr: EdgeRec := ref; BEGIN
        WHILE curr # NIL DO
          IF neighbor = curr.neighbor THEN
            CASE alg.contention OF
              Contention.FurthestFirst =&gt;
                VAR first := curr.head; BEGIN
                  IF dist &gt; first.dist THEN
                    first.index := pktIndex;
                    first.dist := dist
                  END
                END
            | Contention.Random =&gt;
                curr.head := NEW(PktList, index := pktIndex, dist := dist,
                  next := curr.head)
            END;
            RETURN
          END;
          curr := curr.next
        END
      END
    END;
    outEdge := NEW(EdgeRec, neighbor := neighbor, next := ref,
      head := NEW(PktList, index := pktIndex, dist := dist));
    EVAL alg.outTbl.put(pkt.curr(), outEdge)
  END MovePktOut;

PROCEDURE <A NAME="PktListLength"><procedure>PktListLength</procedure></A>(pl: PktList): CARDINAL =
  VAR res: CARDINAL := 0; BEGIN
    WHILE pl # NIL DO INC(res); pl := pl.next END;
    RETURN res
  END PktListLength;

PROCEDURE <A NAME="PktListNth"><procedure>PktListNth</procedure></A>(pl: PktList; n: CARDINAL): PktList =
</PRE><BLOCKQUOTE><EM> Return the <CODE>n</CODE>th element of <CODE>pl</CODE>, where the first element has index 0. It
   is a checked run-time error if <CODE>n</CODE> is not in the range <CODE>[0..|pl|-1]</CODE>, where
   <CODE>|pl|</CODE> denotes the length of the list <CODE>pl</CODE>. </EM></BLOCKQUOTE><PRE>
  BEGIN
    &lt;* ASSERT n &gt;= 0 *&gt;
    WHILE n &gt; 0 DO DEC(n); pl := pl.next END;
    RETURN pl
  END PktListNth;

PROCEDURE <A NAME="SelectPktOut"><procedure>SelectPktOut</procedure></A>(alg: T) =
</PRE><BLOCKQUOTE><EM> Change <CODE>alg.outTbl</CODE> so that exactly one packet is destined for each
   neighbor. Requires <CODE>alg.contention = Contention.Random</CODE>. </EM></BLOCKQUOTE><PRE>
  VAR from: INTEGER; ref: REFANY; it := alg.outTbl.iterate(); BEGIN
    &lt;* ASSERT alg.contention = Contention.Random *&gt;
    WHILE it.next(from, ref) DO
      VAR curr: EdgeRec := ref; BEGIN
        WHILE curr # NIL DO
          VAR len := PktListLength(curr.head); BEGIN
            IF len &gt; 1 THEN
              curr.head := PktListNth(curr.head,
                alg.random.integer(min := 0, max := len - 1));
              curr.head.next := NIL
            END
          END;
          curr := curr.next
        END
      END
    END
  END SelectPktOut;

PROCEDURE <A NAME="MovePktsIn"><procedure>MovePktsIn</procedure></A>(alg: T) =
</PRE><BLOCKQUOTE><EM> Transfers the packets in <CODE>alg.outTbl</CODE> to <CODE>alg.inTbl</CODE> so they are indexed by
   <CODE>to</CODE> node instead of <CODE>from</CODE> node for processing by <CODE>SelectPktsIn</CODE>. Requires
   <CODE>alg.maxQSz # -1</CODE> (i.e., that the queue sizes are bounded). </EM></BLOCKQUOTE><PRE>
  PROCEDURE InsertSorted(
      VAR (*INOUT*) curr: EdgeRec;
      e: EdgeRec; dist: INTEGER) =
    BEGIN
      IF curr = NIL OR curr.head.dist &lt; dist
        THEN e.next := curr; curr := e
        ELSE InsertSorted(curr.next, e, dist)
      END
    END InsertSorted;
  PROCEDURE FindInNeighbor(from, to: CARDINAL; dist: INTEGER): EdgeRec =
  (* Return the &quot;EdgeRec&quot; in &quot;alg.inTbl&quot; denoting the edge from &quot;from&quot; to
     &quot;to&quot;. The table should not contain such an edge. Add a new such edge to
     the table and return it. *)
    VAR res, head: EdgeRec; ref: REFANY; BEGIN
      IF alg.inTbl.get(to, ref) THEN head := ref END;
      res := NEW(EdgeRec, neighbor := from);
      InsertSorted(head, res, dist);
      EVAL alg.inTbl.put(to, head);
      RETURN res
    END FindInNeighbor;
  PROCEDURE AddInPkt(from, to: CARDINAL; pkt: PktList) =
  (* Add the packet &quot;pkt&quot; to the edge from &quot;from&quot; to &quot;to&quot; in &quot;alg.inTbl&quot;. *)
    VAR edge: EdgeRec := FindInNeighbor(from, to, pkt.dist); BEGIN
      pkt.next := edge.head;
      edge.head := pkt
    END AddInPkt;
  VAR it := alg.outTbl.iterate(); from: INTEGER; ref: REFANY; BEGIN
    &lt;* ASSERT alg.maxQSz # -1 *&gt;
    EVAL alg.inTbl.init(sizeHint := alg.graph.numNodes());
    WHILE it.next(from, ref) DO
      VAR curr: EdgeRec := ref; pkt: PktList; BEGIN
        WHILE curr # NIL DO
          pkt := curr.head;
          WHILE pkt # NIL DO
            AddInPkt(from, curr.neighbor, NEW(PktList,
              index := pkt.index, dist := pkt.dist));
            pkt := pkt.next
          END;
          curr := curr.next
        END
      END
    END
  END MovePktsIn;

PROCEDURE <A NAME="TruncateSuffix"><procedure>TruncateSuffix</procedure></A>(VAR (*INOUT*) curr: EdgeRec; cnt: INTEGER): EdgeRec=
  BEGIN
    IF curr = NIL OR cnt = 0
      THEN VAR res := curr; BEGIN curr := NIL; RETURN res END
      ELSE RETURN TruncateSuffix(curr.next, cnt - 1)
    END
  END TruncateSuffix;

PROCEDURE <A NAME="EdgeRecLength"><procedure>EdgeRecLength</procedure></A>(l: EdgeRec): CARDINAL =
  VAR res: CARDINAL := 0; BEGIN
    WHILE l # NIL DO INC(res); l := l.next END;
    RETURN res
  END EdgeRecLength;

PROCEDURE <A NAME="DeleteNth"><procedure>DeleteNth</procedure></A>(VAR (*INOUT*) curr: EdgeRec; n: CARDINAL): EdgeRec =
</PRE><BLOCKQUOTE><EM> Destructively delete the <CODE>n</CODE>th <CODE>EdgeRec</CODE> structure from the list <CODE>curr</CODE>,
   and return the deleted <CODE>EdgeRec</CODE>. The value <CODE>n</CODE> must be in the interval
   <CODE>[0..EdgeRecLength(curr)-1]</CODE>. </EM></BLOCKQUOTE><PRE>
  BEGIN
    IF n = 0
      THEN VAR res := curr; BEGIN curr := curr.next; RETURN res END
      ELSE RETURN DeleteNth(curr.next, n - 1)
    END
  END DeleteNth;

PROCEDURE <A NAME="SelectPktsIn"><procedure>SelectPktsIn</procedure></A>(alg: T) RAISES {Thread.Alerted} =
</PRE><BLOCKQUOTE><EM> Change <CODE>alg.inTbl</CODE> so that the number of packets destined to each node does
   not exceed the number of empty spaces in the node's queue. Which packets
   are selected depends on the value of <CODE>alg.contention</CODE>. Requires <CODE>alg.maxQSz
   # -1</CODE> (i.e., that the queue sizes are bounded). </EM></BLOCKQUOTE><PRE>
  VAR to: INTEGER; ref: REFANY; it := alg.inTbl.iterate(); BEGIN
    &lt;* ASSERT alg.maxQSz # -1 *&gt;
    WHILE it.next(to, ref) DO
      VAR head: EdgeRec := ref; needed := alg.maxQSz - alg.qSz[to]; BEGIN
        CASE alg.contention OF
          Contention.FurthestFirst =&gt;
            VAR tail := TruncateSuffix(head, needed); BEGIN
              WHILE tail # NIL DO
                PktRouteIE.Blocked(alg, alg.pkt[tail.head.index].id,
                  tail.neighbor, to);
                tail := tail.next
              END
            END
        | Contention.Random =&gt;
            VAR
              len := EdgeRecLength(head);
              delCnt := len - needed;
              del: EdgeRec;
            BEGIN
              WHILE delCnt &gt; 0 DO
                del := DeleteNth(head, alg.random.integer(min:=0, max:=len-1));
                PktRouteIE.Blocked(alg, alg.pkt[del.head.index].id,
                  del.neighbor, to);
                DEC(len); DEC(delCnt)
              END
            END
        END;
        EVAL alg.inTbl.put(to, head)
      END
    END
  END SelectPktsIn;

PROCEDURE <A NAME="MoveEligiblePktsOut"><procedure>MoveEligiblePktsOut</procedure></A>(alg: T) RAISES {Thread.Alerted} =
</PRE><BLOCKQUOTE><EM> Generate the <CODE>MovePacket</CODE> events corresponding to the packets in the
   <CODE>alg.outTbl</CODE> data structure, update each such packet's <CODE>curr</CODE> field, and
   update the queue sizes <CODE>alg.qSz</CODE>. </EM></BLOCKQUOTE><PRE>
  VAR from: INTEGER; ref: REFANY; it := alg.outTbl.iterate(); BEGIN
    WHILE it.next(from, ref) DO
      VAR curr: EdgeRec := ref; BEGIN
        WHILE curr # NIL DO
          MoveEligiblePkt(alg, alg.pkt[curr.head.index],
            from, curr.neighbor);
          curr := curr.next
        END
      END
    END
  END MoveEligiblePktsOut;

PROCEDURE <A NAME="MoveEligiblePktsIn"><procedure>MoveEligiblePktsIn</procedure></A>(alg: T) RAISES {Thread.Alerted} =
</PRE><BLOCKQUOTE><EM> Generate the <CODE>MovePacket</CODE> events corresponding to the packets in the
   <CODE>alg.inTbl</CODE> data structure, update each such packet's <CODE>curr</CODE> field, and
   update the queue sizes <CODE>alg.qSz</CODE>. </EM></BLOCKQUOTE><PRE>
  VAR to: INTEGER; ref: REFANY; it := alg.inTbl.iterate(); BEGIN
    WHILE it.next(to, ref) DO
      VAR curr: EdgeRec := ref; BEGIN
        WHILE curr # NIL DO
          MoveEligiblePkt(alg, alg.pkt[curr.head.index],
            curr.neighbor, to);
          curr := curr.next
        END
      END
    END
  END MoveEligiblePktsIn;

PROCEDURE <A NAME="MoveEligiblePkt"><procedure>MoveEligiblePkt</procedure></A>(
    alg: T;
    VAR (*INOUT*) pkt: Packet.T;
    from, to: CARDINAL)
    RAISES {Thread.Alerted} =
  BEGIN
    PktRouteIE.MovePacket(alg, pkt.id, from, to);
    DEC(alg.qSz[from]);
    pkt.moveTo(to);
    INC(alg.qSz[to])
  END MoveEligiblePkt;

PROCEDURE <A NAME="DetectNonActive"><procedure>DetectNonActive</procedure></A>(alg: T) RAISES {Thread.Alerted} =
</PRE><BLOCKQUOTE><EM> Generate <CODE>Absorb</CODE> events and decrement the appropriate queue sizes for
   packets that are no longer active because they have reached their final
   desintations. Swap any non-active packets in <CODE>alg.pkt[0..alg.activePkts-1]</CODE>
   to the end of that array, and decrement <CODE>alg.activePkts</CODE> accordingly. </EM></BLOCKQUOTE><PRE>
  VAR i := 0; BEGIN
    WHILE i &lt; alg.activePkts DO
      WITH pkt = alg.pkt[i], last = alg.activePkts DO
        IF pkt.done() THEN
          PktRouteIE.Absorb(alg, pkt.id, pkt.dest());
          DEC(alg.qSz[pkt.dest()]);
          DEC(last);
          VAR t := pkt; BEGIN
            pkt := alg.pkt[last];
            alg.pkt[last] := t
          END
        ELSE
          INC(i)
        END
      END
    END
  END DetectNonActive;

PROCEDURE <A NAME="TieBreakFromName"><procedure>TieBreakFromName</procedure></A>(nm: TEXT): TieBreak =
  BEGIN
    IF Text.Equal(nm, &quot;firstMatch&quot;) THEN RETURN TieBreak.FirstMatch
    ELSIF Text.Equal(nm, &quot;randomMatch&quot;) THEN RETURN TieBreak.Random
    ELSE &lt;* ASSERT FALSE *&gt;
    END
  END TieBreakFromName;

PROCEDURE <A NAME="ContentionFromName"><procedure>ContentionFromName</procedure></A>(nm: TEXT): Contention =
  BEGIN
    IF Text.Equal(nm, &quot;furthestFirst&quot;) THEN RETURN Contention.FurthestFirst
    ELSIF Text.Equal(nm, &quot;randomFirst&quot;) THEN RETURN Contention.Random
    ELSE &lt;* ASSERT FALSE *&gt;
    END
  END ContentionFromName;

PROCEDURE <A NAME="Run"><procedure>Run</procedure></A>(alg: T) RAISES {Thread.Alerted} =
  VAR graph: Graph.T; BEGIN
    VAR fixed: BOOLEAN; BEGIN
      LOCK VBT.mu DO
        fixed := FormsVBT.GetBoolean(alg.data, &quot;fixed&quot;)
      END;
      alg.random := NEW(Random.Default).init(fixed := fixed)
    END;
    TRY
      graph := CreateGraph(alg);
      alg.graph := graph;
      alg.unweighted := ASP.UnweightedFromGraph(graph);
      InitQueueSz(alg);
      alg.pkt := CreatePkts(alg);
      alg.activePkts := NUMBER(alg.pkt^);
      alg.outTbl := NEW(IntRefTbl.Default);
      alg.inTbl := NEW(IntRefTbl.Default);
      alg.minNbr := NEW(REF ARRAY OF CARDINAL, 10);
      VAR tieRes, contentionRes: TEXT; BEGIN
        LOCK VBT.mu DO
          tieRes := FormsVBT.GetChoice(alg.data, &quot;tieRes&quot;);
          contentionRes := FormsVBT.GetChoice(alg.data, &quot;contentionRes&quot;)
        END;
        alg.tieBreak := TieBreakFromName(tieRes);
        alg.contention := ContentionFromName(contentionRes);
      END;
      MovePkts(alg)
    EXCEPT
    | Topology.BadGraph (msg) =&gt;
        Wr.PutText(Stdio.stderr, &quot;Graph Error: &quot; &amp; msg &amp; &quot;\n&quot;);
        Wr.Flush(Stdio.stderr)
    | Packet.BadPkts (msg) =&gt;
        Wr.PutText(Stdio.stderr, &quot;Packet Error: &quot; &amp; msg &amp; &quot;\n&quot;);
        Wr.Flush(Stdio.stderr)
    END;
  END Run;

PROCEDURE <A NAME="NewPkt"><procedure>NewPkt</procedure></A>(&lt;*UNUSED*&gt; alg: T): Packet.T =
  BEGIN RETURN NEW(Packet.T) END NewPkt;

BEGIN
  ZeusPanel.RegisterAlg(New, &quot;Greedy&quot;, &quot;PktRoute&quot;)
END AlgGreedy.
</PRE>
</inModule>
<PRE>























</PRE>
</BODY>
</HTML>
