<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: mentor/src/pktroute/Graph.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>mentor/src/pktroute/Graph.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright 1993 Digital Equipment Corporation.                             
 Distributed only by permission.                                           
                                                                           
 Last modified on Fri Jul 30 09:03:31 PDT 1993 by heydon                   

<P><PRE>MODULE <module>Graph</module> EXPORTS <A HREF="Graph.i3.html"><implements>Graph</A></implements>, <A HREF="GraphRep.i3.html"><implements>GraphRep</A></implements>;

IMPORT <A HREF="../../../libm3/src/fmtlex/Fmt.i3.html">Fmt</A>;

REVEAL
  <A NAME="T">T</A> = TRep BRANDED OBJECT OVERRIDES
    nodeName := NodeName
  END;

PROCEDURE <A NAME="NodeName"><procedure>NodeName</procedure></A>(&lt;*UNUSED*&gt; g: T; id: CARDINAL): TEXT =
  BEGIN RETURN Fmt.Int(id) END NodeName;

REVEAL
  <A NAME="Sparse">Sparse</A> = SparseRep BRANDED OBJECT OVERRIDES
    init      := Init;
    newNode   := NewNode;
    numNodes  := NumNodes;
    newEdge   := NewEdge;
    neighbors := Neighbors;
  END;

PROCEDURE <A NAME="Init"><procedure>Init</procedure></A>(g: Sparse; sizeHint: CARDINAL := 10): T =
  BEGIN
    g.adj := NEW(REF ARRAY OF NodeList, sizeHint);
    g.nodeCnt := 0;
    RETURN g
  END Init;

PROCEDURE <A NAME="NewNode"><procedure>NewNode</procedure></A>(g: Sparse): CARDINAL =
  VAR next := g.nodeCnt; BEGIN
    IF next &gt; LAST(g.adj^) THEN
      VAR new := NEW(REF ARRAY OF NodeList, g.nodeCnt * 2); BEGIN
        SUBARRAY(new^, 0, g.nodeCnt) := g.adj^;
        g.adj := new
      END
    END;
    g.adj[next] := NIL;
    INC(g.nodeCnt);
    RETURN next
  END NewNode;

PROCEDURE <A NAME="NumNodes"><procedure>NumNodes</procedure></A>(g: Sparse): CARDINAL =
  BEGIN RETURN g.nodeCnt END NumNodes;

PROCEDURE <A NAME="NewEdge"><procedure>NewEdge</procedure></A>(g: Sparse; id1, id2: CARDINAL; weight: REAL := 1.0) =
  PROCEDURE AddEdge(adj: REF ARRAY OF NodeList; from, to: CARDINAL) =
    VAR new := NEW(NodeList, node := to, weight := weight); BEGIN
      new.next := adj[from];
      adj[from] := new
    END AddEdge;
  BEGIN
    &lt;* ASSERT weight &gt;= 0.0 *&gt;
    AddEdge(g.adj, id1, id2);
    AddEdge(g.adj, id2, id1)
  END NewEdge;

TYPE
  SparseIt = Iterator BRANDED OBJECT
    curr: NodeList
  OVERRIDES
    next := SparseItNext
  END;

PROCEDURE <A NAME="Neighbors"><procedure>Neighbors</procedure></A>(g: Sparse; id: CARDINAL): Iterator =
  BEGIN
    RETURN NEW(SparseIt, curr := g.adj[id])
  END Neighbors;

PROCEDURE <A NAME="SparseItNext"><procedure>SparseItNext</procedure></A>(
    it: SparseIt;
    VAR (*OUT*) id: CARDINAL;
    VAR (*OUT*) weight: REAL):
    BOOLEAN =
  BEGIN
    WITH curr = it.curr DO
      IF curr = NIL THEN RETURN FALSE END;
      id := curr.node;
      weight := curr.weight;
      curr := curr.next
    END;
    RETURN TRUE
  END SparseItNext;

BEGIN
END Graph.
</PRE>
</inModule>
<PRE>























</PRE>
</BODY>
</HTML>
