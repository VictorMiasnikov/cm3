<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: mentor/src/kpaths/ShortestPath.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>mentor/src/kpaths/ShortestPath.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright 1993 by Digital Equipment Corp. 

<P><PRE>MODULE <module><implements><A HREF="ShortestPath.i3.html">ShortestPath</A></implements></module>;

IMPORT <A HREF="../../../libm3/src/atom/Atom.i3.html">Atom</A>, <A HREF="../../../libm3/derived/RefSeq.i3.html">RefSeq</A>, <A HREF="../../../libm3/derived/RefList.i3.html">RefList</A>, <A HREF="AtomVertexTbl.i3.html">AtomVertexTbl</A>, <A HREF="AtomEdgeTbl.i3.html">AtomEdgeTbl</A>, <A HREF="Edge.i3.html">Edge</A>, <A HREF="Vertex.i3.html">Vertex</A>;
IMPORT <A HREF="../../../libm3/src/arith/POSIX/Math.i3.html">Math</A>, <A HREF="EdgeSeq.i3.html">EdgeSeq</A>, <A HREF="VertexSeq.i3.html">VertexSeq</A>, <A HREF="../../../libm3/src/rw/Rd.i3.html">Rd</A>, <A HREF="../../../libm3/src/sx/Sx.i3.html">Sx</A>;
IMPORT <A HREF="../../../zeus/src/Algorithm.i3.html">Algorithm</A>, <A HREF="../../../m3core/src/thread/Common/Thread.i3.html">Thread</A>, <A HREF="../../../zeus/src/ZeusPanel.i3.html">ZeusPanel</A>, <A HREF="../../../vbtkit/src/vbtkitutils/Rsrc.i3.html">Rsrc</A>;
IMPORT <A HREF="../../derived/ShortestPathAlgClass.i3.html">ShortestPathAlgClass</A>, <A HREF="../../derived/ShortestPathIE.i3.html">ShortestPathIE</A>;

TYPE
  ZeusT = ShortestPathAlgClass.T BRANDED OBJECT
    OVERRIDES
      run := Run
    END;

PROCEDURE <A NAME="New"><procedure>New</procedure></A> (): Algorithm.T =
  BEGIN
    RETURN NEW(ZeusT (* , data := ZeusPanel.NewForm(&quot;input.fv&quot;) *)).init()
  END New;

PROCEDURE <A NAME="Run"><procedure>Run</procedure></A> (alg: ZeusT) RAISES {Thread.Alerted} =
  &lt;* FATAL Rsrc.NotFound *&gt;
  VAR
    graphRd := Rsrc.Open(&quot;graph.sx&quot;, ZeusPanel.GetPath());
  BEGIN
    (*
    LOCK VBT.mu DO
      N := FormsVBT.GetInteger(alg.data, &quot;N&quot;);
      K := FormsVBT.GetInteger(alg.data, &quot;K&quot;);
    END;
    *)
    LoadGraph(alg, graphRd)
  END Run;

VAR
  EdgeSym := Atom.FromText(&quot;Edge&quot;);
  VertexSym := Atom.FromText(&quot;Vertex&quot;);
  QuerySym := Atom.FromText(&quot;Find&quot;);

PROCEDURE <A NAME="LoadGraph"><procedure>LoadGraph</procedure></A> (alg: Algorithm.T; rd: Rd.T) RAISES {Thread.Alerted} =
  VAR t := NEW(T, alg := alg).init(3);
  BEGIN
    TRY
      LOOP
        VAR sx: RefList.T := Sx.Read(rd);
        BEGIN
          IF sx = NIL THEN
            RAISE Sx.ReadError(&quot;Because!&quot;)
          ELSIF sx.head = EdgeSym THEN
            t.addEdge(sx.tail)
          ELSIF sx.head = VertexSym THEN
            t.addVertex(sx.tail)
          ELSIF sx.head = QuerySym THEN
            EVAL
              t.shortestPath(sx.tail.head, sx.tail.tail.head,
                             NARROW(sx.tail.tail.tail.head, REF INTEGER)^);
            EXIT
          END
        END
      END
    EXCEPT
      Rd.EndOfFile =&gt;            (* SKIP *)
    | Sx.ReadError =&gt;
    END
  END LoadGraph;

REVEAL
  Vertex.<A NAME="T">T</A> = BRANDED REF RECORD
    name: Atom.T;
    edges: EdgeSeq.T;
    numPaths: CARDINAL;
    paths: REF ARRAY OF PathRec;
    qpos: INTEGER;
    x, y: REAL
  END;

TYPE
  PathRec = RECORD
    lastEdge: Edge.T;
    sourceIndex: CARDINAL;
    cost: REAL;
  END;

REVEAL
  Edge.<A NAME="T">T</A> = BRANDED REF RECORD
    name: Atom.T;
    cost: REAL;
    source, target: Vertex.T;
  END;
</PRE> For an edge <CODE>e</CODE>, <CODE>e.name</CODE> and <CODE>e.cost</CODE> are its name and cost;
   and <CODE>e.source</CODE> and <CODE>e.target</CODE> are the vertices it connects.
<P>
   For a vertex <CODE>v</CODE>, <CODE>v.name</CODE> is its name; <CODE>v.edges</CODE> is the sequence
   (in any order) of edges for which <CODE>v</CODE> is the source, <CODE>v.numPaths</CODE>
   is the number of ranks of paths that have been computed ending
   in <CODE>v</CODE>, and for <CODE>i &lt; v.numPaths</CODE>, the <CODE>PathRec</CODE> <CODE>v.paths[i].lastEdge</CODE>
   specifies the last edge of the <CODE>ith</CODE> shortest path ending in
   <CODE>v</CODE>, <CODE>v.paths[i].sourceIndex</CODE> specifies the rank of the maximal
   proper prefix of this path, and <CODE>v.paths[i].cost</CODE> specifies the
   cost of this path.  
<P>
   The entries in <CODE>v.paths[k]</CODE> for <CODE>k &gt;= v.numPaths</CODE> are also relevant
   if their <CODE>cost</CODE> is less than infinity: they are the candidates
   that have been computed for the path of rank <CODE>v.numPaths</CODE>, in
   order of increasing cost.  These are the so-called candidate
   paths.
<P>
   The value of <CODE>v.qpos</CODE> is the index of <CODE>v</CODE> in the priority
   queue (described below), or <CODE>-1</CODE> if <CODE>v</CODE> is not in the
   queue.
<P>
   <CODE>NUMBER(v.paths)</CODE> is at least equal to <CODE>t.ranklimit</CODE>, where <CODE>t</CODE>
   is the unique <CODE>ShortestPath.T</CODE> whose <CODE>addVertex</CODE> method created
   <CODE>v</CODE>. 

<P><PRE>REVEAL
  <A NAME="T">T</A> = Public BRANDED OBJECT
    alg: Algorithm.T := NIL;
    ranklimit: CARDINAL;
    source: Vertex.T;
    vertices: AtomVertexTbl.T;
    edges: AtomEdgeTbl.T;
    pqueue: VertexSeq.T;
    pendingEdges: RefSeq.T;
  OVERRIDES
    init := Init;
    addVertex := AddVertex;
    addEdge := AddEdge;
    shortestPath := ShortestPath;
    weight := Weight
  END;
</PRE> If <CODE>t</CODE> is a <CODE>ShortestPath.T</CODE>, then <CODE>t.ranklimit</CODE> is its rank limit,
   <CODE>q.source</CODE> is the last vertex that was the source of any shortest
   path query, or <CODE>NIL</CODE> if no such queries have been performed,
   <CODE>t.vertices</CODE> and <CODE>t.edges</CODE> map vertex and edge names to
   vertices and edges; and <CODE>pqueue</CODE> is a priority queue represented
   as a Floyd heap.  The vertices in the queue are those with a 
   non-infinite candidate cost (i.e., the priority is 
   <CODE>v.paths[v.numPaths].cost</CODE>, if this is defined).
   <P>
   The sequence <CODE>v.pendingEdges</CODE> contains the <CODE>RefList.T</CODE> that
   describes edges that have been created by <CODE>addEdge</CODE> but not
   yet added to the data structure.  It is necessary because
   edges can be added before their vertices.  

<P><PRE>PROCEDURE <A NAME="Init"><procedure>Init</procedure></A>(t: T; k: CARDINAL): T =
  BEGIN
    t.ranklimit := k;
    t.source := NIL;
    t.vertices := NEW(AtomVertexTbl.Default).init();
    t.edges := NEW(AtomEdgeTbl.Default).init();
    t.pqueue := NEW(VertexSeq.T).init();
    t.pendingEdges := NEW(RefSeq.T).init();
    RETURN t
  END Init;

PROCEDURE <A NAME="AddVertex"><procedure>AddVertex</procedure></A> (t: T; rl: RefList.T) RAISES {Thread.Alerted} =
  VAR
    v := NEW(Vertex.T, name := rl.head, edges := NEW(EdgeSeq.T).init(),
             numPaths := 0, x := NARROW(rl.tail.head, REF REAL)^,
             y := NARROW(rl.tail.tail.head, REF REAL)^,
             paths := NEW(REF ARRAY OF PathRec, t.ranklimit), qpos := -1);
  BEGIN
    FOR i := FIRST(v.paths^) TO LAST(v.paths^) DO
      v.paths[i].cost := LAST(REAL)
    END;
    EVAL t.vertices.put(rl.head, v);
    ShortestPathIE.NewVertex(t.alg, Atom.ToText(v.name), v.x, v.y)
  END AddVertex;

PROCEDURE <A NAME="AddEdge"><procedure>AddEdge</procedure></A>(t: T; rl: RefList.T) =
  BEGIN
    t.pendingEdges.addhi(rl)
  END AddEdge;

PROCEDURE <A NAME="ProcessPending"><procedure>ProcessPending</procedure></A>(t: T) RAISES {Thread.Alerted} =
  VAR i := 0; BEGIN
    WHILE i # t.pendingEdges.size() DO
      (* The first &quot;i&quot; pending edges refer to absent vertices and
         therefore must be retained. *)
      VAR
        rl: RefList.T := t.pendingEdges.get(i);
        name: Atom.T := rl.head;
        sourceName: Atom.T := rl.tail.head;
        targetName: Atom.T := rl.tail.tail.head;
        orientation: Atom.T := rl.tail.tail.tail.head;
        source, target: Vertex.T;
        cost: REAL;
        e: Edge.T;
      BEGIN
        IF t.vertices.get(sourceName, source) AND
          t.vertices.get(targetName, target)
        THEN
          cost :=
              FLOAT(Math.hypot(FLOAT(source.x - target.x, LONGREAL),
                               FLOAT(source.y - target.y, LONGREAL)), REAL);
          (* IF rl.tail.tail.tail # NIL THEN
            cost := NARROW(rl.tail.tail.tail.head, REF REAL)^
          ELSE
            cost := 1.0
          END;
          *)
          e := NEW(Edge.T, name := name, source := source,
            target := target, cost := cost);
          ShortestPathIE.NewEdge(
            t.alg, Atom.ToText(name), Atom.ToText(sourceName),
            Atom.ToText(targetName), Atom.ToText(orientation));
          source.edges.addhi(e);
          t.source := NIL;
          IF i # t.pendingEdges.size() - 1 THEN
            t.pendingEdges.put(i, t.pendingEdges.remhi())
          ELSE
            EVAL t.pendingEdges.remhi()
          END
        ELSE
          INC(i)
        END
      END
    END
  END ProcessPending;

PROCEDURE <A NAME="ShortestPath"><procedure>ShortestPath</procedure></A> (t                     : T;
                        sourceName, targetName: Atom.T;
                        rank                  : CARDINAL): RefList.T
  RAISES {Thread.Alerted} =
  VAR source, target: Vertex.T;
  BEGIN
    ProcessPending(t);
    IF NOT t.vertices.get(sourceName, source)
         OR NOT t.vertices.get(targetName, target) THEN
      RETURN NIL
    END;
    ShortestPathIE.StartFind(
      t.alg, Atom.ToText(sourceName), Atom.ToText(targetName));
    IF source # t.source OR rank &gt;= t.ranklimit THEN
      t.source := source;
      t.ranklimit := MAX(rank + 1, t.ranklimit);
      Reset(t)
    END;
    WHILE target.numPaths &lt;= rank AND t.pqueue.size() # 0 DO
      PromoteCandidate(t)
    END;
    IF target.numPaths &gt; rank THEN
      RETURN ConsPath(t, target, rank)
    ELSE
      RETURN NIL
    END
  END ShortestPath;

PROCEDURE <A NAME="Reset"><procedure>Reset</procedure></A>(t: T) =
</PRE><BLOCKQUOTE><EM> The values <CODE>t.source</CODE> and/or <CODE>t.ranklimit</CODE> have changed: set the 
   <CODE>numPaths</CODE>, <CODE>paths</CODE>, and <CODE>qpos</CODE> fields of each vertex, and the <CODE>pqueue</CODE> 
   field of <CODE>t</CODE>, to satisfy the invariants listed above.  </EM></BLOCKQUOTE><PRE>
  VAR iter := t.vertices.iterate();
    name: Atom.T;
    vertex: Vertex.T;
  BEGIN
    WHILE iter.next(name, vertex) DO
      IF vertex.paths = NIL OR NUMBER(vertex.paths^) &lt; t.ranklimit THEN
        vertex.paths := NEW(REF ARRAY OF PathRec, t.ranklimit)
      END;
      FOR i := FIRST(vertex.paths^) TO LAST(vertex.paths^) DO
        vertex.paths[i].cost := LAST(REAL)
      END;
      vertex.numPaths := 0;
      vertex.qpos := -1
    END;
    t.source.paths[0].cost := 0.0;
    t.source.paths[0].lastEdge := NIL;
    t.source.paths[0].sourceIndex := 0;
    t.pqueue := NEW(VertexSeq.T).init();
    t.pqueue.addhi(t.source);
    t.source.qpos := 0
  END Reset;

PROCEDURE <A NAME="PromoteCandidate"><procedure>PromoteCandidate</procedure></A> (t: T) RAISES {Thread.Alerted} =
  (* Promote the lowest-cost candidate path to be an answer path.  Requires
     &quot;t.pqueue&quot; not be empty. *)
  VAR v := DeleteMin(t);
  BEGIN
    IF v.paths[v.numPaths].lastEdge # NIL THEN
      ShortestPathIE.Promote(
        t.alg, Atom.ToText(v.paths[v.numPaths].lastEdge.name),
        v.paths[v.numPaths].sourceIndex, v.numPaths)
    END;
    INC(v.numPaths);
    IF v.numPaths &lt; NUMBER(v.paths^)
         AND v.paths[v.numPaths].cost &lt; LAST(REAL) THEN
      SetPriority(t, v)
    END;
    FOR i := 0 TO v.edges.size() - 1 DO ConsiderEdge(t, v.edges.get(i)) END
  END PromoteCandidate;

PROCEDURE <A NAME="ConsiderEdge"><procedure>ConsiderEdge</procedure></A>(t: T; e: Edge.T) RAISES {Thread.Alerted} =
 (* Change &quot;e.target.paths&quot; as
   necessary to reflect the fact that a candidate path ending
   in &quot;e.source&quot; has just been promoted to an answer path. *)
  VAR
    source := e.source;
    target := e.target;
    sourceIndex := source.numPaths - 1;
    newCost := e.cost + source.paths[sourceIndex].cost;
  BEGIN
    IF target.numPaths &gt;= t.ranklimit
       OR newCost &gt;= target.paths[t.ranklimit-1].cost
    THEN
      ShortestPathIE.Consider(t.alg, Atom.ToText(e.name), sourceIndex, -1);
      RETURN
    END;
    VAR k := t.ranklimit-1; BEGIN
      WHILE k &gt; target.numPaths AND target.paths[k-1].cost &gt; newCost DO
        target.paths[k] := target.paths[k - 1];
        k := k - 1
      END;
      WITH pathRec = target.paths[k] DO
        pathRec.lastEdge := e;
        pathRec.sourceIndex := sourceIndex;
        pathRec.cost := newCost
      END;
      ShortestPathIE.Consider(t.alg, Atom.ToText(e.name), sourceIndex, k);
      IF k = target.numPaths THEN
        SetPriority(t, target)
      END
    END
  END ConsiderEdge;

PROCEDURE <A NAME="DeleteMin"><procedure>DeleteMin</procedure></A>(t: T): Vertex.T =
 (* Delete from &quot;t.queue&quot; and return the lowest cost vertex.
   Requires &quot;t.pqueue&quot; not be empty. *)
  VAR
    res: Vertex.T := t.pqueue.get(0);
    oldLast: Vertex.T := t.pqueue.remhi();
    oldCost := oldLast.paths[oldLast.numPaths].cost;
    i := 0;
    n := t.pqueue.size();
  PROCEDURE Cost(i: INTEGER): REAL =
    BEGIN
      IF i &gt;= n THEN
        RETURN LAST(REAL)
      ELSE
        VAR v: Vertex.T := t.pqueue.get(i); BEGIN
          RETURN v.paths[v.numPaths].cost
        END
      END
    END Cost;
  BEGIN
    res.qpos := -1;
    IF n = 0 THEN RETURN res END;
    LOOP
      VAR lCost := Cost(2 * i + 1); rCost := Cost(2 * i + 2); BEGIN
        IF lCost &lt;= rCost AND lCost &lt; oldCost THEN
          VAR ch: Vertex.T := t.pqueue.get(2*i+1); BEGIN
            t.pqueue.put(i, ch);
            ch.qpos := i;
            i := 2 * i + 1
          END
        ELSIF rCost &lt;= lCost AND rCost &lt; oldCost THEN
          VAR ch: Vertex.T := t.pqueue.get(2*i+2); BEGIN
            t.pqueue.put(i, ch);
            ch.qpos := i;
            i := 2 * i + 2
          END
        ELSE
          EXIT
        END
      END
    END;
    t.pqueue.put(i, oldLast);
    oldLast.qpos := i;
    RETURN res
  END DeleteMin;

PROCEDURE <A NAME="SetPriority"><procedure>SetPriority</procedure></A>(t: T; v: Vertex.T) =
</PRE><BLOCKQUOTE><EM> Insert <CODE>v</CODE> in <CODE>t</CODE>'s priority queue if it is not present,
   or adjust its priority if it is. If <CODE>v</CODE> is already present,
   it is required that its new cost be at most its old cost. </EM></BLOCKQUOTE><PRE>
  BEGIN
    IF v.qpos &lt; 0 THEN
      t.pqueue.addhi(v);
      v.qpos := t.pqueue.size() - 1
    END;
    VAR
      i := v.qpos;
      parent: CARDINAL;
      vCost := v.paths[v.numPaths].cost;
      parentV: Vertex.T;
    BEGIN
      WHILE i &gt; 0 DO
        parent := (i - 1) DIV 2;
        parentV := t.pqueue.get(parent);
        IF parentV.paths[parentV.numPaths].cost &gt; vCost THEN
          t.pqueue.put(i, parentV);
          parentV.qpos := i;
          i := parent
        ELSE
          EXIT
        END
      END;
      t.pqueue.put(i, v);
      v.qpos := i
    END
  END SetPriority;

PROCEDURE <A NAME="ConsPath"><procedure>ConsPath</procedure></A> (t: T; target: Vertex.T; rank: CARDINAL): RefList.T
  RAISES {Thread.Alerted} =
  (* Return the list representing the path from &quot;t.source&quot; to &quot;target&quot; of
     rank &quot;k&quot;.  Requires that this path be present in &quot;target.paths&quot;. *)
  VAR
    cur       := target;
    res       := RefList.Cons(cur.name, NIL);
    nextEdge  := cur.paths[rank].lastEdge;
    prevIndex := rank;
    nextIndex := cur.paths[rank].sourceIndex;
  BEGIN
    WHILE nextEdge # NIL DO
      (* &quot;res&quot; is the reverse of the path from &quot;cur&quot; to &quot;target&quot;, including
         its vertices. *)
      ShortestPathIE.Traceback(
        t.alg, Atom.ToText(nextEdge.name), nextIndex, prevIndex);
      res := RefList.Cons(nextEdge.name, res);
      cur := nextEdge.source;
      res := RefList.Cons(cur.name, res);
      nextEdge := cur.paths[nextIndex].lastEdge;
      prevIndex := nextIndex;
      nextIndex := cur.paths[nextIndex].sourceIndex
    END;
    RETURN res
  END ConsPath;

PROCEDURE <A NAME="Weight"><procedure>Weight</procedure></A>(t: T; edgeName: Atom.T): REAL =
  VAR e: Edge.T; BEGIN
    IF t.edges.get(edgeName, e) THEN
      RETURN e.cost
    ELSE
      RETURN -1.0
    END
  END Weight;

BEGIN
  ZeusPanel.RegisterAlg(New, &quot;Shortest Path&quot;, &quot;ShortestPath&quot;);
END ShortestPath.
</PRE>
</inModule>
<PRE>























</PRE>
</BODY>
</HTML>
