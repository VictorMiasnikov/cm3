<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: mentor/src/dgraph/GraphView.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>mentor/src/dgraph/GraphView.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright (C) 1994, Digital Equipment Corporation           
 All rights reserved.                                        
 See the file COPYRIGHT for a full description.              

<P><PRE>MODULE <module><implements><A HREF="GraphView.i3.html">GraphView</A></implements></module>;

IMPORT <A HREF="../../derived/DGraphViewClass.i3.html">DGraphViewClass</A>, <A HREF="../../../ui/src/vbt/PaintOp.i3.html">PaintOp</A>, <A HREF="#x1">GraphVBT</A>, <A HREF="#x2">View</A>, <A HREF="../../../zeus/src/ZeusPanel.i3.html">ZeusPanel</A>,
       <A HREF="DepthToColor.i3.html">DepthToColor</A>, <A HREF="../../../ui/src/split/Filter.i3.html">Filter</A>, <A HREF="../../../ui/src/split/TextVBT.i3.html">TextVBT</A>, <A HREF="AdjMatrix.i3.html">AdjMatrix</A>, <A HREF="../../../mg/src/R2.i3.html">R2</A>;

TYPE
 TI = DGraphViewClass.T BRANDED OBJECT
    g: GraphVBT.T;
    v: REF ARRAY OF GraphVBT.Vertex;
  OVERRIDES
    oeSetup := SetupT1;
  END;

  T = TI OBJECT
    markEdgeSave: MarkEdgeSave;
    markVertexSave: MarkVertexSave;
  OVERRIDES
    oeSetup := SetupT;
    oeAddEdge := AddEdge;
    oeMarkEdge := MarkEdge;
    oeMarkVertex := MarkVertex;
    oeUnMarkEdge := UnMarkEdge;
    oeUnMarkVertex := UnMarkVertex;
  END; (* object *)

  MarkEdgeSave = REF ARRAY OF RECORD from, to: INTEGER; color: PaintOp.T END;
  MarkVertexSave = REF ARRAY OF RECORD index: INTEGER; color: PaintOp.T END;

  MyVertex = GraphVBT.Vertex OBJECT v: INTEGER END;

PROCEDURE <A NAME="SetupT1"><procedure>SetupT1</procedure></A>(t: TI; m: AdjMatrix.T) RAISES {}=
  VAR nVertices := m.nVertices();
  BEGIN
    t.g := NEW(GraphVBT.T).init();
    EVAL Filter.Replace(t, t.g);
    t.v := NEW(REF ARRAY OF GraphVBT.Vertex, nVertices);
    FOR i := 0 TO nVertices-1 DO
      WITH coord = m.coord(i) DO
        t.v[i] := NEW(MyVertex, graph := t.g,
                      pos := coord,
                      color := PaintOp.Bg,
                      shape := GraphVBT.VertexShape.Ellipse,
                      size := R2.T{0.075,0.075},
                      label := m.name(i),
                      fontColor := PaintOp.Fg,
                      border := 0.005,
                      v := i).init();
      END;
    END; (* for *)
    VAR i, j: INTEGER;
     BEGIN
      WITH iter = m.edgeIter() DO
        WHILE iter.next(i, j) DO
          AddEdge(t, i, j);
        END; (* while *)
      END; (* with *)
    END;
    t.g.redisplay();
  END SetupT1;

PROCEDURE <A NAME="SetupT"><procedure>SetupT</procedure></A>(t: T; m: AdjMatrix.T) RAISES {}=
  VAR nVertices := m.nVertices();
  BEGIN
    SetupT1(t, m);
    t.markEdgeSave := NEW(MarkEdgeSave, nVertices);
    t.markVertexSave := NEW(MarkVertexSave, nVertices);
    FOR i := 0 TO nVertices-1 DO
      WITH entry = t.markEdgeSave[i] DO
        entry.from := -1; entry.to := -1;
      END;
      WITH entry = t.markVertexSave[i] DO
        entry.index := -1;
      END;
    END; (* for *)
  END SetupT;

PROCEDURE <A NAME="AddEdge"><procedure>AddEdge</procedure></A>(t: TI; i, j: INTEGER) RAISES {}=
  VAR edge: GraphVBT.Edge;
  BEGIN
    LOCK t.g.mu DO
      edge := FindEdge(t, i, j);
    END;
    IF edge = NIL THEN
      edge := NEW(GraphVBT.Edge,
                vertex0 := t.v[i], vertex1 := t.v[j],
                arrow := ARRAY [0 .. 1] OF BOOLEAN{FALSE, TRUE},
                color := DepthToColor.Map(-1)).init();
      t.g.redisplay();
    END;
  END AddEdge;

PROCEDURE <A NAME="MarkEdge"><procedure>MarkEdge</procedure></A>(t: T; i, j: INTEGER; depth: INTEGER) RAISES {}=
  BEGIN
    LOCK t.g.mu DO
      WITH edge = FindEdge(t, i, j) DO
        IF edge # NIL THEN
          WITH entry = t.markEdgeSave[depth] DO
            entry.from := i; entry.to := j;
            entry.color := edge.color;
            edge.setColor(DepthToColor.Map(depth));
          END;
        END;
      END;
    END; (* lock *)
    t.g.redisplay();
  END MarkEdge;

PROCEDURE <A NAME="UnMarkEdge"><procedure>UnMarkEdge</procedure></A>(t: T; i, j: INTEGER; depth: INTEGER) RAISES {}=
  BEGIN
    LOCK t.g.mu DO
      WITH edge = FindEdge(t, i, j) DO
        IF edge # NIL THEN
          WITH entry = t.markEdgeSave[depth] DO
            IF entry.from &gt;= 0 THEN
              edge.setColor(entry.color);
            END;
          END;
        END;
      END;
    END; (* lock *)
    t.g.redisplay();
  END UnMarkEdge;

PROCEDURE <A NAME="MarkVertex"><procedure>MarkVertex</procedure></A>(t: T; i: INTEGER; depth: INTEGER;
                     &lt;* UNUSED *&gt; rcset: AdjMatrix.RCSet) RAISES {}=
  BEGIN
    LOCK t.g.mu DO
      WITH entry = t.markVertexSave[depth] DO
        entry.index := i;
        entry.color := t.v[i].color;
        t.v[i].setColor(DepthToColor.Map(depth));
      END;
    END; (* lock *)
    t.g.redisplay();
  END MarkVertex;

PROCEDURE <A NAME="UnMarkVertex"><procedure>UnMarkVertex</procedure></A>(t: T; i: INTEGER; depth: INTEGER;
                       &lt;* UNUSED *&gt; rcset: AdjMatrix.RCSet) RAISES {}=
  BEGIN
    LOCK t.g.mu DO
      WITH entry = t.markVertexSave[depth] DO
        IF entry.index &gt;= 0 THEN
          t.v[i].setColor(entry.color);
        END;
      END;
    END; (* lock *)
    t.g.redisplay();
  END UnMarkVertex;

PROCEDURE <A NAME="FindEdge"><procedure>FindEdge</procedure></A>(t: TI; i, j: INTEGER): GraphVBT.Edge RAISES {}=
  VAR edgeList := t.v[i].edges;
  BEGIN
    WHILE edgeList # NIL DO
      VAR edge := NARROW (edgeList.head, GraphVBT.Edge);
      BEGIN
        IF edge.vertex0 = t.v[i] AND edge.vertex1 = t.v[j] THEN
          RETURN edge
        END;
      END;
      edgeList := edgeList.tail;
    END; (* while *)
    RETURN NIL;
  END FindEdge;

PROCEDURE <A NAME="NewT"><procedure>NewT</procedure></A>(): View.T RAISES {}=
  VAR dummy := TextVBT.New(&quot;Graph&quot;);
  BEGIN
    RETURN NEW(T).init(dummy);
  END NewT;

PROCEDURE <A NAME="NewTI"><procedure>NewTI</procedure></A>(): View.T RAISES {}=
  VAR dummy := TextVBT.New(&quot;Initial Graph&quot;);
  BEGIN
    RETURN NEW(TI).init(dummy);
  END NewTI;

BEGIN
  ZeusPanel.RegisterView (NewTI, &quot;Initial Graph&quot;, &quot;DGraph&quot;);
  ZeusPanel.RegisterView (NewT, &quot;Graph&quot;, &quot;DGraph&quot;);
END GraphView.
</PRE>
</inModule>
<HR>
<A NAME="x1">interface GraphVBT is in:
</A><UL>
<LI><A HREF="../../../fisheye/src/GraphVBT.i3.html#0TOP0">fisheye/src/GraphVBT.i3</A>
<LI><A HREF="../../../mgkit/src/GraphVBT.i3.html#0TOP0">mgkit/src/GraphVBT.i3</A>
</UL>
<P>
<HR>
<A NAME="x2">interface View is in:
</A><UL>
<LI><A HREF="../../../juno-app/src/View.i3.html#0TOP0">juno-app/src/View.i3</A>
<LI><A HREF="../../../zeus/src/View.i3.html#0TOP0">zeus/src/View.i3</A>
</UL>
<P>
<PRE>























</PRE>
</BODY>
</HTML>
