<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: mentor/src/hash/HashViews.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>mentor/src/hash/HashViews.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright 1992 Digital Equipment Corporation.           
 Distributed only by permission.                         
      modified on Wed Jul 22 00:37:01 1992 by mhb        

<P><PRE>MODULE <module><implements><A HREF="HashViews.i3.html">HashViews</A></implements></module>;

IMPORT <A HREF="../../../libm3/src/geometry/Axis.i3.html">Axis</A>, <A HREF="../../../vbtkit/src/color/ColorName.i3.html">ColorName</A>, <A HREF="../../../ui/src/split/Filter.i3.html">Filter</A>, <A HREF="../../../formsvbt/src/FormsVBT.i3.html">FormsVBT</A>, <A HREF="#x1">GraphVBT</A>, <A HREF="../../derived/HashIE.i3.html">HashIE</A>, <A HREF="../../derived/HashViewClass.i3.html">HashViewClass</A>,
       <A HREF="../../../libm3/derived/IntList.i3.html">IntList</A>, <A HREF="../../../ui/src/vbt/PaintOp.i3.html">PaintOp</A>, <A HREF="../../../mg/src/R2.i3.html">R2</A>, <A HREF="../../../libm3/src/geometry/Rect.i3.html">Rect</A>, <A HREF="../../../m3core/src/text/Text.i3.html">Text</A>, <A HREF="../../../ui/src/split/TextVBT.i3.html">TextVBT</A>, <A HREF="../../../m3core/src/thread/Common/Thread.i3.html">Thread</A>, <A HREF="../../../ui/src/vbt/VBT.i3.html">VBT</A>, <A HREF="#x2">View</A>, <A HREF="../../../zeus/src/ZeusPanel.i3.html">ZeusPanel</A>;

TYPE
  Level = [0..3];
  T = HashViewClass.T BRANDED OBJECT
        mg: MyGraphVBT;
        bucketContents : REF ARRAY OF GraphVBT.Vertex;
        bucketState: REF ARRAY OF GraphVBT.Vertex;
        oldColors: REF ARRAY OF ARRAY Level OF PaintOp.T;
        level: Level;
        opInsert, opDelete, opFind, item: GraphVBT.Vertex;
        operationHighlight, itemHighlight: GraphVBT.VertexHighlight;
        normalColor, compareColor, newColor, checkDeletableColor,
          checkHashPositionColor, operationHighlightColor,
          itemColor, nilColor, emptyColor: PaintOp.T;
      OVERRIDES
        oeSetup := Setup;
        oeInsert := Insert;
        oeDelete := Delete;
        oeFind := Find;
        oeCompare := Compare;
        oeAddToBucket := AddToBucket;
        oeDeleteFromBucket := DeleteFromBucket;
        oeCheckDeletable := CheckDeletable;
        oeCheckHashPosition := CheckHashPosition;
        ueFindReport := FindReport;
        ueStopFindReport := StopFindReport;
      END;

  MyGraphVBT = GraphVBT.T OBJECT
      view: T;
      showingFindPath: BOOLEAN := FALSE;
    OVERRIDES
      mouse := Mouse;
    END;

PROCEDURE <A NAME="Mouse"><procedure>Mouse</procedure></A> (self: MyGraphVBT; READONLY cd: VBT.MouseRec) =
  &lt;*FATAL Thread.Alerted*&gt;
  BEGIN
    LOCK self.mu DO
      IF (cd.clickType = VBT.ClickType.FirstDown) AND NOT cd.cp.gone
           AND NOT cd.cp.offScreen AND NOT self.showingFindPath THEN
        (* Show find path *)
        WITH list = self.verticesAt(Rect.FromPoint(cd.cp.pt)) DO
          IF list = NIL THEN RETURN END;
          WITH label = NARROW (list.head, GraphVBT.Vertex).label DO
            IF label = NIL OR Text.Empty(label) OR Text.Equal(label, &quot;NIL&quot;)
                 OR Text.Equal(label, &quot;EMPTY&quot;) THEN
              RETURN;
            END;
            Thread.Release(self.mu);
            TRY
              HashIE.ReportFind(self.view, label);
            FINALLY
              Thread.Acquire(self.mu);
            END;
          END;
        END;
      ELSIF self.showingFindPath THEN
        Thread.Release(self.mu);
        TRY
          HashIE.StopReportFind(self.view);
        FINALLY
          Thread.Acquire(self.mu);
        END;
      END;
    END;
  END Mouse;

PROCEDURE <A NAME="FindReport"><procedure>FindReport</procedure></A> (view: T; buckets: IntList.T) =
  BEGIN
    IF view.mg = NIL THEN RETURN; END;
    LOCK view.mg.mu DO
      FOR i := 0 TO LAST(view.bucketState^) DO
        SetBucketColor(view, i, PaintOp.Bg, 3);
      END;
      WHILE buckets # NIL DO
        SetBucketColor(view, buckets.head, view.compareColor, 3);
        buckets := buckets.tail;
      END;
      view.mg.showingFindPath := TRUE;
    END;
    view.mg.redisplay();
  END FindReport;

PROCEDURE <A NAME="StopFindReport"><procedure>StopFindReport</procedure></A> (view: T) =
  BEGIN
    IF view.mg = NIL THEN RETURN; END;
    LOCK view.mg.mu DO
      ResetColors(view, 2);
      view.mg.showingFindPath := FALSE;
    END;
    view.mg.redisplay();
  END StopFindReport;

PROCEDURE <A NAME="Setup"><procedure>Setup</procedure></A> (view: T; data: FormsVBT.T; nBuckets: INTEGER) =

  PROCEDURE GetColor (name: TEXT): PaintOp.T =
    &lt;* FATAL ColorName.NotFound, FormsVBT.Error, FormsVBT.Unimplemented *&gt;
    VAR rgb := ColorName.ToRGB(FormsVBT.GetText(data, name));
    BEGIN
      RETURN PaintOp.FromRGB(
               rgb.r, rgb.g, rgb.b,
               PaintOp.Mode.Accurate);
    END GetColor;

  BEGIN
    view.normalColor := GetColor(&quot;NormalColor&quot;);
    view.nilColor := GetColor(&quot;NilColor&quot;);
    view.emptyColor := GetColor(&quot;EmptyColor&quot;);
    view.compareColor := GetColor(&quot;CompareColor&quot;);
    view.newColor := GetColor(&quot;NewColor&quot;);
    view.checkDeletableColor := GetColor(&quot;CheckDeletableColor&quot;);
    view.checkHashPositionColor := GetColor(&quot;CheckHashPositionColor&quot;);
    view.operationHighlightColor := GetColor(&quot;OperationHighlightColor&quot;);
    view.itemColor := GetColor(&quot;ItemColor&quot;);

    view.mg := NEW(MyGraphVBT, world := GraphVBT.WorldRectangle{
                                          w := -3.0, e := 2.5, n :=
                                          FLOAT(nBuckets + 1), s := -1.0},
                   pixelSizeDivisor :=
                     ARRAY [0 .. 1] OF CARDINAL{1, 2 * (nBuckets + 2)},
                   view := view).init();
    view.bucketContents := NEW(REF ARRAY OF GraphVBT.Vertex, nBuckets);
    FOR i := 0 TO LAST(view.bucketContents^) DO
      view.bucketContents[i] :=
        NEW(GraphVBT.Vertex, graph := view.mg, pos := R2.T{0.0, FLOAT(i)},
            label := &quot;NIL&quot;, color := view.nilColor, border := 0.005,
            borderColor := PaintOp.Bg,
            shape := GraphVBT.VertexShape.Rectangle,
            fontColor := PaintOp.Fg, size := R2.T{1.0, 1.0}).init();
    END;
    view.bucketState := NEW(REF ARRAY OF GraphVBT.Vertex, nBuckets);
    FOR i := 0 TO LAST(view.bucketState^) DO
      view.bucketState[i] :=
        NEW(GraphVBT.Vertex, graph := view.mg, pos := R2.T{1.25, FLOAT(i)},
            color := PaintOp.Bg, shape := GraphVBT.VertexShape.Ellipse,
            size := R2.T{1.0, 1.0}).init();
    END;
    WITH opFont = view.mg.font(weight := &quot;bold&quot;, size := 0.05) DO
      view.opInsert :=
        NEW(GraphVBT.Vertex, graph := view.mg,
            pos := R2.T{-2.25, FLOAT(nBuckets)}, font := opFont,
            size := R2.T{1.0, 1.0}, fontColor := PaintOp.Fg,
            label := &quot;INS&quot;, color := view.newColor).init();
      view.opDelete :=
        NEW(
          GraphVBT.Vertex, graph := view.mg,
          pos := R2.T{-2.25, FLOAT(nBuckets - 2)}, size := R2.T{1.0, 1.0},
          fontColor := PaintOp.Fg, label := &quot;DEL&quot;, font := opFont,
          color := view.checkDeletableColor).init();
      view.opFind := NEW(GraphVBT.Vertex, graph := view.mg,
                         pos := R2.T{-2.25, FLOAT(nBuckets - 4)},
                         size := R2.T{1.0, 1.0}, fontColor := PaintOp.Fg,
                         font := opFont, label := &quot;FIND&quot;,
                         color := view.compareColor).init();
    END;
    view.item :=
      NEW(GraphVBT.Vertex, graph := view.mg, color := view.itemColor,
          fontColor := PaintOp.Fg, pos := R2.T{-1.25, FLOAT(nBuckets)},
          size := R2.T{1.0, 1.0}).init();
    view.itemHighlight := NEW(
                            GraphVBT.VertexHighlight, vertex := view.item,
                            color := view.operationHighlightColor,
                            border := R2.T{0.25, 0.5}).init();
    view.oldColors := NEW(REF ARRAY OF ARRAY Level OF PaintOp.T, nBuckets);
    view.level := 0;
    view.operationHighlight := NIL;
    EVAL Filter.Replace(view, view.mg);
  END Setup;

PROCEDURE <A NAME="ResetColors"><procedure>ResetColors</procedure></A> (view: T; level: Level) (* LL = view.mg.mu *) =
  BEGIN
    IF view.operationHighlight = NIL THEN
      Thread.Release(view.mg.mu);
      TRY
        view.operationHighlight :=
          NEW(GraphVBT.VertexHighlight, vertex := view.opInsert,
              color := view.operationHighlightColor,
              border := R2.T{0.25, 0.5}).init();
      FINALLY
        Thread.Acquire(view.mg.mu);
      END;
    END;
    IF view.level &lt;= level THEN RETURN; END;
    FOR i := 0 TO LAST(view.bucketState^) DO
      view.bucketState[i].setColor(view.oldColors[i, level]);
    END;
    view.level := level;
  END ResetColors;

PROCEDURE <A NAME="SetBucketColor"><procedure>SetBucketColor</procedure></A> (view  : T;
                          bucket: INTEGER;
                          color : PaintOp.T;
                          level : CARDINAL   ) =
  BEGIN
    IF level &lt; view.level THEN
      FOR i := 0 TO LAST(view.bucketState^) DO
        view.bucketState[i].setColor(view.oldColors[i, level]);
      END;
    ELSIF level &gt; view.level THEN
      FOR i := 0 TO LAST(view.bucketState^) DO
        FOR l := view.level TO level - 1 DO
          view.oldColors[i, l] := view.bucketState[i].color;
        END;
      END;
    END;
    view.bucketState[bucket].setColor(color);
    view.level := level;
  END SetBucketColor;

PROCEDURE <A NAME="Insert"><procedure>Insert</procedure></A> (view: T; item: TEXT) RAISES {Thread.Alerted} =
  BEGIN
    IF view.mg = NIL THEN RETURN; END;
    LOCK view.mg.mu DO
      ResetColors(view, 0);
      view.operationHighlight.move(view.opInsert);
      view.item.move(pos := R2.T{view.item.pos[0], view.opInsert.pos[1]});
      view.item.setLabel(item);
    END;
    view.mg.animate(0.0, 0.5);
  END Insert;

PROCEDURE <A NAME="Delete"><procedure>Delete</procedure></A> (view: T; item: TEXT) RAISES {Thread.Alerted} =
  BEGIN
    IF view.mg = NIL THEN RETURN; END;
    LOCK view.mg.mu DO
      ResetColors(view, 0);
      view.operationHighlight.move(view.opDelete);
      view.item.move(pos := R2.T{view.item.pos[0], view.opDelete.pos[1]});
      view.item.setLabel(item);
    END;
    view.mg.animate(0.0, 0.5);
  END Delete;

PROCEDURE <A NAME="Find"><procedure>Find</procedure></A> (view: T; item: TEXT) RAISES {Thread.Alerted} =
  BEGIN
    IF view.mg = NIL THEN RETURN; END;
    LOCK view.mg.mu DO
      ResetColors(view, 0);
      view.operationHighlight.move(view.opFind);
      view.item.move(pos := R2.T{view.item.pos[0], view.opFind.pos[1]});
      view.item.setLabel(item);
    END;
    view.mg.animate(0.0, 0.5);
  END Find;

PROCEDURE <A NAME="Compare"><procedure>Compare</procedure></A> (view: T; bucket: INTEGER) =
  BEGIN
    IF view.mg = NIL THEN RETURN; END;
    LOCK view.mg.mu DO
      SetBucketColor(view, bucket, view.compareColor, 1);
    END;
    view.mg.redisplay();
  END Compare;

PROCEDURE <A NAME="AddToBucket"><procedure>AddToBucket</procedure></A> (view: T; key: TEXT; bucket: INTEGER) =
  BEGIN
    IF view.mg = NIL THEN RETURN; END;
    LOCK view.mg.mu DO
      view.bucketContents[bucket].setLabel(key);
      view.bucketContents[bucket].setColor(view.normalColor);
      SetBucketColor(view, bucket, view.newColor, 2);
    END;
    view.mg.redisplay();
  END AddToBucket;

PROCEDURE <A NAME="DeleteFromBucket"><procedure>DeleteFromBucket</procedure></A> (view     : T;
               &lt;* UNUSED *&gt; key      : TEXT;
                            bucket   : INTEGER;
                            markEmpty: BOOLEAN  ) =
  BEGIN
    IF view.mg = NIL THEN RETURN; END;
    LOCK view.mg.mu DO
      IF markEmpty THEN
        view.bucketContents[bucket].setLabel(&quot;EMPTY&quot;);
        view.bucketContents[bucket].setColor(view.emptyColor);
      ELSE
        view.bucketContents[bucket].setLabel(&quot;NIL&quot;);
        view.bucketContents[bucket].setColor(view.nilColor);
      END;
      SetBucketColor(view, bucket, view.newColor, 2);
    END;
    view.mg.redisplay();
  END DeleteFromBucket;

PROCEDURE <A NAME="CheckDeletable"><procedure>CheckDeletable</procedure></A> (view: T; bucket: INTEGER) =
  BEGIN
    IF view.mg = NIL THEN RETURN; END;
    LOCK view.mg.mu DO
      SetBucketColor(view, bucket, view.checkDeletableColor, 1);
    END;
    view.mg.redisplay();
  END CheckDeletable;

PROCEDURE <A NAME="CheckHashPosition"><procedure>CheckHashPosition</procedure></A> (view: T; bucket: INTEGER) =
  BEGIN
    IF view.mg = NIL THEN RETURN; END;
    LOCK view.mg.mu DO
      ResetColors(view, 1);
      SetBucketColor(view, bucket, view.checkHashPositionColor, 2);
    END;
    view.mg.redisplay();
  END CheckHashPosition;

TYPE Shape = TextVBT.T OBJECT OVERRIDES shape := MyShape END;

PROCEDURE <A NAME="MyShape"><procedure>MyShape</procedure></A> (&lt;* UNUSED *&gt; self: Shape;
                                axis: Axis.T;
                   &lt;* UNUSED *&gt; n:    CARDINAL): VBT.SizeRange =
  VAR pref: CARDINAL;
  BEGIN
    IF axis = Axis.T.Hor THEN pref := 300 ELSE pref := 800 END;
    RETURN VBT.SizeRange{0, pref, VBT.DefaultShape.hi};
  END MyShape;

PROCEDURE <A NAME="New"><procedure>New</procedure></A> (): View.T =
  BEGIN
    RETURN NEW(T).init(NEW(Shape).init(&quot;New view&quot;));
  END New;

BEGIN
  ZeusPanel.RegisterView (New, &quot;Buckets&quot;, &quot;Hash&quot;);
END HashViews.
</PRE>
</inModule>
<HR>
<A NAME="x1">interface GraphVBT is in:
</A><UL>
<LI><A HREF="../../../fisheye/src/GraphVBT.i3.html#0TOP0">fisheye/src/GraphVBT.i3</A>
<LI><A HREF="../../../mgkit/src/GraphVBT.i3.html#0TOP0">mgkit/src/GraphVBT.i3</A>
</UL>
<P>
<HR>
<A NAME="x2">interface View is in:
</A><UL>
<LI><A HREF="../../../juno-app/src/View.i3.html#0TOP0">juno-app/src/View.i3</A>
<LI><A HREF="../../../zeus/src/View.i3.html#0TOP0">zeus/src/View.i3</A>
</UL>
<P>
<PRE>























</PRE>
</BODY>
</HTML>
