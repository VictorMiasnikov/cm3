<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: mentor/src/minimax/GamePlay.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>mentor/src/minimax/GamePlay.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright (C) 1994, Digital Equipment Corporation         
 All rights reserved.                                      
 See the file COPYRIGHT for a full description.            

<P><PRE>MODULE <module><implements><A HREF="GamePlay.i3.html">GamePlay</A></implements></module>;

IMPORT <A HREF="GameBoard.i3.html">GameBoard</A>;

REVEAL
  <A NAME="Player">Player</A> = PlayerPublic BRANDED OBJECT
             (* positionPlaying is the position the player is playing *)
             positionPlaying: PlayerId;
           OVERRIDES
             Init     := DoPlayerInit;
             position := DoPlayerPosition;
           END;

  <A NAME="Match">Match</A> =
    MatchPublic BRANDED OBJECT
      (* players is an array of players *)
      players: ARRAY [PlayerId.PlayerA .. PlayerId.PlayerB] OF Player;
      (* The current board position *)
      board: GameBoard.T;
    OVERRIDES
      Init := DoMatchInit;
      Play := DoMatchPlay;
    END;

PROCEDURE <A NAME="DoPlayerInit"><procedure>DoPlayerInit</procedure></A> (self: Player; position: PlayerId): Player =
  BEGIN
    self.positionPlaying := position;
    RETURN self;
  END DoPlayerInit;

PROCEDURE <A NAME="DoPlayerPosition"><procedure>DoPlayerPosition</procedure></A> (self: Player): PlayerId =
  BEGIN
    RETURN self.positionPlaying;
  END DoPlayerPosition;

PROCEDURE <A NAME="DoMatchInit"><procedure>DoMatchInit</procedure></A> (self: Match; playerA, playerB: Player): Match =
  BEGIN
    self.players := ARRAY [PlayerId.PlayerA .. PlayerId.PlayerB] OF
                      Player{playerA, playerB};
    EVAL playerA.Init(PlayerId.PlayerA);
    EVAL playerB.Init(PlayerId.PlayerB);
    self.board := NEW(GameBoard.T).Init();
    RETURN self;
  END DoMatchInit;

PROCEDURE <A NAME="DoMatchPlay"><procedure>DoMatchPlay</procedure></A> (self: Match): PlayerId RAISES ANY =
  VAR
    winner  : PlayerId;
    move    : GameBoard.Move;
    newBoard: GameBoard.T;
  BEGIN
    WHILE NOT self.board.finished(winner) DO
      self.MoveAskedFor(self.board.toMove(), self.board);
      move := self.players[self.board.toMove()].GetMove(self.board);
      newBoard := self.board.Move(move);
      self.MoveMade(self.board.toMove(), move, newBoard);
      self.board := newBoard;
    END;
    RETURN winner;
  END DoMatchPlay;

BEGIN
END GamePlay.
</PRE>
</inModule>
<PRE>























</PRE>
</BODY>
</HTML>
