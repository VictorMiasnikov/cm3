<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: mentor/src/searchtree/BSTAlg.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>mentor/src/searchtree/BSTAlg.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright (C) 1992, Digital Equipment Corporation                         
 All rights reserved.                                                      
 See the file COPYRIGHT for a full description.                            
                                                                           
 Last modified on Wed Jun 15 13:12:34 PDT 1994 by heydon                   
      modified on Tue May  3 13:50:09 PDT 1994 by najork                   

<P><PRE>MODULE <module><implements><A HREF="BSTAlg.i3.html">BSTAlg</A></implements></module>;

IMPORT <A HREF="../../../formsvbt/src/FormsVBT.i3.html">FormsVBT</A>, <A HREF="../../../libm3/src/random/Common/Random.i3.html">Random</A>, <A HREF="../../../m3core/src/text/Text.i3.html">Text</A>, <A HREF="../../../ui/src/vbt/VBT.i3.html">VBT</A>;

&lt;* FATAL FormsVBT.Error, FormsVBT.Unimplemented *&gt;

VAR
  lastIndex: INTEGER := 0;		 (* index of next new Node *)

PROCEDURE <A NAME="GetPanelData"><procedure>GetPanelData</procedure></A>(panel: VBT.T): PanelData =
  VAR pd := NEW (PanelData); BEGIN
    LOCK VBT.mu DO
      pd.nodeCnt := FormsVBT.GetInteger(panel, &quot;nodeCnt&quot;);
      pd.inputType := FormsVBT.GetChoice(panel, &quot;inputtype&quot;);
      pd.deleteType := FormsVBT.GetChoice(panel, &quot;deletetype&quot;);
      IF FormsVBT.GetBoolean(panel, &quot;fixedRand&quot;)
        THEN pd.rand := NEW(Random.Default).init(fixed := TRUE);
        ELSE pd.rand := NEW(Random.Default).init();
      END;
    END;
    (* chop off initial &quot;del_&quot; from &quot;pd.deleteType&quot; *)
    pd.deleteType := Text.Sub(pd.deleteType, 4);
    RETURN pd
  END GetPanelData;

PROCEDURE <A NAME="NewKeys"><procedure>NewKeys</procedure></A>(data: PanelData; input := TRUE): Keys =
  VAR key := NEW(Keys, data.nodeCnt); kind: TEXT; BEGIN
    IF input THEN kind := data.inputType ELSE kind := data.deleteType END;
    IF Text.Equal(kind, &quot;rand&quot;) THEN
      (* construct a random permutation of [1..cnt] *)
      FOR i := 0 TO LAST(key^) DO key[i] := i + 1 END;
      VAR slot: CARDINAL; temp: Key; BEGIN
        WITH last = LAST(key^) DO
          FOR i := 0 TO last - 1 DO
            slot := data.rand.integer (i, last);
            temp := key[i]; key[i] := key[slot]; key[slot] := temp
          END
        END
      END
    ELSIF Text.Equal(kind, &quot;inc&quot;) THEN
      FOR i := 0 TO LAST(key^) DO key[i] := i + 1 END
    ELSE
      FOR i := 0 TO LAST(key^) DO key[i] := data.nodeCnt - i END
    END;
    RETURN key
  END NewKeys;

PROCEDURE <A NAME="NewIndex"><procedure>NewIndex</procedure></A>(): INTEGER =
  BEGIN
    INC(lastIndex);
    RETURN lastIndex
  END NewIndex;

PROCEDURE <A NAME="GetChild"><procedure>GetChild</procedure></A>(node: Node; side: Side): Node =
  BEGIN
    CASE side OF
    | Side.Left =&gt; RETURN node.left
    | Side.Right =&gt; RETURN node.right
    END
  END GetChild;

PROCEDURE <A NAME="SetChild"><procedure>SetChild</procedure></A>(node: Node; side: Side; val: Node) =
  BEGIN
    CASE side OF
    | Side.Left =&gt; node.left := val
    | Side.Right =&gt; node.right := val
    END
  END SetChild;

PROCEDURE <A NAME="Rotate"><procedure>Rotate</procedure></A>(t: Tree; x: Node; side: Side) =
  VAR
    other := OtherSide[side];
    y := GetChild(x, other);
    y_side := GetChild(y, side);
  BEGIN
    SetChild(x, other, y_side);
    IF y_side # NIL THEN y_side.parent := x END;
    y.parent := x.parent;
    IF x.parent = NIL THEN
      t.root := y
    ELSE
      IF x.parent.left = x THEN
        x.parent.left := y
      ELSE
        x.parent.right := y
      END
    END;
    SetChild(y, side, x);
    x.parent := y
  END Rotate;

BEGIN
END BSTAlg.
</PRE>
</inModule>
<PRE>























</PRE>
</BODY>
</HTML>
