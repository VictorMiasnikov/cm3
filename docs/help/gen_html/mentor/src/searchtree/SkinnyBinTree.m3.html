<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: mentor/src/searchtree/SkinnyBinTree.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>mentor/src/searchtree/SkinnyBinTree.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright (C) 1992, Digital Equipment Corporation                         
 All rights reserved.                                                      
 See the file COPYRIGHT for a full description.                            
                                                                           
 Last modified on Thu Sep 17 14:12:31 PDT 1992 by heydon                   
<PRE>&lt;* PRAGMA LL *&gt;

MODULE <module><implements><A HREF="SkinnyBinTree.i3.html">SkinnyBinTree</A></implements></module>;

IMPORT <A HREF="../../../mg/src/MG.i3.html">MG</A>, <A HREF="../../../mgkit/src/GenericTree.i3.html">GenericTree</A>, <A HREF="../../../mg/src/R2Box.i3.html">R2Box</A>, <A HREF="../../../mg/src/R2.i3.html">R2</A>;

REVEAL
  <A NAME="T">T</A> = TPublic BRANDED &quot;SkinnyBinTree.T&quot; OBJECT
    OVERRIDES
      calculateSize := CalculateSize;
      translate := Translate;
    END;

PROCEDURE <A NAME="ChildSizes"><procedure>ChildSizes</procedure></A>(p: T; v: GenericTree.V; VAR (*OUT*) width,height: REAL) =
</PRE><BLOCKQUOTE><EM> Set <CODE>width</CODE> to the sum of the widths of the children of the parent node
   <CODE>p</CODE>, and set <CODE>height</CODE> to the maximum value of their heights. </EM></BLOCKQUOTE><PRE>
  VAR ch: T := p.succ(v, NIL); BEGIN
    width := 0.0;
    height := 0.0;
    WHILE ch # NIL DO
      width := width + ch.width;
      height := MAX(height, ch.height);
      ch := p.succ(v, ch);
    END;
  END ChildSizes;

PROCEDURE <A NAME="CalculateSize"><procedure>CalculateSize</procedure></A>(node: T; v: GenericTree.V) =
  VAR width, height: REAL; BEGIN
    (* Compute width and height of all children *)
    ChildSizes(node, v, width, height);

    (* add extra space for dxChildren and dyChildren *)
    IF node.numChildren &gt; 0 THEN
      width := width + FLOAT(node.numChildren) * node.dxChildren / 2.0;
      height := height + node.dyChildren
    END;

    (* finally, add space for size of root and dyAbove *)
    WITH size = R2Box.Size(node.graphic.appearance.boundingBox(
                  node.graphic, v)) DO
      IF node.numChildren = 1
        THEN node.width := width + size[0] / 2.0
        ELSE node.width := MAX(size[0], width)
      END;
      node.height := height + size[1] + node.dyAbove;
    END;
  END CalculateSize;

PROCEDURE <A NAME="Translate"><procedure>Translate</procedure></A>(node: T; v: GenericTree.V; north, west: REAL) =
  VAR
    dx, dy, northCh: REAL;
    ppos          := GenericTree.ParentPos(node.parent, v);
    leftChWidth   := 0.0;
    bounds        := node.graphic.appearance.boundingBox(node.graphic, v);
    size          := R2Box.Size(bounds);
    middle        := R2Box.Middle(bounds);
  BEGIN
    &lt;* LL = v.mu *&gt;
    IF node.l # NIL
      THEN leftChWidth := node.l.width + (node.dxChildren / 2.0)
      ELSE leftChWidth := size[0] / 2.0
    END;
    dx := ppos[0] + west + leftChWidth - middle[0];
    dy := ppos[1] + north - node.dyAbove - bounds[1].hi;
    IF GenericTree.LinearAnimation(v, R2.T{dx, dy}, node) THEN
      northCh := - ((size[1] / 2.0) + node.dyChildren);
      IF node.l # NIL THEN
        node.l.translate(v, northCh, -leftChWidth);
      END;
      IF node.r # NIL THEN
        node.r.translate(v, northCh, node.dxChildren / 2.0);
      END;
    END;
  END Translate;

BEGIN
END SkinnyBinTree.
</PRE>
</inModule>
<PRE>























</PRE>
</BODY>
</HTML>
