<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: sgml/src/SGML.i3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>sgml/src/SGML.i3</H2></A><HR>
<inInterface>
<PRE></PRE>  SGML parser library                                                    
  Copyright (C) 1997 Michel Dagenais                                     
                                                                         
  This library is free software; you can redistribute it and/or          
  modify it under the terms of the GNU Library General Public            
  License as published by the Free Software Foundation; either           
  version 2 of the License, or (at your option) any later version.       
                                                                         
  This library is distributed in the hope that it will be useful,        
  but WITHOUT ANY WARRANTY; without even the implied warranty of         
  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU      
  Library General Public License for more details.                       
                                                                         
  You should have received a copy of the GNU Library General Public      
  License along with this library; if not, write to the Free             
  Software Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.     
                                                                         
  For more information on this program, contact Michel Dagenais at       
  dagenais@vlsi.polymtl.ca or Electrical Eng. Dept., Ecole Polytechnique 
  P.O. Box 6079, Station A, Montreal, Quebec, Canada, H3C 3A7.           

<P> A Parser object processes the specified sgml files, and calls methods on
   a user defined Application object for each significant parsing event.
   The user defined Application object overrides the methods to react
   appropriately to these events (e.g. print back a modified sgml file,
   construct an abstract syntax tree...). 

<P><PRE>INTERFACE <interface><A HREF="SGML.m3.html">SGML</A></interface>;

IMPORT <A HREF="../../libm3/src/rw/Rd.i3.html">Rd</A>;

TYPE
  ParserOptions = RECORD
      showOpenEntities, showOpenElements, outputCommentDecls,
      outputMarkedSections, outputGeneralEntities,
      mapCatalogDocument: BOOLEAN := FALSE;
      defaultDoctype: TEXT;
      addCatalog, includeParam, enableWarning, addSearchDir,
      activateLink, architecture: REF ARRAY OF TEXT := NIL;
    END;
</PRE> These options define the behavior of the parser.
<P>
   <CODE>ShowOpenEntities</CODE> and <CODE>showOpenElements</CODE> produce information about
   the corresponding entity and element when parsing error messages are
   issued. <CODE>OutputCommentDecls</CODE>, <CODE>outputMarkedSections</CODE> and 
   <CODE>outputGeneralEntities</CODE> determine if the parser produces events 
   when these SGML constructs are encountered.
<P>
   <CODE>DefaultDoctype</CODE> specifies the document type definition to use when
   no DOCTYPE tag is found.
<P>
   <CODE>AddCatalog</CODE> adds the specified file names as SGML DTD catalogs.
   <CODE>IncludeParam</CODE> defines the specified names as parameter entities
   set to <CODE>INCLUDE</CODE> (ENTITY % param <CODE>INCLUDE</CODE>); this way, sections
   of sgml files which were <CODE>IGNORE</CODE> by default may be changed to
   <CODE>INCLUDE</CODE> just by setting this option. <CODE>EnableWarning</CODE> enables the
   named warnings: <CODE>mixed</CODE> mixed content model which does not allow #PCDATA, 
   <CODE>sgmldecl</CODE> dubious constructs in SGML declarations, 
   <CODE>should</CODE> non followed ISO 8879 recommendations, 
   <CODE>default</CODE> defaulted references,
   <CODE>duplicate</CODE> duplicate entity declarations, 
   <CODE>undefined</CODE> undefined elements used in the DTD, 
   <CODE>unclosed</CODE> unclosed start and end tags, 
   <CODE>empty</CODE> empty start and end tags, <CODE>net</CODE> net-enabling start and end tags, 
   <CODE>min-tag</CODE> minimized start and end tags (equivalent to unclosed, empty 
   and net), <CODE>unused-map</CODE> defined but unused short reference maps, 
   <CODE>unused-param</CODE> defined but unused parameter entities, 
   <CODE>notation-sysid</CODE> notation for which no system identifier could be
   generated, <CODE>all</CODE> equivalent to all the above,
   <CODE>no-idref</CODE> do not warn about unresolved references, <CODE>no-significant</CODE> 
   do not warn about non significant characters in literals. 

<P>  <PRE><A HREF="SGML.m3.html#Parser">Parser</A> &lt;: ParserPublic;

  ParserPublic = OBJECT METHODS
      init(options: ParserOptions; programName: TEXT;
          files: REF ARRAY OF TEXT; rds: REF ARRAY OF Rd.T := NIL): Parser;
      run(a: Application): CARDINAL;
      halt();
      inhibitMessages(inhibit: BOOLEAN);
      subdocumentParser(systemId: TEXT): Parser;
      newParser(files: REF ARRAY OF TEXT; rds: REF ARRAY OF Rd.T := NIL):
          Parser;
    END;
</PRE> The call <CODE>p.init(o,p,f,r)</CODE> initializes a parser with options <CODE>o</CODE>,
   program name <CODE>p</CODE> (used in error messages), and <CODE>f</CODE> the array of names
   of files to be parsed. When <CODE>r</CODE> is not specified, files from <CODE>f</CODE> are
   opened. Otherwise, <CODE>f</CODE> is used for file names but the actual input is
   taken from the readers in <CODE>r</CODE>.
<P>
   The call <CODE>p.run(a)</CODE> starts parsing the files and calls back the
   Application object <CODE>a</CODE> for each parsing event. It returns the number
   of errors encountered once the parsing is through.
<P>
   The call <CODE>p.halt()</CODE> stops the parsing, causing the run method to return.
   It is usually called from one the the Application object methods.
<P>
   The call <CODE>p.inhibitMessages(b)</CODE> disables error and warning messages when
   b is TRUE.
<P>
   The call <CODE>p.subdocumentParser(s)</CODE> creates a new parser ready to process
   <CODE>s</CODE> which identifies a subdocument in the context of the file currently
   parsed by <CODE>p</CODE>.
<P>
   The call <CODE>p.newParser(f,r)</CODE> returns a parser using the same options as
   <CODE>p</CODE> but ready to process a new set of files defined by <CODE>f</CODE> and <CODE>r</CODE>.
   Since the options are the same (catalog name, search paths...), caching
   of parsed document type definitions may occur for a significant speedup.
<P>


<P>  <PRE><A HREF="SGML.m3.html#Application">Application</A> &lt;: ApplicationPublic;

  ApplicationPublic = OBJECT METHODS
      init(): Application;
      appInfo(READONLY e: AppinfoEvent);
      startDtd(READONLY e: StartDtdEvent);
      endDtd(READONLY e: EndDtdEvent);
      endProlog(READONLY e: EndPrologEvent);
      startElement(READONLY e: StartElementEvent);
      endElement(READONLY e: EndElementEvent);
      data(READONLY e: DataEvent);
      sdata(READONLY e: SdataEvent);
      pi(READONLY e: PiEvent);
      externalDataEntityRef(READONLY e: ExternalDataEntityRefEvent);
      subdocEntityRef(READONLY e: SubdocEntityRefEvent);
      nonSgmlChar(READONLY e: NonSgmlCharEvent);
      commentDecl(READONLY e: CommentDeclEvent);
      markedSectionStart(READONLY e: MarkedSectionStartEvent);
      markedSectionEnd(READONLY e: MarkedSectionEndEvent);
      ignoredChars(READONLY e: IgnoredCharsEvent);
      generalEntity(READONLY e: GeneralEntityEvent);
      error(READONLY e: ErrorEvent);
      openEntityChange();
      getDetailedLocation(pos: Position): DetailedLocation;
    END;
</PRE> An instance of the <CODE>Application</CODE> type, or one of its descendant type, is
   passed to a Parser and receives the parsing information as methods
   being called back. Each of these methods receives a corresponding
   parsing event structure.
<P>
   The call <CODE>a.init()</CODE> initializes <CODE>a</CODE>, before it is used for parsing.
<P>
   The call <CODE>a.getDetailedLocation(pos)</CODE> returns detailed information about
   the location of <CODE>pos</CODE> within the currently parsed entity. It may only be
   called from within one of the other methods.
<P>
   The other methods are called by the Parser and are overidden in Application
   type descendants to perform the desired work. <CODE>AppInfo</CODE> is called when the
   APPINFO section of the SGML declaration is encountered, <CODE>startDtd</CODE> upon
   encountering the Document Type Definition (DTD), <CODE>endDtd</CODE> at the end of 
   the DTD, <CODE>endProlog</CODE> at the end of the prolog (local markup declarations), 
   <CODE>startElement</CODE> when a start element tag is found, <CODE>endElement</CODE> for a 
   real or implied end element tag, <CODE>data</CODE> for character data (CDATA) 
   within elements or marked sections, <CODE>sdata</CODE> for special character 
   data (SDATA like bitmap images), <CODE>pi</CODE> for a processing instruction, 
   <CODE>externalDataEntityRef</CODE> for a reference to an external data entity, 
   <CODE>subdocEntityRef</CODE> for a reference to a subdoc entity, 
   <CODE>nonSgmlChar</CODE> for non SGML conforming characters, <CODE>commentDecl</CODE> for
   a sequence of comments, <CODE>markedSectionStart</CODE> at the beginning of a marked
   section, <CODE>markedSectionEnd</CODE> at the end of a marked section, <CODE>ignoredChars</CODE>
   for character data within an <CODE>IGNORE</CODE> marked section, <CODE>generalEntity</CODE>
   for a general entity definition (this occurs within the prolog except
   for undefined entities which when referenced are set to the default
   entity content), <CODE>error</CODE> upon encountering a parsing error, and 
   <CODE>openEntityChange</CODE> each time the currently opened entity changes. 

<P>
<P><PRE>PROCEDURE <A HREF="SGML.m3.html#CharRefToCode">CharRefToCode</A>(t: TEXT; VAR c: CharCode): BOOLEAN;
</PRE> While the input files only contain 8 bits ISO-8859 character codes, larger
   16 bits UNICODE codes may be specified by (decimal or hexadecimal) character
   references. For this reason, all such 16 bits codes are kept
   <CODE>escaped</CODE> as character references. Moreover, the special ampersand
   character (&amp;amp;) is also kept as an entity reference throughout the
   processing. This allows all the processing to use ordinary TEXT elements
   which are limited to 8 bits characters. The call <CODE>CharRefToCode(t,c)</CODE>
   return <CODE>TRUE</CODE> when a valid character reference is received in <CODE>t</CODE> 
   and returns the corresponding code in <CODE>c</CODE>. A valid character reference is
   either &amp;amp;amp;, or &amp;amp;#decimalNumber;, or &amp;amp;#xHexaNumber;,
   with the <CODE>number</CODE> within the interval 0..65535). This procedure is
   typically used by applications to process 16 bits characters escaped 
   as character references in Sdata events. 

<P><PRE>TYPE
  CharCode = [0..65535];

  Position = CARDINAL;

  ExternalId = RECORD
      systemId: TEXT;
      publicId: TEXT;
      generatedSystemId: TEXT;
    END;
  (* Depending on the type of external identifier, each of these
     fields may or may not be available (non NIL). At least one should
     be non NIL. *)

  Notation = RECORD
      name: TEXT;
      externalId: ExternalId;
    END;
  (* A named notation with the corresponding external identifier. *)

  EntityDataType = { Sgml, CData, SData, NData, Subdoc, Pi };

  EntityDeclType = { General, Parameter, Doctype, Linktype };

  Entity = RECORD
      name: TEXT;
      dataType: EntityDataType;
      declType: EntityDeclType;
      internalText: TEXT;
      (* Following valid if internalText is NIL *)
      externalId: ExternalId;
      attributes: REF ARRAY OF Attribute;
      notation: Notation;
    END;
  (* For an internal entity, the replacement text is found in &quot;internalText&quot;.
     For external entities, an external identifier, attributes and a notation
     are provided. *)

  AttributeType = { Invalid, Implied, CData, Tokenized };

  AttributeDefaulted = { Specified, Definition, Current };

  CdataChunk = RECORD
      nonSgmlChar: CHAR;
      data: TEXT;
      entityName: TEXT;
    END;
  (* For an SDATA entity reference, entityName is the entity name and data
     the replacement text. For normal data, entityName is NIL and data
     contains the character data. For non SGML conforming characters,
     data and entityName are NIL and nonSgmlChar contains the character. *)

  Attribute = RECORD
      name: TEXT;
      type: AttributeType;
      defaulted: AttributeDefaulted;
      cdataChunks: REF ARRAY OF CdataChunk;
      tokens: TEXT;
      isId: BOOLEAN;
      isGroup: BOOLEAN;
      entities: REF ARRAY OF Entity;
      notation: Notation;
    END;
  (* If the attribute type is Cdata, the value is found in &quot;cdataChunks&quot;,
     otherwise if the type is Tokenized, the value is found in &quot;tokens&quot;.
     For an attribute type NOTATION notation is defined, ENTITY
     or ENTITIES entities is defined. The field isId is TRUE for an attribute
     of type ID. *)

  (* The event structures all contain a position which may be used to
     obtain detailed position information. *)

  PiEvent = RECORD
      pos: Position;
      data: TEXT;
      entityName: TEXT;
    END;
  (* The content of the processing instruction is in data. If it was
     an entity reference, the entityName is provided (non NIL). *)

  ElementContentType = { Empty, CData, RCData, Mixed, Element };

  StartElementEvent = RECORD
      pos: Position;
      gi: TEXT;
      contentType: ElementContentType;
      included: BOOLEAN;
      attributes: REF ARRAY OF Attribute;
    END;
  (* The element type (tag name) is in gi. *)

  EndElementEvent = RECORD
      pos: Position;
      gi: TEXT;
    END;
  (* The element type is in gi. *)

  DataEvent = RECORD
      pos: Position;
      data: TEXT;
    END;

  SdataEvent = RECORD
      pos: Position;
      text: TEXT;
      entityName: TEXT;
    END;
  (* Reference to an internal sdata entity. The replacement text is in text
     and the referenced entity in entityName. *)

  ExternalDataEntityRefEvent = RECORD
      pos: Position;
      entity: Entity;
    END;

  SubdocEntityRefEvent = RECORD
      pos: Position;
      entity: Entity;
    END;

  NonSgmlCharEvent = RECORD
      pos: Position;
      c: CHAR;
    END;

  ErrorType = { Info, Warning, Quantity, IDRef, Capacity, OtherError };

  ErrorEvent = RECORD
      pos: Position;
      type: ErrorType;
      message: TEXT;
    END;

  AppinfoEvent = RECORD
      pos: Position;
      string: TEXT;
    END;

  StartDtdEvent = RECORD
      pos: Position;
      name: TEXT;
      (* If it does not have an external ID all names within will be NIL *)
      externalId: ExternalId;
    END;

  EndDtdEvent = RECORD
      pos: Position;
      name: TEXT;
    END;

  EndPrologEvent = RECORD
      pos: Position;
    END;

  GeneralEntityEvent = RECORD
      entity: Entity;
    END;

  CommentDeclEvent = RECORD
      pos: Position;
      comments: REF ARRAY OF TEXT;
      seps: REF ARRAY OF TEXT;
    END;

  MarkedSectionStatus = { Include, RCData, CData, Ignore };

  MarkedSectionParamType = { Temp, Include, RCData, CData, Ignore, EntityRef };

  MarkedSectionParam = RECORD
      type: MarkedSectionParamType;
      entityName: TEXT;
    END;

  MarkedSectionStartEvent = RECORD
      pos: Position;
      status: MarkedSectionStatus;
      params: REF ARRAY OF MarkedSectionParam;
    END;

  MarkedSectionEndEvent = RECORD
      pos: Position;
      status: MarkedSectionStatus;
    END;

  IgnoredCharsEvent = RECORD
      pos: Position;
      data: TEXT;
    END;

  DetailedLocation = RECORD
      lineNumber: CARDINAL;
      columnNumber: CARDINAL;
      byteOffset: CARDINAL;
      entityOffset: CARDINAL;
      entityName: TEXT;
      filename: TEXT;
    END;
</PRE> Debugging 

<P><PRE>PROCEDURE DumpDefinitions(this: Parser);

END SGML.
</PRE>
</inInterface>
<PRE>























</PRE>
</BODY>
</HTML>
