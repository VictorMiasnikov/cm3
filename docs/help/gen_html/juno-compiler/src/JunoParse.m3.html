<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: juno-compiler/src/JunoParse.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>juno-compiler/src/JunoParse.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright (C) 1992, Digital Equipment Corporation                         
 All rights reserved.                                                      
 See the file COPYRIGHT for a full description.                            
                                                                           
 Last modified on Fri Dec 29 11:16:34 PST 1995 by heydon                   
      modified on Sat Feb 18 19:57:03 PST 1995 by gnelson                  
      modified on Fri Aug  7 21:54:03 PDT 1992 by myers                    

<P><PRE>MODULE <module><implements><A HREF="JunoParse.i3.html">JunoParse</A></implements></module>;

IMPORT <A HREF="#x1">JunoValue</A>;
IMPORT <A HREF="JunoAST.i3.html">JunoAST</A>, <A HREF="JunoLex.i3.html">JunoLex</A>, <A HREF="JunoToken.i3.html">JunoToken</A>, <A HREF="JunoASTUtils.i3.html">JunoASTUtils</A>;
IMPORT <A HREF="../../libm3/src/rw/Rd.i3.html">Rd</A>, <A HREF="../../m3core/src/text/Text.i3.html">Text</A>;
</PRE> The procedures in this module are structured as follows. Not counting the
   public procedures Unit(), Command(), and Expression(), there is roughly a
   one-to-one correspondence between procedures in this module and
   non-terminals in the LL(1) Juno grammar described in the file <CODE>Juno.bnf</CODE>.
   To make it easier to check that any given procedure correctly parses its
   specified non-terminal, each procedure includes the relevant productions
   from the grammar in its comment.
<P>
   Each parsing procedure may raise one of two errors: Rd.Failure and Error.
   Rd.Failure is raised when there is a problem encountered reading from their
   argument stream. Error is raised when either a lex- or parse-error occurs.
<P>
   In the event that Error is raised, we need to <CODE>return</CODE> as much of the
   partial AST constructed up to the point where the error occurred. An
   overriding concern of this implementation to guarantee that any token that
   has been successfully parsed is incorporated in the result AST, even in the
   event that Error is raised. In this way, the JunoUnparse procedures will be
   able to pretty-print all of the tokens that were read successfully. To
   facilitate error recovery, most procedures in this module return their
   result as a VAR (*OUT <PRE>parameter. In some cases, the result is returned as
   a VAR (*INOUT*) (or VAR (*IO*)) parameter; in these cases, the parameter is
   presumed to contain a non-NIL object, some of whose fields may have already
   been filled in at the time of the call.

   Some procedures also take additional READONLY parameters. The tokens
   represented by such parameters have not been incorporated into the result
   AST at the time the procedure is called, so if some parse error occurs, it
   is the responsibility of the procedure to incorporate such READONLY
   parameters into the result AST before raising Error so as to conform to the
   rule described in the previous paragraph. For example, see the procedure
   PH2().

   The grammar contains two versions of most of the Formula/Expression
   nonterminals. The &quot;normal&quot; nonterminals have names like &quot;Formula&quot;, &quot;Form1&quot;,
   &quot;Expr&quot;, and &quot;Expr1&quot;. The mirrors of these nonterminals are special because
   they apply only in the case where a QId (qualified identifier) has already
   been parsed and so the formula is known to start with a QId. The
   nonterminals in this case have names formed by appending the &quot;normal&quot;
   formula/expression nonterminal names with &quot;QId&quot;. Rather than implementing
   separate procedures for these nonterminals, each of the formula/expression
   procedures takes an optional READONLY QId argument. If the argument is NIL,
   then parsing occurs as for the &quot;normal&quot; nonterminal. Otherwise, parsing
   occurs as for the &quot;QId&quot; version of the nonterminal. Both productions are
   listed in the comment for these procedures. Note also that &quot;Form2&quot; and
   &quot;Expr3&quot; have no &quot;QId&quot; counterparts, so they don't take an optional READONLY
   argument.

   There are two procedures provided for matching the current token: Match()
   and MatchKind(). The former should only be used when the type of the
   current token is known. The latter is used when the type of the current
   token is not known, but is expected to have a particular type. *)

TYPE
  LookAhead = RECORD
    s: JunoLex.Stream;			 (* token stream *)
    t: JunoToken.T;			 (* most recently read token *)
    cnt: CARDINAL := 0;                  (* count of number of tokens parsed *)
  END;

VAR
  NilRef: REFANY := NIL;		 (* for use as arg to MatchKind() *)
  End: JunoAST.T;			 (* = JunoAST.End *)
</PRE> ========================= TOP-LEVEL PROCEDURES ========================== 

<P><PRE>REVEAL
  <A NAME="IterativeParse">IterativeParse</A> = BRANDED &quot;JunoParse.IterativeParse&quot; OBJECT
    la: LookAhead
  END;

PROCEDURE <A NAME="StartIterativeParse"><procedure>StartIterativeParse</procedure></A>(READONLY rd: Rd.T): IterativeParse
  RAISES { Rd.Failure, JunoLex.Error } =
  VAR
    lookAhead := LookAhead{s := JunoLex.New(rd), t := NIL};
    ip := NEW(IterativeParse, la := lookAhead);
  BEGIN
    ip.la.t := ip.la.s.next();
    INC(ip.la.cnt);
    RETURN ip;
  END StartIterativeParse;

PROCEDURE <A NAME="FinishIterativeParse"><procedure>FinishIterativeParse</procedure></A>(ip: IterativeParse) =
  BEGIN
    EVAL JunoLex.Close(ip.la.s);
  END FinishIterativeParse;

PROCEDURE <A NAME="GetIndex"><procedure>GetIndex</procedure></A>(ip: IterativeParse): INTEGER =
  BEGIN
    RETURN ip.la.s.lastPos
  END GetIndex;

CONST BlockSet = SET OF JunoToken.Kind
  { JunoToken.Kind.Module, JunoToken.Kind.UI, JunoToken.Kind.Private,
    JunoToken.Kind.Import, JunoToken.Kind.Comment,
    JunoToken.Kind.Const, JunoToken.Kind.Var,
    JunoToken.Kind.Pred, JunoToken.Kind.Func, JunoToken.Kind.Proc };

PROCEDURE <A NAME="Block"><procedure>Block</procedure></A>(
    ip: IterativeParse;
    VAR (*OUT*) ast: JunoAST.Block;
    VAR (*OUT*) tokenCnt: CARDINAL)
  RAISES {Error, JunoLex.Error, Rd.Failure} =
  BEGIN
    TRY
      ip.la.cnt := 0;
      IF ip.la.t.kind = JunoToken.Kind.EndMarker THEN
        ast := NIL; RETURN
      END;
      INC(ip.la.cnt);
      IF ip.la.t.kind IN BlockSet
        THEN Block2(ip.la, ast)
        ELSE RaiseError(ip.la)
      END
    FINALLY
      tokenCnt := ip.la.cnt
    END;
  END Block;

PROCEDURE <A NAME="Command"><procedure>Command</procedure></A>(
    READONLY rd: Rd.T;
    VAR (*OUT*) ast: JunoAST.Cmd;
    VAR (*OUT*) tokenCnt: CARDINAL)
  RAISES {Error, JunoLex.Error, Rd.Failure} =
  VAR la: LookAhead; BEGIN
    TRY
      la.s := JunoLex.New(rd);
      TRY la.t := la.s.next() FINALLY ast := NIL END; (* prime the stream *)
      INC(la.cnt);
      Cmd(la, ast);
      IF la.t.kind = JunoToken.Kind.EndMarker
        THEN DEC(la.cnt)
        ELSE RaiseError(la)
      END;
      EVAL JunoLex.Close(la.s)
    FINALLY
      tokenCnt := la.cnt
    END;
  END Command;

PROCEDURE <A NAME="Expression"><procedure>Expression</procedure></A>(
    READONLY rd: Rd.T;
    VAR (*OUT*) ast: JunoAST.Expr;
    VAR (*OUT*) tokenCnt: CARDINAL)
  RAISES {Error, JunoLex.Error, Rd.Failure} =
  VAR la: LookAhead; BEGIN
    TRY
      la.s := JunoLex.New(rd);
      TRY la.t := la.s.next() FINALLY ast := NIL END; (* prime the stream *)
      INC(la.cnt);
      Expr(la, ast);
      IF la.t.kind = JunoToken.Kind.EndMarker
        THEN DEC(la.cnt)
        ELSE RaiseError(la)
      END;
      EVAL JunoLex.Close(la.s)
    FINALLY
      tokenCnt := la.cnt
    END;
  END Expression;

PROCEDURE <A NAME="FoldHeader"><procedure>FoldHeader</procedure></A>(
    READONLY rd: Rd.T;
    VAR (*OUT*) ast: JunoAST.PredHeader;
    VAR (*OUT*) tokenCnt: CARDINAL)
  RAISES {Error, JunoLex.Error, Rd.Failure} =
  VAR la: LookAhead; BEGIN
    TRY
      la.s := JunoLex.New(rd);
      TRY la.t := la.s.next() FINALLY ast := NIL END; (* prime the stream *)
      INC(la.cnt);
      FoldHeader2(la, ast);
      IF la.t.kind = JunoToken.Kind.EndMarker
        THEN DEC(la.cnt)
        ELSE RaiseError(la)
      END;
      EVAL JunoLex.Close(la.s)
    FINALLY
      tokenCnt := la.cnt
    END;
  END FoldHeader;

PROCEDURE <A NAME="IdList"><procedure>IdList</procedure></A>(
    READONLY rd: Rd.T;
    VAR (*OUT*) ast: JunoAST.IdList;
    VAR (*OUT*) tokenCnt: CARDINAL)
  RAISES {Error, JunoLex.Error, Rd.Failure} =
  VAR la: LookAhead; BEGIN
    TRY
      la.s := JunoLex.New(rd);
      TRY la.t := la.s.next() FINALLY ast := NIL END; (* prime the stream *)
      IF la.t.kind = JunoToken.Kind.EndMarker THEN
        ast := JunoAST.EmptyIdList;
      ELSE
      	INC(la.cnt);
      	IdList0(la, ast);
      	IF la.t.kind = JunoToken.Kind.EndMarker
    	  THEN DEC(la.cnt)
    	  ELSE RaiseError(la)
      	END
      END;
      EVAL JunoLex.Close(la.s)
    FINALLY
      tokenCnt := la.cnt
    END
  END IdList;
</PRE> =============================== BLOCKS ================================== 

<P><PRE>PROCEDURE <A NAME="Block2"><procedure>Block2</procedure></A>(VAR (*IO*) la: LookAhead; VAR (*OUT*) block: JunoAST.Block)
  RAISES {Error, JunoLex.Error, Rd.Failure} =
</PRE><BLOCKQUOTE><EM> Assumes <CODE>la.t.kind IN BlockSet</CODE>.
<P>
<PRE>
      Block = MODULE Id &quot;;&quot;
            | [ FROM Id ] IMPORT IDList &quot;;&quot;
            | Comment
            | UI UIDecl &quot;;&quot;
            | [ PRIVATE ] Decl &quot;;&quot;.
</EM></BLOCKQUOTE><PRE>
  BEGIN
    CASE la.t.kind OF
    | JunoToken.Kind.Module    =&gt; Module(la, block)
    | JunoToken.Kind.Import    =&gt; Import(la, block)
    | JunoToken.Kind.UI        =&gt; UIDecl(la, block)
    | JunoToken.Kind.Comment   =&gt; Comment(la, block)
    ELSE
      VAR private := la.t.kind = JunoToken.Kind.Private; BEGIN
        IF private THEN
          VAR raised := TRUE; BEGIN
            TRY Match(la); raised := FALSE FINALLY (* skip &quot;PRIVATE&quot; *)
              IF raised THEN
                (* use arbitrary JunoAST.Decl in case of error *)
                block := NEW(JunoAST.ConstDecl, private := private, bp := End)
              END
            END
          END
        END;
        Decl(la, block, private);	           (* skip decl *)
        MatchKind(la, JunoToken.Kind.Semi, NilRef) (* skip &quot;;&quot; *)
      END
    END
  END Block2;

PROCEDURE <A NAME="Module"><procedure>Module</procedure></A>(VAR (*IO*) la: LookAhead; VAR (*OUT*) block: JunoAST.Block)
  RAISES {Error, JunoLex.Error, Rd.Failure} =
</PRE><BLOCKQUOTE><EM> Assumes <CODE>la.t.kind = JunoToken.Kind.Module</CODE>.
<P>
<PRE>
       Block = MODULE Id &quot;;&quot;.
</EM></BLOCKQUOTE><PRE>
  VAR module := NEW(JunoAST.Module, bp := End); id: REFANY; BEGIN
    block := module;
    Match(la);                                       (* skip &quot;MODULE&quot; *)
    TRY MatchKind(la, JunoToken.Kind.Id, id) FINALLY (* skip name *)
      module.name := NARROW(id, JunoAST.Id)
    END;
    MatchKind(la, JunoToken.Kind.Semi, NilRef);      (* skip &quot;;&quot; *)
  END Module;

PROCEDURE <A NAME="Import"><procedure>Import</procedure></A>(
    VAR (*IO*) la: LookAhead;
    VAR (*OUT*) block: JunoAST.Block)
  RAISES {Error, JunoLex.Error, Rd.Failure} =
</PRE><BLOCKQUOTE><EM> Assumes <CODE>la.t.kind = JunoToken.Kind.Import</CODE>.
<P>
<PRE>
       Block = IMPORT IDList &quot;;&quot;.
</EM></BLOCKQUOTE><PRE>
  VAR import := NEW(JunoAST.Import, bp := End); BEGIN
    block := import;
    Match(la);                                       (* skip &quot;IMPORT&quot; *)
    IdList0(la, import.idList);                      (* skip Id's *)
    MatchKind(la, JunoToken.Kind.Semi, NilRef);      (* skip &quot;;&quot; *)
  END Import;

PROCEDURE <A NAME="UIDecl"><procedure>UIDecl</procedure></A>(VAR (*IO*) la: LookAhead; VAR (*OUT*) block: JunoAST.Block)
  RAISES {Error, JunoLex.Error, Rd.Failure} =
</PRE><BLOCKQUOTE><EM> Assumes <CODE>la.t.kind = JunoToken.Kind.UI</CODE>.
<P>
<PRE>
       Block  = UI UIDecl &quot;;&quot;
       UIDecl = Id &quot;(&quot; [ ExprList ] &quot;)&quot;.
</EM></BLOCKQUOTE><PRE>
  VAR ui := NEW(JunoAST.UIDecl, bp := End); id: REFANY; BEGIN
    block := ui;
    Match(la);				             (* skip &quot;UI&quot; *)
    TRY MatchKind(la, JunoToken.Kind.Id, id) FINALLY (* skip Id *)
      ui.name := NARROW(id, JunoAST.Id)
    END;
    MatchKind(la, JunoToken.Kind.LPren, NilRef);     (* skip &quot;(&quot; *)
    IF la.t.kind # JunoToken.Kind.RPren
      THEN ExprList(la, ui.args)                     (* parse args *)
      ELSE ui.args := JunoAST.EmptyExprList          (* make args empty *)
    END;
    MatchKind(la, JunoToken.Kind.RPren, NilRef);     (* skip &quot;)&quot; *)
    MatchKind(la, JunoToken.Kind.Semi, NilRef);      (* skip &quot;;&quot; *)
  END UIDecl;

PROCEDURE <A NAME="FoldHeader2"><procedure>FoldHeader2</procedure></A>(
    VAR (*IO*) la: LookAhead;
    VAR (*OUT*) res: JunoAST.PredHeader)
  RAISES {Error, JunoLex.Error, Rd.Failure} =
</PRE><BLOCKQUOTE><EM> Parse something like a <CODE>PredHeader</CODE>, but the trailing parentheses are
   optional if there are no arguments.
<P>
<PRE>
       Id [ &quot;(&quot; [ IDList ] &quot;)&quot; ].
</PRE>
   If only <CODE>Id</CODE> is read, then the <CODE>ins</CODE> field of the result will be
   <CODE>NIL</CODE>. If <CODE>Id()</CODE> is read, then the <CODE>ins</CODE> field of the result will be
   <CODE>JunoAST.EmptyIdList</CODE>.
</EM></BLOCKQUOTE><PRE>
  VAR id: REFANY; BEGIN
    res := NEW(JunoAST.PredHeader, bp := End);
    TRY MatchKind(la, JunoToken.Kind.Id, id) FINALLY (* read Id *)
      res.name := NARROW(id, JunoAST.Id)
    END;
    IF la.t.kind = JunoToken.Kind.EndMarker THEN
      res.ins := NIL;
      RETURN
    END;
    MatchKind(la, JunoToken.Kind.LPren, NilRef);     (* skip &quot;(&quot; *)
    IF la.t.kind # JunoToken.Kind.RPren
      THEN IdList0(la, res.ins)                      (* parse args *)
      ELSE res.ins := JunoAST.EmptyIdList            (* make args empty *)
    END;
    MatchKind(la, JunoToken.Kind.RPren, NilRef)      (* skip &quot;)&quot; *)
  END FoldHeader2;

PROCEDURE <A NAME="Comment"><procedure>Comment</procedure></A>(VAR (*IO*) la: LookAhead; VAR (*OUT*) block: JunoAST.Block)
  RAISES {Error, JunoLex.Error, Rd.Failure} =
</PRE><BLOCKQUOTE><EM> Requires <CODE>la.t.kind = JunoToken.Kind.Comment</CODE>. </EM></BLOCKQUOTE><PRE>
  VAR comment := NEW(JunoAST.Comment, bp := End); txt: REFANY; BEGIN
    block := comment;
    TRY MatchKind(la, JunoToken.Kind.Comment, txt) FINALLY
      comment.txt := NARROW(txt, TEXT);
      comment.private := (Text.GetChar(comment.txt, 0) = '/')
    END
  END Comment;

PROCEDURE <A NAME="Decl"><procedure>Decl</procedure></A>(
    VAR (*IO*) la: LookAhead;
    VAR (*OUT*) decl: JunoAST.Block;
    private: BOOLEAN)
  RAISES {Error, JunoLex.Error, Rd.Failure} =
</PRE><BLOCKQUOTE><EM><P>
<PRE>
       Decl	= CONST ConstDecl
      	| VAR VarDecl
      	| PRED PredDecl
      	| FUNC FuncDecl
      	| PROC ProcDecl.
</EM></BLOCKQUOTE><PRE>
  BEGIN
    CASE la.t.kind OF
      JunoToken.Kind.Const =&gt; ConstDecl(la, decl, private)
    | JunoToken.Kind.Var   =&gt; VarDecl(la, decl, private)
    | JunoToken.Kind.Pred  =&gt; PredDecl(la, decl, private)
    | JunoToken.Kind.Func  =&gt; FuncDecl(la, decl, private)
    | JunoToken.Kind.Proc  =&gt; ProcDecl(la, decl, private)
    ELSE RaiseError(la)
    END
  END Decl;

PROCEDURE <A NAME="ConstDecl"><procedure>ConstDecl</procedure></A>(
    VAR (*IO*) la: LookAhead;
    VAR (*OUT*) decl: JunoAST.Block;
    priv: BOOLEAN)
  RAISES {Error, JunoLex.Error, Rd.Failure} =
</PRE><BLOCKQUOTE><EM> Assumes <CODE>la.t.kind = JunoToken.Kind.Const</CODE>.
<P>
<PRE>
       Decl      = CONST ConstDecl.
       ConstDecl = ConstDeclItem { &quot;,&quot; ConstDeclItem }.
</EM></BLOCKQUOTE><PRE>
  VAR constant := NEW(JunoAST.ConstDecl, private := priv, bp := End); BEGIN
    decl := constant;
    Match(la);                                       (* skip &quot;CONST&quot; *)
    INC(constant.size);
    ConstDeclItem(la, constant.head);	             (* skip ConstDeclItem *)
    VAR curr := constant.head; BEGIN
      WHILE la.t.kind = JunoToken.Kind.Comma DO
        INC(constant.size);
        Match(la);			             (* skip &quot;,&quot; *)
        ConstDeclItem(la, curr.next);	             (* skip ConstDeclItem *)
        curr := curr.next
      END
    END
  END ConstDecl;

PROCEDURE <A NAME="ConstDeclItem"><procedure>ConstDeclItem</procedure></A>(
    VAR (*IO*) la: LookAhead;
    VAR (*OUT*) item: JunoAST.ConstDeclItem)
  RAISES {Error, JunoLex.Error, Rd.Failure} =
</PRE><BLOCKQUOTE><EM><P>
<PRE>
       ConstDeclItem = Id &quot;=&quot; ConstExpr.
</EM></BLOCKQUOTE><PRE>
  VAR id: REFANY; BEGIN
    item := NEW(JunoAST.ConstDeclItem);
    TRY MatchKind(la, JunoToken.Kind.Id, id) FINALLY (* skip Id *)
      item.name := NARROW(id, JunoAST.Id)
    END;
    MatchKind(la, JunoToken.Kind.Equals, NilRef);    (* skip &quot;=&quot; *)
    Expr(la, item.value);                            (* skip Expr *)
  END ConstDeclItem;

PROCEDURE <A NAME="VarDecl"><procedure>VarDecl</procedure></A>(
    VAR (*IO*) la: LookAhead;
    VAR (*OUT*) decl: JunoAST.Block;
    priv: BOOLEAN)
  RAISES {Error, JunoLex.Error, Rd.Failure} =
</PRE><BLOCKQUOTE><EM> Assumes <CODE>la.t.kind = JunoToken.Kind.Var</CODE>.
<P>
<PRE>
       Decl    = VAR VarDecl.
       VarDecl = VarDeclItem { &quot;,&quot; VarDeclItem }.
</EM></BLOCKQUOTE><PRE>
  VAR var := NEW(JunoAST.VarDecl, private := priv, bp := End); BEGIN
    decl := var;
    Match(la);                                       (* skip &quot;VAR&quot; *)
    INC(var.size);
    VarDeclItem(la, var.head);	                     (* skip VarDeclItem *)
    VAR curr := var.head; BEGIN
      WHILE la.t.kind = JunoToken.Kind.Comma DO
        INC(var.size);
        Match(la);			             (* skip &quot;,&quot; *)
        VarDeclItem(la, curr.next);	             (* skip VarDeclItem *)
        curr := curr.next
      END
    END
  END VarDecl;

PROCEDURE <A NAME="VarDeclItem"><procedure>VarDeclItem</procedure></A>(
    VAR (*IO*) la: LookAhead;
    VAR (*OUT*) item: JunoAST.VarDeclItem)
  RAISES {Error, JunoLex.Error, Rd.Failure} =
</PRE><BLOCKQUOTE><EM><P>
<PRE>
       VarDeclItem = Id [ &quot;:=&quot; Expr ].
</EM></BLOCKQUOTE><PRE>
  VAR id: REFANY; BEGIN
    item := NEW(JunoAST.VarDeclItem);
    TRY MatchKind(la, JunoToken.Kind.Id, id) FINALLY (* skip Id *)
      item.name := NARROW(id, JunoAST.Id)
    END;
    IF la.t.kind = JunoToken.Kind.Assign THEN
      MatchKind(la, JunoToken.Kind.Assign, NilRef);  (* skip &quot;:=&quot; *)
      Expr(la, item.value)                           (* skip Expr *)
    ELSE
      item.value := JunoAST.NilExpr
    END
  END VarDeclItem;

PROCEDURE <A NAME="PredDecl"><procedure>PredDecl</procedure></A>(
    VAR (*IO*) la: LookAhead;
    VAR (*OUT*) decl: JunoAST.Block;
    priv: BOOLEAN)
  RAISES {Error, JunoLex.Error, Rd.Failure} =
</PRE><BLOCKQUOTE><EM> Assumes <CODE>la.t.kind = JunoToken.Kind.Pred</CODE>.
<P>
<PRE>
       Decl     = PRED PredDecl
       PredDecl = PredHead IS Formula END.
</EM></BLOCKQUOTE><PRE>
  VAR pred := NEW(JunoAST.PredDecl, private := priv, bp := End); BEGIN
    decl := pred;
    Match(la);                                       (* skip &quot;PRED&quot; *)
    PredHead(la, pred.header);			     (* skip header *)
    Match(la);                                       (* skip &quot;IS&quot; *)
    Formula(la, pred.body);                          (* skip body *)
    MatchKind(la, JunoToken.Kind.End, NilRef)        (* skip &quot;END&quot; *)
  END PredDecl;

PROCEDURE <A NAME="PredHead"><procedure>PredHead</procedure></A>(
    VAR (*IO*) la: LookAhead;
    VAR (*OUT*) header: JunoAST.PredHeader)
  RAISES {Error, JunoLex.Error, Rd.Failure} =
</PRE><BLOCKQUOTE><EM><P>
<PRE>
       PredHead = Id &quot;(&quot; [ IDList ] &quot;)&quot;.
</EM></BLOCKQUOTE><PRE>
  VAR id: REFANY; BEGIN
    header := NEW(JunoAST.PredHeader, bp := End);
    TRY MatchKind(la, JunoToken.Kind.Id, id) FINALLY (* skip pred name *)
      header.name := NARROW(id, JunoAST.Id)
    END;
    MatchKind(la, JunoToken.Kind.LPren, NilRef);     (* skip &quot;(&quot; *)
    IF la.t.kind # JunoToken.Kind.RPren
      THEN IdList0(la, header.ins)	             (* skip in parameters *)
      ELSE header.ins := JunoAST.EmptyIdList	     (* make IN params empty *)
    END;
    MatchKind(la, JunoToken.Kind.RPren, NilRef)      (* skip &quot;)&quot; *)
  END PredHead;

PROCEDURE <A NAME="FuncDecl"><procedure>FuncDecl</procedure></A>(
    VAR (*IO*) la: LookAhead;
    VAR (*OUT*) decl: JunoAST.Block;
    priv: BOOLEAN)
  RAISES {Error, JunoLex.Error, Rd.Failure} =
</PRE><BLOCKQUOTE><EM> Assumes <CODE>la.t.kind = JunoToken.Kind.Func</CODE>.
<P>
<PRE>
       Decl     = FUNC FuncDecl.
       FuncDecl = FuncHead IS Constraint END.
</EM></BLOCKQUOTE><PRE>
  VAR func := NEW(JunoAST.FuncDecl, private := priv, bp := End); BEGIN
    decl := func;
    Match(la);                                       (* skip &quot;FUNC&quot; *)
    FuncHead(la, func.header);			     (* skip header *)
    Match(la);                                       (* skip &quot;IS&quot; *)
    Formula(la, func.body);                          (* skip body *)
    MatchKind(la, JunoToken.Kind.End, NilRef)        (* skip &quot;END&quot; *)
  END FuncDecl;

PROCEDURE <A NAME="FuncHead"><procedure>FuncHead</procedure></A>(
    VAR (*IO*) la: LookAhead;
    VAR (*OUT*) header: JunoAST.FuncHeader)
  RAISES {Error, JunoLex.Error, Rd.Failure} =
</PRE><BLOCKQUOTE><EM><P>
<PRE>
       FuncHead = Id &quot;(&quot; [ IDList ] &quot;)&quot; &quot;=&quot; Id.
</EM></BLOCKQUOTE><PRE>
  VAR id: REFANY; BEGIN
    header := NEW(JunoAST.FuncHeader, bp := End);
    TRY MatchKind(la, JunoToken.Kind.Id, id) FINALLY (* skip result name *)
      header.result := NARROW(id, JunoAST.Id)
    END;
    MatchKind(la, JunoToken.Kind.Equals, NilRef);    (* skip &quot;=&quot; *)
    TRY MatchKind(la, JunoToken.Kind.Id, id) FINALLY (* skip func name *)
      header.name := NARROW(id, JunoAST.Id)
    END;
    MatchKind(la, JunoToken.Kind.LPren, NilRef);     (* skip &quot;(&quot; *)
    IF la.t.kind # JunoToken.Kind.RPren
      THEN IdList0(la, header.ins)	             (* skip in parameters *)
      ELSE header.ins := JunoAST.EmptyIdList	     (* make IN params empty *)
    END;
    MatchKind(la, JunoToken.Kind.RPren, NilRef);     (* skip &quot;)&quot; *)
  END FuncHead;

PROCEDURE <A NAME="ProcDecl"><procedure>ProcDecl</procedure></A>(
    VAR (*IO*) la: LookAhead;
    VAR (*OUT*) decl: JunoAST.Block;
    priv: BOOLEAN)
  RAISES {Error, JunoLex.Error, Rd.Failure} =
</PRE><BLOCKQUOTE><EM> Assumes <CODE>la.t.kind = JunoToken.Kind.Proc</CODE>.
<P>
<PRE>
       Decl     = PROC ProcDecl.
       ProcDecl = ProcHead IS Cmd END.
</EM></BLOCKQUOTE><PRE>
  VAR proc := NEW(JunoAST.ProcDecl, private := priv, bp := End); BEGIN
    decl := proc;
    Match(la);                                       (* skip &quot;PROC&quot; *)
    ProcHead(la, proc.header);		             (* skip header *)
    Match(la);                                       (* skip &quot;IS&quot; *)
    Cmd(la, proc.body);                              (* skip body *)
    MatchKind(la, JunoToken.Kind.End, NilRef)        (* skip &quot;END&quot; *)
  END ProcDecl;

PROCEDURE <A NAME="ProcHead"><procedure>ProcHead</procedure></A>(
    VAR (*IO*) la: LookAhead;
    VAR (*OUT*) header: JunoAST.ProcHeader)
  RAISES {Error, JunoLex.Error, Rd.Failure} =
</PRE><BLOCKQUOTE><EM><P>
<PRE>
       ProcHead = Id PH2 | PH4.
       PH4      = &quot;(&quot; IDList &quot;)&quot; PH5.
</EM></BLOCKQUOTE><PRE>
  BEGIN
    header := NEW(JunoAST.ProcHeader, bp := End);
    CASE la.t.kind OF
    | JunoToken.Kind.Id =&gt;
        VAR newId: JunoAST.Id; id: REFANY; raised := TRUE; BEGIN
          TRY MatchKind(la, JunoToken.Kind.Id, id); raised := FALSE FINALLY
            newId := NARROW(id, JunoAST.Id);
            IF raised THEN header.outs := NewIdList(newId) END
          END;
          PH2(la, newId, header)
        END
    | JunoToken.Kind.LPren =&gt;
        header.outs := JunoAST.EmptyIdList; (* make OUT params an empty list *)
        PH4(la, header)                     (* skip inouts and prochead *)
    ELSE RaiseError(la)
    END
  END ProcHead;

PROCEDURE <A NAME="PH2"><procedure>PH2</procedure></A>(
    VAR (*IO*) la: LookAhead;
    READONLY id: JunoAST.Id;
    VAR (*IO*) header: JunoAST.ProcHeader)
  RAISES {Error, JunoLex.Error, Rd.Failure} =
</PRE><BLOCKQUOTE><EM> Assumes <CODE>header # NIL</CODE>, so if we don't find a token we expect, we can
   simply raise <CODE>Error</CODE> after saving the value <CODE>id</CODE> in <CODE>header</CODE>.
<P>
   <CODE>Id</CODE> is the most recently parsed token. It has a different meaning
   depending on the PH2 alternative taken. In the case of the first
   alternative, <CODE>Id</CODE> is the first of a sequence of OUT parameters. In the
   case of the second alternative, <CODE>Id</CODE> is a single INOUT parameter. In the
   case of the third alternative, <CODE>Id</CODE> is the procedure name.
<P>
<PRE>
       PH2 = { &quot;,&quot; Id } &quot;:=&quot; PH3 | PH5 | PH6.
       PH5 = &quot;:&quot; Id PH6
       PH6 = &quot;(&quot; [ IDList ] &quot;)&quot;.
</EM></BLOCKQUOTE><PRE>
  BEGIN
    CASE la.t.kind OF
    | JunoToken.Kind.Comma, JunoToken.Kind.Assign =&gt;
        IdList0(la, header.outs, id);	              (* skip out params *)
        MatchKind(la, JunoToken.Kind.Assign, NilRef); (* skip &quot;:=&quot; *)
        PH3(la, header);
    | JunoToken.Kind.Colon =&gt;
        header.outs := JunoAST.EmptyIdList;         (* make OUT list empty *)
        header.inouts := NewIdList(id);	            (* initialize INOUTs *)
        PH5(la, id, header)		            (* parse proc name *)
    | JunoToken.Kind.LPren =&gt;
        header.outs := JunoAST.EmptyIdList;         (* make OUT list empty *)
        header.inouts := JunoAST.EmptyIdList;	    (* make INOUT list empty *)
        header.name := id;
        PH6(la, id, header)
    ELSE
        header.outs := NewIdList(id);
        RaiseError(la)
    END
  END PH2;

PROCEDURE <A NAME="PH3"><procedure>PH3</procedure></A>(
    VAR (*IO*) la: LookAhead;
    VAR (*IO*) header: JunoAST.ProcHeader)
  RAISES {Error, JunoLex.Error, Rd.Failure} =
</PRE><BLOCKQUOTE><EM> Assumes <CODE>header # NIL</CODE>, so if we don't find a token we expect, we can
   simply raise <CODE>Error</CODE>. Also assumes <CODE>header.outs # NIL</CODE>, i.e., the OUT
   parameters slot has of <CODE>header</CODE> has already been filled in.
<P>
<PRE>
       PH3 = Id (PH5 | PH6) | PH4.
       PH4 = &quot;(&quot; IDList &quot;)&quot; PH5.
       PH5 = &quot;:&quot; Id PH6.
       PH6 = &quot;(&quot; [ IDList ] &quot;)&quot;.
</EM></BLOCKQUOTE><PRE>
  BEGIN
    CASE la.t.kind OF
    | JunoToken.Kind.Id =&gt;
        VAR newId: JunoAST.Id; id: REFANY; BEGIN
          TRY MatchKind(la, JunoToken.Kind.Id, id) FINALLY
             header.name := NARROW(id, JunoAST.Id)
          END;
          newId := NARROW(id, JunoAST.Id);
          CASE la.t.kind OF
          | JunoToken.Kind.Colon =&gt;
              PH5(la, newId, header)	    (* skip proc name and args *)
          | JunoToken.Kind.LPren =&gt;
              header.inouts := JunoAST.EmptyIdList; (* make INOUTs empty *)
              PH6(la, newId, header)	    (* skip proc name and args *)
          ELSE RaiseError(la)
          END
        END
    | JunoToken.Kind.LPren =&gt; PH4(la, header)
    ELSE RaiseError(la)
    END
  END PH3;

PROCEDURE <A NAME="PH4"><procedure>PH4</procedure></A>(
    VAR (*IO*) la: LookAhead;
    VAR (*IO*) header: JunoAST.ProcHeader)
  RAISES {Error, JunoLex.Error, Rd.Failure} =
</PRE><BLOCKQUOTE><EM> Assumes <CODE>header # NIL</CODE>, so if we don't find a token we expect, we can
   simply raise <CODE>Error</CODE>. This happens implicitly in the calls to
   <CODE>MatchKind()</CODE>.
<P>
<PRE>
       PH4 = &quot;(&quot; IDList &quot;)&quot; PH5.
</EM></BLOCKQUOTE><PRE>
  BEGIN
    header.inout_prens := TRUE;
    MatchKind(la, JunoToken.Kind.LPren, NilRef); (* skip &quot;(&quot; *)
    IdList0(la, header.inouts);		         (* skip inout params *)
    MatchKind(la, JunoToken.Kind.RPren, NilRef); (* skip &quot;)&quot; *)
    PH5(la, NIL, header);
  END PH4;

PROCEDURE <A NAME="PH5"><procedure>PH5</procedure></A>(
    VAR (*IO*) la: LookAhead;
    READONLY inout: JunoAST.Id;
    VAR (*IO*) header: JunoAST.ProcHeader)
  RAISES {Error, JunoLex.Error, Rd.Failure} =
</PRE><BLOCKQUOTE><EM> Assumes <CODE>header # NIL</CODE>. If <CODE>inout # NIL</CODE>, it is installed as the (1-item)
   list of in-out parameters in <CODE>header.inouts</CODE>. Otherwise, it is assumed that
   <CODE>header.inouts # NIL</CODE>, i.e., it has already been filled in.
<P>
<PRE>
       PH5 = &quot;:&quot; Id PH6.
</EM></BLOCKQUOTE><PRE>
  VAR id: REFANY; BEGIN
    IF inout # NIL THEN header.inouts := NewIdList(inout) END;
    MatchKind(la, JunoToken.Kind.Colon, NilRef);     (* skip &quot;:&quot; *)
    TRY MatchKind(la, JunoToken.Kind.Id, id) FINALLY (* skip proc name *)
      header.name := NARROW(id, JunoAST.Id)
    END;
    PH6(la, NIL, header)
  END PH5;

PROCEDURE <A NAME="PH6"><procedure>PH6</procedure></A>(
    VAR (*IO*) la: LookAhead;
    READONLY name: JunoAST.Id;
    VAR (*IO*) header: JunoAST.ProcHeader)
  RAISES {Error, JunoLex.Error, Rd.Failure} =
</PRE><BLOCKQUOTE><EM> Assumes <CODE>header # NIL</CODE>. If <CODE>name # NIL</CODE>, it is installed as the procedure
   name in <CODE>header.name</CODE>. Otherwise, it is assumed that the procedure name has
   already been filled in.
<P>
<PRE>
       PH6 = &quot;(&quot; [ IDList ] &quot;)&quot;.
</EM></BLOCKQUOTE><PRE>
  BEGIN
    IF name # NIL THEN header.name := name END;
    MatchKind(la, JunoToken.Kind.LPren, NilRef); (* skip &quot;(&quot; *)
    IF la.t.kind # JunoToken.Kind.RPren
      THEN IdList0(la, header.ins)	         (* skip in parameters *)
      ELSE header.ins := JunoAST.EmptyIdList	 (* make IN params empty *)
    END;
    MatchKind(la, JunoToken.Kind.RPren, NilRef); (* skip &quot;)&quot; *)
  END PH6;
</PRE> =============================== COMMANDS ================================ 

<P><PRE>PROCEDURE <A NAME="Cmd"><procedure>Cmd</procedure></A>(VAR (*IO*) la: LookAhead; VAR (*OUT*) ast: JunoAST.Cmd)
  RAISES {Error, JunoLex.Error, Rd.Failure} =
</PRE><BLOCKQUOTE><EM><P>
<PRE>
       Cmd = Cmd2 [ &quot;|&quot; Cmd ].
</EM></BLOCKQUOTE><PRE>
  BEGIN
    Cmd2(la, ast);
    IF la.t.kind = JunoToken.Kind.Else THEN
      VAR elseCmd := NEW(JunoAST.Else, c1 := ast, bp := End); BEGIN
        ast := elseCmd;
        Match(la);			 (* skip &quot;|&quot; *)
        Cmd(la, elseCmd.c2)
      END
    END
  END Cmd;

PROCEDURE <A NAME="Cmd2"><procedure>Cmd2</procedure></A>(VAR (*IO*) la: LookAhead; VAR (*OUT*) ast: JunoAST.Cmd)
  RAISES {Error, JunoLex.Error, Rd.Failure} =
</PRE><BLOCKQUOTE><EM><P>
<PRE>
       Cmd2 = Cmd3 | QId QIdCmdTail.
</EM></BLOCKQUOTE><PRE>
  BEGIN
    IF la.t.kind = JunoToken.Kind.Id THEN
      VAR qid: JunoAST.QId := NIL; raised := TRUE; BEGIN
        TRY QID(la, qid); raised := FALSE FINALLY
          IF raised THEN
            ast := NEW(JunoAST.Assign, bp := End,
              vars := JunoASTUtils.NewQIdList(qid, bp := End));
          END
        END;
        QIdCmdTail(la, qid, ast)
      END
    ELSE
      Cmd3(la, ast)
    END
  END Cmd2;

PROCEDURE <A NAME="Cmd3"><procedure>Cmd3</procedure></A>(VAR (*IO*) la: LookAhead; VAR (*OUT*) ast: JunoAST.Cmd)
  RAISES {Error, JunoLex.Error, Rd.Failure} =
</PRE><BLOCKQUOTE><EM> The possible tokens with which the non-terminal <CODE>FormulaX</CODE> may begin are
   given by the constant <CODE>FormulaFirstSet</CODE> below.
<P>
<PRE>
       Cmd3 = Cmd4 | FormulaX FormTail.
</EM></BLOCKQUOTE><PRE>
  CONST FormulaFirstSet = SET OF JunoToken.Kind
    { JunoToken.Kind.LitReal, JunoToken.Kind.LitText, JunoToken.Kind.LPren,
      JunoToken.Kind.LBracket, JunoToken.Kind.Minus, JunoToken.Kind.Nil,
      JunoToken.Kind.True, JunoToken.Kind.False, JunoToken.Kind.Not,
      JunoToken.Kind.Real..JunoToken.Kind.Min };
  BEGIN
    IF la.t.kind IN FormulaFirstSet THEN
      (* In this case, we must be able to parse a Formula *)
      VAR f: JunoAST.Expr; raised := TRUE; BEGIN
        TRY Formula(la, f); raised := FALSE FINALLY
          IF raised THEN ast := NEW(JunoAST.Guard, grd := f, bp := End) END
        END;
        FormTail(la, f, ast);
      END
    ELSE
      Cmd4(la, ast)
    END
  END Cmd3;

PROCEDURE <A NAME="Cmd4"><procedure>Cmd4</procedure></A>(VAR (*IO*) la: LookAhead; VAR (*OUT*) ast: JunoAST.Cmd)
  RAISES {Error, JunoLex.Error, Rd.Failure} =
</PRE><BLOCKQUOTE><EM><P>
<PRE>
       Cmd4 = Cmd6 Cmd5.
</EM></BLOCKQUOTE><PRE>
  BEGIN
    Cmd6(la, ast);
    Cmd5(la, ast, ast)
  END Cmd4;

PROCEDURE <A NAME="Cmd5"><procedure>Cmd5</procedure></A>(
    VAR (*IO*) la: LookAhead;
    VALUE cmd: JunoAST.Cmd;
    VAR (*OUT*) ast: JunoAST.Cmd)
  RAISES {Error, JunoLex.Error, Rd.Failure} =
</PRE><BLOCKQUOTE><EM> <CODE>Cmd</CODE> is the previously parsed command. If the current token is <CODE>;</CODE>, then
   <CODE>cmd</CODE> is concatenated with the next command in the stream to form a command
   sequence. Otherwise, <CODE>ast</CODE> is set to <CODE>cmd</CODE>.
<P>
<PRE>
       Cmd5 = [ &quot;;&quot; Cmd2 ].
</EM></BLOCKQUOTE><PRE>
  BEGIN
    IF la.t.kind = JunoToken.Kind.Semi THEN
      VAR seq := NEW(JunoAST.Seq, c1 := cmd, bp := End); BEGIN
        ast := seq;
        Match(la);			 (* skip &quot;;&quot; *)
        Cmd2(la, seq.c2)
      END
    ELSE
      ast := cmd
    END
  END Cmd5;

PROCEDURE <A NAME="Cmd6"><procedure>Cmd6</procedure></A>(VAR (*IO*) la: LookAhead; VAR (*OUT*) ast: JunoAST.Cmd)
  RAISES {Error, JunoLex.Error, Rd.Failure} =
</PRE><BLOCKQUOTE><EM><P>
<PRE>
       Cmd6 = SKIP | ABORT
            | VAR NearVarList IN Cmd END
            | DO Cmd OD | IF Cmd FI
            | SAVE Id IN Cmd END | &quot;{&quot; Cmd &quot;}&quot;.
</EM></BLOCKQUOTE><PRE>
  BEGIN
    CASE la.t.kind OF
    | JunoToken.Kind.Skip =&gt;
        ast := JunoAST.SkipVal;
        Match(la)					   (* skip &quot;SKIP&quot; *)
    | JunoToken.Kind.Abort =&gt;
        ast := JunoAST.AbortVal;
        Match(la)					   (* skip &quot;ABORT&quot; *)
    | JunoToken.Kind.Var =&gt;
        VAR proj := NEW(JunoAST.Proj, bp := End); BEGIN
          ast := proj;
          Match(la);			                   (* skip &quot;VAR&quot; *)
          NearVarList(la, proj.vars);	                   (* skip variables *)
          MatchKind(la, JunoToken.Kind.In, NilRef);        (* skip &quot;IN&quot; *)
          Cmd(la, proj.body);				   (* skip body *)
          MatchKind(la, JunoToken.Kind.End, NilRef)	   (* skip &quot;END&quot; *)
        END
    | JunoToken.Kind.Do =&gt;
        VAR doCmd := NEW(JunoAST.Do, bp := End); BEGIN
          ast := doCmd;
          Match(la);					   (* skip &quot;DO&quot; *)
          Cmd(la, doCmd.body);				   (* skip body *)
          MatchKind(la, JunoToken.Kind.Od, NilRef)	   (* skip &quot;OD&quot; *)
        END
    | JunoToken.Kind.If =&gt;
        VAR ifCmd := NEW(JunoAST.If, bp := End); BEGIN
          ast := ifCmd;
          Match(la);					   (* skip &quot;IF&quot; *)
          Cmd(la, ifCmd.body);				   (* skip body *)
          MatchKind(la, JunoToken.Kind.Fi, NilRef)	   (* skip &quot;FI&quot; *)
        END
    | JunoToken.Kind.Save =&gt;
        VAR save := NEW(JunoAST.Save, bp := End); id: REFANY; BEGIN
          ast := save;
          Match(la);			                   (* skip &quot;SAVE&quot; *)
          TRY MatchKind(la, JunoToken.Kind.Id, id) FINALLY (* skip Id *)
            save.nm := NEW(JunoAST.QId, bp := End,
              id0 := JunoAST.NilId, id1 := id)
          END;
          MatchKind(la, JunoToken.Kind.In, NilRef);	   (* skip &quot;IN&quot; *)
          Cmd(la, save.body);				   (* skip body *)
          MatchKind(la, JunoToken.Kind.End, NilRef)	   (* skip &quot;END&quot; *)
        END
    | JunoToken.Kind.LBrace =&gt;
        VAR grp := NEW(JunoAST.GroupedCmd, bp := End); BEGIN
          ast := grp;
          Match(la);					   (* skip &quot;{&quot; *)
          Cmd(la, grp.body);				   (* skip body *)
          MatchKind(la, JunoToken.Kind.RBrace, NilRef)	   (* skip &quot;}&quot; *)
        END
    ELSE
        ast := NIL;
        RaiseError(la)
    END
  END Cmd6;

PROCEDURE <A NAME="QIdCmdTail"><procedure>QIdCmdTail</procedure></A>(
    VAR (*IO*) la: LookAhead;
    READONLY qid: JunoAST.QId;
    VAR (*OUT*) ast: JunoAST.Cmd)
  RAISES {Error, JunoLex.Error, Rd.Failure} =
</PRE><BLOCKQUOTE><EM> <CODE>QId</CODE> is the most recently parsed qualified identifier.
<P>
<PRE>
       QIdCmdTail = { &quot;,&quot; QId } QIdCT2
                  | FormulaQId FormTail.
       QIdCT2     = &quot;:=&quot; ExprList Cmd5.
</EM></BLOCKQUOTE><PRE>
  BEGIN
    IF la.t.kind=JunoToken.Kind.Comma OR la.t.kind=JunoToken.Kind.Assign THEN
      VAR qids: JunoAST.QIdList; raised := TRUE; BEGIN
        TRY QIdList(la, qid, qids); raised := FALSE FINALLY
          IF raised THEN
            ast := NEW(JunoAST.Assign, bp := End, vars := qids)
          END
        END;
        QIdCT2(la, qids, ast)
      END
    ELSE
      VAR f: JunoAST.Expr; raised := TRUE; BEGIN
        TRY Formula(la, f, qid); raised := FALSE FINALLY
          IF raised THEN ast := NEW(JunoAST.Guard, grd := f, bp := End) END
        END;
        FormTail(la, f, ast);
      END
    END
  END QIdCmdTail;

PROCEDURE <A NAME="QIdCT2"><procedure>QIdCT2</procedure></A>(
    VAR (*IO*) la: LookAhead;
    READONLY qids: JunoAST.QIdList;
    VAR (*OUT*) ast: JunoAST.Cmd)
  RAISES {Error, JunoLex.Error, Rd.Failure} =
</PRE><BLOCKQUOTE><EM> <CODE>Qids</CODE> is the most recently parsed list of qualified identifiers. Raises
   Error if <CODE>la.t.kind # JunoToken.Kind.Assign</CODE>.
<P>
<PRE>
       IDCT9 = &quot;:=&quot; ExprList Cmd5.
</EM></BLOCKQUOTE><PRE>
   VAR assign := NEW(JunoAST.Assign, vars := qids, bp := End); BEGIN
     ast := assign;
     MatchKind(la, JunoToken.Kind.Assign, NilRef);   (* skip &quot;:=&quot; *)
     ExprList(la, assign.exprs);	             (* skip terms *)
     (*
      * Convert assignment to procedure call if we are certain it must be a
      * procedure call, namely, if there is more than one variable on the left
      * and exactly one call on the right. In the absence of semantic
      * information, we can't be guaranteed it's a procedure call otherwise, so
      * be conservative and treat it as an assignment. Note that an expression
      * of the form: &quot;var := Proc(args)&quot; can be treated *either* as an
      * assignment or as a procedure call. *)
     IF assign.vars.size &gt; 1 AND assign.exprs.size = 1 THEN
       TYPECASE assign.exprs.head.expr OF
         JunoAST.Call(c) =&gt;
           VAR proc := CallToProcCall(c); BEGIN
             proc.outs := assign.vars;
             ast := proc
           END
       ELSE (* SKIP *)
       END
     END;
     Cmd5(la, ast, ast);
   END QIdCT2;

PROCEDURE <A NAME="FormTail"><procedure>FormTail</procedure></A>(
    VAR (*IO*) la: LookAhead;
    READONLY f: JunoAST.Expr;
    VAR (*OUT*) ast: JunoAST.Cmd)
  RAISES {Error, JunoLex.Error, Rd.Failure} =
</PRE><BLOCKQUOTE><EM> <CODE>F</CODE> is the most recently parsed formula. If it is followed by <CODE>-&gt;</CODE>, then
   <CODE>ast</CODE> is set to a guarded command with guard <CODE>f</CODE>. Otherwise, <CODE>f</CODE> must be a
   call expression (if not, Error is raised), and <CODE>ast</CODE> is set to the
   procedure call command equivalent to <CODE>f</CODE>.
<P>
<PRE>
       FormTail = Cmd5			 (* proc call 
</EM></BLOCKQUOTE><PRE>
|           | &quot;-&gt;&quot; Cmd2.		 (* guard *)
*)
  BEGIN
    IF la.t.kind = JunoToken.Kind.Guard THEN
      VAR grd := NEW(JunoAST.Guard, grd := f, bp := End); BEGIN
        ast := grd;
        Match(la);			 (* skip &quot;-&gt;&quot; *)
        Cmd2(la, grd.body)		 (* skip guard body *)
      END
    ELSE
      TYPECASE f OF
      | JunoAST.Call(c) =&gt;
          Cmd5(la, CallToProcCall(c), ast);
      ELSE
          ast := NEW(JunoAST.Guard, grd := f, bp := End);
          (* If the error message produced by the &quot;expected '-&gt;' token&quot; is too
	     specific, we can change the following to simply RAISE Error with
             argument ParseError(la). *)
          MatchKind(la, JunoToken.Kind.Guard, NilRef); (* signal error *)
      END
    END
  END FormTail;

PROCEDURE <A NAME="CallToProcCall"><procedure>CallToProcCall</procedure></A>(call: JunoAST.Call): JunoAST.ProcCall =
</PRE><BLOCKQUOTE><EM> Convert a call expression into a procedure call command. </EM></BLOCKQUOTE><PRE>
  BEGIN
    RETURN NEW(JunoAST.ProcCall, outs := JunoAST.EmptyQIdList,
      inouts := call.inouts, inout_parens := call.inout_parens,
      name := call.name, ins := call.ins, bp := End)
  END CallToProcCall;
</PRE> =============================== FORMULAS ================================ 

<P><PRE>PROCEDURE <A NAME="Formula"><procedure>Formula</procedure></A>(
    VAR (*IO*) la: LookAhead;
    VAR (*OUT*) ast: JunoAST.Expr;
    READONLY qid: JunoAST.QId := NIL)
  RAISES {Error, JunoLex.Error, Rd.Failure} =
</PRE><BLOCKQUOTE><EM><P>
<PRE>
       Formula    = Form1 [ OR Formula ].
       FormulaQId = Form1QId [ OR Formula ].
</EM></BLOCKQUOTE><PRE>
  BEGIN
    Form1(la, ast, qid);
    IF la.t.kind = JunoToken.Kind.Or THEN
      VAR or := NEW(JunoAST.Or, f1 := ast, bp := End); BEGIN
        ast := or;
        Match(la);			 (* skip &quot;OR&quot; *)
        Formula(la, or.f2)
      END
    END
  END Formula;

PROCEDURE <A NAME="Form1"><procedure>Form1</procedure></A>(
    VAR (*IO*) la: LookAhead;
    VAR (*OUT*) ast: JunoAST.Expr;
    READONLY qid: JunoAST.QId := NIL)
  RAISES {Error, JunoLex.Error, Rd.Failure} =
</PRE><BLOCKQUOTE><EM><P>
<PRE>
       Form1    = Form2 [ AND Form1 ].
       Form1QId = Form3QId [ AND Form1 ].
</EM></BLOCKQUOTE><PRE>
  BEGIN
    IF qid = NIL THEN Form2(la, ast) ELSE Form3(la, ast, qid) END;
    IF la.t.kind = JunoToken.Kind.And THEN
      VAR and := NEW(JunoAST.And, f1 := ast, bp := End); BEGIN
        ast := and;
        Match(la);			 (* skip &quot;AND&quot; *)
        Form1(la, and.f2)
      END
    END
  END Form1;

PROCEDURE <A NAME="Form2"><procedure>Form2</procedure></A>(VAR (*IO*) la: LookAhead; VAR (*OUT*) ast: JunoAST.Expr)
  RAISES {Error, JunoLex.Error, Rd.Failure} =
</PRE><BLOCKQUOTE><EM><P>
<PRE>
       Form2 = Form3 | NOT Form2.
</EM></BLOCKQUOTE><PRE>
  BEGIN
    IF la.t.kind = JunoToken.Kind.Not THEN
      VAR not := NEW(JunoAST.Not, bp := End); BEGIN
        ast := not;
        Match(la);			 (* skip &quot;NOT&quot; *)
        Form2(la, not.f)
      END
    ELSE
      Form3(la, ast)
    END
  END Form2;

PROCEDURE <A NAME="Form3"><procedure>Form3</procedure></A>(
    VAR (*IO*) la: LookAhead;
    VAR (*OUT*) ast: JunoAST.Expr;
    READONLY qid: JunoAST.QId := NIL)
  RAISES {Error, JunoLex.Error, Rd.Failure} =
</PRE><BLOCKQUOTE><EM> This procedure actually binds <CODE>ast</CODE> to an object that is a proper subtype
   of JunoAST.Relation.
<P>
<PRE>
       Form3      = Expr [ RelationOp Expr ].
       Form3QId   = ExprQId  [ RelationOp Expr ].
       RelationOp = &quot;=&quot; | &quot;#&quot; | &quot;&lt;&quot; | &quot;&gt;&quot; | &quot;&lt;=&quot; | &quot;&gt;=&quot; | CONG | PARA | HOR | VER.
</EM></BLOCKQUOTE><PRE>
  VAR rel: JunoAST.Relation; BEGIN
    Expr(la, ast, qid);
    CASE la.t.kind OF
    | JunoToken.Kind.Equals, JunoToken.Kind.Near =&gt;
  	  rel := NEW(JunoAST.Equals, bp := End, e1 := ast,
  	    near := (la.t.kind = JunoToken.Kind.Near));
  	  ast := rel;
  	  Match(la);			              (* skip &quot;=&quot; or &quot;~&quot; *)
  	  Expr(la, rel.e2)
    | JunoToken.Kind.Differs =&gt;
  	  rel := NEW(JunoAST.Differs, e1 := ast, bp := End);
  	  ast := rel;
  	  Match(la);			              (* skip &quot;#&quot; *)
  	  Expr(la, rel.e2)
    | JunoToken.Kind.Less =&gt;
  	  rel := NEW(JunoAST.Less, e1 := ast, bp := End);
  	  ast := rel;
  	  Match(la);			              (* skip &quot;&lt;&quot; *)
  	  Expr(la, rel.e2)
    | JunoToken.Kind.Greater =&gt;
  	  rel := NEW(JunoAST.Greater, e1 := ast, bp := End);
  	  ast := rel;
  	  Match(la);			              (* skip &quot;&gt;&quot; *)
  	  Expr(la, rel.e2)
    | JunoToken.Kind.AtMost =&gt;
  	  rel := NEW(JunoAST.AtMost, e1 := ast, bp := End);
  	  ast := rel;
  	  Match(la);			              (* skip &quot;&lt;=&quot; *)
  	  Expr(la, rel.e2)
    | JunoToken.Kind.AtLeast =&gt;
  	  rel := NEW(JunoAST.AtLeast, e1 := ast, bp := End);
  	  ast := rel;
  	  Match(la);			              (* skip &quot;&gt;=&quot; *)
  	  Expr(la, rel.e2)
    | JunoToken.Kind.Cong =&gt;
  	  rel := NEW(JunoAST.Cong, e1 := ast, bp := End);
  	  ast := rel;
  	  Match(la);			              (* skip &quot;CONG&quot; *)
  	  Expr(la, rel.e2)
    | JunoToken.Kind.Para =&gt;
  	  rel := NEW(JunoAST.Para, e1 := ast, bp := End);
  	  ast := rel;
  	  Match(la);			              (* skip &quot;PARA&quot; *)
  	  Expr(la, rel.e2)
    | JunoToken.Kind.Hor =&gt;
  	  rel := NEW(JunoAST.Hor, e1 := ast, bp := End);
  	  ast := rel;
  	  Match(la);			              (* skip &quot;HOR&quot; *)
  	  Expr(la, rel.e2)
    | JunoToken.Kind.Ver =&gt;
  	  rel := NEW(JunoAST.Ver, e1 := ast, bp := End);
  	  ast := rel;
  	  Match(la);			              (* skip &quot;VER&quot; *)
  	  Expr(la, rel.e2)
    ELSE (* SKIP *)
    END
  END Form3;
</PRE> ============================== EXPRESSIONS ============================== 

<P><PRE>PROCEDURE <A NAME="Expr"><procedure>Expr</procedure></A>(
    VAR (*IO*) la: LookAhead;
    VAR (*OUT*) ast: JunoAST.Expr;
    READONLY qid: JunoAST.QId := NIL)
  RAISES {Error, JunoLex.Error, Rd.Failure} =
</PRE><BLOCKQUOTE><EM><P>
<PRE>
       Expr    = Expr1 [ REL Expr1 ].
       ExprQId = Expr1QId [ REL Expr1 ].
</EM></BLOCKQUOTE><PRE>
  BEGIN
    Expr1(la, ast, qid);
    IF la.t.kind = JunoToken.Kind.Rel THEN
      VAR rel := NEW(JunoAST.Rel, e1 := ast, bp := End); BEGIN
        ast := rel;
        Match(la);			 (* skip &quot;REL&quot; *)
        Expr1(la, rel.e2);
      END
    END
  END Expr;

PROCEDURE <A NAME="Expr1"><procedure>Expr1</procedure></A>(
    VAR (*IO*) la: LookAhead;
    VAR (*OUT*) ast: JunoAST.Expr;
    READONLY qid: JunoAST.QId := NIL)
  RAISES {Error, JunoLex.Error, Rd.Failure} =
</PRE><BLOCKQUOTE><EM><P>
<PRE>
       Expr1    = Expr2 [ Expr1Tail ].
       Expr1QId = Expr2QId [ Expr1Tail ].
</EM></BLOCKQUOTE><PRE>
  BEGIN
    Expr2(la, ast, qid);
    Expr1Tail(la, ast)
  END Expr1;

PROCEDURE <A NAME="Expr1Tail"><procedure>Expr1Tail</procedure></A>(
    VAR (*IO*) la: LookAhead;
    VAR (*INOUT*) ast: JunoAST.Expr)
  RAISES {Error, JunoLex.Error, Rd.Failure} =
</PRE><BLOCKQUOTE><EM> On entry, <CODE>ast</CODE> is the expression parsed so far. If the next token is NOT
   an <CODE>AddOp</CODE>, then this procedure consumes no tokens and leaves <CODE>ast</CODE>
   unchanged.
<P>
<PRE>
       Expr1Tail = AddOp Expr2 [ Expr1Tail ].
       AddOp     = &quot;+&quot; | &quot;-&quot; | &quot;&amp;&quot;.
</EM></BLOCKQUOTE><PRE>
  VAR add: JunoAST.BuiltInAddFunc; BEGIN
    CASE la.t.kind OF
    | JunoToken.Kind.Plus =&gt;   add := NEW(JunoAST.Plus,   e1 := ast, bp :=End);
    | JunoToken.Kind.Minus =&gt;  add := NEW(JunoAST.Minus,  e1 := ast, bp :=End);
    | JunoToken.Kind.Concat =&gt; add := NEW(JunoAST.Concat, e1 := ast, bp :=End);
    ELSE RETURN
    END;
    ast := add;
    Match(la);				 (* skip &quot;+&quot;, &quot;-&quot;, or &quot;&amp;&quot; *)
    Expr2(la, add.e2);
    Expr1Tail(la, ast)
  END Expr1Tail;

PROCEDURE <A NAME="Expr2"><procedure>Expr2</procedure></A>(
    VAR (*IO*) la: LookAhead;
    VAR (*OUT*) ast: JunoAST.Expr;
    READONLY qid: JunoAST.QId := NIL)
  RAISES {Error, JunoLex.Error, Rd.Failure} =
</PRE><BLOCKQUOTE><EM><P>
<PRE>
       Expr2    = Expr3 [ Expr2Tail ].
       Expr2QId = Expr4QId [ Expr2Tail ].
</EM></BLOCKQUOTE><PRE>
  BEGIN
    IF qid = NIL THEN Expr3(la, ast) ELSE Expr4(la, ast, qid) END;
    Expr2Tail(la, ast)
  END Expr2;

PROCEDURE <A NAME="Expr2Tail"><procedure>Expr2Tail</procedure></A>(
    VAR (*IO*) la: LookAhead;
    VAR (*INOUT*) ast: JunoAST.Expr)
  RAISES {Error, JunoLex.Error, Rd.Failure} =
</PRE><BLOCKQUOTE><EM> On entry, <CODE>ast</CODE> is the expression parsed so far. If the next token is NOT
   a <CODE>MulOp</CODE>, then this procedure consumes no tokens and leaves <CODE>ast</CODE>
   unchanged.
<P>
<PRE>
       Expr2Tail = MulOp Expr3 [ Expr2Tail ].
       MulOp     = &quot;*&quot; | &quot;/&quot; | DIV | MOD.
</EM></BLOCKQUOTE><PRE>
  VAR mul: JunoAST.BuiltInMulFunc; BEGIN
    CASE la.t.kind OF
    | JunoToken.Kind.Times =&gt;  mul := NEW(JunoAST.Times,  e1 := ast, bp :=End);
    | JunoToken.Kind.Divide =&gt; mul := NEW(JunoAST.Divide, e1 := ast, bp :=End);
    | JunoToken.Kind.Div =&gt;    mul := NEW(JunoAST.Div,    e1 := ast, bp :=End);
    | JunoToken.Kind.Mod =&gt;    mul := NEW(JunoAST.Mod,    e1 := ast, bp :=End);
    ELSE RETURN
    END;
    ast := mul;
    Match(la);				 (* skip &quot;*&quot;, &quot;/&quot;, &quot;DIV&quot;, or &quot;MOD&quot; *)
    Expr3(la, mul.e2);
    Expr2Tail(la, ast)
  END Expr2Tail;

PROCEDURE <A NAME="Expr3"><procedure>Expr3</procedure></A>(VAR (*IO*) la: LookAhead; VAR (*OUT*) ast: JunoAST.Expr)
  RAISES {Error, JunoLex.Error, Rd.Failure} =
</PRE><BLOCKQUOTE><EM><P>
<PRE>
       Expr3 = Expr4 | &quot;-&quot; Expr3.
</EM></BLOCKQUOTE><PRE>
  BEGIN
    IF la.t.kind = JunoToken.Kind.Minus THEN
      VAR minus := NEW(JunoAST.UMinus, bp := End); BEGIN
        ast := minus;
        Match(la);                         (* skip &quot;-&quot; *)
        Expr3(la, minus.e)
      END
    ELSE
      Expr4(la, ast)
    END
  END Expr3;

PROCEDURE <A NAME="Expr4"><procedure>Expr4</procedure></A>(
    VAR (*IO*) la: LookAhead;
    VAR (*OUT*) ast: JunoAST.Expr;
    READONLY qid: JunoAST.QId := NIL)
  RAISES {Error, JunoLex.Error, Rd.Failure} =
</PRE><BLOCKQUOTE><EM><P>
<PRE>
       Expr4       = NIL | TRUE | FALSE | Literal | BuiltIn
                   | QID [ QIDExprTail ]
                   | &quot;(&quot; LPExprTail
                   | &quot;[&quot; ExprList &quot;]&quot;.
       Expr4QId    = [ QIDExprTail ].
       QIDExprTail = [ &quot;:&quot; QID ] LPET7.
       LPET7       = &quot;(&quot; [ ExprList ] &quot;)&quot;.
</EM></BLOCKQUOTE><PRE>
  CONST FirstQIDExprTail = SET OF JunoToken.Kind{
    JunoToken.Kind.Colon, JunoToken.Kind.LPren};
  BEGIN
    IF qid = NIL THEN
      CASE la.t.kind OF
      | JunoToken.Kind.Nil =&gt;   ast := JunoAST.NilVal;   Match(la)
      | JunoToken.Kind.True =&gt;  ast := JunoAST.TrueVal;  Match(la)
      | JunoToken.Kind.False =&gt; ast := JunoAST.FalseVal; Match(la)
      | JunoToken.Kind.LitReal =&gt;
          VAR num := NEW(JunoAST.Number, bp := End); BEGIN
            ast := num;
            MatchReal(la, JunoToken.Kind.LitReal, num.val)  (* skip real *)
          END
      | JunoToken.Kind.LitText =&gt;
          VAR txt := NEW(JunoAST.Text, bp := End); t: REFANY; BEGIN
            ast := txt;
            TRY MatchKind(la, JunoToken.Kind.LitText, t) FINALLY
              txt.val := NARROW(t, TEXT)	            (* skip text *)
            END
          END
      | FIRST(JunoToken.ResvdId)..LAST(JunoToken.ResvdId) =&gt;
          BuiltIn(la, ast)
      | JunoToken.Kind.Id =&gt;				       (* Id of QID *)
          VAR qid := NEW(JunoAST.QId, bp := End); id: REFANY; BEGIN
            TRY
              TRY MatchKind(la, JunoToken.Kind.Id, id) FINALLY (* skip ID *)
                qid.id0 := NARROW(id, JunoAST.Id)
              END;
              QID(la, qid);				       (* parse QID *)
            FINALLY
              ast := qid
            END;
            IF la.t.kind IN FirstQIDExprTail THEN
              QIDExprTail(la, qid, ast)
            END
          END
      | JunoToken.Kind.LPren =&gt;
          VAR raised := TRUE; BEGIN
            TRY Match(la); raised := FALSE FINALLY
              IF raised THEN ast := NEW(JunoAST.GroupedExpr, bp := End) END
            END
          END;
          LPExprTail(la, ast)				       (* skip tail *)
      | JunoToken.Kind.LBracket =&gt;
          VAR lst := NEW(JunoAST.List, bp := End); BEGIN
            ast := lst;
            Match(la);					       (* skip &quot;[&quot; *)
            ExprList(la, lst.elts);		               (* skip list *)
            MatchKind(la, JunoToken.Kind.RBracket, NilRef);    (* skip &quot;]&quot; *)
          END
      ELSE
          ast := NIL;
          RaiseError(la)
      END
    ELSE
      IF la.t.kind IN FirstQIDExprTail THEN
        QIDExprTail(la, qid, ast)
      ELSE
        ast := qid
      END
    END
  END Expr4;

PROCEDURE <A NAME="BuiltIn"><procedure>BuiltIn</procedure></A>(
    VAR (*IO*) la: LookAhead;
    VAR (*OUT*) ast: JunoAST.Expr)
  RAISES {Error, JunoLex.Error, Rd.Failure} =
</PRE><BLOCKQUOTE><EM> Assumes <CODE>la.t.kind</CODE> is in <CODE>JunoToken.ResvdId</CODE>. </EM></BLOCKQUOTE><PRE>
  VAR
    up: JunoAST.BIUPred;
    uf: JunoAST.BIUFunc;
    bf: JunoAST.BIBFunc;
  BEGIN
    CASE la.t.kind OF &lt;* NOWARN *&gt;
      JunoToken.Kind.Real    =&gt; up := NEW(JunoAST.IsReal,  bp := End);
    | JunoToken.Kind.Text    =&gt; up := NEW(JunoAST.IsText,  bp := End);
    | JunoToken.Kind.Pair    =&gt; up := NEW(JunoAST.IsPair,  bp := End);
    | JunoToken.Kind.Int     =&gt; up := NEW(JunoAST.IsInt,   bp := End);
    | JunoToken.Kind.Floor   =&gt; uf := NEW(JunoAST.Floor,   bp := End);
    | JunoToken.Kind.Ceiling =&gt; uf := NEW(JunoAST.Ceiling, bp := End);
    | JunoToken.Kind.Round   =&gt; uf := NEW(JunoAST.Round,   bp := End);
    | JunoToken.Kind.Abs     =&gt; uf := NEW(JunoAST.Abs,     bp := End);
    | JunoToken.Kind.Sin     =&gt; uf := NEW(JunoAST.Sin,     bp := End);
    | JunoToken.Kind.Cos     =&gt; uf := NEW(JunoAST.Cos,     bp := End);
    | JunoToken.Kind.Ln      =&gt; uf := NEW(JunoAST.Ln,      bp := End);
    | JunoToken.Kind.Exp     =&gt; uf := NEW(JunoAST.Exp,     bp := End);
    | JunoToken.Kind.Car     =&gt; uf := NEW(JunoAST.Car,     bp := End);
    | JunoToken.Kind.Cdr     =&gt; uf := NEW(JunoAST.Cdr,     bp := End);
    | JunoToken.Kind.Max     =&gt; bf := NEW(JunoAST.Max,     bp := End);
    | JunoToken.Kind.Min     =&gt; bf := NEW(JunoAST.Min,     bp := End);
    | JunoToken.Kind.Atan    =&gt; bf := NEW(JunoAST.Atan,    bp := End);
    END;
    CASE la.t.kind OF &lt;* NOWARN *&gt;
      JunoToken.Kind.Real..JunoToken.Kind.Int =&gt;
        ast := up;
        Match(la);			             (* skip predicate name *)
        MatchKind(la, JunoToken.Kind.LPren, NilRef); (* skip &quot;(&quot; *)
        Expr(la, up.e);			             (* skip argument *)
        MatchKind(la, JunoToken.Kind.RPren, NilRef); (* skip &quot;)&quot; *)
    | JunoToken.Kind.Floor..JunoToken.Kind.Cdr =&gt;
        ast := uf;
        Match(la);			             (* skip function name *)
        MatchKind(la, JunoToken.Kind.LPren, NilRef); (* skip &quot;(&quot; *)
        Expr(la, uf.e);			             (* skip argument *)
        MatchKind(la, JunoToken.Kind.RPren, NilRef); (* skip &quot;)&quot; *)
    | JunoToken.Kind.Max..JunoToken.Kind.Atan =&gt;
        ast := bf;
        Match(la);			             (* skip function name *)
        MatchKind(la, JunoToken.Kind.LPren, NilRef); (* skip &quot;(&quot; *)
        Expr(la, bf.e1);		             (* skip 1st argument *)
        MatchKind(la, JunoToken.Kind.Comma, NilRef); (* skip &quot;,&quot; *)
        Expr(la, bf.e2);		             (* skip 2nd argument *)
        MatchKind(la, JunoToken.Kind.RPren, NilRef); (* skip &quot;)&quot; *)
    END
  END BuiltIn;

PROCEDURE <A NAME="QIDExprTail"><procedure>QIDExprTail</procedure></A>(
    VAR (*IO*) la: LookAhead;
    READONLY qid: JunoAST.QId;
    VAR (*OUT*) ast: JunoAST.Expr)
  RAISES {Error, JunoLex.Error, Rd.Failure} =
</PRE><BLOCKQUOTE><EM> Assumes <CODE>la.t.kind = JunoToken.Kind.Colon or </CODE>la.t.kind =
   JunoToken.Kind.LPren<CODE>.
<P>
   </CODE>Qid<CODE> is the most recently parsed qualified identifier. Its meaning depends
   on the next token. If the next token is </CODE>:<CODE>, then </CODE>qid<CODE> is the name of an
   INOUT parameter to a procedure call. Otherwise, it is the name of the
   procedure, predicate, or function being called. In either case, </CODE>ast<CODE> is
   actually bound to an object of type JunoAST.Call.
<P>
<PRE>
       QIDExprTail = [ &quot;:&quot; QID ] LPET7.
       LPET7       = &quot;(&quot; [ ExprList ] &quot;)&quot;.
</EM></BLOCKQUOTE><PRE>
  VAR call := NEW(JunoAST.Call, bp := End); BEGIN
    ast := call;
    IF la.t.kind = JunoToken.Kind.Colon THEN
      call.inouts := JunoASTUtils.NewQIdList(qid, bp := End);
      Match(la);			           (* skip &quot;:&quot; *)
      QID(la, call.name)		           (* read QID call name *)
    ELSE
      call.inouts := JunoAST.EmptyExprList;      (* make INOUT params empty *)
      call.name := qid;
    END;
    MatchKind(la, JunoToken.Kind.LPren, NilRef); (* skip &quot;(&quot; *)
    IF la.t.kind # JunoToken.Kind.RPren
      THEN ExprList(la, call.ins)		 (* skip expr list *)
      ELSE call.ins := JunoAST.EmptyExprList     (* make IN params empty *)
    END;
    MatchKind(la, JunoToken.Kind.RPren, NilRef); (* skip &quot;)&quot; *)
  END QIDExprTail;

PROCEDURE <A NAME="LPExprTail"><procedure>LPExprTail</procedure></A>(VAR (*IO*) la: LookAhead; VAR (*OUT*) ast: JunoAST.Expr)
  RAISES {Error, JunoLex.Error, Rd.Failure} =
</PRE><BLOCKQUOTE><EM><P>
<PRE>
       LPExprTail = LPET2 | LPET3.
       LPET2      = E NearVarList &quot;::&quot; Constraint &quot;)&quot;.      (* existential quant 
</EM></BLOCKQUOTE><PRE>
|  LPET3      = Formula LPET4.                (* paren formula, pair, inouts *)
*)
  BEGIN
    IF la.t.kind = JunoToken.Kind.Exists THEN
      VAR ex := NEW(JunoAST.Exists, bp := End); BEGIN
        ast := ex;
        Match(la);					  (* skip &quot;E&quot; *)
        NearVarList(la, ex.vars);			  (* skip var list *)
        MatchKind(la, JunoToken.Kind.SuchThat, NilRef);	  (* skip &quot;::&quot; *)
        Formula(la, ex.f);				  (* skip constraint *)
        MatchKind(la, JunoToken.Kind.RPren, NilRef);	  (* skip final &quot;)&quot; *)
      END
    ELSE
      VAR first: JunoAST.Expr; raised := TRUE; BEGIN
        TRY Formula(la, first); raised := FALSE FINALLY	  (* skip first expr *)
          IF raised THEN
            ast := NEW(JunoAST.GroupedExpr, expr := first, bp := End)
          END
        END;
        LPET4(la, first, ast)
      END
    END
  END LPExprTail;

PROCEDURE <A NAME="LPET4"><procedure>LPET4</procedure></A>(
    VAR (*IO*) la: LookAhead;
    READONLY f1: JunoAST.Expr;
    VAR (*OUT*) ast: JunoAST.Expr)
  RAISES {Error, JunoLex.Error, Rd.Failure} =
</PRE><BLOCKQUOTE><EM> <CODE>F1</CODE> is the most recently parsed formula. Its meaning depends on which
   LPET4 alternative is taken. If the first alternative is taken, then <CODE>ast</CODE>
   will be a grouped expression containing <CODE>f1</CODE> or a procedure call expression
   with <CODE>f1</CODE> as the single INOUT parameter to the call. If the second
   alternative is taken, then <CODE>f1</CODE> is considered the first expression in an
   expression list forming a pair of the INOUT parameters to a procedure call.
<P>
<PRE>
       LPET4 = &quot;)&quot; [ LPET6 ]
             | &quot;,&quot; Expr LPET5.
       LPET6 = &quot;:&quot; QID LPET7.
</EM></BLOCKQUOTE><PRE>
  BEGIN
    CASE la.t.kind OF
    | JunoToken.Kind.RPren =&gt;
        VAR raised := TRUE; BEGIN
          TRY Match(la); raised := FALSE FINALLY
            IF raised THEN
              ast := NEW(JunoAST.GroupedExpr, expr := f1, bp := End)
            END
          END
        END;
        IF la.t.kind = JunoToken.Kind.Colon THEN
          VAR call := NEW(JunoAST.Call, inout_parens := TRUE,
            inouts := JunoASTUtils.NewExprList(f1, bp := End), bp := End);
          BEGIN
            ast := call;
            LPET6(la, call)
          END
        ELSE
          ast := NEW(JunoAST.GroupedExpr, expr := f1, bp := End)
        END
    | JunoToken.Kind.Comma =&gt;
        VAR f2: JunoAST.Expr := NIL; raised := TRUE; BEGIN
          TRY
            Match(la);                     (* skip &quot;,&quot; *)
            Expr(la, f2);                  (* skip second expr *)
            raised := FALSE
          FINALLY
            IF raised THEN
              ast := NEW(JunoAST.Pair, e1 := f1, e2 := f2, bp := End)
            END
          END;
          LPET5(la, f1, f2, ast)
        END
    ELSE
        ast := NEW(JunoAST.GroupedExpr, expr := f1, bp := End);
        RaiseError(la)
    END
  END LPET4;

PROCEDURE <A NAME="LPET5"><procedure>LPET5</procedure></A>(
    VAR (*IO*) la: LookAhead;
    READONLY f1, f2: JunoAST.Expr;
    VAR (*OUT*) ast: JunoAST.Expr)
  RAISES {Error, JunoLex.Error, Rd.Failure} =
</PRE><BLOCKQUOTE><EM> <CODE>F1</CODE> and <CODE>f2</CODE> are the two most recently parsed expressions of an expression
   list. Their meaning depends on which LPET5 alternative is taken. If the
   first alternative is taken, then they are either the two elements of a pair
   or the two INOUT parameters to a procedure. If the second alternative is
   taken, then they are the first two of many INOUT parameters to a procedure.
<P>
<PRE>
       LPET5 = &quot;)&quot; [ LPET6 ]
             | &quot;,&quot; QIDList &quot;)&quot; LPET6.
       LPET6 = &quot;:&quot; QID LPET7.
</EM></BLOCKQUOTE><PRE>
  BEGIN
    CASE la.t.kind OF
    | JunoToken.Kind.RPren =&gt;
        VAR raised := TRUE; BEGIN
          TRY Match(la); raised := FALSE FINALLY
            IF raised THEN
              ast := NEW(JunoAST.Pair, e1 := f1, e2 := f2, bp := End)
            END
          END
        END;
        IF la.t.kind = JunoToken.Kind.Colon THEN
          VAR call := NEW(JunoAST.Call, inout_parens := TRUE,
            inouts := NewExprList2(f1, f2), bp := End);
          BEGIN
            ast := call;
            LPET6(la, call)
          END
        ELSE
          ast := NEW(JunoAST.Pair, e1 := f1, e2 := f2, bp := End)
        END
    | JunoToken.Kind.Comma =&gt;
        VAR
          exprs := NewExprList2(f1, f2);
          call := NEW(JunoAST.Call, bp := End,
            inout_parens := TRUE, inouts := exprs);
          qid_list: JunoAST.QIdList;
        BEGIN
          ast := call;
          VAR raised := TRUE; BEGIN
            TRY Match(la); raised := FALSE FINALLY     (* skip &quot;,&quot; *)
              IF raised THEN
                (* fix &quot;call&quot; so it will seem to contain &gt; 2 inouts *)
                INC(call.inouts.size);
                exprs.head.next.next := exprs.head
              END
            END
          END;
          TRY QIdList(la, NIL, qid_list) FINALLY       (* skip QID's *)
            INC(exprs.size, qid_list.size);
            exprs.head.next.next := qid_list.head
          END;
          MatchKind(la, JunoToken.Kind.RPren, NilRef); (* skip &quot;)&quot; *)
          LPET6(la, call)                              (* skip rest of proc *)
        END
    ELSE
        ast := NEW(JunoAST.Pair, e1 := f1, e2 := f2, bp := End);
        RaiseError(la)
    END;
  END LPET5;

PROCEDURE <A NAME="LPET6"><procedure>LPET6</procedure></A>(
    VAR (*IO*) la: LookAhead;
    VAR (*IO*) ast: JunoAST.Call)
  RAISES {Error, JunoLex.Error, Rd.Failure} =
</PRE><BLOCKQUOTE><EM> Assumes <CODE>ast</CODE> is initially a JunoAST.Call whose <CODE>inouts</CODE> field has been
   filled in. Expects JunoToken.Kind.Colon as next token.
<P>
<PRE>
       LPET6 = &quot;:&quot; QID LPET7.
       LPET7 = &quot;(&quot; [ ExprList ] &quot;)&quot;.
</EM></BLOCKQUOTE><PRE>
  BEGIN
    MatchKind(la, JunoToken.Kind.Colon, NilRef); (* skip &quot;:&quot; *)
    QID(la, ast.name);			         (* skip proc name *)
    MatchKind(la, JunoToken.Kind.LPren, NilRef); (* skip &quot;(&quot; *)
    IF la.t.kind # JunoToken.Kind.RPren
      THEN ExprList(la, ast.ins)		 (* skip expr list *)
      ELSE ast.ins := JunoAST.EmptyExprList      (* make IN params empty *)
    END;
    MatchKind(la, JunoToken.Kind.RPren, NilRef); (* skip &quot;)&quot; *)
  END LPET6;
</PRE> ============================= MISCELLANEOUS ============================= 

<P><PRE>PROCEDURE <A NAME="ExprList"><procedure>ExprList</procedure></A>(
    VAR (*IO*) la: LookAhead;
    VAR (*IO*) elist: JunoAST.ExprList)
  RAISES {Error, JunoLex.Error, Rd.Failure} =
</PRE><BLOCKQUOTE><EM> If <CODE>elist = NIL</CODE>, then this procedure parses the next expression as the
   first element of a list of expressions, and initializes <CODE>elist</CODE> to a new
   JunoAST.ExprList whose first element is that expression. Otherwise, it
   assumes <CODE>elist</CODE> is already initialized to a JunoAST.ExprList with a single
   element. In either case, this procedure parses the rest of the expression
   list.
<P>
<PRE>
       ExprList = Expr { &quot;,&quot; Expr }.
</EM></BLOCKQUOTE><PRE>
  VAR curr: JunoAST.ExprLink; BEGIN
    IF elist = NIL THEN
      curr := NEW(JunoAST.ExprLink);
      elist := NEW(JunoAST.ExprList, size := 1, head := curr, bp := End);
      Expr(la, curr.expr)
    ELSE
      curr := elist.head
    END;
    WHILE la.t.kind = JunoToken.Kind.Comma DO
      INC(elist.size);
      curr.next := NEW(JunoAST.ExprLink);
      curr := curr.next;
      Match(la);			 (* skip &quot;,&quot; *)
      Expr(la, curr.expr)		 (* skip Expr *)
    END
  END ExprList;

PROCEDURE <A NAME="NewExprList2"><procedure>NewExprList2</procedure></A>(READONLY e1, e2: JunoAST.Expr): JunoAST.ExprList =
</PRE><BLOCKQUOTE><EM> Return an expression list of length 2 containing <CODE>e1</CODE> and <CODE>e2</CODE>. </EM></BLOCKQUOTE><PRE>
  VAR
    curr := NEW(JunoAST.ExprLink, expr := e1);
    result := NEW(JunoAST.ExprList, size := 2, head := curr, bp := End);
  BEGIN
    curr.next := NEW(JunoAST.ExprLink, expr := e2);
    RETURN result
  END NewExprList2;

PROCEDURE <A NAME="QID"><procedure>QID</procedure></A>(VAR (*IO*) la: LookAhead; VAR (*IO*) qid: JunoAST.QId)
  RAISES {Error, JunoLex.Error, Rd.Failure} =
</PRE><BLOCKQUOTE><EM> If <CODE>qid = NIL</CODE>, <CODE>qid</CODE> is first bound to a new JunoAST.QId. In this case,
   <CODE>la.t.kind</CODE> must be <CODE>JunoToken.Kind.Id</CODE>, or else the Error exception is
   raised with <CODE>qid # NIL</CODE> and <CODE>qid.id0 = NIL</CODE>. In the error-free case when
   the current token is an identifier, the procedure stores the current token
   in <CODE>qid.id0</CODE>. If <CODE>qid # NIL</CODE>, it is assumed that <CODE>qid</CODE> contains the first
   part of a (potentially) qualified identifier in <CODE>qid.id0</CODE>.
<P>
   If the identifier is not qualified (i.e., no JunoToken.Kind.Dot appears in
   the token stream after the first identifier), then the single identifier is
   stored in <CODE>qid.id1</CODE>, and <CODE>qid.id0</CODE> is set to the special value
   <CODE>JunoAST.NilId</CODE>.
<P>
<PRE>
       QID = Id [ &quot;.&quot; Id ].
</EM></BLOCKQUOTE><PRE>
  VAR id: REFANY; BEGIN
    IF qid = NIL THEN
      qid := NEW(JunoAST.QId, bp := End);
      TRY MatchKind(la, JunoToken.Kind.Id, id) FINALLY
        qid.id0 := NARROW(id, JunoAST.Id)
      END
    END;
    IF la.t.kind = JunoToken.Kind.Dot THEN
      Match(la);					  (* skip &quot;.&quot; *)
      TRY MatchKind(la, JunoToken.Kind.Id, id) FINALLY
        qid.id1 := NARROW(id, JunoAST.Id)
      END
    ELSE
      qid.id1 := qid.id0;
      qid.id0 := JunoAST.NilId;
    END
  END QID;

PROCEDURE <A NAME="QIdList"><procedure>QIdList</procedure></A>(
    VAR (*IO*) la: LookAhead;
    VALUE qid: JunoAST.QId;
    VAR (*OUT*) qids: JunoAST.QIdList)
  RAISES {Error, JunoLex.Error, Rd.Failure} =
</PRE><BLOCKQUOTE><EM> Read a QIDList from <CODE>la</CODE>, storing the result in <CODE>qids</CODE>. If <CODE>qid # NIL</CODE>, it
   is expected to contain the first QID of the list. Otherwise, the first
   element of the list is read from <CODE>la</CODE>; in this case, if the current
   token is not <CODE>JunoToken.Kind.Id</CODE>, this procedure raises Error.
<P>
<PRE>
       QIDList = QID { &quot;,&quot; QID }.
</EM></BLOCKQUOTE><PRE>
  VAR curr: JunoAST.ExprLink; BEGIN
    TRY IF qid = NIL THEN QID(la, qid) END FINALLY
      qids := JunoASTUtils.NewQIdList(qid, bp := End)
    END;
    curr := qids.head;
    WHILE la.t.kind = JunoToken.Kind.Comma DO
      INC(qids.size);
      curr.next := NEW(JunoAST.ExprLink);
      curr := curr.next;
      Match(la);			   (* skip &quot;,&quot; *)
      TRY qid := NIL; QID(la, qid) FINALLY (* skip QId *)
        curr.expr := qid
      END
    END
  END QIdList;

PROCEDURE <A NAME="NearVar"><procedure>NearVar</procedure></A>(
    VAR (*IO*) la: LookAhead;
    VAR (*IO*) nv: JunoAST.NearVarLink)
  RAISES {Error, JunoLex.Error, Rd.Failure} =
</PRE><BLOCKQUOTE><EM> If <CODE>nv</CODE> = NIL, then a new <CODE>NearVarLink</CODE> is allocated for <CODE>nv</CODE>; otherwise,
   the information in <CODE>nv</CODE> is overwritten.
<P>
<PRE>
       NearVar = Id [ (&quot;~&quot; | &quot;=&quot;) Expr ].
</EM></BLOCKQUOTE><PRE>
  BEGIN
    IF nv = NIL THEN nv := NEW(JunoAST.NearVarLink) END;
    VAR id: REFANY; BEGIN
      TRY MatchKind(la, JunoToken.Kind.Id, id) FINALLY
        nv.id := NARROW(id, JunoAST.Id)
      END
    END;
    IF la.t.kind = JunoToken.Kind.Near THEN
      Match(la);			 (* skip &quot;~&quot; *)
      Expr(la, nv.hint)			 (* skip expression *)
    ELSIF la.t.kind = JunoToken.Kind.Equals THEN
      nv.frozen := TRUE;
      Match(la);			 (* skip &quot;=&quot; *)
      Expr(la, nv.hint)			 (* skip expression *)
    ELSE
      nv.hint := JunoAST.NilExpr
    END
  END NearVar;

PROCEDURE <A NAME="NearVarList"><procedure>NearVarList</procedure></A>(
    VAR (*IO*) la: LookAhead;
    VAR (*OUT*) nv: JunoAST.NearVarList)
  RAISES {Error, JunoLex.Error, Rd.Failure} =
</PRE><BLOCKQUOTE><EM><P>
<PRE>
       NearVarList = NearVar { &quot;,&quot; NearVar }.
</EM></BLOCKQUOTE><PRE>
  VAR last: JunoAST.NearVarLink; BEGIN
    nv := NEW(JunoAST.NearVarList, bp := End);
    NearVar(la, nv.head);
    last := nv.head;
    INC(nv.size);
    WHILE la.t.kind = JunoToken.Kind.Comma DO
      INC(nv.size);
      last.next := NEW(JunoAST.NearVarLink);
      last := last.next;
      Match(la);					  (* skip &quot;,&quot; *)
      NearVar(la, last)			                  (* skip NearVar *)
    END
  END NearVarList;

PROCEDURE <A NAME="IdList0"><procedure>IdList0</procedure></A>(
    VAR (*IO*) la: LookAhead;
    VAR (*OUT*) ids: JunoAST.IdList;
    READONLY first: JunoAST.Id := NIL)
  RAISES {Error, JunoLex.Error, Rd.Failure} =
</PRE><BLOCKQUOTE><EM><P>
<PRE>
       IDList = Id { &quot;,&quot; Id }.
</EM></BLOCKQUOTE><PRE>
  VAR id: REFANY; curr := NEW(JunoAST.IdLink); BEGIN
    ids := NEW(JunoAST.IdList, size := 1, head := curr, bp := End);
    IF first = NIL THEN
      TRY MatchKind(la, JunoToken.Kind.Id, id) FINALLY
        curr.id := NARROW(id, JunoAST.Id)
      END
    ELSE
      curr.id := first
    END;
    WHILE la.t.kind = JunoToken.Kind.Comma DO
      INC(ids.size);
      curr.next := NEW(JunoAST.IdLink);
      curr := curr.next;
      Match(la);					  (* skip &quot;,&quot; *)
      TRY MatchKind(la, JunoToken.Kind.Id, id) FINALLY    (* skip Id *)
        curr.id := NARROW(id, JunoAST.Id)
      END
    END
  END IdList0;

PROCEDURE <A NAME="NewIdList"><procedure>NewIdList</procedure></A>(READONLY id: JunoAST.Id := NIL): JunoAST.IdList =
</PRE><BLOCKQUOTE><EM> Create and return a new JunoAST.IdList. If <CODE>id # NIL</CODE>, then the
   returned list has size 1 and the single value <CODE>id</CODE>. Otherwise, the list
   has size 0. </EM></BLOCKQUOTE><PRE>
  VAR result := NEW(JunoAST.IdList, bp := End); BEGIN
    IF id # NIL THEN
      result.size := 1;
      result.head := NEW(JunoAST.IdLink, id := id)
    END;
    RETURN result
  END NewIdList;

PROCEDURE <A NAME="MatchKind"><procedure>MatchKind</procedure></A>(
    VAR (*IO*) la: LookAhead;
    READONLY kind: JunoToken.Kind;
    VAR (*OUT*) val: REFANY
  ) RAISES {Error, JunoLex.Error, Rd.Failure} =
</PRE><BLOCKQUOTE><EM> Like Match() below, only raises <CODE>Error</CODE> with the appropriate <CODE>ErrorRec</CODE>
   if <CODE>la.t.kind # kind</CODE>. <CODE>Val</CODE> is set to the value corresponding to the
   current token; this only has meaning if <CODE>kind</CODE> is one of:
   <CODE>JunoToken.Kind.LitReal</CODE>, <CODE>JunoToken.Kind.LitText</CODE>, <CODE>JunoToken.Kind.Id</CODE>,
   or <CODE>JunoToken.Kind.Comment</CODE>. </EM></BLOCKQUOTE><PRE>
  BEGIN
    IF la.t.kind # kind THEN
      val := NIL;
      RaiseError(la, kind)
    END;
    val := la.t.val;			 (* do assignment in all cases *)
    Match(la)
  END MatchKind;

PROCEDURE <A NAME="MatchReal"><procedure>MatchReal</procedure></A>(
    VAR (*IO*) la: LookAhead;
    READONLY kind: JunoToken.Kind;
    VAR (*OUT*) num: JunoValue.Real
  ) RAISES {Error, JunoLex.Error, Rd.Failure} =
</PRE><BLOCKQUOTE><EM> Like Match() below, only raises <CODE>Error</CODE> with the appropriate <CODE>ErrorRec</CODE>
   if <CODE>la.t.kind # kind</CODE>. <CODE>Val</CODE> is set to the real value corresponding to
   the current token, namely <CODE>la.t.num</CODE>. This operation is meaningful only
   if <CODE>kind</CODE> is <CODE>JunoToken.Kind.LitReal</CODE>. </EM></BLOCKQUOTE><PRE>
  BEGIN
    IF la.t.kind # kind THEN RaiseError(la, kind) END;
    num := la.t.num;
    Match(la)
  END MatchReal;

PROCEDURE <A NAME="Match"><procedure>Match</procedure></A>(VAR (*IO*) la: LookAhead) RAISES {JunoLex.Error, Rd.Failure} =
</PRE><BLOCKQUOTE><EM> Reads the next token into <CODE>la.t</CODE>. If there is a lex error reading the next
   token, it is converted into the appropriate <CODE>JunoLex.ErrorRec</CODE> and
   <CODE>JunoLex.Error</CODE> is raised. </EM></BLOCKQUOTE><PRE>
  BEGIN
    la.t := la.s.next();
    INC(la.cnt)
  END Match;

PROCEDURE <A NAME="RaiseError"><procedure>RaiseError</procedure></A>(
    VAR (*IO*) la: LookAhead;
    READONLY kind := JunoToken.Kind.Unknown)
  RAISES {Error} =
</PRE><BLOCKQUOTE><EM> Raises <CODE>Error</CODE> with an argument <CODE>ErrorRec</CODE> corresponding to the current
   token, and the expected kind of token <CODE>kind</CODE>. This procedure also has the
   side-effect of decrementing <CODE>la.cnt</CODE>, since we don't want to count the
   token that caused the parse error as having been parsed. </EM></BLOCKQUOTE><PRE>
  BEGIN
    DEC(la.cnt);
    RAISE Error(NEW(ErrorRec, found := JunoToken.Copy(la.t),
      additional := JunoLex.Close(la.s), expected := kind))
  END RaiseError;

BEGIN
  End := JunoAST.End
END JunoParse.
</PRE>
</inModule>
<HR>
<A NAME="x1">interface JunoValue is in:
</A><UL>
<LI><A HREF="../../juno-machine/src/JunoValue.i3.html#0TOP0">juno-machine/src/JunoValue.i3</A>
<LI><A HREF="../../pkl-fonts/src/JunoValue.i3.html#0TOP0">pkl-fonts/src/JunoValue.i3</A>
</UL>
<P>
<PRE>























</PRE>
</BODY>
</HTML>
