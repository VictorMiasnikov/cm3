<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: juno-compiler/src/JunoUnparse.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>juno-compiler/src/JunoUnparse.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright (C) 1992, Digital Equipment Corporation                         
 All rights reserved.                                                      
 See the file COPYRIGHT for a full description.                            
                                                                           
 Last modified on Thu Mar 28 14:47:44 PST 1996 by heydon                   
      modified on Tue Aug 31 22:16:59 PDT 1993 by gnelson                  
      modified on Fri Aug  7 21:54:02 PDT 1992 by myers                    

<P><PRE>MODULE <module><implements><A HREF="JunoUnparse.i3.html">JunoUnparse</A></implements></module>;

IMPORT <A HREF="JunoAST.i3.html">JunoAST</A> AS AST, <A HREF="#x1">JunoValue</A>, <A HREF="IndexedNF.i3.html">IndexedNF</A>;
IMPORT <A HREF="../../libm3/src/atom/Atom.i3.html">Atom</A>, <A HREF="../../libm3/src/formatter/Formatter.i3.html">Formatter</A>, <A HREF="../../libm3/src/rw/Wr.i3.html">Wr</A>, <A HREF="../../m3core/src/text/Text.i3.html">Text</A>, <A HREF="../../m3core/src/thread/Common/Thread.i3.html">Thread</A>, <A HREF="../../libm3/src/fmtlex/Fmt.i3.html">Fmt</A>, <A HREF="../../libm3/src/rw/Stdio.i3.html">Stdio</A>;
</PRE> Provides procedures for unparsing Juno units (interfaces and modules),
   commands, and expressions.  A number of valid tokens may be specified so
   that incorrect programs may be unparsed up to the point of failure. 

<P><PRE>CONST
  IdIndent = 2;                    (* indentation for identifiers *)
  CmdIndent = 2;                   (* indentation for decl commands *)
  PredIndent = 2;		   (* indentation for decl predicates *)

PROCEDURE <A NAME="Block"><procedure>Block</procedure></A>(
    wr: Wr.T;
    ast: AST.Block;
    tokens, indent, width, prec: CARDINAL;
    debug, private: BOOLEAN)
  RAISES {Wr.Failure} =
  VAR f := Formatter.New(wr, width); BEGIN
    Unparse(f, ast, tokens, indent, prec, debug, private);
    Formatter.Close(f)
  END Block;

PROCEDURE <A NAME="Cmd"><procedure>Cmd</procedure></A>(
    wr: Wr.T;
    ast: AST.Cmd;
    tokens, indent, width, prec: CARDINAL;
    debug: BOOLEAN)
  RAISES {Wr.Failure} =
  VAR f := Formatter.New(wr, width); BEGIN
    Unparse(f, ast, tokens, indent, prec, debug, private := TRUE);
    Formatter.Close(f)
  END Cmd;

PROCEDURE <A NAME="Expr"><procedure>Expr</procedure></A>(
    wr: Wr.T;
    ast: AST.Expr;
    tokens, indent, width, prec: CARDINAL;
    debug: BOOLEAN)
  RAISES {Wr.Failure} =
  VAR f := Formatter.New(wr, width); BEGIN
    Unparse(f, ast, tokens, indent, prec, debug, private := TRUE);
    Formatter.Close(f)
  END Expr;

PROCEDURE <A NAME="P"><procedure>P</procedure></A>(wr: Wr.T; ast: AST.T; indent: CARDINAL := 6;
  width: CARDINAL := 75; prec := Prec; debug := FALSE; private := TRUE;
  errast: AST.T := NIL) RAISES {Wr.Failure} =
  VAR f := Formatter.New(wr, width); BEGIN
    Unparse(f, ast, LAST(INTEGER), indent, prec, debug, private, errast);
    Formatter.Close(f)
  END P;

PROCEDURE <A NAME="ToFmt"><procedure>ToFmt</procedure></A>(fmt: Formatter.T; ast: AST.T; indent, prec: CARDINAL;
  debug, private: BOOLEAN; errast: AST.T := NIL) RAISES {Wr.Failure} =
  BEGIN
    Unparse(fmt, ast, LAST(INTEGER), indent, prec, debug, private, errast)
  END ToFmt;

PROCEDURE <A NAME="Debug"><procedure>Debug</procedure></A>(r: REFANY) =
&lt;* FATAL Wr.Failure, Thread.Alerted *&gt;
  BEGIN
    TYPECASE r OF &lt;*NOWARN*&gt;
    | AST.T (ast) =&gt; P(Stdio.stderr, ast, indent := 4, debug := TRUE)
    | JunoValue.T (v) =&gt; JunoValue.Unparse(Stdio.stderr, v, Prec)
    END;
    Wr.PutChar(Stdio.stderr, '\n')
  END Debug;

VAR (*CONST*) refReal := NEW(REF JunoValue.Real); (* Used by &quot;RefReal&quot; *)

PROCEDURE <A NAME="RefReal"><procedure>RefReal</procedure></A>(r: JunoValue.Real): REF JunoValue.Real =
  BEGIN refReal^ := r; RETURN refReal END RefReal;

EXCEPTION TokenLimit;           (* only used inside &quot;Unparse&quot; *)
</PRE> Here is the real unparsing machinery.  All of the unparsing procedures
   have been encapsulated in this big <CODE>Unparse</CODE> procedure, which holds the
   global state of the formatter. 

<P><PRE>PROCEDURE <A NAME="Unparse"><procedure>Unparse</procedure></A>(
    fmt: Formatter.T;
    ast: AST.T;
    tokens, indent, prec: INTEGER;
    debug, private: BOOLEAN;
    errast: AST.T := NIL)
  RAISES {Wr.Failure} =

  (* Formatter state: We need a little more information than just the
     formatter itself.  &quot;count&quot; is the number of tokens output so far, and
     each of the unparsing routines bumps &quot;count&quot; when it sends a token to
     the formatter.  The invariant that &quot;count &lt;= tokens&quot; is maintained,
     and the exception &quot;TokenLimit&quot; is raised if &quot;count = tokens&quot;. *)

  VAR
    count := 0;

  PROCEDURE Block(ast: AST.Block) RAISES {TokenLimit, Wr.Failure} =
    VAR appendSemi := TRUE; BEGIN
      CheckStart(ast);
      TYPECASE ast OF
      | AST.Module (m) =&gt;
          Token(&quot;MODULE &quot;);
          Id(m.name)
      | AST.Import (i) =&gt;
	  Token(&quot;IMPORT &quot;);
	  IdList(i.idList)
      | AST.Comment (c) =&gt;
          Comment(c);
          appendSemi := FALSE
      | AST.UIDecl (d) =&gt;
          UIDecl(d)
      | AST.Decl (d) =&gt;
          Decl(d)
      ELSE
	Token(&quot;&lt;UNRECOGNIZED DECLARATION FORM&gt;&quot;)
      END;
      IF appendSemi THEN Token(&quot;;&quot;) END;
      CheckFinish(ast)
    END Block;

  PROCEDURE Comment(c: AST.Comment) RAISES {TokenLimit, Wr.Failure} =
    VAR
      str := Text.Sub(c.txt, 2, Text.Length(c.txt)-4);
      len := Text.Length(str);		 (* length of comment string *)
      i := 0;				 (* current index into comment *)

    PROCEDURE SkipWhiteSpace(): BOOLEAN =
    (* Returns TRUE iff the whitespace should be interpretted as starting a
       new paragraph. *)
      VAR c: CHAR; newlines := 0; BEGIN
	WHILE i &lt; len DO
	  c := Text.GetChar(str, i);
          IF c = '\n' THEN INC(newlines)
	  ELSIF c # ' ' AND c # '\t' THEN EXIT
          END;
	  INC(i)
	END;
        RETURN newlines &gt; 1
      END SkipWhiteSpace;

    PROCEDURE SkipWord() RAISES {Wr.Failure} =
      VAR c: CHAR; init := i; BEGIN
        WHILE i &lt; len DO
          c := Text.GetChar(str, i);
          IF c = ' ' OR c = '\n' OR c = '\t' THEN EXIT END;
          INC(i)
        END;
	Print(Text.Sub(str, init, i-init))
      END SkipWord;

    PROCEDURE SkipVerbatim(newP: BOOLEAN) RAISES {Wr.Failure} =
      VAR init: INTEGER; BEGIN
        IF newP THEN
          Formatter.NewLine(fmt, offset := 0, freshLine := FALSE)
        END;
        REPEAT
          Formatter.NewLine(fmt, offset := -3, freshLine := FALSE);
          init := i;
          i := Text.FindChar(str, '\n', start := init+1);
          IF i = -1 THEN i := len END;
          Print(Text.Sub(str, init, i-init));
          INC(i)
        UNTIL i &gt;= len OR Text.GetChar(str, i) # '|';
        DEC(i);
        IF i &lt; len THEN
          Formatter.NewLine(fmt, offset := 0, freshLine := FALSE);
          IF SkipWhiteSpace() THEN
            Formatter.NewLine(fmt, offset := 0, freshLine := FALSE)
          END
        END
      END SkipVerbatim;

    (* Comment *)
    VAR newP: BOOLEAN; BEGIN
      Begin(indent := 0);
      (* Print open-comment characters *)
      Token(Text.Sub(c.txt, 0, 2));
      Print(&quot; &quot;);
      (* Indent the body of the comment in its own group *)
      Begin(indent := 0);
      newP := SkipWhiteSpace();
      WHILE i &lt; len DO
        IF Text.GetChar(str, i) = '|' AND i &gt; 0
           AND Text.GetChar(str, i-1) = '\n' THEN
          (* start of verbatim lines *)
          SkipVerbatim(newP)
        ELSIF newP THEN
          (* paragraph break -- leave a blank line *)
          Formatter.NewLine(fmt, offset := 0, freshLine := FALSE);
          Formatter.NewLine(fmt, offset := 0, freshLine := FALSE)
        ELSE
          (* word break *)
          Break(indent := 0);
        END;
        SkipWord();
        Print(&quot; &quot;);
        newP := SkipWhiteSpace();
      END;
      End();
      (* Print close-comment characters *)
      Break(indent := 0);
      Print(Text.Sub(c.txt, Text.Length(c.txt)-2, 2));
      End()
    END Comment;

  PROCEDURE UIDecl(ast: AST.UIDecl) RAISES {TokenLimit, Wr.Failure} =
    BEGIN
      Token(&quot;UI &quot;);
      CheckStart(ast);
      Begin();
      Id(ast.name);
      Token(&quot;(&quot;);
      ExprList(ast.args);
      Token(&quot;)&quot;);
      End();
      CheckFinish(ast)
    END UIDecl;

  PROCEDURE Decl(ast: AST.Decl) RAISES {TokenLimit, Wr.Failure} =
    BEGIN
      IF ast.private THEN
        Token(&quot;PRIVATE &quot;)
      END;
      TYPECASE ast OF &lt;* NOWARN *&gt;
      | AST.ConstDecl (cd) =&gt;
	  Token(&quot;CONST &quot;);
          UnitedBreak(indent := IdIndent);
          Begin();
          ConstDeclItem(cd.head);
          VAR cnt := cd.size - 1; curr := cd.head.next; BEGIN
            WHILE cnt # 0 DO
              OpL2(&quot;,&quot;, united := TRUE);
              ConstDeclItem(curr);
              DEC(cnt);
              curr := curr.next
            END
          END;
          End()
      | AST.VarDecl (vd) =&gt;
          Token(&quot;VAR &quot;);
          UnitedBreak(indent := IdIndent);
          Begin();
          VarDeclItem(vd.head);
          VAR cnt := vd.size - 1; curr := vd.head.next; BEGIN
            WHILE cnt # 0 DO
              OpL2(&quot;,&quot;, united := TRUE);
              VarDeclItem(curr);
              DEC(cnt);
              curr := curr.next
            END
          END;
          End()
      | AST.PredDecl (pd) =&gt;
	  Token(&quot;PRED &quot;);
          PredHeader(pd.header);
          IF private THEN
            Token(&quot; IS &quot;);
            UnitedBreak(PredIndent);
            Expr(pd.body, TYPECODE(NULL));
            Print(&quot; &quot;);
            UnitedBreak(0);
	    Token(&quot;END&quot;)
          END
      | AST.FuncDecl (fd) =&gt;
	  Token(&quot;FUNC &quot;);
          FuncHeader(fd.header);
          IF private THEN
            Token(&quot; IS &quot;);
            UnitedBreak(PredIndent);
            Expr(fd.body, TYPECODE(NULL));
            Print(&quot; &quot;);
            UnitedBreak(0);
	    Token(&quot;END&quot;)
          END
      | AST.ProcDecl (pd) =&gt;
	  Token(&quot;PROC &quot;);
          ProcHeader(pd.header);
          IF private THEN
            Token(&quot; IS &quot;);
            UnitedBreak(CmdIndent);
            Cmd(pd.body, TYPECODE(NULL));
            Print(&quot; &quot;);
            UnitedBreak(0);
	    Token(&quot;END&quot;)
          END
      END
    END Decl;

  PROCEDURE ConstDeclItem(cdi: AST.ConstDeclItem)
      RAISES {TokenLimit, Wr.Failure} =
    BEGIN
      Begin();
      Id(cdi.name);
      Token(&quot; = &quot;);
      Expr(cdi.value, TYPECODE(NULL));
      End();
    END ConstDeclItem;

  PROCEDURE VarDeclItem(vdi: AST.VarDeclItem)
      RAISES {TokenLimit, Wr.Failure} =
    BEGIN
      Begin();
      Id(vdi.name);
      IF vdi.value # AST.NilExpr THEN
        Token(&quot; := &quot;);
        Expr(vdi.value, TYPECODE(NULL))
      END;
      End();
    END VarDeclItem;

  PROCEDURE PredHeader(header: AST.PredHeader)
      RAISES {TokenLimit, Wr.Failure} =
    BEGIN
      CheckStart(header);
      Break(2);
      Begin();
      Id(header.name);
      Token(&quot;(&quot;);
      IdList(header.ins);
      Token(&quot;)&quot;);
      End();
      CheckFinish(header)
    END PredHeader;

  PROCEDURE FuncHeader(header: AST.FuncHeader)
      RAISES {TokenLimit, Wr.Failure} =
    BEGIN
      CheckStart(header);
      Break(2);
      Begin();
      Id(header.result);
      Print(&quot; &quot;);
      Token(&quot;=&quot;);
      Print(&quot; &quot;);
      Break(4);
      Id(header.name);
      Token(&quot;(&quot;);
      IdList(header.ins);
      Token(&quot;)&quot;);
      End();
      CheckFinish(header)
    END FuncHeader;

  PROCEDURE ProcHeader(header: AST.ProcHeader)
      RAISES {TokenLimit, Wr.Failure} =
    BEGIN
      CheckStart(header);
      Break(2);
      Begin();
      IF header.outs # NIL AND header.outs.size # 0 THEN
        Begin();
        IdList(header.outs); Op3(&quot;:=&quot;)
      END;
      IF header.inouts # NIL AND header.inouts.size # 0 THEN
        IF header.inout_prens THEN Token(&quot;(&quot;) END;
        IdList(header.inouts);
        IF header.inout_prens THEN Token(&quot;)&quot;) END;
        Token(&quot;:&quot;)
      END;
      Id(header.name);
      Token(&quot;(&quot;);
      IdList(header.ins);
      Token(&quot;)&quot;);
      IF header.outs # NIL AND header.outs.size # 0 THEN
        End()
      END;
      End();
      CheckFinish(header)
    END ProcHeader;

  PROCEDURE Cmd(ast: AST.Cmd; typeCode: CARDINAL)
      RAISES {TokenLimit, Wr.Failure} =
    BEGIN
      CheckStart(ast);
      IF TYPECODE(ast) # typeCode THEN Begin() END;
      TYPECASE ast OF
        AST.Skip =&gt; Token(&quot;SKIP&quot;)
      | AST.Abort =&gt; Token(&quot;ABORT&quot;)
      | AST.Halt =&gt; Token(&quot;HALT&quot;)
      | AST.Fail =&gt; Token(&quot;FAIL&quot;)
      | AST.Assign (a) =&gt;
          Begin();
          ExprList(a.vars);
          Op3(&quot;:=&quot;);
          ExprList(a.exprs);
          End()
      | AST.ProcCall (pc) =&gt;
          Begin(indent := 2);
          IF pc.outs.size # 0 THEN
            ExprList(pc.outs);
            Op2(&quot;:=&quot;)
          END;
          IF pc.inouts.size # 0 THEN
            IF (pc.inout_parens) THEN Token(&quot;(&quot;) END;
            ExprList(pc.inouts);
            IF (pc.inout_parens) THEN Token(&quot;)&quot;) END;
            Token(&quot;:&quot;)
          END;
          QId(pc.name);
          Token(&quot;(&quot;);
          ExprList(pc.ins);
          Token(&quot;)&quot;);
          End()
      | AST.If (ifCmd) =&gt;
          Token(&quot;IF&quot;);
          Print(&quot; &quot;);
          UnitedBreak(indent := 2);
          Cmd(ifCmd.body, TYPECODE(NULL));
          Print(&quot; &quot;);
          UnitedBreak(indent := 0);
          Token(&quot;FI&quot;);
      | AST.Do (doCmd) =&gt;
          Token(&quot;DO&quot;);
          Print(&quot; &quot;);
          UnitedBreak(indent := 2);
          Cmd(doCmd.body, TYPECODE(NULL));
          Print(&quot; &quot;);
          UnitedBreak(indent := 0);
          Token(&quot;OD&quot;);
      | AST.Flip (flip) =&gt;
	  Print(&quot;FLIP(&quot;);
	  Cmd(flip.body, TYPECODE(NULL));
	  Print(&quot;)&quot;)
      | AST.Safe (safe) =&gt;
	  Print(&quot;SAFE(&quot;);
          VAR cmd := safe.body; BEGIN
            LOOP
              TYPECASE cmd OF
                AST.Safe (s) =&gt; cmd := s.body
              ELSE EXIT
              END
            END;
	    Cmd(cmd, TYPECODE(NULL))
          END;
	  Print(&quot;)&quot;)
      | AST.Save (s) =&gt;
          Token(&quot;SAVE &quot;);
          QId(s.nm);
          Op(&quot;IN&quot;);
	  UnitedBreak(2);
          Cmd(s.body, TYPECODE(NULL));
          Print(&quot; &quot;);
	  UnitedBreak(0);
          Token(&quot;END&quot;)
      | AST.Proj (p) =&gt;
          Begin(indent := 0);
            Token(&quot;VAR &quot;);
	    UnitedBreak(2);
	    NearVarList(p.vars);
	    Print(&quot; &quot;);
	    UnitedBreak(0);
	  End();
	  Token(&quot;IN &quot;);
	  UnitedBreak(2);
	  Cmd(p.body, TYPECODE(NULL));
	  Print(&quot; &quot;);
	  UnitedBreak(0);
          Token(&quot;END&quot;)
      | AST.Seq (seq) =&gt;
          Cmd(seq.c1, TYPECODE(seq));
          OpL2(&quot;;&quot;, united := TRUE);
          Cmd(seq.c2, TYPECODE(seq))
      | AST.Guard (grd) =&gt;
          Begin();
          Expr(grd.grd, TYPECODE(grd));
          Op3(&quot;-&gt;&quot;);
          Cmd(grd.body, TYPECODE(grd));
          End()
      | AST.Else (e) =&gt;
          Cmd(e.c1, TYPECODE(e));
          Op(&quot;|&quot;);
          Cmd(e.c2, TYPECODE(e))
      | AST.Query (q) =&gt;
	  Print(&quot;(&quot;);
	  Expr(q.f, TYPECODE(q));
	  Print(&quot;)&quot;);
          Op(&quot;?&quot;);
          Print(&quot;(&quot;);
          IdList2(q.vars);
          Print(&quot;)&quot;)
      | AST.ConjQuery (cq) =&gt;
	  Print(&quot;(&quot;);
	  Conj(cq.conj^);
	  Print(&quot;)&quot;);
          Op(&quot;?&quot;);
          Print(&quot;(&quot;);
          Vars(cq.var^);
          Print(&quot;)&quot;)
      | AST.GroupedCmd (gc) =&gt;
          Token(&quot;{&quot;);
	  Begin();
          Print(&quot; &quot;);
          Cmd(gc.body, TYPECODE(gc));
          Print(&quot; &quot;);
          UnitedBreak();
	  End();
          Token(&quot;}&quot;)
      ELSE
        Token(&quot;&lt;UNRECOGNIZED CMD&gt;&quot;)
      END;
      IF TYPECODE(ast) # typeCode THEN End() END;
      CheckFinish(ast);
    END Cmd;

  PROCEDURE QId(qid: AST.QId) RAISES {TokenLimit, Wr.Failure} =
    BEGIN
      CheckStart(qid);
      IF qid.id0 # AST.NilId THEN Id(qid.id0); Token(&quot;.&quot;) END;
      Id(qid.id1);
      IF debug AND qid.type # AST.IdType.None THEN
        Print(&quot;[&quot;); Print(Fmt.Int(qid.index)); Print(&quot;]&quot;)
      END;
      CheckFinish(qid);
    END QId;

  PROCEDURE Id(a: Atom.T) RAISES {TokenLimit, Wr.Failure} =
    BEGIN
      Check();
      Print(Atom.ToText(a));
      INC(count)
    END Id;

  PROCEDURE NearVarList(nvl: AST.NearVarList)
    RAISES {TokenLimit, Wr.Failure} =
    BEGIN
      CheckStart(nvl);
      VAR curr := nvl.head; BEGIN
        Begin();
        WHILE curr # NIL DO
          NearVarLink(curr);
          IF curr.next # NIL THEN OpL2(&quot;,&quot;, united := FALSE) END;
          curr := curr.next
        END;
        End()
      END;
      CheckFinish(nvl);
    END NearVarList;

  PROCEDURE NearVarLink(nv: AST.NearVarLink) RAISES {TokenLimit, Wr.Failure} =
    BEGIN
      Begin();
      Id(nv.id);
      IF debug AND nv.index # 0 THEN
        Print(&quot;[&quot;); Print(Fmt.Int(nv.index)); Print(&quot;]&quot;)
      END;
      IF nv.hint # AST.NilExpr THEN
        IF nv.frozen
          THEN Op(&quot;=&quot;)
          ELSE Op(&quot;~&quot;)
        END;
        Expr(nv.hint, TYPECODE(NULL))
      END;
      End()
    END NearVarLink;

  PROCEDURE IdList(ids: AST.IdList) RAISES {TokenLimit, Wr.Failure} =
    BEGIN
      CheckStart(ids);
      VAR id := ids.head; BEGIN
        Begin(indent := 0);
        WHILE id # NIL DO
          Id(id.id);
          IF debug AND id.index # 0 THEN
            Print(&quot;[&quot;); Print(Fmt.Int(id.index)); Print(&quot;]&quot;)
          END;
          IF id.next # NIL THEN OpL2(&quot;,&quot;) END;
          id := id.next
        END;
        End()
      END;
      CheckFinish(ids);
    END IdList;

  PROCEDURE IdList2(ids: AST.NearVarList) RAISES {TokenLimit, Wr.Failure} =
  (* Unparse a near-var list as if it were an id-list. Don't unparse hints, if
     any, but do print annotations to indicate which variables are hinted and
     which are E-quantified. *)
    BEGIN
      CheckStart(ids);
      VAR id := ids.head; BEGIN
        Begin(indent := 0);
        WHILE id # NIL DO
          IF id.evar THEN Print(&quot;&lt;&quot;) END;
          IF id.frozen THEN Print(&quot;~&quot;) END;
          Id(id.id);
          IF debug AND id.index # 0 THEN
            Print(&quot;[&quot;); Print(Fmt.Int(id.index)); Print(&quot;]&quot;)
          END;
          IF id.evar THEN Print(&quot;&gt;&quot;) END;
          IF id.next # NIL THEN OpL2(&quot;,&quot;) END;
          id := id.next
        END;
        End()
      END;
      CheckFinish(ids);
    END IdList2;

  PROCEDURE Vars(READONLY vars: AST.Vars; unquantified := TRUE)
      RAISES {TokenLimit, Wr.Failure} =
    BEGIN
      Begin(indent := 0);
      FOR i := FIRST(vars) TO LAST(vars) DO
        IF unquantified AND vars[i].evar THEN Print(&quot;&lt;&quot;) END;
        IF vars[i].frozen THEN Print(&quot;~&quot;) END;
        Id(vars[i].id);
        IF debug AND vars[i].index # 0 THEN
          Print(&quot;[&quot;); Print(Fmt.Int(vars[i].index)); Print(&quot;]&quot;)
        END;
        IF unquantified AND vars[i].evar THEN Print(&quot;&gt;&quot;) END;
        IF i &lt; LAST(vars) THEN OpL2(&quot;,&quot;) END
      END;
      End()
    END Vars;

  PROCEDURE Conj(READONLY conj: AST.Formulas)
      RAISES {TokenLimit, Wr.Failure} =
  (* Unparse a conjunction of formulas. We must be careful to parenthesize OR
     nodes, since OR has weaker binding power than AND. *)
    VAR expr: AST.Expr; BEGIN
      Begin();
      IF NUMBER(conj) = 0 THEN
        Print(&quot;TRUE&quot;)
      ELSE
	FOR i := FIRST(conj) TO LAST(conj) DO
          IF FIRST(conj) &lt; LAST(conj) AND ISTYPE(conj[i], AST.Or)
            THEN expr := NEW(AST.GroupedExpr, expr := conj[i])
            ELSE expr := conj[i]
          END;
	  Expr(expr, TYPECODE(NULL));
	  IF i &lt; LAST(conj) THEN Op2(&quot;AND&quot;, united := TRUE) END
        END
      END;
      End()
    END Conj;

  PROCEDURE ExprList(exprs: AST.ExprList)
      RAISES {TokenLimit, Wr.Failure} =
    BEGIN
      CheckStart(exprs);
      VAR e := exprs.head; BEGIN
        Begin(indent := 0);
        WHILE e # NIL DO
          Expr(e.expr, TYPECODE(NULL));
          IF e.next # NIL THEN OpL2(&quot;,&quot;) END;
          e := e.next
        END;
        End()
      END;
      CheckFinish(exprs);
    END ExprList;

  PROCEDURE Expr(expr: AST.Expr; typeCode: CARDINAL)
      RAISES {TokenLimit, Wr.Failure} =
    BEGIN
      CheckStart(expr);
      IF typeCode # TYPECODE(expr) THEN Begin() END;
      TYPECASE expr OF
        AST.Call (c) =&gt;
          Begin(indent := 2);
          IF c.inouts.size # 0 THEN
            IF (c.inout_parens) THEN Token(&quot;(&quot;) END;
            ExprList(c.inouts);
            IF (c.inout_parens) THEN Token(&quot;)&quot;) END;
            Token(&quot;:&quot;)
          END;
          QId(c.name);
          Token(&quot;(&quot;);
          ExprList(c.ins);
          Token(&quot;)&quot;);
          End()
      | AST.True =&gt; Token(&quot;TRUE&quot;)
      | AST.False =&gt; Token(&quot;FALSE&quot;)
      | AST.Number (n) =&gt; Value(RefReal(n.val))
      | AST.Text (t) =&gt; Value(t.val)
      | AST.QId (qid) =&gt; QId(qid)
      | AST.Nil =&gt; Token(&quot;NIL&quot;)
      | AST.And (f) =&gt;
          Expr(f.f1, TYPECODE(f));
          Op2(&quot;AND&quot;, united := TRUE);
          Expr(f.f2, TYPECODE(f))
      | AST.Or (f) =&gt;
          Expr(f.f1, TYPECODE(f));
          Op(&quot;OR&quot;, united := TRUE);
          Expr(f.f2, TYPECODE(f))
      | AST.Not (f) =&gt;
          Token(&quot;NOT &quot;);
          Expr(f.f, TYPECODE(f))
      | AST.Exists (f) =&gt;
          Token(&quot;(&quot;);
          Token(&quot;E &quot;);
          NearVarList(f.vars);
          Print(&quot; &quot;);
          Token(&quot;::&quot;);
          Print(&quot; &quot;);
	  UnitedBreak(2);
          Expr(f.f, TYPECODE(f));
          Token(&quot;)&quot;)
      | IndexedNF.T (inf) =&gt;
          NormalForm(SUBARRAY(inf.var^,  0, inf.v_cnt),
                     SUBARRAY(inf.conj^, 0, inf.c_cnt))
      | AST.NormalForm (nf) =&gt;
          NormalForm(nf.var^, nf.conj^)
      | AST.BIUPred (p) =&gt; BIUPred(p)
      | AST.Relation (r) =&gt; Relation(r)
      | AST.UMinus (e) =&gt; Token(&quot;-&quot;); Expr(e.e, TYPECODE(e))
      | AST.BIUFunc (f) =&gt; BIUFunc(f)
      | AST.BuiltInAddFunc (e) =&gt;
          Expr(e.e1, TYPECODE(e));
          TYPECASE e OF
            AST.Plus =&gt; Op2(&quot;+&quot;)
          | AST.Minus =&gt; Op2(&quot;-&quot;)
          | AST.Concat =&gt; Op2(&quot;&amp;&quot;)
          ELSE
            Token(&quot;&lt;UNRECOGNIZED ADDFUNC&gt;&quot;)
          END;
          Expr(e.e2, TYPECODE(e))
      | AST.BuiltInMulFunc (e) =&gt;
          Expr(e.e1, TYPECODE(e));
          TYPECASE e OF
            AST.Times =&gt; Op2(&quot;*&quot;)
          | AST.Divide =&gt; Op2(&quot;/&quot;)
          | AST.Div =&gt; Op2(&quot;DIV&quot;)
          | AST.Mod =&gt; Op2(&quot;MOD&quot;)
          ELSE
            Token(&quot;&lt;UNRECOGNIZED MULFUNC&gt;&quot;)
          END;
          Expr(e.e2, TYPECODE(e))
      | AST.Pair (e) =&gt;
          Token(&quot;(&quot;); Begin();
          Expr(e.e1, TYPECODE(e));
          OpL2(&quot;,&quot;);
          Expr(e.e2, TYPECODE(e));
          Token(&quot;)&quot;); End()
      | AST.Rel (e) =&gt;
          Expr(e.e1, TYPECODE(e));
          Op2(&quot;REL&quot;);
          Expr(e.e2, TYPECODE(e))
      | AST.BIBFunc(e) =&gt;	 (* MAX, MIN, ATAN only *)
          TYPECASE e OF &lt;* NOWARN *&gt;
            AST.Max =&gt; Token(&quot;MAX&quot;)
          | AST.Min =&gt; Token(&quot;MIN&quot;)
          | AST.Atan =&gt; Token(&quot;ATAN&quot;)
          END;
          Token(&quot;(&quot;);
          Expr(e.e1, TYPECODE(e));
          OpL2(&quot;,&quot;);
          Expr(e.e2, TYPECODE(e));
          Token(&quot;)&quot;);
      | AST.List (e) =&gt;
          Token(&quot;[&quot;);
          ExprList(e.elts);
          Token(&quot;]&quot;)
      | AST.GroupedExpr (e) =&gt;
          Token(&quot;(&quot;);
          Expr(e.expr, TYPECODE(e));
          Break(1);
          Token(&quot;)&quot;)
      ELSE
        Token(&quot;&lt;UNIMPLEMENTED EXPRESSION FORM&gt;&quot;)
      END;
      IF typeCode # TYPECODE(expr) THEN End() END;
      CheckFinish(expr);
    END Expr;

  PROCEDURE BIUPred(up: AST.BIUPred)
      RAISES {TokenLimit, Wr.Failure} =
    VAR name: TEXT; BEGIN
      CheckStart(up);
      TYPECASE up OF &lt;* NOWARN *&gt;
        AST.IsReal =&gt; name := &quot;REAL&quot;
      | AST.IsText =&gt; name := &quot;TEXT&quot;
      | AST.IsPair =&gt; name := &quot;PAIR&quot;
      | AST.IsInt  =&gt; name := &quot;INT&quot;
      END;
      Token(name);
      Token(&quot;(&quot;);
      Expr(up.e, TYPECODE(up));
      Token(&quot;)&quot;);
      CheckFinish(up);
    END BIUPred;

  PROCEDURE Relation(r: AST.Relation)
      RAISES {TokenLimit, Wr.Failure} =
    VAR name: TEXT; BEGIN
      CheckStart(r);
      TYPECASE (r) OF
        AST.Equals(e) =&gt; IF e.near THEN name := &quot;~&quot; ELSE name := &quot;=&quot; END
      | AST.Differs =&gt; name := &quot;#&quot;
      | AST.Less    =&gt; name := &quot;&lt;&quot;
      | AST.Greater =&gt; name := &quot;&gt;&quot;
      | AST.AtMost  =&gt; name := &quot;&lt;=&quot;
      | AST.AtLeast =&gt; name := &quot;&gt;=&quot;
      | AST.Cong    =&gt; name := &quot;CONG&quot;
      | AST.Para    =&gt; name := &quot;PARA&quot;
      | AST.Hor     =&gt; name := &quot;HOR&quot;
      | AST.Ver     =&gt; name := &quot;VER&quot;
      ELSE
        name := &quot;&lt;UNIMPLEMENTED RELATION&gt;&quot;
      END;
      Begin();
      Expr(r.e1, TYPECODE(r));
      Op3(name);
      Expr(r.e2, TYPECODE(r));
      End();
      CheckFinish(r);
    END Relation;

  PROCEDURE BIUFunc(uf: AST.BIUFunc)
      RAISES {TokenLimit, Wr.Failure} =
  (* NOTE: This procedure does *not* handle the case where &quot;uf&quot; is a
     AST.UMinus. *)
    VAR name: TEXT; BEGIN
      CheckStart(uf);
      TYPECASE uf OF &lt;* NOWARN *&gt;
        AST.Floor   =&gt; name := &quot;FLOOR&quot;
      | AST.Ceiling =&gt; name := &quot;CEILING&quot;
      | AST.Round   =&gt; name := &quot;ROUND&quot;
      | AST.Abs     =&gt; name := &quot;ABS&quot;
      | AST.Sin     =&gt; name := &quot;SIN&quot;
      | AST.Cos     =&gt; name := &quot;COS&quot;
      | AST.Exp     =&gt; name := &quot;EXP&quot;
      | AST.Ln      =&gt; name := &quot;LN&quot;
      | AST.Car     =&gt; name := &quot;CAR&quot;
      | AST.Cdr     =&gt; name := &quot;CDR&quot;
      END;
      Token(name);
      Token(&quot;(&quot;);
      Expr(uf.e, TYPECODE(uf));
      Token(&quot;)&quot;);
      CheckFinish(uf)
    END BIUFunc;

  PROCEDURE NormalForm(READONLY vars: AST.Vars; READONLY conj: AST.Formulas)
      RAISES {TokenLimit, Wr.Failure} =
    BEGIN
      IF NUMBER(vars) = 0 THEN
        Conj(conj)
      ELSE
        Token(&quot;(&quot;); Token(&quot;E &quot;);
        Vars(vars, unquantified := FALSE);
        Op(&quot;::&quot;);
        UnitedBreak(2);
        Conj(conj);
        Token(&quot;)&quot;)
      END
    END NormalForm;

  PROCEDURE CheckStart(ast: AST.T) RAISES {TokenLimit, Wr.Failure} =
  (* Check to see if &quot;ast&quot; is the start of &quot;errast&quot;; if so, write '\001' to
     the output stream and set &quot;foundStart&quot;. *)
    BEGIN
      Check();
      IF ast = errast AND NOT foundStart AND ast # NIL THEN
        Print(&quot;\001&quot;);
        foundStart := TRUE
      END
    END CheckStart;

  PROCEDURE CheckFinish(ast: AST.T) RAISES {Wr.Failure} =
    BEGIN
      IF ast = errast AND NOT foundFinish AND ast # NIL THEN
        Print(&quot;\002&quot;);
        foundFinish := TRUE
      END
    END CheckFinish;

  PROCEDURE Check() RAISES {TokenLimit} =
    BEGIN
      &lt;* ASSERT count &lt;= tokens *&gt;
      IF count = tokens THEN RAISE TokenLimit END
    END Check;

  PROCEDURE Token(t: TEXT) RAISES {TokenLimit, Wr.Failure} =
    BEGIN
      Check();
      Print(t);
      INC(count)
    END Token;

  PROCEDURE Value(v: JunoValue.T) RAISES {TokenLimit, Wr.Failure} =
    BEGIN
      Check();
      JunoValue.UnparseToFmt(fmt, v, prec);
      INC(count)
    END Value;

  PROCEDURE Print(t: TEXT) RAISES {Wr.Failure} =
    BEGIN
      Formatter.PutText(fmt, t, raw := TRUE)
    END Print;

  &lt;* INLINE *&gt;
  PROCEDURE OpNoSpaces(t: TEXT; united := FALSE)
      RAISES {TokenLimit, Wr.Failure} =
    VAR len := - (Text.Length(t) + 1); BEGIN
      IF united
        THEN UnitedBreak(indent := len)
        ELSE Break(indent := len)
      END;
      Token(t);
    END OpNoSpaces;

  PROCEDURE Op(t: TEXT; united := FALSE) RAISES {TokenLimit, Wr.Failure} =
  (* Introduces an ordinary operation into the stream, with spaces on either
     side.  A break is introduced before the op so that the resulting output
     looks like
|         A
|      op B
     if the operation is broken or
|         A op B
     if it is not. If &quot;united = TRUE&quot;, then all siblings of the break will
     also break if this one does. *)
    BEGIN
      Print(&quot; &quot;);
      OpNoSpaces(t, united);
      Print(&quot; &quot;)
    END Op;

  PROCEDURE Op2(t: TEXT; united := FALSE) RAISES {TokenLimit, Wr.Failure} =
  (* Like &quot;Op&quot;, but puts the op at the end of the first line instead of
    the start of the second. *)
  BEGIN
    Print(&quot; &quot;);
    Token(t);
    Print(&quot; &quot;);
    IF united THEN
      UnitedBreak(indent := 0)
    ELSE
      Break(indent := 0)
    END
  END Op2;

  PROCEDURE Op3(t: TEXT) RAISES {TokenLimit, Wr.Failure} =
  (* Format
|  LeftArg op
|    RightArg
*)
  BEGIN
    Print(&quot; &quot;);
    Token(t);
    Print(&quot; &quot;);
    Break(indent := 2);
  END Op3;

  PROCEDURE OpL2(t: TEXT; united := FALSE) RAISES {TokenLimit, Wr.Failure} =
  (* Like OpL, only the output text is broken to look like this:
|
|    Aop
|    B
  *)
    BEGIN
      Token(t);
      Print(&quot; &quot;);
      IF united
        THEN UnitedBreak(indent := 0)
        ELSE Break(indent := 0)
      END
    END OpL2;

  PROCEDURE Begin(indent: INTEGER := 0) RAISES {Wr.Failure} =
    BEGIN
      Formatter.Begin(fmt, indent)
    END Begin;

  PROCEDURE End() RAISES {Wr.Failure} =
    BEGIN
      Formatter.End(fmt)
    END End;

  PROCEDURE Break(indent: INTEGER := 0) RAISES {Wr.Failure} =
    BEGIN
      (* Formatter.PartialBreak(fmt, offset := indent); *)
      Formatter.Break(fmt, offset := indent,
        type := Formatter.BreakType.NonOptimal)
    END Break;

  PROCEDURE UnitedBreak(indent: INTEGER := 0) RAISES {Wr.Failure} =
    BEGIN
      Formatter.UnitedBreak(fmt, offset := indent)
    END UnitedBreak;

  VAR foundStart, foundFinish := FALSE; BEGIN (* Unparse *)
    IF errast # NIL THEN
      (* find predecessor; crash if predecessor undefined *)
      WHILE errast.bp # AST.End DO errast := errast.bp END
    END;
    FOR i := 1 TO indent DO Print(&quot; &quot;) END;
    Begin();
    TRY
      TYPECASE ast OF
        AST.IdList(l)      =&gt; IdList(l)
      | AST.NearVarList(l) =&gt; NearVarList(l)
      | AST.ExprList(l)    =&gt; ExprList(l)
      | AST.Expr(e)        =&gt; Expr(e, TYPECODE(NULL))
      | AST.Cmd(c)         =&gt; Cmd(c, TYPECODE(NULL))
      | AST.Block(b)       =&gt; Block(b)
      ELSE
        Token(&quot;&lt;UNRECOGNIZED AST TYPE&gt;&quot;)
      END
    EXCEPT
      TokenLimit =&gt; (* SKIP *)
    END;
    End();
    Formatter.Flush(fmt)
  END Unparse;

BEGIN
END JunoUnparse.
</PRE>
</inModule>
<HR>
<A NAME="x1">interface JunoValue is in:
</A><UL>
<LI><A HREF="../../juno-machine/src/JunoValue.i3.html#0TOP0">juno-machine/src/JunoValue.i3</A>
<LI><A HREF="../../pkl-fonts/src/JunoValue.i3.html#0TOP0">pkl-fonts/src/JunoValue.i3</A>
</UL>
<P>
<PRE>























</PRE>
</BODY>
</HTML>
