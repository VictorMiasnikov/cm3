<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: juno-compiler/src/JunoCompileNF.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>juno-compiler/src/JunoCompileNF.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright (C) 1992, Digital Equipment Corporation                         
 All rights reserved.                                                      
 See the file COPYRIGHT for a full description.                            
                                                                           
 Last modified on Thu Jun 26 15:22:51 PDT 1997 by heydon                   

<P><PRE>MODULE <module><implements><A HREF="JunoCompileNF.i3.html">JunoCompileNF</A></implements></module>;

IMPORT <A HREF="JunoAST.i3.html">JunoAST</A>, <A HREF="JunoScope.i3.html">JunoScope</A>, <A HREF="StackTbl.i3.html">StackTbl</A>, <A HREF="JunoASTUtils.i3.html">JunoASTUtils</A> AS Utils;
IMPORT   <A HREF="IndexedNF.i3.html">IndexedNF</A>, <A HREF="JunoUnparse.i3.html">JunoUnparse</A>;
FROM <A HREF="JunoCompileErr.i3.html">JunoCompileErr</A> IMPORT Error, Raise;
IMPORT <A HREF="../../libm3/src/rw/Wr.i3.html">Wr</A>, <A HREF="../../libm3/src/atom/Atom.i3.html">Atom</A>, <A HREF="../../libm3/derived/IntRefTbl.i3.html">IntRefTbl</A>, <A HREF="../../libm3/derived/RefList.i3.html">RefList</A>, <A HREF="../../libm3/src/fmtlex/Fmt.i3.html">Fmt</A>;
FROM <A HREF="../../libm3/src/rw/Stdio.i3.html">Stdio</A> IMPORT stderr;
FROM <A HREF="../../m3core/src/thread/Common/Thread.i3.html">Thread</A> IMPORT Alerted;

&lt;* FATAL Wr.Failure, Alerted *&gt;
VAR debug := 0;
</PRE><BLOCKQUOTE><EM> = 0  =&gt; no debugging output
<PRE>
      &gt;= 1  =&gt; show predicate/assignment extraction &amp; in-line expansions
      &gt;= 2  =&gt; show hint propagation
</EM></BLOCKQUOTE><PRE>

VAR (* CONST *)
  dummyVar := Atom.FromText(&quot;_x&quot;);      (* name of newly introduced &quot;E&quot; vars *)
</PRE> ============================= Normalize ================================= 

<P><PRE>PROCEDURE <A NAME="Normalize"><procedure>Normalize</procedure></A>(p: JunoAST.Formula; tbl: StackTbl.T): JunoAST.NormalForm =
  VAR inf := IndexedNF.New(var_cnt := 100, conj_cnt := 200); BEGIN
    Flatten(p, tbl, inf);
    B1(tbl, inf);
    VAR res := IndexedNF.ToNF(inf); BEGIN
      IndexedNF.Dispose(inf);
      RETURN res
    END
  END Normalize;

PROCEDURE <A NAME="Flatten"><procedure>Flatten</procedure></A>(
    READONLY p: JunoAST.Formula;
    tbl: StackTbl.T;
    VAR (*INOUT*) res: IndexedNF.T) =
</PRE><BLOCKQUOTE><EM> <CODE>p</CODE> is interpretted as a conjunction. Ungroup the conjuncts and store them
   in <CODE>res</CODE>. There is one special case:
<P>
   For each conjunct of the form <CODE>(E nearvars :: P)</CODE>, add the existentially
   quantified variables to the list of variables to solve for in <CODE>res</CODE>, and
   add any hints for those variables and the ungrouped body <CODE>P</CODE> to the
   conjuncts of <CODE>res</CODE>.
<P>
   IMPLEMENTATION: To make this more efficient, we may want to allocate the
   <CODE>stack</CODE> local from an avail list. </EM></BLOCKQUOTE><PRE>

  VAR
    stack := NEW(REF JunoAST.Formulas, 50);
    sp: CARDINAL := 0;

  PROCEDURE Push(READONLY f: JunoAST.Formula) =
    BEGIN
      IF sp = NUMBER(stack^) THEN
        VAR new := NEW(REF JunoAST.Formulas, 2 * NUMBER(stack^)); BEGIN
          SUBARRAY(new^, 0, NUMBER(stack^)) := stack^;
          stack := new
        END
      END;
      stack[sp] := f;
      INC(sp)
    END Push;

  (* Flatten *)
  VAR pred: JunoAST.Formula; BEGIN
    res.bp := p; res.v_cnt := 0; res.c_cnt := 0;
    Push(p);
    WHILE sp # 0 DO
      DEC(sp);
      pred := Utils.Ungroup(stack[sp]);
      TYPECASE pred OF
        JunoAST.And (and) =&gt;
          Push(and.f2); Push(and.f1)
      | JunoAST.Or (or) =&gt;
	  PROCEDURE IsTrue(f: JunoAST.NormalForm): BOOLEAN =
	    BEGIN
	      RETURN NUMBER(f.var^) = 0 AND NUMBER(f.conj^) = 1
	        AND f.conj[0] = JunoAST.TrueVal
	    END IsTrue;
          VAR f1 := Normalize(or.f1, tbl); f2 := Normalize(or.f2, tbl); BEGIN
            IF IsTrue(f1) OR IsTrue(f2) THEN
              IndexedNF.AddConj(res, JunoAST.TrueVal)
            ELSE
              IndexedNF.AddConj(res, NEW(JunoAST.Or, bp := or, f1:=f1, f2:=f2))
            END
          END
      | JunoAST.Not (not) =&gt;
          IndexedNF.AddConj(res, NEW(JunoAST.Not, bp := not,
            f := Normalize(not.f, tbl)))
      | JunoAST.Exists (e) =&gt;
          VAR uv := e.vars; hints := Utils.ExtractHints(uv); BEGIN
            Push(e.f);
            IF hints # JunoAST.TrueVal THEN Push(hints) END;
            IndexedNF.AddVarList(res, Utils.StripHints(uv));
            (* Set the &quot;evar&quot; bits of the variables just added to &quot;res&quot; *)
            FOR i := res.v_cnt - uv.size TO res.v_cnt - 1 DO
              res.var[i].evar := TRUE
            END
          END
      ELSE
          IndexedNF.AddConj(res, pred)
      END
    END
  END Flatten;

PROCEDURE <A NAME="B1"><procedure>B1</procedure></A>(tbl: StackTbl.T; VAR (*INOUT*) res: IndexedNF.T) =
</PRE><BLOCKQUOTE><EM> Change <CODE>res</CODE> into an equivalent formula, all of whose simple formulas are
   normal. This process may require the introduction of new existentially
   quantified variables; the indices for the new variables are taken from
   <CODE>tbl</CODE>.
<P>
   Requires all top-level formulas in <CODE>res.conj</CODE> to be ungrouped. This code is
   still responsible for ungrouping the sub-expressions in equalities. </EM></BLOCKQUOTE><PRE>
  VAR i: CARDINAL := 0; BEGIN
    WHILE i # res.c_cnt DO
      TYPECASE res.conj[i] OF
        JunoAST.Or, JunoAST.Not =&gt;
          INC(i) (* don't modify compound formulas *)
      | JunoAST.Equals (p) =&gt;                          (* rules C2.30, C2.33 *)
          VAR
            e1 := Utils.Ungroup(p.e1); e2 := Utils.Ungroup(p.e2);
            var: JunoAST.AtomicExpr := NIL; rhs: JunoAST.Expr;
          BEGIN
            TYPECASE e1 OF JunoAST.AtomicExpr (ae) =&gt;      var := ae; rhs := e2
            ELSE TYPECASE e2 OF JunoAST.AtomicExpr (ae) =&gt; var := ae; rhs := e1
            ELSE (* SKIP *)
            END END;
            IF var # NIL THEN
              TYPECASE rhs OF                                (* form: x ~= y *)
                JunoAST.AtomicExpr =&gt; (* SKIP *)               (* rule C2.38 *)
              ELSE
                VAR flat := Unnest(rhs, tbl); BEGIN
                  IF flat.vars.size # 0 THEN  (* form: v ~= f(...,g(..),...) *)
                    res.conj[i] := NEW(JunoAST.Equals,         (* rule C2.33 *)
                      e1 := var, e2 := flat.t0, near := p.near);
                    IndexedNF.AddVarList(res, flat.vars);
                    FormEqualities(flat.vars, flat.terms, near := p.near);
                    IndexedNF.AddConjList(res, flat.terms)
                  ELSIF var # e1 THEN
                    res.conj[i] := NEW(JunoAST.Equals,   (* swap lhs and rhs *)
                      e1 := var, e2 := rhs, near := p.near)
                  END
                END
              END;
              INC(i)
	    ELSE                                   (* form: f(...) ~= g(...) *)
	      VAR v := DummyVar(tbl); qid := Utils.QIdFromNearVar(v); BEGIN
		(* swap in last element *)
		DEC(res.c_cnt);
		res.conj[i] := res.conj[res.c_cnt];
		(* add two new equalities *)
		IndexedNF.AddVar(res, v);                      (* rule C2.30 *)
		IndexedNF.AddConj(res, NEW(JunoAST.Equals,
		  bp := p, e1 := qid, e2 := e1, near := p.near));
		IndexedNF.AddConj(res, NEW(JunoAST.Equals,
		  bp := p, e1 := qid, e2 := e2, near := p.near))
	      END
	    END (* IF *)
          END (* BEGIN *)
      ELSE                                         (* form: p(...,g(..),...) *)
        VAR flat := Unnest(res.conj[i], tbl); BEGIN            (* rule C2.32 *)
          IF flat.vars.size # 0 THEN
            res.conj[i] := flat.t0;
            IndexedNF.AddVarList(res, flat.vars);
            FormEqualities(flat.vars, flat.terms);
            IndexedNF.AddConjList(res, flat.terms)
          END
        END;
        INC(i)
      END (* TYPECASE *)
    END (* WHILE *)
  END B1;

PROCEDURE <A NAME="DummyVar"><procedure>DummyVar</procedure></A>(tbl: StackTbl.T): JunoAST.NearVarLink =
</PRE><BLOCKQUOTE><EM> Return an unhinted, E-quantified dummy variable with the next available
   local index according to <CODE>tbl</CODE>. </EM></BLOCKQUOTE><PRE>
  VAR res := NEW(JunoAST.NearVarLink, id := dummyVar,
    hint := JunoAST.NilExpr, evar := TRUE, index := tbl.next_index);
  BEGIN
    INC(tbl.next_index);
    RETURN res
  END DummyVar;

PROCEDURE <A NAME="FormEqualities"><procedure>FormEqualities</procedure></A>(
    v: JunoAST.NearVarList;
    t: JunoAST.ExprList;
    near := FALSE) =
</PRE><BLOCKQUOTE><EM> Requires <CODE>v</CODE> and <CODE>t</CODE> are lists of the same length. Destructively changes
   the list <CODE>t</CODE> so that the new value for <CODE>t_i.expr</CODE> is an equality between
   <CODE>v_i</CODE> and the old value of <CODE>t_i.expr</CODE>. If <CODE>near</CODE> is <CODE>TRUE</CODE>, then each
   equality is a near equality. </EM></BLOCKQUOTE><PRE>
  VAR curr_v := v.head; curr_t := t.head; BEGIN
    &lt;* ASSERT v.size = t.size *&gt;
    WHILE curr_t # NIL DO
      curr_t.expr := NEW(JunoAST.Equals, near := near,
        e1 := Utils.QIdFromNearVar(curr_v), e2 := curr_t.expr);
      curr_v := curr_v.next;
      curr_t := curr_t.next
    END
  END FormEqualities;

TYPE
  FlatRec = RECORD
    t0: JunoAST.Expr;
    vars: JunoAST.NearVarList;
    terms: JunoAST.ExprList;
  END;
</PRE> If <CODE>fr</CODE> is a <CODE>FlatRec</CODE>, then each variable in <CODE>fr.vars</CODE> is unhinted and has
   its <CODE>evar</CODE> bit set. 

<P><PRE>PROCEDURE <A NAME="Unnest"><procedure>Unnest</procedure></A>(t: JunoAST.Expr; tbl: StackTbl.T): FlatRec =
</PRE><BLOCKQUOTE><EM> The expression <CODE>t</CODE> is assumed to be of the form <CODE>f(e1,e2,...,en)</CODE>, where
   <CODE>f</CODE> represents either a built-in or user-defined predicate or function
   (note that <CODE>t</CODE> is not a parenthesized expression). Each of the argument
   expressions <CODE>e_i</CODE> is either atomic or non-atomic. The procedure returns a
   <CODE>fr: FlatRec</CODE> such that <CODE>fr.t0</CODE> is obtained from <CODE>t</CODE> by replacing each
   non-atomic argument by a dummy variable, <CODE>fr.vars</CODE> is a list of these dummy
   variables, and <CODE>fr.terms</CODE> contains the union of the terms obtained by
   recursively unnesting the original non-atomic <CODE>e_i</CODE> in <CODE>t</CODE>. </EM></BLOCKQUOTE><PRE>
  VAR
    last_v := NEW(JunoAST.NearVarLink);	 (* sentinal *)
    curr_t := NEW(JunoAST.ExprLink, expr := t);
    last_t := curr_t;
    f: FlatRec;
  BEGIN
    &lt;* ASSERT NOT ISTYPE(t, JunoAST.AtomicExpr) *&gt;
    f.vars := NEW(JunoAST.NearVarList, head := last_v);
    f.terms := NEW(JunoAST.ExprList, size := 1, head := curr_t);
    (* Loop invariants:
|        1) All terms before &quot;curr_t&quot; have been unnested.
|        2) All expressions on the list &quot;f.terms.head&quot; are ungrouped.
|        3) &quot;last_t&quot; points to the last expression on the list &quot;f.terms.head&quot;.
|        4) &quot;last_v&quot; points to the last var on the list &quot;f.vars&quot;.
    *)
    WHILE curr_t # NIL DO
      PROCEDURE ProcessArg(e: JunoAST.Expr): JunoAST.Expr =
        BEGIN
          e := Utils.Ungroup(e);
          IF ISTYPE(e, JunoAST.AtomicExpr) THEN RETURN e END;
          VAR v: JunoAST.NearVarLink := DummyVar(tbl); BEGIN
            last_v.next := v;
            last_v := v;
            INC(f.vars.size);
            last_t.next := NEW(JunoAST.ExprLink, expr := e);
            last_t := last_t.next;
            INC(f.terms.size);
            RETURN Utils.QIdFromNearVar(v)
          END
        END ProcessArg;
      BEGIN
        curr_t.expr := Utils.MapArgs(curr_t.expr, ProcessArg);
        curr_t := curr_t.next
      END
    END;
    f.vars.head := f.vars.head.next;	 (* skip sentinal *)
    f.t0 := f.terms.head.expr;
    f.terms.head := f.terms.head.next;
    DEC(f.terms.size);
    RETURN f
  END Unnest;
</PRE> ================================ ToCmd ================================== 

<P><PRE>TYPE
  FormCmd = RECORD
    f: JunoAST.Formula;
    kind: CARDINAL
  END;
  FormCmds = ARRAY OF FormCmd;
</PRE> A <CODE>FormCmd</CODE> represents a formula to be compiled into a command. If <CODE>fc</CODE> is
   a <CODE>FormCmd</CODE>, then <CODE>fc.kind</CODE> is 0 if <CODE>fc.f</CODE> is to become a test, 1 if <CODE>fc.f</CODE>
   is to become an assignment to its left variable, and 2 if <CODE>fc.f</CODE> is to
   become an assignment to its right variable. 

<P><PRE>EXCEPTION ExtraNearConstraints;
</PRE><BLOCKQUOTE><EM> Raised by Phase2() </EM></BLOCKQUOTE><PRE>

PROCEDURE <A NAME="ToCmd"><procedure>ToCmd</procedure></A>(
    nf: JunoAST.NormalForm;
    scp: JunoScope.T;
    stack_tbl: StackTbl.T;
    xtra_vars: JunoAST.NearVarList := NIL):
  JunoAST.Cmd RAISES {Error} =
</PRE><BLOCKQUOTE><EM> IMPLEMENTATION: The implementation works in two phases. Roughly speaking,
   the first phase repeatedly reduces the number of unknowns and in-line
   substitutes non-primitive predicate and function calls. The second phase
   propagates hints without reducing the number of unknowns. In both phases,
   the <CODE>frozen</CODE> bit of a variable is set whenever an assignment to that
   variable is generated in the result, indicating that the variable has a
   valid value prior to the query.
<P>
   The first phase works in two parts. The first part (Phase1A) works by
   extracting predicate calls and equalities all of whose components are
   literals or known variables. The normal simple formulas so extracted are
   converted to queries (with no unknowns) and assignment statements. As
   opposed to the Prolog implementation, this implementation extracts formulas
   to be converted to queries before it extracts formulas to be converted to
   assignments. </EM></BLOCKQUOTE><PRE>

  VAR
    inf := IndexedNF.New(var_cnt := 100, conj_cnt := 200);
    cmds := NEW(REF FormCmds, 20); (* extracted commands *)
    tbl: IntRefTbl.T;	 (* maps variable index to list of AST's (formulas) *)
    last_near: CARDINAL; (* number of near constraints in &quot;inf.conj[]&quot; *)
    cmd_cnt: CARDINAL;   (* number of commands in &quot;cmds[]&quot; *)
</PRE> IMPLEMENTATION: For efficiency, we may want to allocate the <CODE>cmds</CODE> from
   an avail list. 

<P>   Invariants:
<PRE>
      
           I1: 0 &lt;= last_near &lt;= inf.c_cnt
               0 &lt;= cmd_cnt &lt;= NUMBER(cmd^)
      
           I2: Variables inf.var[0..inf.v_cnt-1] are unknown.
      
           I3: Formulas inf.conj[0..last_near-1] are near constraints.
               Forumlas inf.conj[last_near..inf.c_cnt-1] are non-near constraints.
      
           I4: Formulas cmd[0..cmd_cnt-1] are formulas that have been extracted to
               be converted to queries (with no unknowns) and assignments. The
      	   &quot;b3cnt&quot; field of these formulas is 0 if they are to become tests,
               1 if they are to become assignments to their left variable, and 2
               if they are to become assignments to their right variable. 


<P>  <PRE>PROCEDURE SwapV(VAR (*INOUT*) v1, v2: JunoAST.NearVarLink) =
    VAR temp := v1; BEGIN v1 := v2; v2 := temp END SwapV;

  PROCEDURE SwapF(VAR (*INOUT*) f1, f2: JunoAST.Formula) =
    VAR temp := f1; BEGIN f1 := f2; f2 := temp END SwapF;

  PROCEDURE MakeCmd(VAR (*INOUT*) f: JunoAST.Formula; kind: CARDINAL) =
  (* Move &quot;f&quot; from its location in &quot;inf.conj&quot; to the end of the &quot;cmds&quot; array.
     Grow the &quot;cmds&quot; array if necessary. *)
    BEGIN
      CopyCmd(f, kind);
      DEC(inf.c_cnt);
      f := inf.conj[inf.c_cnt]
    END MakeCmd;

  PROCEDURE CopyCmd(VAR (*INOUT*) f: JunoAST.Formula; kind: CARDINAL) =
  (* Copy &quot;f&quot; from its location in &quot;inf.conj&quot; to the end of the &quot;cmds&quot; array.
     Grow the &quot;cmds&quot; array if necessary. *)
    BEGIN
      IF cmd_cnt &gt; LAST(cmds^) THEN
        VAR new := NEW(REF FormCmds, 2 * NUMBER(cmds^)); BEGIN
          SUBARRAY(new^, 0, NUMBER(cmds^)) := cmds^;
          cmds := new
        END
      END;
      cmds[cmd_cnt] := FormCmd{kind := kind, f := f};
      INC(cmd_cnt);
    END CopyCmd;

  PROCEDURE UpdateB3Cnts(index: INTEGER) =
    VAR ref: REFANY; BEGIN
      IF NOT tbl.get(index, ref) THEN &lt;* ASSERT FALSE *&gt; END;
      VAR l: RefList.T := ref; BEGIN
        WHILE l # NIL DO
          VAR f: JunoAST.Formula := l.head; BEGIN DEC(f.b3cnt) END;
          l := l.tail
        END
      END;
      EVAL tbl.delete(index, ref)
    END UpdateB3Cnts;

  PROCEDURE PermuteNearToPrefix(start: CARDINAL) =
  (* Establish I3, assuming that I3 is already valid for formulas in the
     range [0..start-1]. Namely, the initial assumptions are that:
|
|       Formulas inf.conj[0..last_near-1] are near constraints.
|       Forumlas inf.conj[last_near..start-1] are non-near constraints. *)
    BEGIN
      FOR i := start TO inf.c_cnt - 1 DO
        TYPECASE inf.conj[i] OF JunoAST.Equals (e) =&gt;
          IF e.near THEN
            SwapF(inf.conj[last_near], inf.conj[i]);
            INC(last_near)
          END
        ELSE (* SKIP *)
        END
      END
    END PermuteNearToPrefix;

  PROCEDURE Phase1A() =
  (* Extract queries and assignments from &quot;inf.conj[last_near..inf.c_cnt-1]&quot;
     into &quot;cmd[0..cmd_cnt-1]&quot;. Assumes and maintains I1 - I4. Sets &quot;inf.c_cnt&quot;
     to the lowest and &quot;cmd_cnt&quot; to the highest possible values that still
     maintain these invariants. *)
    VAR done := FALSE; debugOutput := FALSE; BEGIN
      IF debug &gt;= 1 THEN
        Wr.PutText(stderr, &quot;\n    Extracting predicates/assignments:\n&quot;)
      END;
      WHILE NOT done DO
        done := TRUE;
        (* Extract as many queries as possible *)
        VAR i := last_near; BEGIN
          WHILE i &lt; inf.c_cnt DO
            IF inf.conj[i].b3cnt = 0 THEN
              IF debug &gt;= 1 THEN
                debugOutput := TRUE;
                Wr.PutText(stderr, &quot;      b3cnt = 0, &quot;);
                JunoUnparse.P(stderr, inf.conj[i], indent := 0, debug := TRUE);
                Wr.PutChar(stderr, '\n')
              END;
              MakeCmd(inf.conj[i], kind := 0)
            ELSE
              INC(i)
            END
          END
        END;
        (* Attempt to extract a single assignment *)
        FOR i := last_near TO inf.c_cnt - 1 DO
          IF inf.conj[i].b3cnt = 1 THEN
            TYPECASE inf.conj[i] OF JunoAST.Equals (e) =&gt;
              IF debug &gt;= 1 THEN
                debugOutput := TRUE;
                Wr.PutText(stderr, &quot;      b3cnt = 1, &quot;);
                JunoUnparse.P(stderr, inf.conj[i], indent := 0, debug := TRUE);
                Wr.PutChar(stderr, '\n')
              END;
              VAR
                j: CARDINAL;
                qid := UnknownVar(e, SUBARRAY(inf.var^, 0, inf.v_cnt), j);
              BEGIN
                IF qid # NIL THEN
                  (* update variable array *)
                  DEC(inf.v_cnt);
                  inf.var[j] := inf.var[inf.v_cnt];
                  (* update b3cnt fields of relevant expressions *)
                  UpdateB3Cnts(qid.index);
                  &lt;* ASSERT inf.conj[i].b3cnt = 0 *&gt;
                  (* make the formula a command *)
                  MakeCmd(inf.conj[i], kind := 1 + ORD(qid = e.e2));
                  done := FALSE;
                  (* assignment found; exit FOR loop early *)
                  EXIT
                END
              END
            ELSE (* SKIP *)
            END
          END (* IF *)
        END (* FOR *)
      END;  (* WHILE *)
      IF debug &gt;= 1 THEN
        IF NOT debugOutput THEN Wr.PutText(stderr, &quot;      &lt;none&gt;\n&quot;) END;
        Wr.Flush(stderr)
      END
    END Phase1A;

  PROCEDURE Phase1B(): BOOLEAN RAISES {Error} =
  (* In-line expand the first reducible predicate or formula in the array
     &quot;inf.conj[last_near..inf.c_cnt]&quot; and return TRUE. If all these formulas
     are irreducible, return FALSE. Raises &quot;Error&quot; if any of the formulas is
     an invalid application of a function, predicate, or compound formula in a
     constraint. *)
    VAR i := last_near; nf: JunoAST.NormalForm; BEGIN
      WHILE i &lt; inf.c_cnt AND Irreducible(inf.conj[i], nf) DO INC(i) END;
      IF i = inf.c_cnt THEN RETURN FALSE END;
      IF debug &gt;= 1 THEN
        Wr.PutText(stderr, &quot;    In-line substituting:\n&quot;);
        JunoUnparse.P(stderr, inf.conj[i], indent := 6, debug := TRUE);
        Wr.PutText(stderr, &quot;\n        with\n&quot;);
        JunoUnparse.P(stderr, nf, indent := 6, debug := TRUE);
        Wr.PutChar(stderr, '\n'); Wr.Flush(stderr)
      END;
      VAR old_v_cnt, old_c_cnt: CARDINAL; BEGIN
        (* in-line substitute &quot;inf.conj[i]&quot; *)
        DEC(inf.c_cnt);
        inf.conj[i] := inf.conj[inf.c_cnt];
        old_v_cnt := inf.v_cnt;
        IndexedNF.AddVarArray(inf, nf.var^);
        old_c_cnt := inf.c_cnt;
        IndexedNF.AddConjArray(inf, nf.conj^);

        (* update use table *)
        UpdateUseTbl(tbl,
          SUBARRAY(inf.var^, old_v_cnt, inf.v_cnt - old_v_cnt),
          SUBARRAY(inf.conj^, old_c_cnt, inf.c_cnt - old_c_cnt));

        (* move new near constraints to front *)
        PermuteNearToPrefix(start := old_c_cnt)
      END;
      RETURN TRUE
    END Phase1B;

  PROCEDURE Irreducible(
      VAR (*INOUT*) f: JunoAST.Formula;
      VAR (*OUT*) newF: JunoAST.NormalForm)
    : BOOLEAN RAISES {Error} =
  (* Return &quot;TRUE&quot; if &quot;f&quot; is a primitive solve predicate. In this
     case, the value of &quot;newF&quot; on exit is undefined, and if &quot;f&quot; is an
     equality, its left-hand side is required to be a literal or QId.

     Otherwise, &quot;f&quot; is a predicate or an equality between a variable and a
     function; set &quot;newF&quot; to the result of replacing the predicate or function
     in &quot;f&quot; by its definition and return &quot;FALSE&quot;.

     Requires &quot;f&quot; is not a near constraint.

     Raises &quot;Error&quot; if &quot;f&quot; is an invalid application of a function, predicate,
     or compound formula (i.e., &quot;OR&quot; or &quot;NOT&quot;) in a constraint. *)
    BEGIN
      TYPECASE f OF &lt;* NOWARN *&gt;
	JunoAST.LitPred =&gt; RETURN TRUE
      | JunoAST.IsReal, JunoAST.IsText =&gt; RETURN TRUE
      | JunoAST.IsPair (ip) =&gt; newF := ExpandIsPair(ip)
      | JunoAST.Or, JunoAST.Not, JunoAST.IsInt =&gt;
          Raise(&quot;You can't write this in a constraint&quot;, f)
      | JunoAST.Call (c) =&gt;
	  newF := Substitute(c.normal_form, c.ins, NIL,
	    stack_tbl.next_index - 1, c)
      | JunoAST.Equals (e) =&gt;
          &lt;* ASSERT NOT e.near *&gt;
          &lt;* ASSERT ISTYPE(e.e1, JunoAST.AtomicExpr) *&gt;
          TYPECASE e.e2 OF             (* check for triples *)
            JunoAST.AtomicExpr, JunoAST.Plus, JunoAST.Times, JunoAST.Pair,
            JunoAST.Atan, JunoAST.Sin, JunoAST.Cos, JunoAST.Exp,
            JunoAST.Ln =&gt;
              RETURN TRUE
          | JunoAST.List(list) =&gt;
              newF := ExpandList(e.e1, list)
          ELSE
              newF := Substitute(GetFunc(e.e2), Actuals(e.e2), e.e1,
		stack_tbl.next_index - 1, e)
          END
      | JunoAST.Relation (r) =&gt;
	  newF := Substitute(GetPred(r), Actuals(r), NIL,
	    stack_tbl.next_index - 1, r);
      END;
      RETURN FALSE
    END Irreducible;

  PROCEDURE Substitute(
      nf: JunoAST.NormalForm;
      actuals: JunoAST.ExprList;	 (* IN parameters *)
      last_actual: JunoAST.Expr;	 (* single OUT result parameter *)
      index_delta: CARDINAL;
      bp: JunoAST.Formula):
    JunoAST.NormalForm =
  (* Return a new normal-form formula equivalent to &quot;nf&quot; with all free
     occurrences of the &quot;formals&quot; in &quot;nf&quot; replaced by the corresponding
     &quot;actuals&quot;, and with the index of each *local* variable of &quot;nf&quot;
     incremented by &quot;index_delta&quot;. The last actual only counts if it is not
     NIL. This procedure has the side-effect of incrementing the global
     &quot;stack_tbl.next_index&quot; by the number of local variables in &quot;nf&quot;, namely,
     &quot;NUMBER(nf.var^)&quot;. All newly consed nodes have the given &quot;bp&quot;. *)
    VAR
      arg_cnt := actuals.size + ORD(last_actual # NIL);
      args := NEW(REF ARRAY OF JunoAST.Expr, arg_cnt);

    PROCEDURE SubstExpr(e: JunoAST.Expr): JunoAST.Expr =
      BEGIN
	TYPECASE Utils.Ungroup(e) OF
	  JunoAST.LitValue =&gt; RETURN e
	| JunoAST.QId (q) =&gt;
            IF q.type = JunoAST.IdType.Local THEN
              IF q.index &lt; 0 THEN	 (* &quot;q&quot; is a formal parameter *)
                RETURN args[arg_cnt + q.index]
	      ELSE			 (* &quot;q&quot; is a local variable *)
	        RETURN NEW(JunoAST.QId, bp := q, id0 := q.id0, id1 := q.id1,
	          type := q.type, index := q.index + index_delta)
              END
            ELSE RETURN q		 (* &quot;q&quot; is a const or global var *)
            END
	ELSE
	    RETURN Utils.MapArgs(e, SubstExpr)
	END
      END SubstExpr;

    (* Substitute *)
    VAR subst_inf := IndexedNF.New(var_cnt := 100, conj_cnt := 200); BEGIN
      (* Copy &quot;nf&quot; to global &quot;subst_inf&quot; *)
      IndexedNF.FromNF(nf, subst_inf);
      subst_inf.bp := bp;
      (* Build &quot;args&quot; array with IN parameters last (in order)
	 and single OUT parameter first (if it exists) *)
      VAR curr := actuals.head; i := arg_cnt - actuals.size; BEGIN
        WHILE curr # NIL DO
          &lt;* ASSERT ISTYPE(curr.expr, JunoAST.AtomicExpr) *&gt;
          args[i] := curr.expr; INC(i);
          curr := curr.next
        END;
        IF last_actual # NIL THEN args[0] := last_actual END
      END;
      (* Increase the index of each local variable by &quot;index_delta&quot; *)
      FOR i := 0 TO subst_inf.v_cnt - 1 DO
        WITH v = subst_inf.var[i] DO
          v := NEW(JunoAST.NearVarLink, id := v.id, hint := v.hint,
            evar := v.evar, index := v.index + index_delta)
        END
      END;
      (* Copy and substitute in each conjunct *)
      FOR i := 0 TO subst_inf.c_cnt - 1 DO
        subst_inf.conj[i] := SubstExpr(subst_inf.conj[i])
      END;
      INC(stack_tbl.next_index, subst_inf.v_cnt);
      VAR res := IndexedNF.ToNF(subst_inf); BEGIN
        IndexedNF.Dispose(subst_inf);
        RETURN res
      END
    END Substitute;

  PROCEDURE GetFunc(call: JunoAST.Expr): JunoAST.NormalForm RAISES {Error} =
  (* Return the normal form bound in &quot;scp&quot; to the user-defined or built-in
     function at the root of &quot;call&quot;. Requires that &quot;call&quot; is not one of the
     built-in functions understood by the Juno solver or a &quot;JunoAST.List&quot;.

     If &quot;call&quot; has type &quot;JunoAST.Call&quot;, then this procedure relies on the fact
     that JunoCompile.AnnotateAtoms has annotated &quot;call.normal_form&quot; such
     that: if &quot;call&quot; represents a function call, then &quot;call.normal_form&quot; is
     the normal form for that function's body, and if &quot;call&quot; represents a
     functional procedure call, then &quot;call.normal_form&quot; is NIL. In the latter
     case, &quot;Error&quot; will be raised with an appropriate message.

     Raises &quot;Error&quot; if &quot;call&quot; is illegal in a constraint. *)
    VAR name: JunoAST.QId; BEGIN
      TYPECASE call OF
        JunoAST.UMinus  =&gt; name := JunoAST.UMinusName
      | JunoAST.Car     =&gt; name := JunoAST.CarName
      | JunoAST.Cdr     =&gt; name := JunoAST.CdrName
      | JunoAST.Minus   =&gt; name := JunoAST.MinusName
      | JunoAST.Divide  =&gt; name := JunoAST.DivideName
      | JunoAST.Rel     =&gt; name := JunoAST.RelName
      | JunoAST.Atan, JunoAST.Sin, JunoAST.Cos, JunoAST.Exp, JunoAST.Ln,
        JunoAST.Plus, JunoAST.Times, JunoAST.Pair, JunoAST.List =&gt;
          &lt;* ASSERT FALSE *&gt;
      | JunoAST.Call (c) =&gt;
          IF c.normal_form # NIL
            THEN RETURN c.normal_form
            ELSE Raise(&quot;Procedure calls aren't allowed in constraints&quot;, c)
          END
      ELSE
	  Raise(&quot;You can't write this in a constraint&quot;, call)
      END;
      VAR dummy: JunoScope.Entity; f: JunoScope.Func; BEGIN
	f := JunoScope.LookupQId(scp, name, dummy);
	&lt;* ASSERT f # NIL *&gt;		 (* Built-ins must be in defined *)
	RETURN f.normal_form
      END
    END GetFunc;

  PROCEDURE GetPred(r: JunoAST.Relation): JunoAST.NormalForm RAISES {Error} =
  (* Return the normal form bound in &quot;scp&quot; to the built-in relation &quot;r&quot;.
     Requires that &quot;r&quot; is not the equality relation.

     Raises &quot;Error&quot; if &quot;r&quot; is an invalid predicate in a constraint. *)
    VAR name: JunoAST.QId; BEGIN
      TYPECASE r OF
	JunoAST.Cong =&gt; name := JunoAST.CongName
      | JunoAST.Para =&gt; name := JunoAST.ParaName
      | JunoAST.Hor  =&gt; name := JunoAST.HorName
      | JunoAST.Ver  =&gt; name := JunoAST.VerName
      | JunoAST.Equals =&gt; &lt;* ASSERT FALSE *&gt;
      ELSE
	  Raise(&quot;You can't write this in a constraint&quot;, r)
      END;
      VAR dummy: JunoScope.Entity; p: JunoScope.Pred; BEGIN
	p := JunoScope.LookupQId(scp, name, dummy);
	&lt;* ASSERT p # NIL *&gt;		 (* built-ins must be in scope *)
	RETURN p.normal_form
      END
    END GetPred;

  PROCEDURE ExpandIsPair(ip: JunoAST.IsPair): JunoAST.NormalForm =
  (* Return a new normal-form formula equivalent to &quot;ip&quot; that does not contain
     a PAIR predicate at the top level. In particular, &quot;PAIR(e)&quot; is tranlated
     into the formula: &quot;(E _x[1], _x[2] :: e = (_x[1], _x[2]))&quot;. The indices
     for the newly quantified variables are actually taken from
     &quot;stack_tbl.next_index&quot;.*)
    VAR
      vars := NewNearVars(2);
      f := NEW(REF JunoAST.Formulas, 1);
    BEGIN
      f[0] := NEW(JunoAST.Equals, bp := ip, e1 := ip.e,
        e2 := NEW(JunoAST.Pair,
          e1 := Utils.QIdFromNearVar(vars[0]),
          e2 := Utils.QIdFromNearVar(vars[1])));
      RETURN NEW(JunoAST.NormalForm, conj := f, var := vars)
    END ExpandIsPair;

  PROCEDURE ExpandList(
      id: JunoAST.AtomicExpr;
      l: JunoAST.List):
    JunoAST.NormalForm =
  (* Return a new normal-form formula not containing any &quot;JunoAST.List&quot;'s at
     the top level and equivalent to &quot;id = l&quot;. For example, if &quot;id&quot; is &quot;a&quot; and
     &quot;l&quot; is &quot;[1,2,3]&quot;, then return the formula:
|        (E _x[1], _x[2] ::
|                a = (1, _x[1])
|        AND _x[1] = (2, _x[2])
|        AND _x[2] = (3, NIL))
     The indices for the newly quantified variables are actually taken from
     &quot;stack_tbl.next_index&quot;. *)
    VAR
      vars := NewNearVars(l.elts.size - 1);
      f := NEW(REF JunoAST.Formulas, l.elts.size);
      lhs: JunoAST.AtomicExpr := id;
      cdr: JunoAST.NearVarLink;
      currE := l.elts.head;
    BEGIN
      FOR i := FIRST(f^) TO LAST(f^) DO
        IF i &lt; LAST(f^) THEN cdr := vars[i] ELSE cdr := NIL END;
        VAR cdrVal := QIdFromNearVarOrNil(cdr); BEGIN
          f[i] := NEW(JunoAST.Equals, e1 := lhs,
            e2 := NEW(JunoAST.Pair, e1 := currE.expr, e2 := cdrVal));
          lhs := cdrVal
        END;
        currE := currE.next
      END;
      RETURN NEW(JunoAST.NormalForm, conj := f, var := vars)
    END ExpandList;

  PROCEDURE NewNearVars(cnt: CARDINAL): REF JunoAST.Vars =
  (* Return an array of size &quot;cnt&quot; of unhinted E-quantified dummy near vars.
     The dummy variables have the same name (the value of the global
     &quot;dummyVar&quot;), but they are given distinct indices from &quot;stack_tbl&quot;. *)
    VAR res := NEW(REF JunoAST.Vars, cnt); BEGIN
      FOR i := FIRST(res^) TO LAST(res^) DO
        res[i] := NEW(JunoAST.NearVarLink, id := dummyVar, evar := TRUE,
          hint := JunoAST.NilExpr, index := stack_tbl.next_index);
        INC(stack_tbl.next_index)
      END;
      RETURN res
    END NewNearVars;

  PROCEDURE Phase2() RAISES { ExtraNearConstraints } =
    (* Extract hint assignments from &quot;inf.conj[0..inf.c_cnt-1]&quot;. Requires I1
       - I4. Any near constraints that could be extracted are deleted from
       &quot;inf.conj[0..inf.c_cnt-1]&quot;. Raises &quot;ExtraNearConstraints&quot; if there were
       any active near constraints that could not be extracted.

       IMPLEMENTATION: During this phase, the &quot;b3cnt&quot; field of the formulas
       that are active candidates for processing represents the number of
       unhinted (not unknown) variables remaining in that formula. See Allan's
       notes from 9-Nov-92 for more details about this phase. *)
    VAR
      done := FALSE;
      last_unhinted := inf.v_cnt;
      active_near := last_near;
    (* Invariant:
|
|      I2': Variables inf.var[0..last_unhinted-1] are unhinted unknowns.
|           Variables inf.var[last_unhinted..inf.v_cnt-1] are hinted unknowns.
    *)

    PROCEDURE ShowConstraints(msg: TEXT) =
      CONST n = 7; nl = &quot;\n        &quot;; BEGIN
        Wr.PutText(stderr, &quot;\n    &quot; &amp; msg);
        Wr.PutText(stderr, &quot;\n      Unhinted Vars:&quot; &amp; nl);
        IF 0 &gt; last_unhinted - 1 THEN Wr.PutText(stderr, &quot;&lt;none&gt;&quot;) END;
        FOR i := 0 TO last_unhinted - 1 DO
          JunoUnparse.P(stderr, Utils.QIdFromNearVar(inf.var[i]),
            indent := 0, debug := TRUE);
          IF i &lt; last_unhinted - 1 THEN
            IF i MOD n = (n - 1)
              THEN Wr.PutText(stderr, nl)
              ELSE Wr.PutText(stderr, &quot;, &quot;)
            END
          END
        END;
        Wr.PutText(stderr, &quot;\n      Hinted Vars:&quot; &amp; nl);
        IF last_unhinted &gt; inf.v_cnt - 1 THEN Wr.PutText(stderr, &quot;&lt;none&gt;&quot;) END;
        FOR i := last_unhinted TO inf.v_cnt - 1 DO
          JunoUnparse.P(stderr, Utils.QIdFromNearVar(inf.var[i]),
            indent := 0, debug := TRUE);
          IF i &lt; inf.v_cnt - 1 THEN
            IF (i - last_unhinted) MOD n = (n - 1)
              THEN Wr.PutText(stderr, nl)
              ELSE Wr.PutText(stderr, &quot;, &quot;)
            END
          END
        END;
        Wr.PutText(stderr, &quot;\n      Constraints:&quot; &amp; nl);
        IF 0 &gt; inf.c_cnt - 1 THEN Wr.PutText(stderr, &quot;&lt;none&gt;&quot;) END;
        FOR i := 0 TO inf.c_cnt - 1 DO
          Wr.PutText(stderr, &quot;b3cnt = &quot; &amp; Fmt.Int(inf.conj[i].b3cnt) &amp; &quot;, &quot;);
          JunoUnparse.P(stderr, inf.conj[i], indent := 0, debug := TRUE);
          IF i &lt; inf.c_cnt - 1 THEN Wr.PutText(stderr, nl) END
        END;
        Wr.PutChar(stderr, '\n'); Wr.Flush(stderr)
      END ShowConstraints;

    (* Phase2 *)
    BEGIN
      (* Some variables (passed in queries to the compiler) may already be
	 designated as hinted, so move them to the end of the array. *)
      VAR i := 0; BEGIN
        WHILE i &lt; last_unhinted DO
          IF inf.var[i].frozen THEN
            DEC(last_unhinted);
            SwapV(inf.var[i], inf.var[last_unhinted]);
            UpdateB3Cnts(inf.var[last_unhinted].index)
          ELSE
            INC(i)
          END
        END
      END;

      (* Extract hint-initializing assignments (Rules 2 &amp; 4) *)
      IF debug &gt;= 2 THEN
        ShowConstraints(&quot;Extracting hint assignments from:&quot;)
      END;
      WHILE NOT done DO
        done := TRUE;
        FOR i := 0 TO inf.c_cnt - 1 DO
          IF inf.conj[i].b3cnt = 1 THEN
            TYPECASE inf.conj[i] OF JunoAST.Equals (e) =&gt;
              VAR
                j: CARDINAL;
                qid := UnknownVar(e, SUBARRAY(inf.var^,0,last_unhinted), j);
              BEGIN
                IF qid # NIL THEN
                  (* update variable array *)
                  (* Set &quot;inf.var[j]&quot; to a copy of itself w/ &quot;frozen:=TRUE&quot; *)
                  VAR old := inf.var[j]; BEGIN
                    inf.var[j] := NEW(JunoAST.NearVarLink, id := old.id,
                      evar := old.evar, frozen := TRUE, hint := old.hint,
                      index := old.index, next := old.next)
                  END;
                  DEC(last_unhinted);
                  SwapV(inf.var[j], inf.var[last_unhinted]);
                  (* update b3cnt fields of relevant expressions *)
                  UpdateB3Cnts(qid.index);
                  &lt;* ASSERT inf.conj[i].b3cnt = 0 *&gt;
                  (* copy formula to &quot;cmds&quot; array *)
                  CopyCmd(inf.conj[i], kind := 1 + ORD(qid = e.e2));
                  (* Delete near formula within &quot;inf.conj&quot; *)
                  IF e.near THEN
                    DEC(inf.c_cnt);
                    inf.conj[i] := inf.conj[inf.c_cnt];
                    DEC(active_near)
                  END;
                  done := FALSE;
                  (* assignment found; exit FOR loop early *)
                  EXIT
                END
              END
            ELSE (* SKIP *)
            END
          END
        END
      END; (* WHILE *)

      (* Eliminate near constraints on hinted or known vars (Rule 3) *)
      IF debug &gt;= 2 THEN
        ShowConstraints(&quot;Eliminating extra near constraints:&quot;)
      END;
      VAR i := 0; near_found: BOOLEAN; BEGIN
        WHILE i &lt; inf.c_cnt DO
          near_found := FALSE;
          IF inf.conj[i].b3cnt = 0 THEN
            TYPECASE inf.conj[i] OF JunoAST.Equals (e) =&gt;
              IF e.near THEN
                near_found := TRUE;
                DEC(inf.c_cnt);
                inf.conj[i] := inf.conj[inf.c_cnt];
                DEC(active_near)
              END
            ELSE (* SKIP *)
            END
          END;
          IF NOT near_found THEN INC(i) END
        END
      END;
      IF debug &gt;= 2 THEN
        ShowConstraints(&quot;Done processing hints:&quot;)
      END;
      IF active_near &gt; 0 THEN RAISE ExtraNearConstraints END
    END Phase2;

  PROCEDURE PreQueryCmd(READONLY forms: FormCmds): JunoAST.Cmd RAISES {Error} =
  (* Return a sequence of queries and assignments formed from the formulas in
     &quot;forms&quot;. The order of the commands in the result matches the order of the
     input formulas. The &quot;kind&quot; field of each formula is used to produce a
     corresponding query (when &quot;b3cnt = 0&quot;) or assignment (when &quot;b3cnt = 1&quot; or
     &quot;b3cnt = 2&quot;). The result always ends with &quot;JunoAST.SkipVal&quot;. *)
    VAR res: JunoAST.Cmd := JunoAST.SkipVal; curr: JunoAST.Cmd; BEGIN
      FOR i := LAST(forms) TO FIRST(forms) BY -1 DO
	CASE forms[i].kind OF &lt;* NOWARN *&gt;
	  0 =&gt;
	    curr := C2q2(forms[i].f)
	| 1 =&gt;
	    VAR eq := NARROW(forms[i].f, JunoAST.Equals); BEGIN
	      curr := NEW(JunoAST.Assign, bp := forms[i].f,
		vars := Utils.NewQIdList(eq.e1),
		exprs := Utils.NewExprList(eq.e2))
	    END
	| 2 =&gt;
	    VAR eq := NARROW(forms[i].f, JunoAST.Equals); BEGIN
	      curr := NEW(JunoAST.Assign, bp := forms[i].f,
		vars := Utils.NewQIdList(eq.e2),
		exprs := Utils.NewExprList(eq.e1))
	    END
	END;
	IF curr # JunoAST.SkipVal THEN
	  res := NEW(JunoAST.Seq, c1 := curr, c2 := res)
	END
      END;
      RETURN res
    END PreQueryCmd;

  PROCEDURE C2q2(pred: JunoAST.Formula): JunoAST.Cmd RAISES {Error} =
  (* This procedure works like &quot;JunoCompile.C2q&quot;, but with the following
     exceptions:
|      1) It does not handle &quot;And&quot; and &quot;Exists&quot; predicates, and
|      2) It recursively calls &quot;ToCmd&quot; (instead of &quot;C2q&quot;) on the arguments
|         to &quot;Not&quot; and &quot;Or&quot;.
  *)
    BEGIN
      TYPECASE Utils.Ungroup(pred) OF
	JunoAST.True  =&gt; RETURN JunoAST.SkipVal
      | JunoAST.False =&gt; RETURN JunoAST.FailVal
      | JunoAST.Differs (p) =&gt;				       (* rule C2.17 *)
	  RETURN NEW(JunoAST.Flip, body := NEW(JunoAST.Query,
	    f := NEW(JunoAST.Equals, bp := p, e1 := p.e1, e2 := p.e2),
	    vars := JunoAST.EmptyNVList))
      | JunoAST.Not (p) =&gt;
	  RETURN NEW(JunoAST.Flip, bp := p,
	    body := ToCmd(p.f, scp, stack_tbl))
      | JunoAST.Or (p) =&gt;
	  RETURN NEW(JunoAST.Else, bp := p,
	    c1 := ToCmd(p.f1, scp, stack_tbl),
	    c2 := ToCmd(p.f2, scp, stack_tbl))
      ELSE						       (* rule C2.21 *)
	  RETURN NEW(JunoAST.Query, f := pred, vars := JunoAST.EmptyNVList)
      END
    END C2q2;

  PROCEDURE Combine(pre: JunoAST.Cmd; post: IndexedNF.T): JunoAST.Cmd =
  (* Return a sequence of the commands &quot;pre&quot; and the solve query &quot;post&quot;.
     Either or both of &quot;pre&quot; and &quot;post&quot; may be &quot;empty&quot;. &quot;Pre&quot; is empty if it
     is &quot;JunoAST.SkipVal&quot;. &quot;Post&quot; is empty if it has no variables to solve for
     (in which case, it is required not to have any conjuncts to solve for
     either). If one or the other is empty, return the other. If both are
     empty, return &quot;pre&quot; (i.e., &quot;SKIP&quot;). *)
    VAR res: JunoAST.Cmd; BEGIN
      IF post.v_cnt = 0 THEN &lt;* ASSERT post.c_cnt = 0 *&gt; RETURN pre END;
      VAR
        qNF := IndexedNF.ToNF(post);
        qCmd := NEW(JunoAST.ConjQuery, var := qNF.var, conj := qNF.conj);
      BEGIN
        IF pre = JunoAST.SkipVal
          THEN res := qCmd
          ELSE res := NEW(JunoAST.Seq, c1 := pre, c2 := qCmd)
        END
      END;
      RETURN res
    END Combine;

  (* ToCmd *)
  BEGIN
    (* Check the pre-condition *)
    FOR i := 0 TO LAST(nf.var^) DO
      WITH v = nf.var[i] DO
        &lt;* ASSERT v.evar=TRUE AND v.hint=JunoAST.NilExpr AND v.frozen=FALSE *&gt;
      END
    END;
    IF xtra_vars # NIL THEN
      VAR l := xtra_vars.head; BEGIN
    	WHILE l # NIL DO
    	  &lt;* ASSERT l.evar=FALSE AND l.hint=JunoAST.NilExpr *&gt;
    	  l := l.next
    	END
      END
    END;

    (* Create an indexed normal form that includes &quot;vars&quot; *)
    IndexedNF.FromNF(nf, inf);
    IF xtra_vars # NIL THEN IndexedNF.AddVarList(inf, xtra_vars) END;

    (* Create and initialize a new use table *)
    tbl := NEW(IntRefTbl.Default).init(inf.v_cnt);
    UpdateUseTbl(tbl, SUBARRAY(inf.var^, 0, inf.v_cnt), nf.conj^);

    (* Establish I1, I2, I3, and I4 *)
    last_near := 0;
    cmd_cnt := 0;
    PermuteNearToPrefix(start := 0);

    (* Loop until no more substitutions are possible *)
    IF debug &gt;= 1 THEN
      Wr.PutText(stderr, &quot;\n  Simplifying constraint...\n&quot;)
    END;
    REPEAT Phase1A() UNTIL NOT Phase1B();

    (* Propagate hints *)
    TRY Phase2() EXCEPT ExtraNearConstraints =&gt;
      Raise(&quot;Extra near constraints&quot;, nf)
    END;

    (* Construct return result *)
    VAR body := Combine(PreQueryCmd(SUBARRAY(cmds^, 0, cmd_cnt)), inf); BEGIN
      IndexedNF.Dispose(inf);
      RETURN NEW(JunoAST.Safe, bp := nf, body := body)
    END
  END ToCmd;

PROCEDURE <A NAME="UpdateUseTbl"><procedure>UpdateUseTbl</procedure></A>(
    VAR (*INOUT*) tbl: IntRefTbl.T;
    READONLY vars: JunoAST.Vars;
    READONLY nsf: JunoAST.Formulas) =
</PRE><BLOCKQUOTE><EM> Incrementally update <CODE>tbl</CODE> to contain new entries for the variables <CODE>vars</CODE>.
   For each <CODE>v</CODE> in the resulting table, let <CODE>tbl(v) = l</CODE> before the call (<CODE>l</CODE>
   is the empty list for any <CODE>v</CODE> in <CODE>vars</CODE>), and let <CODE>tbl(v) = l'</CODE> after the
   call. Then <CODE>l' = lpre &amp; l</CODE>, where <CODE>lpre</CODE> is a list of those formulas in
   <CODE>nsf</CODE> that contain <CODE>v</CODE>. The formula <CODE>f</CODE> appears in <CODE>lpre</CODE> exactly as many
   times as <CODE>v</CODE> occurs in <CODE>f</CODE>, which number is also added to <CODE>f.b3cnt</CODE>.
   Requires that each <CODE>nsf[i]</CODE> is a normal simple formula. </EM></BLOCKQUOTE><PRE>
  VAR curr_nsf: JunoAST.Formula;

  PROCEDURE CntUse(e: JunoAST.Expr): JunoAST.Expr =
  (* For each local variable &quot;v&quot; that occurs in &quot;e&quot; and whose index is
     registered in &quot;tbl&quot;, add the formula &quot;curr_nsf&quot; to the list of formulas
     associated with &quot;v&quot; in the table &quot;tbl&quot;, and increment the &quot;b3cnt&quot; field
     of &quot;curr_nsf&quot;. Returns Modula-3 &quot;NIL&quot; (so it can be passed as an argument
     to &quot;JunoASTUtils.MapArgs&quot;). *)
    BEGIN
      e := Utils.Ungroup(e);
      TYPECASE e OF
        JunoAST.LitValue =&gt; (* SKIP *)
      | JunoAST.QId (q) =&gt;
          VAR ref: REFANY; BEGIN
            &lt;* ASSERT q.index # 0 *&gt;
            IF q.type = JunoAST.IdType.Local
              AND tbl.get(q.index, ref) THEN
      	      INC(curr_nsf.b3cnt);
              VAR rl: RefList.T := ref; BEGIN
      	        EVAL tbl.put(q.index, RefList.Cons(curr_nsf, rl))
              END
            END
          END
      | JunoAST.Not (not) =&gt;
          EVAL CntUse(not.f);
      | JunoAST.Or (or) =&gt;
          EVAL CntUse(or.f1);
          EVAL CntUse(or.f2);
      | JunoAST.NormalForm (nf) =&gt;
          FOR i := 0 TO LAST(nf.conj^) DO
            EVAL CntUse(nf.conj[i])
          END
      ELSE
          EVAL Utils.MapArgs(e, CntUse)
      END;
      RETURN NIL
    END CntUse;

  (* UpdateUseTbl *)
  BEGIN
    (* Install a new entry in the table for each variable *)
    FOR i := FIRST(vars) TO LAST(vars) DO
      &lt;* ASSERT vars[i].index # 0 *&gt;
      EVAL tbl.put(vars[i].index, NIL);
    END;

    (* Update the table and use counts for each normal simple formula *)
    FOR i := FIRST(nsf) TO LAST(nsf) DO
      curr_nsf := nsf[i];
      TYPECASE curr_nsf OF JunoAST.Equals (e) =&gt;
        EVAL CntUse(e.e1);
        EVAL CntUse(e.e2)
      ELSE
        EVAL CntUse(curr_nsf)
      END
    END
  END UpdateUseTbl;
</PRE> ========================== MISCELLANEOUS ================================ 

<P><PRE>PROCEDURE <A NAME="Actuals"><procedure>Actuals</procedure></A>(call: JunoAST.Expr): JunoAST.ExprList =
</PRE><BLOCKQUOTE><EM> Return the actual arguments to <CODE>call</CODE> in left-to-right order. </EM></BLOCKQUOTE><PRE>
  VAR res := NEW(JunoAST.ExprList);
  PROCEDURE ConsArg(e: JunoAST.Expr): JunoAST.Expr =
    BEGIN
      res.head := NEW(JunoAST.ExprLink, expr := e, next := res.head);
      INC(res.size);
      RETURN e
    END ConsArg;
  (* Actuals *)
  BEGIN
    EVAL Utils.MapArgs(call, ConsArg);
    res.head := ReverseExprList(res.head);
    RETURN res;
  END Actuals;

PROCEDURE <A NAME="ReverseExprList"><procedure>ReverseExprList</procedure></A>(el: JunoAST.ExprLink): JunoAST.ExprLink =
</PRE><BLOCKQUOTE><EM> Destructively reverse the list <CODE>el</CODE> in place, and return a pointer to the
   head of the new list. </EM></BLOCKQUOTE><PRE>
  VAR curr, next: JunoAST.ExprLink; BEGIN
    IF el # NIL THEN
      next := el.next;
      el.next := NIL;
      WHILE next # NIL DO
        curr := next;
        next := curr.next;
        curr.next := el;
        el := curr
      END
    END;
    RETURN el
  END ReverseExprList;

PROCEDURE <A NAME="UnknownVar"><procedure>UnknownVar</procedure></A>(
    r: JunoAST.Relation;
    READONLY unknowns: JunoAST.Vars;
    VAR (*OUT*) index: CARDINAL):
    JunoAST.QId =
</PRE><BLOCKQUOTE><EM> Return <CODE>r.e1</CODE> or <CODE>r.e2</CODE> if either is a local variable appearing in
   <CODE>unknowns</CODE>. In this case, set <CODE>index</CODE> to the index of the identifier in
   <CODE>unknowns</CODE>. If there is no such local variable, the value of
   <CODE>index</CODE> is undefined; return <CODE>NIL</CODE>. </EM></BLOCKQUOTE><PRE>

  PROCEDURE CheckExpr(e: JunoAST.Expr; VAR (*OUT*) index: CARDINAL):
      JunoAST.QId =
    VAR j: INTEGER; BEGIN
      TYPECASE e OF JunoAST.QId (q) =&gt;
    	IF q.type = JunoAST.IdType.Local THEN
    	  j := MemVars(q.index, unknowns);
    	  IF j &gt;= 0 THEN index := j; RETURN q END
    	END
      ELSE (* SKIP *)
      END;
      RETURN NIL
    END CheckExpr;

  (* UnknownVar *)
  VAR res: JunoAST.QId; BEGIN
    res := CheckExpr(r.e1, index);
    IF res = NIL THEN res := CheckExpr(r.e2, index) END;
    RETURN res
  END UnknownVar;

PROCEDURE <A NAME="MemVars"><procedure>MemVars</procedure></A>(index: INTEGER; READONLY vars: JunoAST.Vars): INTEGER =
</PRE><BLOCKQUOTE><EM> Return the index of the variable in <CODE>vars</CODE> having index <CODE>index</CODE>, or -1 if
   there is no such variable. </EM></BLOCKQUOTE><PRE>
  BEGIN
    &lt;* ASSERT index # 0 *&gt;
    FOR i := FIRST(vars) TO LAST(vars) DO
      IF index = vars[i].index THEN RETURN i END
    END;
    RETURN -1
  END MemVars;

PROCEDURE <A NAME="QIdFromNearVarOrNil"><procedure>QIdFromNearVarOrNil</procedure></A>(v: JunoAST.NearVarLink): JunoAST.AtomicExpr =
</PRE><BLOCKQUOTE><EM> If <CODE>v = NIL</CODE>, return the Juno value <CODE>NIL</CODE>; otherwise, return
   <CODE>Utils.QIdFromNearVar(v)</CODE>. </EM></BLOCKQUOTE><PRE>
  BEGIN
    IF v = NIL
      THEN RETURN JunoAST.NilVal
      ELSE RETURN Utils.QIdFromNearVar(v)
    END
  END QIdFromNearVarOrNil;

BEGIN
END JunoCompileNF.
</PRE>
</inModule>
<PRE>























</PRE>
</BODY>
</HTML>
