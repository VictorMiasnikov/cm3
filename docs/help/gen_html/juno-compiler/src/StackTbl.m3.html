<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: juno-compiler/src/StackTbl.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>juno-compiler/src/StackTbl.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright (C) 1992, Digital Equipment Corporation                         
 All rights reserved.                                                      
 See the file COPYRIGHT for a full description.                            
                                                                           
 Last modified on Fri Jan 20 15:17:09 PST 1995 by heydon                   

<P><PRE>MODULE <module><implements><A HREF="StackTbl.i3.html">StackTbl</A></implements></module>;

IMPORT <A HREF="../../libm3/src/atom/Atom.i3.html">Atom</A>, <A HREF="../../libm3/derived/AtomIntTbl.i3.html">AtomIntTbl</A>;

TYPE
  Entry = RECORD a: Atom.T; n: INTEGER END;
  Stack = REF ARRAY OF Entry;

REVEAL
  <A NAME="T">T</A> = Public BRANDED &quot;StackTbl.T&quot; OBJECT
    tbl: AtomIntTbl.Default := NIL;
    stack: Stack := NIL;
    sp: CARDINAL
  OVERRIDES
    init := Init
  END;

CONST InitSize = 20;
</PRE> IMPLEMENTATION:
<P>
   The stack table is implemented with a table and a stack of <CODE>(Atom.T,
   INTEGER)</CODE> pairs. The table maps <CODE>Atom.T</CODE>'s to <CODE>INTEGER</CODE>'s, and it holds the
   true mapping at any point in time. Hence, the <CODE>Lookup</CODE> procedure simply
   looks up the supplied name in the table. The stack is used to store marks,
   and old <CODE>(Atom.T, INTEGER)</CODE> associations.
<P>
   The <CODE>Mark</CODE> procedure pushes a special <CODE>(NIL, 0)</CODE> pair on the stack. The
   procedure <CODE>Push(nm)</CODE> pushes the pair <CODE>(nm, oldVal)</CODE> to the stack if <CODE>nm</CODE>
   was associated with the integer <CODE>oldVal</CODE> in the table, or pushes the pair
   <CODE>(nm, 0)</CODE> if <CODE>nm</CODE> was not previously associated in the table. It also
   associates <CODE>nm</CODE> with the next index in the table. The <CODE>PushFormal</CODE>
   procedure is similar.
<P>
   Finally the <CODE>PopToMark</CODE> procedure pops entries from the stack until it gets
   to a distinguished <CODE>(NIL, 0)</CODE> pair. For each entry <CODE>(nm, val)</CODE>, it restores
   the association for <CODE>nm</CODE> in table according to <CODE>val</CODE>: if <CODE>val</CODE> is non-zero,
   it associates <CODE>nm</CODE> with <CODE>val</CODE> in the table, and if <CODE>val</CODE> is zero, it
   deletes <CODE>nm</CODE> from the table. 

<P>
<P><PRE>PROCEDURE <A NAME="Mark"><procedure>Mark</procedure></A>(t: T) =
</PRE><BLOCKQUOTE><EM> Push a special NIL marker on the stack. </EM></BLOCKQUOTE><PRE>
  BEGIN
    PushP(t, NIL, 0)
  END Mark;

PROCEDURE <A NAME="PopToMark"><procedure>PopToMark</procedure></A>(t: T) =
  VAR dummy: INTEGER; BEGIN
    DEC(t.sp);
    LOOP
      WITH entry = t.stack[t.sp] DO
        IF entry.a = NIL THEN EXIT END;
        IF entry.n = 0
          THEN EVAL t.tbl.delete(entry.a, dummy)
          ELSE EVAL t.tbl.put(entry.a, entry.n)
        END
      END;
      DEC(t.sp)
    END
  END PopToMark;

PROCEDURE <A NAME="Push"><procedure>Push</procedure></A>(t: T; nm: Atom.T) =
  VAR n: INTEGER; BEGIN
    IF t.tbl.get(nm, n)
      THEN PushP(t, nm, n);
      ELSE PushP(t, nm, 0);
    END;
    EVAL t.tbl.put(nm, t.next_index);
    INC(t.next_index)
  END Push;

PROCEDURE <A NAME="PushFormal"><procedure>PushFormal</procedure></A>(t: T; nm: Atom.T) =
  VAR n: INTEGER; BEGIN
    IF t.tbl.get(nm, n)
      THEN PushP(t, nm, n);
      ELSE PushP(t, nm, 0);
    END;
    EVAL t.tbl.put(nm, t.next_formal);
    DEC(t.next_formal)
  END PushFormal;

PROCEDURE <A NAME="Lookup"><procedure>Lookup</procedure></A>(t: T; nm: Atom.T): INTEGER =
  VAR n: INTEGER; BEGIN
    IF NOT t.tbl.get(nm, n) THEN n := 0 END;
    RETURN n
  END Lookup;

PROCEDURE <A NAME="PushP"><procedure>PushP</procedure></A>(t: T; a: Atom.T; n: INTEGER) =
  BEGIN
    IF NUMBER(t.stack^) = t.sp THEN
      (* grow the stack by a factor of 2 *)
      VAR new := NEW(Stack, 2 * NUMBER(t.stack^)); BEGIN
        SUBARRAY(new^, 0, NUMBER(t.stack^)) := t.stack^;
        t.stack := new;
      END
    END;
    t.stack[t.sp].a := a;
    t.stack[t.sp].n := n;
    INC(t.sp)
  END PushP;

PROCEDURE <A NAME="Init"><procedure>Init</procedure></A>(t: T): T =
  BEGIN
    t.next_index := 1;
    t.next_formal := -1;
    IF t.tbl = NIL
      THEN t.tbl := NEW(AtomIntTbl.Default).init(sizeHint := InitSize)
      ELSE EVAL t.tbl.init(sizeHint := InitSize)
    END;
    IF t.stack = NIL THEN
      t.stack := NEW(Stack, InitSize)
    END;
    t.sp := 0;
    RETURN t
  END Init;

BEGIN
END StackTbl.
</PRE>
</inModule>
<PRE>























</PRE>
</BODY>
</HTML>
