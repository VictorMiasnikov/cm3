<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: juno-compiler/src/JunoAssemble.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>juno-compiler/src/JunoAssemble.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright (C) 1992, Digital Equipment Corporation                         
 All rights reserved.                                                      
 See the file COPYRIGHT for a full description.                            
                                                                           
 Last modified on Mon Feb 24 16:46:56 PST 1997 by heydon                   
      modified on Sat Oct 17 18:03:41 PDT 1992 by gnelson                  
      modified on Fri Aug  7 21:54:07 PDT 1992 by myers                    

<P><PRE>MODULE <module><implements><A HREF="JunoAssemble.i3.html">JunoAssemble</A></implements></module>;

IMPORT <A HREF="BuiltInSlots.i3.html">BuiltInSlots</A>, <A HREF="JunoAST.i3.html">JunoAST</A>, <A HREF="JunoCompileRep.i3.html">JunoCompileRep</A>, <A HREF="JunoScope.i3.html">JunoScope</A>, <A HREF="JunoASTUtils.i3.html">JunoASTUtils</A> AS Utils;
IMPORT <A HREF="../../juno-machine/src/JunoByteCode.i3.html">JunoByteCode</A> AS BC, <A HREF="../../juno-machine/src/JunoRT.i3.html">JunoRT</A>, <A HREF="../../juno-machine/src/JunoRTError.i3.html">JunoRTError</A>, <A HREF="#x1">JunoValue</A>, <A HREF="WriteVal.i3.html">WriteVal</A>;
IMPORT <A HREF="../../libm3/derived/IntSeq.i3.html">IntSeq</A>;

TYPE
  IntList = REF RECORD
    val: INTEGER;
    next: IntList
  END;

  LabelData = RECORD
    loc: INTEGER;		(* location (index) of label in &quot;code&quot; *)
    refs: IntList;		(* list of references (jumps) to this label *)
  END;

VAR
  mu := NEW(MUTEX);
  loc: CARDINAL;			 (* current code location *)
  code := NEW(JunoRT.ByteStream, 1000);	 (* result bytestream buffer *)
  labelSeq := NEW(IntSeq.T).init(sizeHint := 100);

TYPE Z3 = ARRAY [0..3] OF INTEGER;

CONST NoLabel = 0; (* the number of an invalid label *)

PROCEDURE <A NAME="Cmd"><procedure>Cmd</procedure></A>(
    res: JunoCompileRep.Result;
    scp: JunoScope.T;
    temp_cnt: CARDINAL;
    type: CmdType):
    JunoRT.ByteStream =

  PROCEDURE Pred(
      pred: JunoAST.Formula;
      t, f: CARDINAL;
      tBranch: BOOLEAN) =
  (* Generate code for &quot;pred&quot; at the current location in the code stream.
     Generate a branch to label &quot;t&quot; if &quot;pred&quot; evaluates true, and to label &quot;f&quot;
     if &quot;pred&quot; evaluates false. Only generate a branch to &quot;t&quot; at the *end* of
     the stream if &quot;tBranch&quot; is &quot;TRUE&quot;.

     The code generated to evaluate a predicate must restore the stack to its
     original state before branching to &quot;t&quot; or &quot;f&quot;.

     NOTE: We do not need to handle the &quot;JunoAST&quot; cases &quot;False&quot;, &quot;And&quot;, &quot;Or&quot;,
     &quot;Not&quot;, &quot;Exists&quot;, or &quot;Differs&quot;, since those are eliminated by the
     compilation procedure &quot;JunoCompile.Cmd.C2q&quot;. *)

    PROCEDURE UnaryPred(p: JunoAST.BIUPred; bc: JunoRT.ByteCode) =
      BEGIN
        Expr(p.e, f);
        PushByte(bc);
        FBranch(f);
	IF tBranch THEN Branch(t) END
      END UnaryPred;

    PROCEDURE Relation(p: JunoAST.Relation; bc: JunoRT.ByteCode) =
      BEGIN
        Expr(p.e1, f);
        IF Utils.AlwaysDefined(p.e2) THEN
          Expr(p.e2, f);
          PushByte(bc);
          FBranch(f);
          IF tBranch THEN Branch(t) END
        ELSE
          VAR popFirst := NewLabel(); BEGIN
            Expr(p.e2, popFirst);
	    PushByte(bc);
	    FBranch(f);
	    Branch(t);
	    SetLabelLoc(popFirst);
	    PushByte(BC.DECSP); PushByte(1);
	    Branch(f)
          END
        END
      END Relation;

    (* Pred *)
    BEGIN
      pred.start := loc;
      TYPECASE pred OF &lt;* NOWARN *&gt;
	JunoAST.True =&gt; IF tBranch THEN Branch(t) END
      | JunoAST.GroupedExpr (p) =&gt; Pred(p.expr, t, f, tBranch)
      | JunoAST.IsReal (p) =&gt; UnaryPred(p, BC.IS_REAL)
      | JunoAST.IsText (p) =&gt; UnaryPred(p, BC.IS_TEXT)
      | JunoAST.IsPair (p) =&gt; UnaryPred(p, BC.IS_PAIR)
      | JunoAST.IsInt (p)  =&gt; UnaryPred(p, BC.IS_INT)
      | JunoAST.Equals (p)  =&gt; Relation(p, BC.EQUAL)
      | JunoAST.Less (p)    =&gt; Relation(p, BC.LESS)
      | JunoAST.AtMost (p)  =&gt; Relation(p, BC.AT_MOST)
      | JunoAST.Cong (p)    =&gt; Relation(p, BC.CONG)
      | JunoAST.Para (p)    =&gt; Relation(p, BC.PARA)
      | JunoAST.Hor (p)     =&gt; Relation(p, BC.HOR)
      | JunoAST.Ver (p)     =&gt; Relation(p, BC.VER)
      | JunoAST.Greater (p) =&gt;
          Relation(NEW(JunoAST.Relation, e1 := p.e2, e2 := p.e1), BC.LESS)
      | JunoAST.AtLeast (p) =&gt;
          Relation(NEW(JunoAST.Relation, e1 := p.e2, e2 := p.e1), BC.AT_MOST)
      | JunoAST.Call (c) =&gt;
          (* Push IN's *)
          ExprList(c.ins, f);
          (* Generate CALL instruction *)
          PushByte(BC.CALL);
          PushULong(c.name.index);
          (* Skip past IN's *)
          Repeat(BC.DECSP, c.ins.size);
          (* Branch appropriately *)
          FBranch(f);
          IF tBranch THEN Branch(t) END
      END;
      pred.end := loc
    END Pred;

  PROCEDURE OutInout(outCnt: CARDINAL; inouts: JunoAST.ExprList) =
    BEGIN
      Repeat(BC.INCSP, outCnt);		 (* Leave space for OUT's *)
      QIdList(inouts);			 (* Push INOUT's *)
    END OutInout;

  PROCEDURE AfterCall(inCnt: CARDINAL; inouts: JunoAST.ExprList;
    uJump: BOOLEAN; label: INTEGER := NoLabel) =
  (* Lay down the instructions after a &quot;JunoAST.Call&quot; node has been assembled.
     Namely, write instructions to skip the SP down past the &quot;inCnt&quot; IN
     parameters and to pop the INOUT parameters into &quot;inouts&quot;. Then, if
     &quot;uJump&quot; is TRUE, write a &quot;UJUMP&quot; instruction to the offset &quot;label&quot;.

     On return from a user-defined function, functional procedure, or
     functional external procedure, the internal condition bit is set as the
     function was defined or not. The UBRANCH instruction tests if the
     condition bit was set. If it was, the instruction is a no-op; otherwise,
     it decrements SP past the single OUT parameter, sets the internal
     &quot;undefined term&quot; address, and branches to its argument &quot;label&quot;. *)
    BEGIN
      Repeat(BC.DECSP, inCnt);
      PopQIds(inouts.head, scp);
      IF uJump THEN
        PushByte(BC.UJUMP);
        AddReference(label)
      END
    END AfterCall;

  PROCEDURE AfterProcCall(inCnt: CARDINAL; inouts, outs: JunoAST.ExprList) =
    BEGIN
      Repeat(BC.DECSP, inCnt);
      PopQIds(inouts.head, scp);
      PopQIds(outs.head, scp)
    END AfterProcCall;

  PROCEDURE Apply(outCnt, inoutCnt: CARDINAL;
      ins: JunoAST.ExprList; u: CARDINAL) =
  (* Assuming space has already been left for the OUT parameters and the INOUT
     parameters have been pushed on the stack, write code to assemble an
     &quot;APPLY&quot; expression with &quot;outCnt&quot; OUT parameters, &quot;inoutCnt&quot; INOUT
     parameters, and with IN parameters in &quot;ins&quot;. The generated code branches
     to &quot;u&quot; in the event that the APPLY bytecode instruction is undefined or
     if any of the expressions in &quot;ins&quot; is undefined. *)
    BEGIN
      ins.start := loc;
      (* Push APPLY arguments except the first *)
      ExprListTail(ins.head.next, u, level := outCnt + inoutCnt);
      (* Push 1st APPLY (closure) argument *)
      VAR expr := ins.head.expr; BEGIN
        IF Utils.AlwaysDefined(expr) THEN
          Expr(expr, u := NoLabel)
        ELSE
          VAR def, pop := NewLabel(); BEGIN
            Expr(expr, pop);
            Branch(def);
            SetLabelLoc(pop);
            Repeat(BC.DECSP, outCnt + inoutCnt + ins.size - 1);
            Branch(u);
            SetLabelLoc(def)
          END
        END
      END;
      ins.end := loc;
      (* Generate APPLY instruction *)
      PushByte(BC.APPLY);
      PushUShort(outCnt);
      PushUShort(inoutCnt);
      PushUShort(ins.size - 1);
      AddReference(u);
      (* On return from &quot;APPLY&quot;, adjust the stack pointer to account for the
         IN parameters in the run-time closure. *)
      PushByte(BC.CLDECSP)
    END Apply;

  PROCEDURE Expr(expr: JunoAST.Expr; u: CARDINAL) =
  (* Generate code at the current location in the code stream to place the
     value of &quot;expr&quot; on the top of the stack. Generate a branch to label &quot;u&quot;
     if any portion of the evaluation is undefined; in that case, the code
     must restore the stack to its original state before executing the
     instruction to branch to &quot;u&quot;. *)

    PROCEDURE UnaryFunc(f: JunoAST.BIUFunc; bc: JunoRT.ByteCode) =
      BEGIN
        Expr(f.e, u);
        PushByte(bc);
        IF u # NoLabel THEN
          AddReference(u)
	ELSE
	  (* this case can arise because the negation of a literal
             number is always defined *)
          StuffShort(loc, u);
          INC(loc, 2);
        END
      END UnaryFunc;

    PROCEDURE BinaryFunc(
        f: JunoAST.BIBFunc;
        bc: JunoRT.ByteCode;
        uArg := TRUE) =
      BEGIN
        Expr(f.e1, u);
        IF Utils.AlwaysDefined(f.e2) THEN
          Expr(f.e2, u := NoLabel);
          PushByte(bc);
          IF uArg THEN AddReference(u) END
        ELSE
          VAR popFirst, defined := NewLabel(); BEGIN
            Expr(f.e2, popFirst);
            Branch(defined);
            SetLabelLoc(popFirst);
            PushByte(BC.DECSP); PushByte(1);
            Branch(u);
            SetLabelLoc(defined);
            PushByte(bc);
            IF uArg THEN AddReference(u) END;
          END
        END
      END BinaryFunc;

    (* Expr *)
    BEGIN
      expr.start := loc;
      TYPECASE expr OF &lt;* NOWARN *&gt;
      | JunoAST.Number (e) =&gt;
          PushByte(BC.PUSHNUM);
          PushReal(e.val)
      | JunoAST.Nil =&gt;
          PushByte(BC.PUSHNIL)
      | JunoAST.Text (e) =&gt;
	  PushByte(BC.PUSHG);
	  PushULong(e.index)
      | JunoAST.QId (e) =&gt;
	  &lt;* ASSERT e.index # 0 *&gt;
	  CASE e.type OF &lt;* NOWARN *&gt;
	  | JunoAST.IdType.Local =&gt;
	      PushByte(BC.PUSHL);
	      PushShort(e.index)
	  | JunoAST.IdType.Const, JunoAST.IdType.Var =&gt;
	      PushByte(BC.PUSHG);
	      PushULong(e.index)
          | JunoAST.IdType.Proc =&gt;
              PushByte(BC.NEWCL);
              PushULong(e.index)
          | JunoAST.IdType.ExtProc =&gt;
              PushByte(BC.NEWEXTCL);
              PushULong(e.index)
	  END
      | JunoAST.GroupedExpr (e) =&gt; Expr(e.expr, u)
      | JunoAST.UMinus (e)  =&gt; UnaryFunc(e, BC.NEGATE)
      | JunoAST.Floor (e)   =&gt; UnaryFunc(e, BC.FLOOR_)
      | JunoAST.Ceiling (e) =&gt; UnaryFunc(e, BC.CEILING_)
      | JunoAST.Round (e)   =&gt; UnaryFunc(e, BC.ROUND_)
      | JunoAST.Abs (e)     =&gt; UnaryFunc(e, BC.ABS_)
      | JunoAST.Sin (e)     =&gt; UnaryFunc(e, BC.SIN)
      | JunoAST.Cos (e)     =&gt; UnaryFunc(e, BC.COS)
      | JunoAST.Exp (e)     =&gt; UnaryFunc(e, BC.EXP)
      | JunoAST.Ln (e)      =&gt; UnaryFunc(e, BC.LN)
      | JunoAST.Car (e)     =&gt; UnaryFunc(e, BC.CAR)
      | JunoAST.Cdr (e)     =&gt; UnaryFunc(e, BC.CDR)
      | JunoAST.Plus (e)    =&gt; BinaryFunc(e, BC.ADD)
      | JunoAST.Minus (e)   =&gt; BinaryFunc(e, BC.SUBTRACT)
      | JunoAST.Concat (e)  =&gt; BinaryFunc(e, BC.CONCAT)
      | JunoAST.Times (e)   =&gt; BinaryFunc(e, BC.MULTIPLY)
      | JunoAST.Divide (e)  =&gt; BinaryFunc(e, BC.DIVIDE)
      | JunoAST.Div (e)     =&gt; BinaryFunc(e, BC.DIV_)
      | JunoAST.Mod (e)     =&gt; BinaryFunc(e, BC.MOD_)
      | JunoAST.Rel (e)     =&gt; BinaryFunc(e, BC.REL)
      | JunoAST.Max (e)     =&gt; BinaryFunc(e, BC.MAX_)
      | JunoAST.Min (e)     =&gt; BinaryFunc(e, BC.MIN_)
      | JunoAST.Atan (e)    =&gt; BinaryFunc(e, BC.ATAN)
      | JunoAST.Pair (e)    =&gt; BinaryFunc(e, BC.CONS, uArg := FALSE)
      | JunoAST.List (e) =&gt;
          ExprList(e.elts, u);
          PushByte(BC.LIST);
          PushUShort(e.elts.size)
      | JunoAST.Call (c) =&gt;
          IF BuiltInSlots.IsCloseProc(c.name) THEN
            (* CLOSE(...) *)
            ExprList(c.ins, u);
            PushByte(BC.CLOSE);
            PushUShort(c.ins.size - 1);
            AddReference(u);
          ELSIF BuiltInSlots.IsApplyProc(c.name) THEN
            (* APPLY(...) *)
            OutInout(1, c.inouts);
            Apply(1, c.inouts.size, c.ins, u);
            (* After the call to APPLY, the condition bit is irrelevant, since
               calls to user-defined procedures never fail, and since the
               &quot;APPLY&quot; bytecode automatically signals a run-time error in the
               event that a call to an external procedure fails. *)
            AfterCall(c.ins.size - 1, c.inouts, uJump := FALSE)
          ELSE
            (* User-defined function, functional procedure, or functional
               external procedure *)
            OutInout(1, c.inouts);
            ExprList(c.ins, u, level := 1 + c.inouts.size);
            (* Generate CALL instruction *)
            VAR uJump: BOOLEAN; BEGIN
              CASE c.name.type OF &lt;* NOWARN *&gt;
              | JunoAST.IdType.Func =&gt;    PushByte(BC.CALL);    uJump := TRUE
              | JunoAST.IdType.Proc =&gt;    PushByte(BC.CALL);    uJump := FALSE
              | JunoAST.IdType.ExtProc =&gt; PushByte(BC.CALLEXT); uJump := FALSE
              END;
              PushULong(c.name.index);
              AfterCall(c.ins.size, c.inouts, uJump := uJump, label := u)
            END
          END
      END;
      expr.end := loc
    END Expr;

  PROCEDURE QIdList(l: JunoAST.ExprList) =
  (* Generates code to push the qualified identifiers in &quot;l&quot; onto the stack.
     Since the evaluation of a QId is never undefined, we need not generate
     code to restore the stack in the event of an undefined term. It is an
     unchecked runtime error for &quot;l&quot; to contain an expression &quot;e&quot; such that
     &quot;NOT Utils.AlwaysDefined(e)&quot;. *)
    VAR curr := l.head; BEGIN
      l.start := loc;
      WHILE curr # NIL DO
        Expr(curr.expr, u := NoLabel);
        curr := curr.next
      END;
      l.end := loc
    END QIdList;

  PROCEDURE ExprList(l: JunoAST.ExprList; u: CARDINAL; level: CARDINAL := 0) =
  (* Generate code to push the expressions in the list &quot;l&quot; onto the stack,
     branching to label &quot;u&quot; if any of them are undefined. In this case, the
     stack is restored to its original state (before the first expression of
     &quot;l&quot; was evaluated) and an additional &quot;level&quot; elements are then popped
     off the stack before branching to &quot;u&quot;. *)
    BEGIN
      l.start := loc;
      ExprListTail(l.head, u, level);
      l.end := loc
    END ExprList;

  PROCEDURE ExprListTail(
      l: JunoAST.ExprLink;
      u: CARDINAL;
      level: CARDINAL) =
  (* Generate code to push the expressions in the list &quot;l&quot; onto the stack,
     branching to label &quot;u&quot; if any are undefined. However, in this case, the
     stack is restored to its original state, and an additional &quot;level&quot;
     elements are popped from the stack before branching to &quot;u&quot;.

     We use the global &quot;labelsSeq&quot; as a stack of labels. We push a new label
     value for each expression so we know afterward how much to decrement the
     stack pointer for each expression. This procedure preserves the size of
     &quot;labelsSeq&quot;; we could add code to assert that here, but instead, we
     simply assert at the end of &quot;JunoAssemble.Cmd&quot; the the size of the
     sequence is 0. *)
    VAR newLabels := FALSE; pop: INTEGER; stackSz := 0; BEGIN
      (* Push expressions *)
      WHILE l # NIL DO
        IF Utils.AlwaysDefined(l.expr) THEN
          pop := NoLabel
        ELSIF level + stackSz = 0 THEN
          pop := u
        ELSE
          pop := NewLabel();
          newLabels := TRUE
        END;
        labelSeq.addhi(pop); INC(stackSz);
        Expr(l.expr, pop);
        l := l.next
      END;

      (* Generate code to pop (if necessary) *)
      IF newLabels THEN
        (* write branches for each new label *)
        VAR end := NewLabel(); BEGIN
          Branch(end);
          WHILE stackSz &gt; 0 DO
            pop := labelSeq.remhi(); DEC(stackSz);
            IF pop # NoLabel AND pop # u THEN
              SetLabelLoc(pop);
              Repeat(BC.DECSP, level + stackSz);
              Branch(u)
            END
          END;
          SetLabelLoc(end)
        END
      ELSE
        (* pop &quot;stackSz&quot; elements from &quot;labelSeq&quot; stack *)
        WHILE stackSz &gt; 0 DO
          pop := labelSeq.remhi();
          DEC(stackSz)
        END
      END
    END ExprListTail;

  PROCEDURE Cmd0(
      cmd: JunoAST.Cmd;
      h, g, u: CARDINAL;
      hBranch: BOOLEAN) =
  (* Generate code for &quot;cmd&quot; at the current location in the code stream.
     Generate a branch to label &quot;h&quot; on successful termination, to label
     &quot;g&quot; on a guard failure, and to label &quot;u&quot; on an &quot;undefined term&quot; run-time
     error. Only generate a branch to &quot;h&quot; at the end of the stream if
     &quot;hBranch&quot; is &quot;TRUE&quot;. *)
    BEGIN
      cmd.start := loc;
      TYPECASE cmd OF &lt;* NOWARN *&gt;
	JunoAST.Skip =&gt;
	  IF hBranch THEN Branch(h) END
      | JunoAST.Abort =&gt;
	  PushByte(BC.ERROR);
	  PushByte(ORD(JunoRTError.Code.Abort));
	  IF hBranch THEN Branch(h) END
      | JunoAST.Halt =&gt;
	  PushByte(BC.ERROR);
	  PushByte(ORD(JunoRTError.Code.Halt));
	  IF hBranch THEN Branch(h) END
      | JunoAST.Fail =&gt;
	  Branch(g)
      | JunoAST.Assign (c) =&gt;
          ExprList(c.exprs, u);
	  PopQIds(c.vars.head, scp);
	  IF hBranch THEN Branch(h) END
      | JunoAST.If (c) =&gt;
	  VAR l := NewLabel(); BEGIN
	    Cmd0(c.body, h, l, u, hBranch := TRUE);
	    SetLabelLoc(l);
	    PushByte(BC.ERROR);
	    PushByte(ORD(JunoRTError.Code.IfFailure))
	  END
      | JunoAST.Do (c) =&gt;
	  VAR l := NewLabel(); BEGIN
	    SetLabelLoc(l);
	    Cmd0(c.body, l, h, u, hBranch := TRUE)
	  END
      | JunoAST.Seq (c) =&gt;
          Cmd0Seq(c, h, g, u, hBranch)
      | JunoAST.Else (c) =&gt;
	  VAR l := NewLabel(); BEGIN
	    Cmd0(c.c1, h, l, u, hBranch := TRUE);
	    SetLabelLoc(l);
	    Cmd0(c.c2, h, g, u, hBranch)
	  END
      | JunoAST.GroupedCmd (c) =&gt;
	  Cmd0(c.body, h, g, u, hBranch)
      | JunoAST.Flip (c) =&gt;
	  Cmd0(c.body, g, h, u, hBranch := TRUE)
      | JunoAST.Safe (c) =&gt;
	  Cmd0(c.body, h, g, g, hBranch)
      | JunoAST.ProcCall (c) =&gt;
          IF BuiltInSlots.IsApplyProc(c.name) THEN
            (* APPLY(...) *)
            OutInout(c.outs.size, c.inouts);
            Apply(c.outs.size, c.inouts.size, c.ins, u);
            AfterProcCall(c.ins.size - 1, c.inouts, c.outs)
          ELSE
            (* procedure or external procedure command *)
            OutInout(c.outs.size, c.inouts);
            ExprList(c.ins, u, level := c.outs.size + c.inouts.size);
	    (* Generate CALL instruction *)
            CASE c.name.type OF &lt;* NOWARN *&gt;
            | JunoAST.IdType.Proc =&gt; PushByte(BC.CALL)
            | JunoAST.IdType.ExtProc =&gt; PushByte(BC.CALLEXT)
            END;
            PushULong(c.name.index);
            AfterProcCall(c.ins.size, c.inouts, c.outs)
          END;
          IF hBranch THEN Branch(h) END
      | JunoAST.Query (q) =&gt;
	  &lt;* ASSERT q.vars.size = 0 *&gt;
          (* Query of form: P?() *)
          Pred(q.f, h, g, tBranch := hBranch)
      | JunoAST.ConjQuery (q) =&gt;
	  (* Query of form: P?(v1,...,vn) (represented by arrays) *)
          IF NUMBER(q.conj^) &gt; 0 THEN	 (* skip &quot;(TRUE)?(v1,...,vn)&quot; *)
	    PushConjVars(q.var^);
	    PushConstraints(q.var^, q.conj^);
	    VAR l := NewLabel(); BEGIN
	      TBranch(l);
	      (* Skip over OUT vars if solve failed *)
	      Repeat(BC.DECSP, NUMBER(q.var^));
	      Branch(g);
	      SetLabelLoc(l);
	      (* Update INOUT's in &quot;q.vars&quot; *)
	      PopConjVars(q.var^)
            END
          END;
          IF hBranch THEN Branch(h) END
      END; (* TYPECASE *)
      cmd.end := loc
    END Cmd0;

  PROCEDURE Cmd0Seq(
      seq: JunoAST.Seq;
      h, g, u: CARDINAL;
      hBranch: BOOLEAN) =
  (* Equivalent to:
|
|      VAR l := NewLabel(); BEGIN
|        Cmd0(c.c1, l, g, u, hBranch := FALSE);
|        SetLabelLoc(l)
|      END;
|      Cmd0(c.c2, h, g, u, hBranch)
|
     except that fewer frames are used on the stack if &quot;seq&quot; is a
     long list of semi-colon-separated commands. *)
    VAR curr := seq; BEGIN
      (* Loop down list to generate code *)
      LOOP
        VAR l := NewLabel(); BEGIN
          Cmd0(curr.c1, l, g, u, hBranch := FALSE);
          SetLabelLoc(l);
        END;
        TYPECASE curr.c2 OF JunoAST.Seq (next) =&gt;
          next.start := loc; curr := next
        ELSE EXIT
        END
      END;
      Cmd0(curr.c2, h, g, u, hBranch);

      (* Now, loop a second time to set &quot;end&quot; locations *)
      curr := seq;
      LOOP
        TYPECASE curr.c2 OF JunoAST.Seq (next) =&gt;
          next.end := loc; curr := next
        ELSE EXIT
        END
      END
    END Cmd0Seq;

  (* Cmd *)
  BEGIN
    LOCK mu DO
      loc := 0;
      InitLabelTable();
      Repeat(BC.PUSHM3NIL, temp_cnt);
      VAR h := NewLabel(); err := NewLabel(); BEGIN
        CASE type OF
          CmdType.Pred, CmdType.Func =&gt;
            (* When the bodies of predicates and functions are compiled as
               commands, they become queries that either halt or fail. The
               procedure for a predicate must set the condition bit as the
               command halted or failed, to indicate if the predicate was true
               or not. The procedure for a function must also set the
               condition bit as the command halted or failed, to indicate if
               the function was defined or not. *)
            Cmd0(res.cmd, h, err, NoLabel, hBranch := FALSE);
            SetLabelLoc(h);
            PushByte(BC.C_ON);
            PushByte(BC.RET);
            SetLabelLoc(err);
            PushByte(BC.C_OFF);
            PushByte(BC.RET);
        | CmdType.Proc =&gt;
            (* User-defined procedures are total: they never fail. Hence, we
               don't need to set the condition bit at the end of the
               procedure. *)
            Cmd0(res.cmd, h, NoLabel, err, hBranch := FALSE);
            SetLabelLoc(h);
            PushByte(BC.RET);
            SetLabelLoc(err);
            PushByte(BC.ERROR);
            PushByte(ORD(JunoRTError.Code.UndefTerm))
        END
      END;
      res.cmd.end := loc;
      BackPatchReferences();
      &lt;* ASSERT labelSeq.size() = 0 *&gt;
      RETURN CodeCopy();
    END
  END Cmd;

PROCEDURE <A NAME="PushConjVars"><procedure>PushConjVars</procedure></A>(READONLY v: JunoAST.Vars) =
</PRE><BLOCKQUOTE><EM> Push the <CODE>frozen</CODE> variables in <CODE>v</CODE> to the stack; push <CODE>unfrozen</CODE> variables
   in <CODE>v</CODE> as <CODE>NIL</CODE>. To decrease the size of the output code, do run-length
   encoding on the <CODE>PUSHM3NIL</CODE>'s. </EM></BLOCKQUOTE><PRE>
  VAR i := FIRST(v); nilCnt := 0; BEGIN
    WHILE i &lt;= LAST(v) DO
      IF v[i].frozen THEN
        IF nilCnt &gt; 0 THEN Repeat(BC.PUSHM3NIL, nilCnt); nilCnt := 0 END;
        PushByte(BC.PUSHL);
        PushShort(v[i].index)
      ELSE
        INC(nilCnt)
      END;
      INC(i)
    END;
    IF nilCnt &gt; 0 THEN Repeat(BC.PUSHM3NIL, nilCnt) END
  END PushConjVars;

PROCEDURE <A NAME="PopConjVars"><procedure>PopConjVars</procedure></A>(READONLY v: JunoAST.Vars) =
</PRE><BLOCKQUOTE><EM> Pop the non-temporary variables in <CODE>vars</CODE> in reverse order; discard
   temporary variables. To decrease the size of the output code, do run-length
   encoding on the <CODE>DECSP</CODE>'s. </EM></BLOCKQUOTE><PRE>
  VAR i := LAST(v); tempCnt := 0; BEGIN
    WHILE i &gt;= FIRST(v) DO
      IF v[i].evar THEN
        INC(tempCnt)
      ELSE
        IF tempCnt &gt; 0 THEN Repeat(BC.DECSP, tempCnt); tempCnt := 0 END;
        &lt;* ASSERT v[i].index # 0 *&gt;
        PushByte(BC.POPL);
        PushShort(v[i].index)
      END;
      DEC(i)
    END;
    IF tempCnt &gt; 0 THEN Repeat(BC.DECSP, tempCnt) END
  END PopConjVars;

PROCEDURE <A NAME="PushConstraints"><procedure>PushConstraints</procedure></A>(
    READONLY v: JunoAST.Vars;
    READONLY c: JunoAST.Formulas) =
  VAR index := NUMBER(v); argNum := NEW(REF ARRAY OF Z3, NUMBER(c)); BEGIN
    FOR i := FIRST(argNum^) TO LAST(argNum^) DO
      ProcessLeaf1(c[i], argNum[i], v, index)
    END;
    PushByte(BC.SOLVE);
    PushUShort(NUMBER(v));		 (* # of variables (INOUTS) *)
    PushUShort(index - NUMBER(v));	 (* # of constants (INS)    *)
    PushUShort(NUMBER(c));		 (* # of constraints        *)
    FOR i := FIRST(argNum^) TO LAST(argNum^) DO
      ProcessLeaf2(c[i], argNum[i])
    END
  END PushConstraints;

PROCEDURE <A NAME="ProcessLeaf1"><procedure>ProcessLeaf1</procedure></A>(
    f: JunoAST.Formula;
    VAR (* OUT *) z3: Z3;
    READONLY inouts: JunoAST.Vars;
    VAR (* INOUT *) index: CARDINAL) =
</PRE><BLOCKQUOTE><EM> Set <CODE>z3</CODE> to the canonicalized form of <CODE>f</CODE>. Increment <CODE>index</CODE> by the number
   of knowns in <CODE>f</CODE>, where an <CODE>id</CODE> is known if it does not appear in <CODE>inouts</CODE>.
   For each such known, generate code to push its value. </EM></BLOCKQUOTE><PRE>
  VAR j: CARDINAL := 0;

  PROCEDURE Atom(e: JunoAST.AtomicExpr) =
    BEGIN
      TYPECASE e OF &lt;* NOWARN *&gt;
        JunoAST.LitValue (lit) =&gt;
          z3[j] := index;
          INC(index);
          TYPECASE lit OF &lt;* NOWARN *&gt;
            JunoAST.Text (t) =&gt;   PushByte(BC.PUSHG); PushULong(t.index)
          | JunoAST.Number (n) =&gt; PushByte(BC.PUSHNUM); PushReal(n.val)
          | JunoAST.Nil =&gt;        PushByte(BC.PUSHNIL)
          END
      | JunoAST.QId (qid) =&gt;
          CASE qid.type OF &lt;* NOWARN *&gt;
            JunoAST.IdType.Local =&gt;
	      VAR k := Utils.MemVars(qid, inouts); BEGIN
		IF k # -1 THEN	(* INOUT *)
		  z3[j] := k
		ELSE		(* IN Local var *)
		  z3[j] := index;
		  INC(index);
		  PushByte(BC.PUSHL);
		  PushShort(qid.index)
		END
	      END
          | JunoAST.IdType.Var, JunoAST.IdType.Const =&gt;
	      z3[j] := index;
	      INC(index);
	      PushByte(BC.PUSHG);
	      PushULong(qid.index)
          END
      END;
      INC(j)
    END Atom;
  BEGIN
    TYPECASE f OF &lt;* NOWARN *&gt;
      JunoAST.Equals (eq) =&gt;
        Atom(eq.e1);
        TYPECASE eq.e2 OF &lt;* NOWARN *&gt;
          JunoAST.AtomicExpr =&gt; Atom(eq.e2)
        | JunoAST.BIUFunc (e) =&gt; Atom(e.e)
        | JunoAST.BIBFunc (e) =&gt; Atom(e.e1); Atom(e.e2)
        END
    | JunoAST.BIUPred (e) =&gt; Atom(e.e)
    END
  END ProcessLeaf1;

PROCEDURE <A NAME="ProcessLeaf2"><procedure>ProcessLeaf2</procedure></A>(f: JunoAST.Formula; READONLY z3: Z3) =
  BEGIN
    TYPECASE f OF &lt;* NOWARN *&gt;
      JunoAST.Equals (eq) =&gt;
        TYPECASE eq.e2 OF &lt;* NOWARN *&gt;
        | JunoAST.Pair =&gt;
            PushByte(BC.CONS_C);
            PushUShort(z3[0]);
            PushUShort(z3[1]);
            PushUShort(z3[2]);
        | JunoAST.Plus =&gt;
            PushByte(BC.SUM_C);
            PushUShort(z3[0]);
            PushUShort(z3[1]);
            PushUShort(z3[2]);
        | JunoAST.Times =&gt;
            PushByte(BC.PROD_C);
            PushUShort(z3[0]);
            PushUShort(z3[1]);
            PushUShort(z3[2]);
        | JunoAST.Atan =&gt;
            PushByte(BC.ATAN_C);
            PushUShort(z3[0]);
            PushUShort(z3[1]);
            PushUShort(z3[2]);
        | JunoAST.AtomicExpr =&gt;
            PushByte(BC.EQUAL_C);
            PushUShort(z3[0]);
            PushUShort(z3[1]);
        | JunoAST.Sin =&gt;
            PushByte(BC.SIN_C);
            PushUShort(z3[0]);
            PushUShort(z3[1]);
        | JunoAST.Cos =&gt;
            PushByte(BC.COS_C);
            PushUShort(z3[0]);
            PushUShort(z3[1]);
        | JunoAST.Exp =&gt;
            PushByte(BC.EXP_C);
            PushUShort(z3[0]);
            PushUShort(z3[1]);
        END
    | JunoAST.IsReal =&gt;
        PushByte(BC.REAL_C);
        PushUShort(z3[0]);
    | JunoAST.IsText =&gt;
        PushByte(BC.TEXT_C);
        PushUShort(z3[0]);
    END
  END ProcessLeaf2;

PROCEDURE <A NAME="PopIds"><procedure>PopIds</procedure></A>(id: JunoAST.IdLink) =
</PRE><BLOCKQUOTE><EM> Generate code to pop the locals indexed in <CODE>ids</CODE> in reverse order. </EM></BLOCKQUOTE><PRE>
  BEGIN
    IF id # NIL THEN
      PopIds(id.next);
      &lt;* ASSERT id.index # 0 *&gt;
      PushByte(BC.POPL);
      PushShort(id.index)
    END
  END PopIds;

PROCEDURE <A NAME="PopQIds"><procedure>PopQIds</procedure></A>(v: JunoAST.ExprLink; scp: JunoScope.T) =
</PRE><BLOCKQUOTE><EM> The list <CODE>v</CODE> is a list of QId's. Generate code to pop the values on the
   stack into the locations designated by these QId's in reverse order. It is
   a checked run-time error for the type of any QId in <CODE>v</CODE> not to be a local
   or global variable. </EM></BLOCKQUOTE><PRE>
  BEGIN
    IF v # NIL THEN
      PopQIds(v.next, scp);
      VAR qid: JunoAST.QId := v.expr; BEGIN
        &lt;* ASSERT qid.index # 0 *&gt;
        CASE qid.type OF &lt;* NOWARN *&gt;
        | JunoAST.IdType.Local =&gt;
            PushByte(BC.POPL);
            PushShort(qid.index)
        | JunoAST.IdType.Var =&gt;
            PushByte(BC.POPG);
            PushULong(qid.index)
        END
      END
    END
  END PopQIds;

PROCEDURE <A NAME="Branch"><procedure>Branch</procedure></A>(lbl: CARDINAL) =
  BEGIN
    PushByte(BC.JUMP);
    AddReference(lbl);
  END Branch;

PROCEDURE <A NAME="TBranch"><procedure>TBranch</procedure></A>(lbl: CARDINAL) =
  BEGIN
    PushByte(BC.TJUMP);
    AddReference(lbl);
  END TBranch;

PROCEDURE <A NAME="FBranch"><procedure>FBranch</procedure></A>(lbl: CARDINAL) =
  BEGIN
    PushByte(BC.FJUMP);
    AddReference(lbl);
  END FBranch;

PROCEDURE <A NAME="PushByte"><procedure>PushByte</procedure></A>(byte: JunoRT.ByteCode) =
  BEGIN
    IF loc &gt; LAST(code^) THEN
      VAR new := NEW(JunoRT.ByteStream, 2 * NUMBER(code^)); BEGIN
        SUBARRAY(new^, 0, NUMBER(code^)) := code^;
        code := new
      END
    END;
    code[loc] := byte;
    INC(loc)
  END PushByte;

&lt;* INLINE *&gt;
PROCEDURE <A NAME="PushUShort"><procedure>PushUShort</procedure></A>(i: CARDINAL) =
  BEGIN WriteVal.UShort(code, loc, i) END PushUShort;

&lt;* INLINE *&gt;
PROCEDURE <A NAME="PushShort"><procedure>PushShort</procedure></A>(i: INTEGER) =
  BEGIN WriteVal.Short(code, loc, i) END PushShort;

&lt;* INLINE *&gt;
PROCEDURE <A NAME="PushULong"><procedure>PushULong</procedure></A>(i: CARDINAL) =
  BEGIN WriteVal.ULong(code, loc, i) END PushULong;

&lt;* INLINE *&gt;
PROCEDURE <A NAME="PushReal"><procedure>PushReal</procedure></A>(r: JunoValue.Real) =
  BEGIN WriteVal.Real(code, loc, r) END PushReal;

PROCEDURE <A NAME="Repeat"><procedure>Repeat</procedure></A>(instruction: JunoRT.ByteCode; cnt: CARDINAL) =
  BEGIN
    WHILE cnt &gt; 0 DO
      PushByte(instruction);
      WITH num = MIN(cnt, 255) DO
        PushByte(num);
        DEC(cnt, num)
      END
    END
  END Repeat;

&lt;* INLINE *&gt;
PROCEDURE <A NAME="StuffShort"><procedure>StuffShort</procedure></A>(at: CARDINAL; val: INTEGER) =
</PRE><BLOCKQUOTE><EM> Stuffs the 2-byte value <CODE>val</CODE> at locations <CODE>code[at..at+1]</CODE>. </EM></BLOCKQUOTE><PRE>
  BEGIN WriteVal.Short(code, at, val) END StuffShort;
</PRE> Label Procedures ------------------------------------------------------- 

<P><PRE>CONST FirstLabel = 1; (* the number of the first valid label *)

VAR
  last_label: INTEGER; (* the most-recently granted valid label *)
  labels := NEW(REF ARRAY OF LabelData, 500);

&lt;* INLINE *&gt;
PROCEDURE <A NAME="InitLabelTable"><procedure>InitLabelTable</procedure></A>() =
</PRE><BLOCKQUOTE><EM> Clears the label table and resets the last label number. </EM></BLOCKQUOTE><PRE>
  BEGIN last_label := FirstLabel - 1 END InitLabelTable;

PROCEDURE <A NAME="NewLabel"><procedure>NewLabel</procedure></A>(): CARDINAL =
</PRE><BLOCKQUOTE><EM> Returns a new, unused label. A new label has a <CODE>loc</CODE> of -1 and an empty
   references list. All valid label numbers are strictly positive. </EM></BLOCKQUOTE><PRE>
  BEGIN
    INC(last_label);
    IF last_label &gt;= NUMBER(labels^) THEN
      VAR newLabels := NEW(REF ARRAY OF LabelData, 2 * NUMBER(labels^)); BEGIN
        SUBARRAY(newLabels^, 0, NUMBER(labels^)) := labels^;
        labels := newLabels
      END
    END;
    labels[last_label].loc := -1;
    labels[last_label].refs := NIL;
    RETURN last_label
  END NewLabel;

&lt;* INLINE *&gt;
PROCEDURE <A NAME="SetLabelLoc"><procedure>SetLabelLoc</procedure></A>(lbl: CARDINAL) =
</PRE><BLOCKQUOTE><EM> Assert that label <CODE>lbl</CODE> should be associated with index <CODE>loc</CODE> in the output
   <CODE>code</CODE>. It is either a checked or an unchecked run-time error to set the
   label location of a label that was not obtained by a call to NewLabel(). </EM></BLOCKQUOTE><PRE>
  BEGIN
    &lt;* ASSERT lbl &gt;= FirstLabel *&gt;
    labels[lbl].loc := loc;
  END SetLabelLoc;

&lt;* INLINE *&gt;
PROCEDURE <A NAME="AddReference"><procedure>AddReference</procedure></A>(lbl: CARDINAL) =
</PRE><BLOCKQUOTE><EM> Assert that the bytes at locations <CODE>code[loc..loc+1]</CODE> should eventually be
   back-patched to be relative offsets to label <CODE>lbl</CODE>, and increment <CODE>loc</CODE>
   past these bytes. </EM></BLOCKQUOTE><PRE>
  BEGIN
    &lt;* ASSERT lbl &gt;= FirstLabel *&gt;
    WITH data = labels[lbl] DO
      data.refs := NEW(IntList, val := loc, next := data.refs)
    END;
    INC(loc, 2);
  END AddReference;

PROCEDURE <A NAME="BackPatchReferences"><procedure>BackPatchReferences</procedure></A>() =
</PRE><BLOCKQUOTE><EM> Backpatch the 2-bytes in <CODE>code</CODE> whose indices are stored in the lists
   <CODE>labels[FirstLabel..last_label].refs</CODE> to contain relative offsets to the
   corresponding label locations <CODE>labels[FirstLabel..last_label].loc</CODE>. </EM></BLOCKQUOTE><PRE>
  BEGIN
    FOR lbl := FirstLabel TO last_label DO
      VAR p := labels[lbl].refs; BEGIN
        WHILE p # NIL DO
          StuffShort(p.val, labels[lbl].loc - (p.val + 2));
          p := p.next
        END
      END
    END
  END BackPatchReferences;

PROCEDURE <A NAME="CodeCopy"><procedure>CodeCopy</procedure></A>(): JunoRT.ByteStream =
</PRE><BLOCKQUOTE><EM> Return a copy of <CODE>code[0..loc-1]</CODE>. </EM></BLOCKQUOTE><PRE>
  VAR res := NEW(JunoRT.ByteStream, loc); BEGIN
    res^ := SUBARRAY(code^, 0, loc);
    RETURN res
  END CodeCopy;

BEGIN END JunoAssemble.
</PRE>
</inModule>
<HR>
<A NAME="x1">interface JunoValue is in:
</A><UL>
<LI><A HREF="../../juno-machine/src/JunoValue.i3.html#0TOP0">juno-machine/src/JunoValue.i3</A>
<LI><A HREF="../../pkl-fonts/src/JunoValue.i3.html#0TOP0">pkl-fonts/src/JunoValue.i3</A>
</UL>
<P>
<PRE>























</PRE>
</BODY>
</HTML>
