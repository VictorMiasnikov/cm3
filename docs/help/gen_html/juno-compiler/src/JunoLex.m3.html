<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: juno-compiler/src/JunoLex.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>juno-compiler/src/JunoLex.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright (C) 1992, Digital Equipment Corporation                         
 All rights reserved.                                                      
 See the file COPYRIGHT for a full description.                            
                                                                           
 Last modified on Fri Mar 17 14:27:29 PST 1995 by heydon                   
      modified on Wed Feb 15 16:27:30 PST 1995 by gnelson                  
      modified on Fri Aug  7 21:54:04 PDT 1992 by myers                    

<P><PRE>MODULE <module><implements><A HREF="JunoLex.i3.html">JunoLex</A></implements></module>;

IMPORT <A HREF="#x1">JunoValue</A>;
IMPORT <A HREF="JunoToken.i3.html">JunoToken</A>;
IMPORT <A HREF="../../libm3/src/atom/Atom.i3.html">Atom</A>, <A HREF="../../libm3/src/rw/Rd.i3.html">Rd</A>, <A HREF="../../libm3/src/fmtlex/Lex.i3.html">Lex</A>, <A HREF="../../m3core/src/float/IEEE-default/FloatMode.i3.html">FloatMode</A>, <A HREF="../../m3core/src/text/Text.i3.html">Text</A>, <A HREF="../../m3core/src/thread/Common/Thread.i3.html">Thread</A>;

&lt;* FATAL Thread.Alerted *&gt;
</PRE> The procedures in this module that raise <CODE>Rd.Failure</CODE> do so when they
   encounter a problem reading from their <CODE>Stream</CODE> argument. 

<P><PRE>REVEAL
  <A NAME="Private">Private</A> = MUTEX BRANDED &quot;JunoLex.Private&quot; OBJECT
    c: CHAR;				 (* next character *)
    rd: Rd.T;                            (* input stream *)
    token: JunoToken.T;			 (* most recent token *)
    buff: REF ARRAY OF CHAR;		 (* character buffer *)
    sz: CARDINAL;			 (* buffer size *)
  END;

  <A NAME="Stream">Stream</A> = Public BRANDED &quot;JunoLex.Stream&quot; OBJECT OVERRIDES
    next := Next;
  END;
</PRE> Conceptually, a Stream <CODE>s</CODE> contains a stream of characters that can be
   thought of as a text. This implementation maintains the following
   invariant:
<P>
<PRE>
         The character &quot;s.c&quot; concatenated with the characters remaining on
         &quot;s.rd&quot; yields the stream of tokens that have yet to be returned by a
         call to &quot;s.next()&quot;.
</PRE>
   Hence, <CODE>s.c</CODE> is a one-character look-ahead buffer. In the case where the
   previous token ended with the last character in <CODE>s.rd</CODE>, <CODE>s.c</CODE> is set to
   the special value ' ' (space). 

<P><PRE>CONST InitBuffSize = 40; Space = ' ';

PROCEDURE <A NAME="New"><procedure>New</procedure></A>(rd: Rd.T): Stream RAISES {Rd.Failure} =
  &lt;* FATAL Rd.EndOfFile *&gt;
  VAR result := NEW(Stream, c := Space, rd := rd, sz := InitBuffSize); BEGIN
    result.token := NEW(JunoToken.T);
    result.buff := NEW(REF ARRAY OF CHAR, result.sz);
    IF NOT Rd.EOF(result.rd) THEN result.c := Rd.GetChar(result.rd) END;
    RETURN result
  END New;

PROCEDURE <A NAME="Close"><procedure>Close</procedure></A>(s: Stream): TEXT =
  BEGIN
    RETURN Text.FromChar(s.c)
  END Close;

PROCEDURE <A NAME="Next"><procedure>Next</procedure></A>(s: Stream): JunoToken.T RAISES {Error, Rd.Failure} =
  BEGIN
    LOCK s DO
      TRY GetNextToken(s) EXCEPT
        Rd.EndOfFile =&gt; s.token.kind := JunoToken.Kind.EndMarker
      END;
      RETURN s.token
    END
  END Next;

PROCEDURE <A NAME="GetNextToken"><procedure>GetNextToken</procedure></A>(s: Stream) RAISES {Error, Rd.EndOfFile, Rd.Failure} =
</PRE><BLOCKQUOTE><EM> Assuming <CODE>s</CODE> is not a closed stream, read the next token from <CODE>s</CODE> after
   skipping any initial whitespace. Raises Error(ErrorKind.BadInitialChar) if
   the first character appearing after any whitespace and nested comments is
   an illegal starting character for a token; this exception may also be
   raised by procedures called by this procedure. Raises Rd.EndOfFile if no
   token was found on <CODE>s</CODE> after any initial whitespace. </EM></BLOCKQUOTE><PRE>

  PROCEDURE GetReal() RAISES {Error, Rd.Failure} =
  (* Read a real number, and store its kind and value in &quot;s.token&quot;. *)
    BEGIN
      s.token.kind := JunoToken.Kind.LitReal;
      s.token.num := FLOAT(ReadReal(s), JunoValue.Real)
    END GetReal;

  (* GetNextToken *)
  BEGIN
    (* skip whitespace *)
    WHILE s.c IN Lex.Blanks DO s.c := Rd.GetChar(s.rd) END;
    s.lastPos := Rd.Index(s.rd) - 1;

    TRY
      CASE s.c OF
      | '0'..'9' =&gt;                      (* numeric literal *)
          GetReal()
      | '.' =&gt;				 (* &quot;.&quot; operator *)
          TRY s.c := Rd.GetChar(s.rd) EXCEPT
            Rd.EndOfFile =&gt;
              s.token.kind := JunoToken.Kind.Dot; RAISE Rd.EndOfFile
          END;
          IF '0' &lt;= s.c AND s.c &lt;= '9' THEN
            Rd.UnGetChar(s.rd); GetReal()
          ELSE
            s.token.kind := JunoToken.Kind.Dot
          END
      | '\&quot;' =&gt;                          (* text literal *)
          s.token.kind := JunoToken.Kind.LitText;
          s.token.val := ReadText(s);
          s.c := Rd.GetChar(s.rd)
      | 'a'..'z', 'A'..'Z', '_' =&gt;       (* identifier or keyword *)
          s.token.kind := ReadId(s, s.token.val)
      | ';' =&gt;				 (* &quot;;&quot; operator *)
          s.token.kind := JunoToken.Kind.Semi;
          s.c := Rd.GetChar(s.rd)
      | ',' =&gt;				 (* &quot;,&quot; operator *)
          s.token.kind := JunoToken.Kind.Comma;
          s.c := Rd.GetChar(s.rd)
      | ')' =&gt;				 (* &quot;)&quot; operator *)
          s.token.kind := JunoToken.Kind.RPren;
          s.c := Rd.GetChar(s.rd)
      | '[' =&gt;				 (* &quot;[&quot; operator *)
          s.token.kind := JunoToken.Kind.LBracket;
          s.c := Rd.GetChar(s.rd)
      | ']' =&gt;				 (* &quot;]&quot; operator *)
          s.token.kind := JunoToken.Kind.RBracket;
          s.c := Rd.GetChar(s.rd)
      | '{' =&gt;				 (* &quot;{&quot; operator *)
          s.token.kind := JunoToken.Kind.LBrace;
          s.c := Rd.GetChar(s.rd)
      | '}' =&gt;				 (* &quot;}&quot; operator *)
          s.token.kind := JunoToken.Kind.RBrace;
          s.c := Rd.GetChar(s.rd)
      | '|' =&gt;				 (* &quot;|&quot; operator *)
          s.token.kind := JunoToken.Kind.Else;
          s.c := Rd.GetChar(s.rd)
      | '~' =&gt;				 (* &quot;~&quot; operator *)
          s.token.kind := JunoToken.Kind.Near;
          s.c := Rd.GetChar(s.rd)
      | '=' =&gt;				 (* &quot;=&quot; operator *)
          s.token.kind := JunoToken.Kind.Equals;
          s.c := Rd.GetChar(s.rd)
      | '#' =&gt;				 (* &quot;#&quot; operator *)
          s.token.kind := JunoToken.Kind.Differs;
          s.c := Rd.GetChar(s.rd)
      | '+' =&gt;				 (* &quot;+&quot; operator *)
          s.token.kind := JunoToken.Kind.Plus;
          s.c := Rd.GetChar(s.rd)
      | '*' =&gt;				 (* &quot;*&quot; operator *)
          s.token.kind := JunoToken.Kind.Times;
          s.c := Rd.GetChar(s.rd)
      | '&amp;' =&gt;				 (* &quot;&amp;&quot; operator *)
          s.token.kind := JunoToken.Kind.Concat;
          s.c := Rd.GetChar(s.rd)
      | '/' =&gt;				 (* &quot;/&quot; operator *)
          TRY s.c := Rd.GetChar(s.rd) EXCEPT
            Rd.EndOfFile =&gt;
              s.token.kind := JunoToken.Kind.Divide; RAISE Rd.EndOfFile
          END;
          IF s.c = '*' THEN
            s.token.kind := JunoToken.Kind.Comment;
            s.token.val := ReadComment(s, private := TRUE);
            s.c := Rd.GetChar(s.rd)
          ELSE
            s.token.kind := JunoToken.Kind.Divide
          END
      | '(' =&gt;				 (* &quot;(&quot; operator or comment-start *)
          TRY s.c := Rd.GetChar(s.rd) EXCEPT
            Rd.EndOfFile =&gt;
              s.token.kind := JunoToken.Kind.LPren; RAISE Rd.EndOfFile
          END;
          IF s.c = '*' THEN
            s.token.kind := JunoToken.Kind.Comment;
            s.token.val := ReadComment(s, private := FALSE);
            s.c := Rd.GetChar(s.rd)
          ELSE
            s.token.kind := JunoToken.Kind.LPren
          END
      | ':' =&gt;				 (* &quot;:&quot; or &quot;:=&quot; operator *)
          TRY s.c := Rd.GetChar(s.rd) EXCEPT
            Rd.EndOfFile =&gt;
              s.token.kind := JunoToken.Kind.Colon; RAISE Rd.EndOfFile
          END;
          IF s.c = '=' THEN
            s.token.kind := JunoToken.Kind.Assign;
            s.c := Rd.GetChar(s.rd)
          ELSIF s.c = ':' THEN
            s.token.kind := JunoToken.Kind.SuchThat;
            s.c := Rd.GetChar(s.rd)
          ELSE
            s.token.kind := JunoToken.Kind.Colon;
          END
      | '-' =&gt;				 (* &quot;-&quot; or &quot;-&gt;&quot; operator *)
          TRY s.c := Rd.GetChar(s.rd) EXCEPT
            Rd.EndOfFile =&gt;
              s.token.kind := JunoToken.Kind.Minus; RAISE Rd.EndOfFile
          END;
          IF s.c = '&gt;' THEN
            s.token.kind := JunoToken.Kind.Guard;
            s.c := Rd.GetChar(s.rd)
          ELSE
            s.token.kind := JunoToken.Kind.Minus;
          END
      | '&lt;' =&gt;				 (* &quot;&lt;&quot; or &quot;&lt;=&quot; operator *)
          TRY s.c := Rd.GetChar(s.rd) EXCEPT
            Rd.EndOfFile =&gt;
              s.token.kind := JunoToken.Kind.Less; RAISE Rd.EndOfFile
          END;
          IF s.c = '=' THEN
            s.token.kind := JunoToken.Kind.AtMost;
            s.c := Rd.GetChar(s.rd)
          ELSE
            s.token.kind := JunoToken.Kind.Less;
          END
      | '&gt;' =&gt;				 (* &quot;&gt;&quot; or &quot;&gt;=&quot; operator *)
          TRY s.c := Rd.GetChar(s.rd) EXCEPT
            Rd.EndOfFile =&gt;
              s.token.kind := JunoToken.Kind.Greater; RAISE Rd.EndOfFile
          END;
          IF s.c = '=' THEN
            s.token.kind := JunoToken.Kind.AtLeast;
            s.c := Rd.GetChar(s.rd)
          ELSE
            s.token.kind := JunoToken.Kind.Greater;
          END
      ELSE
	  Rd.UnGetChar(s.rd);
	  RaiseError(ErrorKind.BadInitialChar, &quot;&quot;)
      END
    EXCEPT
      Rd.EndOfFile =&gt; s.c := Space (* EOF encountered after a legal token *)
    END
  END GetNextToken;

PROCEDURE <A NAME="FlushBuf"><procedure>FlushBuf</procedure></A>(
    prefix: TEXT;
    READONLY buf: ARRAY OF CHAR;
    VAR (*INOUT*) pos: CARDINAL)
  : TEXT =
</PRE><BLOCKQUOTE><EM> Return <CODE>Text.FromChars(SUBARRAY(buf, 0, pos))</CODE> appended to <CODE>prefix</CODE>, and
   set <CODE>pos</CODE> to 0. </EM></BLOCKQUOTE><PRE>
  VAR txt := Text.FromChars(SUBARRAY(buf, 0, pos)); res: TEXT; BEGIN
    IF prefix = NIL
      THEN res := txt
      ELSE res := prefix &amp; txt
    END;
    pos := 0;
    RETURN res
  END FlushBuf;

CONST BufSize = 100;
TYPE Buffer = ARRAY [0..BufSize-1] OF CHAR;

PROCEDURE <A NAME="ReadText"><procedure>ReadText</procedure></A>(s: Stream): TEXT RAISES {Error, Rd.Failure} =
</PRE><BLOCKQUOTE><EM> Assuming <CODE>s.c</CODE> is the opening double-quote of a text string, return the
   text string on <CODE>s</CODE>, processing all escape Modula-3 character escape
   sequences.
<P>
   Raises Error(ErrorKind.UnclosedText) if end-of-file occurs before the
   terminating double-quote; RAISES Error(ErrorKind.BadEscapeChar) if an
   illegal escape suffix follows the escape character <CODE>\</CODE> in the literal.
<P>
   Note: Since a text may be followed by end-of-file, and since we must
   return a valid result in that case, this procedure does *not* maintain the
   invariant that <CODE>s.c</CODE> contains the next unprocessed character; that
   character will be the first character on <CODE>s.rd</CODE>. </EM></BLOCKQUOTE><PRE>
  VAR buf: Buffer; pos: CARDINAL := 0; res := &quot;&quot;;

  &lt;* INLINE *&gt;
  PROCEDURE AppendChar(c: CHAR) =
    BEGIN
      IF pos = BufSize THEN res := FlushBuf(res, buf, pos) END;
      buf[pos] := c; INC(pos)
    END AppendChar;

  PROCEDURE ReadOctEscape(VAR (*INOUT*) pos: CARDINAL): CHAR
    RAISES {Rd.EndOfFile, Rd.Failure} =
  (* Requires &quot;s.c&quot; is one of the characters '0'..'7'. *)
    CONST OctDigits = SET OF CHAR{'0'..'7'}; Zero = ORD('0');
    VAR val := ORD(s.c) - Zero; BEGIN
      buf[pos] := s.c; INC(pos);
      FOR i := 1 TO 2 DO
        s.c := Rd.GetChar(s.rd);
        IF NOT s.c IN OctDigits THEN
          Rd.UnGetChar(s.rd); RAISE Rd.EndOfFile
        END;
        buf[pos] := s.c; INC(pos);
        val := (val * 8) + (ORD(s.c) - Zero)
      END;
      IF val &gt; ORD(LAST(CHAR)) THEN RAISE Rd.EndOfFile END;
      RETURN VAL(val, CHAR)
    END ReadOctEscape;

  PROCEDURE ReadEscape(): CHAR RAISES {Error, Rd.Failure} =
    VAR escPos: CARDINAL; escChar: CHAR; BEGIN
      (* guarantee that buffer will not be flushed in this procedure *)
      IF pos + 4 &gt;= BufSize THEN res := FlushBuf(res, buf, pos) END;
      escPos := pos;
      buf[escPos] := s.c; INC(escPos);	 (* save '\\' *)
      TRY
      	s.c := Rd.GetChar(s.rd);
      	CASE s.c OF
      	| 'n'  =&gt; escChar := '\n'
      	| 't'  =&gt; escChar := '\t'
      	| 'r'  =&gt; escChar := '\r'
      	| 'f'  =&gt; escChar := '\f'
      	| '\\' =&gt; escChar := '\\'
      	| '\&quot;' =&gt; escChar := '\&quot;'
      	| '0'..'7' =&gt; escChar := ReadOctEscape(escPos)
      	ELSE Rd.UnGetChar(s.rd); RAISE Rd.EndOfFile
      	END
      EXCEPT
        Rd.EndOfFile =&gt;
          res := FlushBuf(res, buf, escPos);
          RaiseError(ErrorKind.BadEscapeChar, &quot;\&quot;&quot; &amp; res)
      END;
      RETURN escChar
    END ReadEscape;

  (* ReadText *)
  BEGIN
    TRY
      TRY
      	LOOP
          s.c := Rd.GetChar(s.rd);
      	  CASE s.c OF
      	  | '\\' =&gt; AppendChar(ReadEscape())
      	  | '\&quot;' =&gt; EXIT
      	  ELSE      AppendChar(s.c)
      	  END;
      	END
      FINALLY
        res := FlushBuf(res, buf, pos)
      END
    EXCEPT
      Rd.EndOfFile =&gt; RaiseError(ErrorKind.UnclosedText, &quot;\&quot;&quot; &amp; res)
    END;
    RETURN res
  END ReadText;

PROCEDURE <A NAME="PrefixChars"><procedure>PrefixChars</procedure></A>(rd: Rd.T; start: CARDINAL): TEXT RAISES {Rd.Failure} =
  &lt;* FATAL Rd.EndOfFile *&gt;
  VAR curr := Rd.Index(rd); buf := NEW(REF ARRAY OF CHAR, curr - start); BEGIN
    Rd.Seek(rd, start);
    FOR i := 0 TO curr - start - 1 DO buf[i] := Rd.GetChar(rd) END;
    RETURN Text.FromChars(buf^)
  END PrefixChars;

PROCEDURE <A NAME="ReadReal"><procedure>ReadReal</procedure></A>(s: Stream): REAL RAISES {Error, Rd.Failure} =
</PRE><BLOCKQUOTE><EM> This procedure assumes that <CODE>s.rd</CODE> is positioned one character *past* the
   first character of the real number. </EM></BLOCKQUOTE><PRE>
  VAR res: REAL; start: CARDINAL; BEGIN
    start := Rd.Index(s.rd) - 1;
    Rd.Seek(s.rd, start);
    TRY
      res := Lex.Real(s.rd);
      s.c := Rd.GetChar(s.rd)
    EXCEPT
      Lex.Error, FloatMode.Trap =&gt;
        RaiseError(ErrorKind.BadReal, PrefixChars(s.rd, start))
    | Rd.EndOfFile =&gt; s.c := Space
    END;
    RETURN res
  END ReadReal;

PROCEDURE <A NAME="ReadId"><procedure>ReadId</procedure></A>(s: Stream; VAR (*OUT*) id: REFANY): JunoToken.Kind
    RAISES {Rd.Failure} =
</PRE><BLOCKQUOTE><EM> Parse the identifier appearing on <CODE>s</CODE>. If it is a true identifier, return
   <CODE>JunoToken.Kind.Id</CODE>, and set <CODE>id</CODE> to the <CODE>Atom.T</CODE> corresponding to the
   parsed identifier. Otherwise, it must be a keyword or a reserved
   identifier; in that case, return the proper JunoToken.Kind and leave <CODE>id</CODE>
   unchanged. </EM></BLOCKQUOTE><PRE>
  VAR sz: CARDINAL := 0;

  PROCEDURE DoubleSBuf() =
  (* Double the size of &quot;s.buff&quot;. *)
    VAR newSz := s.sz * 2; buff := NEW(REF ARRAY OF CHAR, newSz); BEGIN
      SUBARRAY(buff^, 0, sz) := SUBARRAY(s.buff^, 0, sz);
      s.buff := buff; s.sz := newSz;
    END DoubleSBuf;

  PROCEDURE ReadIdToBuf() RAISES {Rd.Failure} =
  (* Read identifier off &quot;s&quot; into &quot;s.buff&quot;. *)
    CONST IdChars = SET OF CHAR{'a'..'z', 'A'..'Z', '0'..'9', '_'}; BEGIN
      TRY
    	WHILE s.c IN IdChars DO
    	  IF sz = s.sz THEN DoubleSBuf() END;
    	  s.buff[sz] := s.c; INC(sz);
    	  s.c := Rd.GetChar(s.rd)
    	END
      EXCEPT
    	Rd.EndOfFile =&gt; s.c := Space
      END
    END ReadIdToBuf;

  PROCEDURE MatchTail(tail: TEXT): BOOLEAN =
  (* Return &quot;TRUE&quot; iff the characters in &quot;s.buff[1..sz-1]&quot; match the text
     &quot;Text.Sub(tail, 1)&quot;. *)
    CONST Start = 1; BEGIN
      IF sz # Text.Length(tail) THEN RETURN FALSE END;
      WITH buf = s.buff DO
      	FOR i := Start TO sz - 1 DO
	  IF buf[i] # Text.GetChar(tail, i) THEN RETURN FALSE END
      	END
      END;
      RETURN TRUE
    END MatchTail;

  PROCEDURE Keyword(): JunoToken.Kind =
  (* If the characters &quot;SUBARRAY(s.buff^, 0, sz)&quot; are a keyword, then return
     the corresponding token kind. Otherwise, return &quot;JunoToken.Kind.Id&quot;. *)
    BEGIN
      CASE s.buff[0] OF
      | 'A' =&gt;
    	IF MatchTail(&quot;AND&quot;) THEN RETURN JunoToken.Kind.And
    	ELSIF MatchTail(&quot;ABS&quot;) THEN RETURN JunoToken.Kind.Abs
    	ELSIF MatchTail(&quot;ATAN&quot;) THEN RETURN JunoToken.Kind.Atan
    	ELSIF MatchTail(&quot;ABORT&quot;) THEN RETURN JunoToken.Kind.Abort
    	END
      | 'C' =&gt;
    	IF MatchTail(&quot;CAR&quot;) THEN RETURN JunoToken.Kind.Car
    	ELSIF MatchTail(&quot;CDR&quot;) THEN RETURN JunoToken.Kind.Cdr
    	ELSIF MatchTail(&quot;CONG&quot;) THEN RETURN JunoToken.Kind.Cong
    	ELSIF MatchTail(&quot;CONST&quot;) THEN RETURN JunoToken.Kind.Const
    	ELSIF MatchTail(&quot;COS&quot;) THEN RETURN JunoToken.Kind.Cos
    	ELSIF MatchTail(&quot;CEILING&quot;) THEN RETURN JunoToken.Kind.Ceiling
    	END
      | 'D' =&gt;
    	IF MatchTail(&quot;DO&quot;) THEN RETURN JunoToken.Kind.Do
    	ELSIF MatchTail(&quot;DIV&quot;) THEN RETURN JunoToken.Kind.Div
    	END
      | 'E' =&gt;
    	IF sz = 1 THEN RETURN JunoToken.Kind.Exists
    	ELSIF MatchTail(&quot;END&quot;) THEN RETURN JunoToken.Kind.End
    	ELSIF MatchTail(&quot;EXP&quot;) THEN RETURN JunoToken.Kind.Exp
    	END
      | 'F' =&gt;
    	IF MatchTail(&quot;FI&quot;) THEN RETURN JunoToken.Kind.Fi
    	ELSIF MatchTail(&quot;FALSE&quot;) THEN RETURN JunoToken.Kind.False
    	ELSIF MatchTail(&quot;FLOOR&quot;) THEN RETURN JunoToken.Kind.Floor
    	ELSIF MatchTail(&quot;FUNC&quot;) THEN RETURN JunoToken.Kind.Func
    	END
      | 'H' =&gt;
    	IF MatchTail(&quot;HOR&quot;) THEN RETURN JunoToken.Kind.Hor END
      | 'I' =&gt;
    	IF MatchTail(&quot;IF&quot;) THEN RETURN JunoToken.Kind.If
    	ELSIF MatchTail(&quot;IN&quot;) THEN RETURN JunoToken.Kind.In
    	ELSIF MatchTail(&quot;IS&quot;) THEN RETURN JunoToken.Kind.Is
    	ELSIF MatchTail(&quot;INT&quot;) THEN RETURN JunoToken.Kind.Int
    	ELSIF MatchTail(&quot;IMPORT&quot;) THEN RETURN JunoToken.Kind.Import
    	END
      | 'L' =&gt;
    	IF sz = 2 AND s.buff[1] = 'N' THEN RETURN JunoToken.Kind.Ln END
      | 'M' =&gt;
    	IF MatchTail(&quot;MAX&quot;) THEN RETURN JunoToken.Kind.Max
    	ELSIF MatchTail(&quot;MIN&quot;) THEN RETURN JunoToken.Kind.Min
    	ELSIF MatchTail(&quot;MOD&quot;) THEN RETURN JunoToken.Kind.Mod
    	ELSIF MatchTail(&quot;MODULE&quot;) THEN RETURN JunoToken.Kind.Module
    	END
      | 'N' =&gt;
        IF sz = 3 THEN
    	  IF MatchTail(&quot;NOT&quot;) THEN RETURN JunoToken.Kind.Not
    	  ELSIF MatchTail(&quot;NIL&quot;) THEN RETURN JunoToken.Kind.Nil
    	  END
        END
      | 'O' =&gt;
    	IF sz = 2 THEN
    	  IF s.buff[1] = 'D' THEN RETURN JunoToken.Kind.Od
    	  ELSIF s.buff[1] = 'R' THEN RETURN JunoToken.Kind.Or END
    	END
      | 'P' =&gt;
    	IF MatchTail(&quot;PARA&quot;) THEN RETURN JunoToken.Kind.Para
    	ELSIF MatchTail(&quot;PAIR&quot;) THEN RETURN JunoToken.Kind.Pair
    	ELSIF MatchTail(&quot;PRED&quot;) THEN RETURN JunoToken.Kind.Pred
    	ELSIF MatchTail(&quot;PRIVATE&quot;) THEN RETURN JunoToken.Kind.Private
    	ELSIF MatchTail(&quot;PROC&quot;) THEN RETURN JunoToken.Kind.Proc
    	END
      | 'R' =&gt;
    	IF MatchTail(&quot;REAL&quot;) THEN RETURN JunoToken.Kind.Real
    	ELSIF MatchTail(&quot;REL&quot;) THEN RETURN JunoToken.Kind.Rel
    	ELSIF MatchTail(&quot;ROUND&quot;) THEN RETURN JunoToken.Kind.Round
    	END
      | 'S' =&gt;
    	IF MatchTail(&quot;SKIP&quot;) THEN RETURN JunoToken.Kind.Skip
    	ELSIF MatchTail(&quot;SAVE&quot;) THEN RETURN JunoToken.Kind.Save
    	ELSIF MatchTail(&quot;SIN&quot;) THEN RETURN JunoToken.Kind.Sin
    	END
      | 'T' =&gt;
        IF sz = 4 THEN
    	  IF MatchTail(&quot;TEXT&quot;) THEN RETURN JunoToken.Kind.Text
    	  ELSIF MatchTail(&quot;TRUE&quot;) THEN RETURN JunoToken.Kind.True
    	  END
        END
      | 'U' =&gt;
        IF sz = 2 AND MatchTail(&quot;UI&quot;) THEN RETURN JunoToken.Kind.UI END
      | 'V' =&gt;
        IF sz = 3 THEN
    	  IF MatchTail(&quot;VER&quot;) THEN RETURN JunoToken.Kind.Ver
    	  ELSIF MatchTail(&quot;VAR&quot;) THEN RETURN JunoToken.Kind.Var
    	  END
        END
      ELSE (* SKIP *)
      END;
      RETURN JunoToken.Kind.Id
    END Keyword;

  (* ReadId *)
  BEGIN
    ReadIdToBuf();
    IF 'A' &lt;= s.buff[0] AND s.buff[0] &lt;= 'Z' THEN
      VAR res := Keyword(); BEGIN
        IF res # JunoToken.Kind.Id THEN RETURN res END
      END
    END;
    id := Atom.FromText(Text.FromChars(SUBARRAY(s.buff^, 0, sz)));
    RETURN JunoToken.Kind.Id
  END ReadId;

PROCEDURE <A NAME="ReadComment"><procedure>ReadComment</procedure></A>(s: Stream; private: BOOLEAN): TEXT
    RAISES {Error, Rd.Failure} =
</PRE><BLOCKQUOTE><EM> Assuming <CODE>s.c</CODE> contains the '*' character at the start of the comment,
   return the TEXT of the full comment including the start- and end-of-comment
   markers and any nested comments. Raises <CODE>Error(ErrorKind.UnclosedComment)</CODE>
   if the file ends before the final end-of-comment characters. If <CODE>private =
   FALSE</CODE>, then the end-of-comment characters are Modula-3 style, otherwise,
   they are C style.
<P>
   Note: Since a comment may be followed by end-of-file, and since we must
   return a valid result in that case, this procedure does *not* maintain the
   invariant that <CODE>s.c</CODE> contains the next unprocessed character; that
   character will be the first character on <CODE>s.rd</CODE>. </EM></BLOCKQUOTE><PRE>
  VAR buf: Buffer; i: CARDINAL; res := &quot;&quot;;

  &lt;* INLINE *&gt;
  PROCEDURE AppendChar(c: CHAR) =
    BEGIN
      IF i = BufSize THEN res := FlushBuf(res, buf, i) END;
      buf[i] := c; INC(i)
    END AppendChar;

  (* ReadComment *)
  VAR first, last: CHAR; BEGIN
    &lt;* ASSERT s.c = '*' *&gt;
    res := &quot;&quot;;
    IF private
      THEN buf[0] := '/'; last := '/'
      ELSE buf[0] := '('; last := ')'
    END;
    buf[1] := '*'; i := 2;
    TRY
      TRY
      	s.c := Rd.GetChar(s.rd);
      	LOOP
      	  first := s.c;
          CASE first OF
      	    '(', '/' =&gt;
      	      s.c := Rd.GetChar(s.rd);
      	      IF s.c = '*' THEN
            	res := FlushBuf(res, buf, i);
      	    	res := res &amp; ReadComment(s, private := (first = '/'));
                s.c := Rd.GetChar(s.rd)
      	      ELSE
            	AppendChar(first)
      	      END
      	  | '*' =&gt;
              AppendChar(first);
      	      s.c := Rd.GetChar(s.rd);
      	      IF s.c = last THEN
                AppendChar(last);
      	    	EXIT
      	      END
      	  ELSE
              AppendChar(first);
              s.c := Rd.GetChar(s.rd)
      	  END
      	END
      FINALLY
        res := FlushBuf(res, buf, i)
      END
    EXCEPT
      Rd.EndOfFile =&gt; RaiseError(ErrorKind.UnclosedComment, res)
    END;
    RETURN res
  END ReadComment;

PROCEDURE <A NAME="RaiseError"><procedure>RaiseError</procedure></A>(kind: ErrorKind; t: TEXT) RAISES {Error} =
  BEGIN
    RAISE Error(NEW(ErrorRec, kind := kind, initialChars := t))
  END RaiseError;

PROCEDURE <A NAME="ErrorText"><procedure>ErrorText</procedure></A>(e: ErrorKind): TEXT =
  BEGIN
    CASE e OF
    | ErrorKind.BadInitialChar  =&gt; RETURN &quot;Illegal initial character&quot;
    | ErrorKind.BadEscapeChar   =&gt; RETURN &quot;Illegal \\ escape&quot;
    | ErrorKind.BadReal         =&gt; RETURN &quot;Illegal number&quot;
    | ErrorKind.UnclosedComment =&gt; RETURN &quot;Unclosed comment&quot;
    | ErrorKind.UnclosedText    =&gt; RETURN &quot;Unclosed text literal&quot;
    END
  END ErrorText;

BEGIN END JunoLex.
</PRE>
</inModule>
<HR>
<A NAME="x1">interface JunoValue is in:
</A><UL>
<LI><A HREF="../../juno-machine/src/JunoValue.i3.html#0TOP0">juno-machine/src/JunoValue.i3</A>
<LI><A HREF="../../pkl-fonts/src/JunoValue.i3.html#0TOP0">pkl-fonts/src/JunoValue.i3</A>
</UL>
<P>
<PRE>























</PRE>
</BODY>
</HTML>
