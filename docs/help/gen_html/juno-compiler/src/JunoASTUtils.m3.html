<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: juno-compiler/src/JunoASTUtils.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>juno-compiler/src/JunoASTUtils.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright (C) 1992, Digital Equipment Corporation                         
 All rights reserved.                                                      
 See the file COPYRIGHT for a full description.                            
                                                                           
 Last modified on Mon Feb 24 17:19:24 PST 1997 by heydon                   

<P><PRE>MODULE <module><implements><A HREF="JunoASTUtils.i3.html">JunoASTUtils</A></implements></module>;

IMPORT <A HREF="BuiltInSlots.i3.html">BuiltInSlots</A>, <A HREF="JunoAST.i3.html">JunoAST</A>, <A HREF="#x1">JunoValue</A>, <A HREF="../../libm3/src/atom/Atom.i3.html">Atom</A>;
</PRE> ====================== Id/QId/NearVar Conversions ======================= 

<P><PRE>PROCEDURE <A NAME="QIdFromNearVar"><procedure>QIdFromNearVar</procedure></A>(v: JunoAST.NearVarLink): JunoAST.QId =
  BEGIN
    RETURN NEW(JunoAST.QId, id0 := JunoAST.NilId, id1 := v.id,
      type := JunoAST.IdType.Local, index := v.index)
  END QIdFromNearVar;

PROCEDURE <A NAME="QIdFromIds"><procedure>QIdFromIds</procedure></A>(mod, id: JunoAST.Id): JunoAST.QId =
  BEGIN
    &lt;* ASSERT id # JunoAST.NilId *&gt;
    RETURN NEW(JunoAST.QId, bp := JunoAST.End, id0 := mod, id1 := id)
  END QIdFromIds;

PROCEDURE <A NAME="QIdFromTexts"><procedure>QIdFromTexts</procedure></A>(mod, id: TEXT): JunoAST.QId =
  BEGIN
    RETURN NEW(JunoAST.QId, bp := JunoAST.End,
      id0 := Atom.FromText(mod), id1 := Atom.FromText(id))
  END QIdFromTexts;

PROCEDURE <A NAME="QIdFromId"><procedure>QIdFromId</procedure></A>(id: JunoAST.Id): JunoAST.QId =
  BEGIN
    &lt;* ASSERT id # NIL *&gt;
    RETURN NEW(JunoAST.QId, bp := JunoAST.End,
      id0 := JunoAST.NilId, id1 := id)
  END QIdFromId;

PROCEDURE <A NAME="QIdFromText"><procedure>QIdFromText</procedure></A>(t: TEXT): JunoAST.QId =
  BEGIN RETURN QIdFromId(Atom.FromText(t)) END QIdFromText;
</PRE> ====================== IdList Conversions =============================== 

<P><PRE>PROCEDURE <A NAME="IdListToNearVarList"><procedure>IdListToNearVarList</procedure></A>(l: JunoAST.IdList): JunoAST.NearVarList =
  VAR h := l.head; hres: JunoAST.NearVarLink := NIL; BEGIN
    WHILE h # NIL DO
      hres := NEW(JunoAST.NearVarLink, id := h.id, index := h.index,
        hint := JunoAST.NilExpr, next := hres);
      h := h.next
    END;
    RETURN NEW(JunoAST.NearVarList, bp := l, size := l.size, head := hres)
  END IdListToNearVarList;

PROCEDURE <A NAME="IdListToQIdList"><procedure>IdListToQIdList</procedure></A>(l: JunoAST.IdList): JunoAST.QIdList =
  VAR head, last: JunoAST.ExprLink := NIL; curr := l.head; BEGIN
    WHILE curr # NIL DO
      VAR new := NEW(JunoAST.ExprLink, expr := QIdFromId(curr.id)); BEGIN
        IF head = NIL
          THEN head := new
          ELSE last.next := new
        END;
        last := new
      END;
      curr := curr.next
    END;
    RETURN NEW(JunoAST.QIdList, bp := l, size := l.size, head := head)
  END IdListToQIdList;
</PRE> ==================== Create New 1-Item Lists ============================ 

<P><PRE>PROCEDURE <A NAME="NewExprList"><procedure>NewExprList</procedure></A>(e: JunoAST.Expr; bp: JunoAST.T := NIL): JunoAST.ExprList=
  BEGIN
    RETURN NEW(JunoAST.ExprList, size := 1, bp := bp,
      head := NEW(JunoAST.ExprLink, expr := e))
  END NewExprList;

PROCEDURE <A NAME="NewQIdList"><procedure>NewQIdList</procedure></A>(qid: JunoAST.QId; bp: JunoAST.T := NIL): JunoAST.QIdList =
  BEGIN
    RETURN NEW(JunoAST.QIdList, size := 1, bp := bp,
      head := NEW(JunoAST.ExprLink, expr := qid))
  END NewQIdList;

PROCEDURE <A NAME="NewIdList"><procedure>NewIdList</procedure></A>(id: JunoAST.Id; index: INTEGER := 0): JunoAST.IdList =
  BEGIN
    RETURN NEW(JunoAST.IdList, bp := JunoAST.End, size := 1,
      head := NEW(JunoAST.IdLink, id := id, index := index))
  END NewIdList;
</PRE> ======================== Membership Tests =============================== 

<P><PRE>PROCEDURE <A NAME="MemIdList"><procedure>MemIdList</procedure></A>(id: JunoAST.Id; l: JunoAST.IdList): BOOLEAN =
  VAR curr := l.head; BEGIN
    WHILE curr # NIL AND curr.id # id DO curr := curr.next END;
    RETURN curr # NIL
  END MemIdList;

PROCEDURE <A NAME="MemNearVarList"><procedure>MemNearVarList</procedure></A>(id: JunoAST.Id; l: JunoAST.NearVarList):
  JunoAST.NearVarLink =
  VAR curr := l.head; BEGIN
    WHILE curr # NIL AND curr.id # id DO curr := curr.next END;
    RETURN curr
  END MemNearVarList;
</PRE> ======================= Operations on IdList's ========================== 

<P><PRE>PROCEDURE <A NAME="CopyIdLinks"><procedure>CopyIdLinks</procedure></A>(l: JunoAST.IdLink; VAR (*OUT*) last: JunoAST.IdLink):
  JunoAST.IdLink =
</PRE><BLOCKQUOTE><EM> Return a copy of <CODE>l</CODE>, but set <CODE>last</CODE> to the last <CODE>JunoAST.IdLink</CODE> of the
   new list. If <CODE>l</CODE> is <CODE>NIL</CODE>, then <CODE>last</CODE> is set to <CODE>NIL</CODE>. </EM></BLOCKQUOTE><PRE>
  VAR res: JunoAST.IdLink := NIL; BEGIN
    last := NIL;
    WHILE l # NIL DO
      VAR new := NEW(JunoAST.IdLink, id := l.id, index := l.index); BEGIN
        IF last = NIL
          THEN res := new
          ELSE last.next := new
        END;
        last := new
      END;
      l := l.next
    END;
    RETURN res;
  END CopyIdLinks;

PROCEDURE <A NAME="CopyIdList"><procedure>CopyIdList</procedure></A>(l: JunoAST.IdList): JunoAST.IdList =
  VAR dummy: JunoAST.IdLink; BEGIN
    RETURN NEW(JunoAST.IdList, bp := l, size := l.size,
      head := CopyIdLinks(l.head, dummy))
  END CopyIdList;

PROCEDURE <A NAME="ConcatIdLists"><procedure>ConcatIdLists</procedure></A>(l1, l2: JunoAST.IdList): JunoAST.IdList =
  VAR last, dummy: JunoAST.IdLink; head := CopyIdLinks(l1.head, last); BEGIN
    last.next := CopyIdLinks(l2.head, dummy);
    RETURN NEW(JunoAST.IdList, bp := l1, size := l1.size + l2.size,
      head := head);
  END ConcatIdLists;
</PRE> ===================== Operations on NearVarList's ======================= 

<P><PRE>PROCEDURE <A NAME="NearVarListUnion"><procedure>NearVarListUnion</procedure></A>(l1, l2: JunoAST.NearVarList): JunoAST.NearVarList =
  VAR h1 := l1.head; h2 := l2.head; BEGIN
    WHILE h1 # NIL DO
      h2 := NEW(JunoAST.NearVarLink, id := h1.id, index := h1.index,
        frozen := h1.frozen, hint := h1.hint, evar := h1.evar, next := h2);
      h1 := h1.next
    END;
    RETURN NEW(JunoAST.NearVarList, size := l1.size + l2.size, head := h2)
  END NearVarListUnion;

PROCEDURE <A NAME="CopyLinks"><procedure>CopyLinks</procedure></A>(l: JunoAST.NearVarLink): JunoAST.NearVarLink =
</PRE><BLOCKQUOTE><EM> Return a copy of the list <CODE>l</CODE>. </EM></BLOCKQUOTE><PRE>
  VAR res, last: JunoAST.NearVarLink := NIL; BEGIN
    WHILE l # NIL DO
      VAR new: JunoAST.NearVarLink; BEGIN
      	new := NEW(JunoAST.NearVarLink, id := l.id, evar := l.evar,
      	  frozen := l.frozen, hint := l.hint, index := l.index);
      	IF last = NIL
      	  THEN res := new
      	  ELSE last.next := new
      	END;
      	last := new
      END;
      l := l.next
    END;
    RETURN res
  END CopyLinks;

PROCEDURE <A NAME="NearVarListCopy"><procedure>NearVarListCopy</procedure></A>(l: JunoAST.NearVarList): JunoAST.NearVarList =
  BEGIN
    RETURN NEW(JunoAST.NearVarList, size := l.size, head := CopyLinks(l.head))
  END NearVarListCopy;

PROCEDURE <A NAME="ExtractHints"><procedure>ExtractHints</procedure></A>(vars: JunoAST.NearVarList): JunoAST.Formula =
  VAR
    res: JunoAST.Formula := NIL;
    h_in: JunoAST.NearVarLink := vars.head;
    eq: JunoAST.Equals;
  BEGIN
    &lt;* ASSERT vars.size &gt; 0 *&gt;
    WHILE h_in # NIL DO
      IF h_in.hint # JunoAST.NilExpr THEN
        eq := NEW(JunoAST.Equals, bp := vars, near := NOT h_in.frozen,
           e1 := QIdFromNearVar(h_in), e2 := h_in.hint);
        IF res = NIL
          THEN res := eq
          ELSE res := NEW(JunoAST.And, bp := vars, f1 := eq, f2 := res)
        END
      END;
      h_in := h_in.next
    END;
    IF res = NIL THEN res := JunoAST.TrueVal END;
    RETURN res
  END ExtractHints;

PROCEDURE <A NAME="StripHints"><procedure>StripHints</procedure></A>(vars: JunoAST.NearVarList): JunoAST.NearVarList =
</PRE><BLOCKQUOTE><EM> IMPLEMENTATION: The resulting list is <CODE>vars</CODE> in reverse order. </EM></BLOCKQUOTE><PRE>
  VAR
    res := NEW(JunoAST.NearVarList, bp := vars, size := vars.size);
    curr := vars.head;
  BEGIN
    WHILE curr # NIL DO
      res.head := NEW(JunoAST.NearVarLink, id := curr.id, index := curr.index,
        hint := JunoAST.NilExpr, next := res.head);
      curr := curr.next
    END;
    RETURN res
  END StripHints;
</PRE> ============================= MapArgs =================================== 

<P><PRE>PROCEDURE <A NAME="MapArgs"><procedure>MapArgs</procedure></A>(expr: JunoAST.Expr; p: Mappee): JunoAST.Expr =
  BEGIN
    TYPECASE expr OF &lt;* NOWARN *&gt;
      JunoAST.Call (e) =&gt;
        RETURN NEW(JunoAST.Call, bp := e, inouts := e.inouts,
          inout_parens := e.inout_parens, name := e.name,
          ins := NEW(JunoAST.ExprList, bp := e.ins, size := e.ins.size,
            head := MapExprList(e.ins.head, p)),
          normal_form := e.normal_form);
    | JunoAST.LitPred =&gt; RETURN expr
    | JunoAST.BIUPred (e) =&gt;
        VAR res: JunoAST.BIUPred; BEGIN
          TYPECASE e OF &lt;* NOWARN *&gt;
            JunoAST.IsReal =&gt; res := NEW(JunoAST.IsReal)
          | JunoAST.IsText =&gt; res := NEW(JunoAST.IsText)
          | JunoAST.IsPair =&gt; res := NEW(JunoAST.IsPair)
          | JunoAST.IsInt  =&gt; res := NEW(JunoAST.IsInt)
          END;
          res.bp := e; res.e := p(e.e);
          RETURN res
        END
    | JunoAST.Relation (e) =&gt;
        VAR res: JunoAST.Relation; BEGIN
          TYPECASE e OF &lt;* NOWARN *&gt;
            JunoAST.Equals (eq) =&gt; res := NEW(JunoAST.Equals, near := eq.near)
          | JunoAST.Differs	=&gt; res := NEW(JunoAST.Differs)
          | JunoAST.Less	=&gt; res := NEW(JunoAST.Less)
          | JunoAST.Greater	=&gt; res := NEW(JunoAST.Greater)
          | JunoAST.AtMost	=&gt; res := NEW(JunoAST.AtMost)
          | JunoAST.AtLeast	=&gt; res := NEW(JunoAST.AtLeast)
          | JunoAST.Cong	=&gt; res := NEW(JunoAST.Cong)
          | JunoAST.Para	=&gt; res := NEW(JunoAST.Para)
          | JunoAST.Hor		=&gt; res := NEW(JunoAST.Hor)
          | JunoAST.Ver		=&gt; res := NEW(JunoAST.Ver)
          END;
          res.bp := e; res.e1 := p(e.e1); res.e2 := p(e.e2);
          RETURN res
        END
    | JunoAST.BIUFunc (e) =&gt;
        VAR res: JunoAST.BIUFunc; BEGIN
          TYPECASE e OF &lt;* NOWARN *&gt;
            JunoAST.UMinus  =&gt; res := NEW(JunoAST.UMinus)
          | JunoAST.Floor   =&gt; res := NEW(JunoAST.Floor)
          | JunoAST.Ceiling =&gt; res := NEW(JunoAST.Ceiling)
          | JunoAST.Round   =&gt; res := NEW(JunoAST.Round)
          | JunoAST.Abs     =&gt; res := NEW(JunoAST.Abs)
          | JunoAST.Sin     =&gt; res := NEW(JunoAST.Sin)
          | JunoAST.Cos     =&gt; res := NEW(JunoAST.Cos)
          | JunoAST.Exp     =&gt; res := NEW(JunoAST.Exp)
          | JunoAST.Ln      =&gt; res := NEW(JunoAST.Ln)
          | JunoAST.Car     =&gt; res := NEW(JunoAST.Car)
          | JunoAST.Cdr     =&gt; res := NEW(JunoAST.Cdr)
          END;
          res.bp := e; res.e := p(e.e);
          RETURN res
        END
    | JunoAST.BIBFunc (e) =&gt;
        VAR res: JunoAST.BIBFunc; BEGIN
          TYPECASE e OF &lt;* NOWARN *&gt;
            JunoAST.Plus   =&gt; res := NEW(JunoAST.Plus)
          | JunoAST.Minus  =&gt; res := NEW(JunoAST.Minus)
          | JunoAST.Concat =&gt; res := NEW(JunoAST.Concat)
          | JunoAST.Times  =&gt; res := NEW(JunoAST.Times)
          | JunoAST.Divide =&gt; res := NEW(JunoAST.Divide)
          | JunoAST.Div    =&gt; res := NEW(JunoAST.Div)
          | JunoAST.Mod    =&gt; res := NEW(JunoAST.Mod)
          | JunoAST.Pair   =&gt; res := NEW(JunoAST.Pair)
          | JunoAST.Rel    =&gt; res := NEW(JunoAST.Rel)
          | JunoAST.Max    =&gt; res := NEW(JunoAST.Max)
          | JunoAST.Min    =&gt; res := NEW(JunoAST.Min)
          | JunoAST.Atan   =&gt; res := NEW(JunoAST.Atan)
          END;
          res.bp := e; res.e1 := p(e.e1); res.e2 := p(e.e2);
          RETURN res
        END
    | JunoAST.List (e) =&gt;
        RETURN NEW(JunoAST.List, bp := e, elts := NEW(JunoAST.ExprList,
          size := e.elts.size, head := MapExprList(e.elts.head, p)))
    END
  END MapArgs;

PROCEDURE <A NAME="MapExprList"><procedure>MapExprList</procedure></A>(el: JunoAST.ExprLink; p: Mappee): JunoAST.ExprLink =
</PRE><BLOCKQUOTE><EM> Return the list of expressions obtained by mapping <CODE>p</CODE> over each element of
   <CODE>el</CODE>. </EM></BLOCKQUOTE><PRE>
  BEGIN
    IF el = NIL THEN
      RETURN NIL
    ELSE
      RETURN NEW(JunoAST.ExprLink, expr := p(el.expr),
        next := MapExprList(el.next, p))
    END
  END MapExprList;
</PRE> ======================= Operations on JunoAST.Vars ====================== 

<P><PRE>PROCEDURE <A NAME="MemVars"><procedure>MemVars</procedure></A>(qid: JunoAST.QId; READONLY vars: JunoAST.Vars): INTEGER =
  BEGIN
    FOR i := FIRST(vars) TO LAST(vars) DO
      &lt;* ASSERT vars[i].index # 0 *&gt;
      IF vars[i].index = qid.index THEN
        &lt;* ASSERT vars[i].id = qid.id1 *&gt;
        RETURN i
      END
    END;
    RETURN -1
  END MemVars;
</PRE> ==================== Create New Special-Purpose AST's =================== 

<P><PRE>PROCEDURE <A NAME="NewNumber"><procedure>NewNumber</procedure></A>(x: JunoValue.Real): JunoAST.Expr =
  BEGIN
    IF x &lt; 0.0
      THEN RETURN NEW(JunoAST.UMinus, e := NEW(JunoAST.Number, val := ABS(x)))
      ELSE RETURN NEW(JunoAST.Number, val := x)
    END
  END NewNumber;

PROCEDURE <A NAME="NewPoint"><procedure>NewPoint</procedure></A>(x, y: JunoValue.Real): JunoAST.Pair =
  BEGIN
    RETURN NEW(JunoAST.Pair,
      e1 := NewNumber(x),
      e2 := NewNumber(y))
  END NewPoint;

PROCEDURE <A NAME="NewASTFromValue"><procedure>NewASTFromValue</procedure></A>(v: JunoValue.T): JunoAST.T =
  BEGIN
    IF JunoValue.IsList(v)
      THEN RETURN NewASTList(v)
      ELSE RETURN NewASTFromValue2(v)
    END
  END NewASTFromValue;

PROCEDURE <A NAME="NewASTFromValue2"><procedure>NewASTFromValue2</procedure></A>(v: JunoValue.T): JunoAST.T =
  BEGIN
    TYPECASE v OF &lt;* NOWARN *&gt;
      JunoValue.Null =&gt; RETURN JunoAST.NilVal
    | TEXT (t) =&gt; RETURN NEW(JunoAST.Text, val := t)
    | REF JunoValue.Real (r) =&gt; RETURN NEW(JunoAST.Number, val := r^)
    | REF JunoValue.Pair (p) =&gt; RETURN NEW(JunoAST.Pair,
        e1 := NewASTFromValue(p.car), e2 := NewASTFromValue(p.cdr))
    END
  END NewASTFromValue2;

PROCEDURE <A NAME="NewASTList"><procedure>NewASTList</procedure></A>(v: JunoValue.T): JunoAST.T =
</PRE><BLOCKQUOTE><EM> Requires <CODE>v</CODE> to be a non-empty list value (i.e. <CODE>JunoValue.IsList(v)</CODE>). </EM></BLOCKQUOTE><PRE>
  VAR
    res := NEW(JunoAST.List, elts := NEW(JunoAST.ExprList));
    curr, new: JunoAST.ExprLink := NIL;
  BEGIN
    &lt;* ASSERT v # JunoValue.Nil *&gt;
    WITH list = res.elts DO
      WHILE v # JunoValue.Nil DO
        TYPECASE v OF &lt;* NOWARN *&gt; REF JunoValue.Pair (p) =&gt;
          new := NEW(JunoAST.ExprLink, expr := NewASTFromValue(p.car));
          IF curr = NIL
            THEN list.head := new
            ELSE curr.next := new
          END;
          curr := new;
          INC(list.size);
          v := p.cdr
        END
      END
    END;
    RETURN res
  END NewASTList;

PROCEDURE <A NAME="NewAssign"><procedure>NewAssign</procedure></A>(v: JunoAST.QId; e: JunoAST.Expr): JunoAST.Assign =
  BEGIN
    RETURN NEW(JunoAST.Assign, vars := NewQIdList(v), exprs := NewExprList(e))
  END NewAssign;
</PRE> ============================ Miscellaneous ============================== 

<P><PRE>PROCEDURE <A NAME="Ungroup"><procedure>Ungroup</procedure></A>(ast: JunoAST.T): JunoAST.T =
  BEGIN
    LOOP
      TYPECASE ast OF
        JunoAST.GroupedCmd (c) =&gt;  ast := c.body
      | JunoAST.GroupedExpr (e) =&gt; ast := e.expr
      ELSE RETURN ast
      END
    END
  END Ungroup;

PROCEDURE <A NAME="EqualQIds"><procedure>EqualQIds</procedure></A>(qid1, qid2: JunoAST.QId): BOOLEAN =
  BEGIN RETURN qid1.id0 = qid2.id0 AND qid1.id1 = qid2.id1 END EqualQIds;

PROCEDURE <A NAME="FirstProcCall"><procedure>FirstProcCall</procedure></A>(cmd: JunoAST.Cmd; qid: JunoAST.QId): JunoAST.ProcCall =
  VAR res: JunoAST.ProcCall := NIL; BEGIN
    TYPECASE cmd OF
      NULL =&gt; (*SKIP*)
    | JunoAST.ProcCall (pc) =&gt;
        IF EqualQIds(pc.name, qid) THEN res := pc END
    ELSE
      VAR it := cmd.iterator(); c: JunoAST.T; BEGIN
        WHILE res = NIL AND it.next((*OUT*) c) DO
          TYPECASE c OF JunoAST.Cmd (cmd0) =&gt;
            res := FirstProcCall(cmd0, qid)
          ELSE (*SKIP*)
          END
        END
      END
    END;
    RETURN res
  END FirstProcCall;

PROCEDURE <A NAME="AlwaysDefined"><procedure>AlwaysDefined</procedure></A>(e: JunoAST.Expr): BOOLEAN =
</PRE><BLOCKQUOTE><EM> Note: the definition of this procedure is very important to the correct
   functioning of the compiler and assembler. </EM></BLOCKQUOTE><PRE>
  BEGIN
    TYPECASE e OF
      JunoAST.LitValue, JunoAST.QId =&gt; RETURN TRUE
    | JunoAST.GroupedExpr (g) =&gt; RETURN AlwaysDefined(g.expr)
    | JunoAST.List (l) =&gt; RETURN ExprsDefined(l.elts)
    | JunoAST.Pair (p) =&gt;
        RETURN AlwaysDefined(p.e1) AND AlwaysDefined(p.e2)
    | JunoAST.UMinus (m) =&gt; RETURN ISTYPE(m.e,JunoAST.Number)
    | JunoAST.Call (c) =&gt;
        CASE c.name.type OF &lt;* NOWARN *&gt;
          JunoAST.IdType.ExtProc =&gt;
            (* An external procedure call is always defined so long as its
               arguments are defined; this is because the EXTCALL bytecode
               immediately signals a run-time error if the procedure failed on
               its arguments. *)
            RETURN ExprsDefined(c.ins)
        | JunoAST.IdType.Proc =&gt;
            (* A user-defined procedure call (except for the special &quot;APPLY&quot;
               and &quot;CLOSE&quot; built-in user-defined procedures) is always defined
               so long as its arguments are defined. *)
            RETURN NOT BuiltInSlots.IsApplySlot(c.name.index)
               AND NOT BuiltInSlots.IsCloseSlot(c.name.index)
               AND ExprsDefined(c.ins)
        | JunoAST.IdType.Func, JunoAST.IdType.None =&gt;
            (* User-defined function calls can always be undefined, regardless
               of their args. *)
            RETURN FALSE
        END
    ELSE (* SKIP *)
    END;
    RETURN FALSE
  END AlwaysDefined;

PROCEDURE <A NAME="ExprsDefined"><procedure>ExprsDefined</procedure></A>(el: JunoAST.ExprList): BOOLEAN =
</PRE><BLOCKQUOTE><EM> Return TRUE iff <CODE>AlwaysDefined(e)</CODE> for every <CODE>e</CODE> in the list <CODE>el</CODE>. </EM></BLOCKQUOTE><PRE>
  VAR curr := el.head; BEGIN
    WHILE curr # NIL DO
      IF NOT AlwaysDefined(curr.expr) THEN RETURN FALSE END;
      curr := curr.next
    END;
    RETURN TRUE
  END ExprsDefined;

BEGIN
END JunoASTUtils.
</PRE>
</inModule>
<HR>
<A NAME="x1">interface JunoValue is in:
</A><UL>
<LI><A HREF="../../juno-machine/src/JunoValue.i3.html#0TOP0">juno-machine/src/JunoValue.i3</A>
<LI><A HREF="../../pkl-fonts/src/JunoValue.i3.html#0TOP0">pkl-fonts/src/JunoValue.i3</A>
</UL>
<P>
<PRE>























</PRE>
</BODY>
</HTML>
