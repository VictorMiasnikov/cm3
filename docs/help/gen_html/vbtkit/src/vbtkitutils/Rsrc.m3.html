<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: vbtkit/src/vbtkitutils/Rsrc.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>vbtkit/src/vbtkitutils/Rsrc.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright (C) 1989-1992, Digital Equipment Corporation                    
 All rights reserved.                                                      
 See the file COPYRIGHT for a full description.                            
                                                                           
 Last modified on Mon Jan 30 14:52:52 PST 1995 by kalsow                   
      modified on Fri Jul  9 21:30:45 PDT 1993 by mhb                      
      modified on Fri May 14 20:33:49 PDT 1993 by meehan                   
      modified on Fri Mar 27 03:02:41 1992 by steveg                       
      modified on Tue May 15 11:28:23 PDT 1990 by mcjones                  
      modified on Fri Oct 6 13:58:16 PDT 1989 by brooks                    
      modified on Sun May 21 19:48:08 PDT 1989 by gidi                     

<P><PRE>MODULE <module><implements><A HREF="Rsrc.i3.html">Rsrc</A></implements></module>;

IMPORT <A HREF="../../../libm3/src/bundleintf/Bundle.i3.html">Bundle</A>, <A HREF="../../../libm3/src/rw/FileRd.i3.html">FileRd</A>, <A HREF="../../../libm3/src/os/Common/OSError.i3.html">OSError</A>, <A HREF="../../../libm3/src/os/Common/Pathname.i3.html">Pathname</A>, <A HREF="../../../libm3/src/rw/Rd.i3.html">Rd</A>, <A HREF="../../../libm3/src/rw/TextRd.i3.html">TextRd</A>, <A HREF="../../../m3core/src/thread/Common/Thread.i3.html">Thread</A>;
IMPORT <A HREF="../../../libm3/src/params/Env.i3.html">Env</A>, <A HREF="../../../libm3/derived/RefList.i3.html">RefList</A>, <A HREF="../../../m3core/src/text/Text.i3.html">Text</A>;

PROCEDURE <A NAME="Open"><procedure>Open</procedure></A> (name: TEXT; p: Path): Rd.T RAISES {NotFound} =
  BEGIN
    IF Pathname.Absolute(name) THEN
      IF Pathname.Valid(name) THEN
        TRY RETURN FileRd.Open(name) EXCEPT OSError.E =&gt; END
      END;
      RAISE NotFound
    END;
    WHILE p # NIL DO
      TYPECASE p.head OF
      | NULL =&gt;                  &lt;* ASSERT FALSE *&gt;
      | Pathname.T (pn) =&gt;
          TRY
            RETURN FileRd.Open(Pathname.Join(pn, name, NIL))
          EXCEPT
          | OSError.E =&gt;
          END
      | Bundle.T (b) =&gt;
          WITH t = Bundle.Get(b, name) DO
            IF t # NIL THEN RETURN TextRd.New(t) END
          END
      ELSE
        &lt;* ASSERT FALSE *&gt;
      END;
      p := p.tail;
    END;
    RAISE NotFound
  END Open;

PROCEDURE <A NAME="Get"><procedure>Get</procedure></A> (name: TEXT; p: Path): TEXT
  RAISES {NotFound, Rd.Failure, Thread.Alerted} =
  BEGIN
    WHILE p # NIL DO
      TYPECASE p.head OF
      | NULL =&gt;                  &lt;* ASSERT FALSE *&gt;
      | Pathname.T (pn) =&gt;
          VAR rd: Rd.T := NIL;
          BEGIN
            TRY
              rd := FileRd.Open (Pathname.Join (pn, name, NIL))
            EXCEPT
            | OSError.E =&gt;
            END;
            IF rd # NIL THEN
              TRY
                RETURN Rd.GetText (rd, LAST (CARDINAL))
              FINALLY
                Rd.Close (rd)
              END
            END
          END
      | Bundle.T (b) =&gt;
          WITH t = Bundle.Get (b, name) DO IF t # NIL THEN RETURN t END END
      ELSE
        &lt;* ASSERT FALSE *&gt;
      END;
      p := p.tail
    END;
    RAISE NotFound
  END Get;

PROCEDURE <A NAME="BuildPath"><procedure>BuildPath</procedure></A> (a1, a2, a3, a4: REFANY := NIL): Path =
  BEGIN
    RETURN RefList.AppendD (
             Convert (a1),
             RefList.AppendD (
               Convert (a2), RefList.AppendD (Convert (a3), Convert (a4))))
  END BuildPath;

PROCEDURE <A NAME="Convert"><procedure>Convert</procedure></A> (a: REFANY): Path =
  BEGIN
    TYPECASE a OF
    | NULL =&gt; RETURN NIL
    | Bundle.T (b) =&gt; RETURN RefList.List1 (b)
    | TEXT (t) =&gt; RETURN ExpandPath (t)
    ELSE                         &lt;* ASSERT FALSE *&gt;
    END
  END Convert;

PROCEDURE <A NAME="ExpandPath"><procedure>ExpandPath</procedure></A> (path: TEXT): RefList.T =
  BEGIN
    IF NOT Text.Empty (path) AND Text.GetChar (path, 0) = '$' THEN
      path := Env.Get (Text.Sub (path, 1, LAST (CARDINAL)))
    END;
    IF path = NIL OR Text.Empty (path) THEN
      RETURN NIL
    ELSIF Pathname.Valid (path) THEN
      RETURN RefList.List1 (path)
    ELSE                         &lt;* ASSERT FALSE *&gt;
    END
  END ExpandPath;

BEGIN
END Rsrc.
</PRE>
</inModule>
<PRE>























</PRE>
</BODY>
</HTML>
