<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: vbtkit/src/lego/TrillSwitchVBT.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>vbtkit/src/lego/TrillSwitchVBT.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright (C) 1992, Digital Equipment Corporation                         
 All rights reserved.                                                      
 See the file COPYRIGHT for a full description.                            
                                                                           
 Last modified on Mon Feb 15 16:40:07 PST 1993 by mhb        
      modified on Fri Feb 12 15:55:38 PST 1993 by meehan     
      modified on Tue Jun 16 13:08:08 PDT 1992 by muller     
      modified on Fri Jul 26 15:36:28 PDT 1991 by steveg     

<P><PRE>MODULE <module><implements><A HREF="TrillSwitchVBT.i3.html">TrillSwitchVBT</A></implements></module>;

IMPORT <A HREF="../vbtkitutils/AutoRepeat.i3.html">AutoRepeat</A>, <A HREF="../../../ui/src/split/ButtonVBT.i3.html">ButtonVBT</A>, <A HREF="FeedbackVBT.i3.html">FeedbackVBT</A>, <A HREF="../../../ui/src/split/Filter.i3.html">Filter</A>, <A HREF="MultiClass.i3.html">MultiClass</A>,
       <A HREF="MultiFilter.i3.html">MultiFilter</A>, <A HREF="../../../libm3/src/geometry/Point.i3.html">Point</A>, <A HREF="../../../libm3/src/geometry/Rect.i3.html">Rect</A>, <A HREF="SwitchVBT.i3.html">SwitchVBT</A>, <A HREF="../../../ui/src/vbt/Trestle.i3.html">Trestle</A>, <A HREF="../../../ui/src/vbt/TrestleComm.i3.html">TrestleComm</A>, <A HREF="../../../ui/src/vbt/VBT.i3.html">VBT</A>;

REVEAL
  <A NAME="T">T</A> = Public BRANDED OBJECT
        firstDown: VBT.MouseRec;
        repeater : Repeater;
        trsl     : Trestle.T := NIL
      OVERRIDES
        init     := Init;
        callback := Callback;
        pre      := Pre;
        post     := Post;
        cancel   := Cancel;
        mouse    := Mouse;
        reshape  := Reshape;
        rescreen := Rescreen
      END;

TYPE
  MC = SwitchVBT.MC OBJECT END;

TYPE
  Repeater = AutoRepeat.T OBJECT
               v: T
             OVERRIDES
               repeat    := Repeat;
               canRepeat := CanRepeat
             END;

PROCEDURE <A NAME="Init"><procedure>Init</procedure></A> (v: T; f: FeedbackVBT.T): T =
  BEGIN
    EVAL ButtonVBT.T.init(v, f, Action);
    MultiClass.Be(v, NEW(MC));
    WITH ch = MultiFilter.Child(f) DO
      IF ch # NIL THEN MultiClass.BeChild(v, ch) END;
    END;
    v.repeater := NEW(Repeater, v := v).init();
    RETURN v
  END Init;

PROCEDURE <A NAME="Callback"><procedure>Callback</procedure></A> (&lt;* UNUSED *&gt;          v : T;
                    &lt;* UNUSED *&gt; READONLY cd: VBT.MouseRec) =
  BEGIN
  END Callback;

PROCEDURE <A NAME="Action"><procedure>Action</procedure></A>(
    &lt;* UNUSED *&gt; selfAsButtonVBT: ButtonVBT.T;
    &lt;* UNUSED *&gt; READONLY cd: VBT.MouseRec) =
  BEGIN
  END Action;

PROCEDURE <A NAME="Repeat"><procedure>Repeat</procedure></A> (r: Repeater) =
  BEGIN
    LOCK VBT.mu DO
      WITH v = r.v DO v.firstDown.time := 0; v.callback (v.firstDown) END
    END
  END Repeat;

PROCEDURE <A NAME="Rescreen"><procedure>Rescreen</procedure></A> (v: T; READONLY cd: VBT.RescreenRec) =
  BEGIN
    v.trsl := Trestle.ScreenOf(v, Point.Origin).trsl;
    Public.rescreen(v, cd)
  END Rescreen;

PROCEDURE <A NAME="Reshape"><procedure>Reshape</procedure></A> (v: T; READONLY cd: VBT.ReshapeRec) =
  BEGIN
    IF v.trsl = NIL AND NOT Rect.IsEmpty(cd.new) THEN
      v.trsl := Trestle.ScreenOf(v, Point.Origin).trsl
    END;
    Public.reshape(v, cd)
  END Reshape;

PROCEDURE <A NAME="CanRepeat"><procedure>CanRepeat</procedure></A> (r: Repeater): BOOLEAN =
  &lt;* FATAL TrestleComm.Failure *&gt;
  BEGIN
    LOCK VBT.mu DO
      WITH trsl = r.v.trsl DO
        IF trsl = NIL THEN
          (* somehow uninstalled; stop trilling *)
          RETURN FALSE
        ELSE
         (* an installed vbt; continue trilling as long
           as there are no pending mouse clicks or keystrokes
           on their way to any top-level window installed by
           the trsl in wich v is installed. *)
          RETURN Trestle.AllCeded(trsl)
        END
      END
    END
  END CanRepeat;

PROCEDURE <A NAME="Pre"><procedure>Pre</procedure></A> (v: T) =
  BEGIN
    FeedbackVBT.Excited(Filter.Child(v));
    v.callback(v.firstDown);
    AutoRepeat.Start(v.repeater)
  END Pre;

PROCEDURE <A NAME="Post"><procedure>Post</procedure></A> (v: T) =
  BEGIN
    AutoRepeat.Stop(v.repeater);
    FeedbackVBT.Normal (Filter.Child (v));
  END Post;

PROCEDURE <A NAME="Cancel"><procedure>Cancel</procedure></A> (v: T) =
  BEGIN
    AutoRepeat.Stop(v.repeater);
    FeedbackVBT.Normal(Filter.Child(v));
  END Cancel;

PROCEDURE <A NAME="Mouse"><procedure>Mouse</procedure></A> (v: T; READONLY cd: VBT.MouseRec) =
  BEGIN
    IF cd.clickType = VBT.ClickType.FirstDown THEN v.firstDown := cd END;
    ButtonVBT.T.mouse(v, cd)
  END Mouse;

BEGIN
END TrillSwitchVBT.
</PRE>
</inModule>
<PRE>























</PRE>
</BODY>
</HTML>
