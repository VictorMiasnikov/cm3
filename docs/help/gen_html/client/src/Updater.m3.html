<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: client/src/Updater.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>client/src/Updater.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright 1996-2003 John D. Polstra.
 * All rights reserved.
 *
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions
 * are met:
 * 1. Redistributions of source code must retain the above copyright
 *    notice, this list of conditions and the following disclaimer.
 * 2. Redistributions in binary form must reproduce the above copyright
 *    notice, this list of conditions and the following disclaimer in the
 *    documentation and/or other materials provided with the distribution.
 * 3. All advertising materials mentioning features or use of this software
 *    must display the following acknowledgment:
 *      This product includes software developed by John D. Polstra.
 * 4. The name of the author may not be used to endorse or promote products
 *    derived from this software without specific prior written permission.
 *
 * THIS SOFTWARE IS PROVIDED BY THE AUTHOR ``AS IS'' AND ANY EXPRESS OR
 * IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES
 * OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED.
 * IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR ANY DIRECT, INDIRECT,
 * INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT
 * NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
 * DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
 * THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
 * (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF
 * THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 *
 * $Id: Updater.m3.html,v 1.3 2010-04-29 17:17:58 wagner Exp $ 

<P><PRE>MODULE <module><implements><A HREF="Updater.i3.html">Updater</A></implements></module>;

IMPORT
  <A HREF="../../suplib/src/Attic.i3.html">Attic</A>, <A HREF="../../suplib/src/CVProto.i3.html">CVProto</A>, <A HREF="CheckoutCreator.i3.html">CheckoutCreator</A>, <A HREF="CheckoutUpdater.i3.html">CheckoutUpdater</A>, <A HREF="../../suplib/src/ErrMsg.i3.html">ErrMsg</A>, <A HREF="../../libm3/src/os/Common/File.i3.html">File</A>,
  <A HREF="../../suplib/src/FileAttr.i3.html">FileAttr</A>, <A HREF="../../suplib/src/FileID.i3.html">FileID</A>, <A HREF="../../suplib/src/FileStatus.i3.html">FileStatus</A>, <A HREF="FileUpdater.i3.html">FileUpdater</A>, <A HREF="../../libm3/src/rw/FileWr.i3.html">FileWr</A>, <A HREF="Fixup.i3.html">Fixup</A>, <A HREF="../../libm3/src/fmtlex/Fmt.i3.html">Fmt</A>,
  <A HREF="../../libm3/src/os/Common/FS.i3.html">FS</A>, <A HREF="../../suplib/src/GzipRd.i3.html">GzipRd</A>, <A HREF="../../suplib/src/Logger.i3.html">Logger</A>, <A HREF="../../suplib/src/MD5.i3.html">MD5</A>, <A HREF="../../suplib/src/MD5Wr.i3.html">MD5Wr</A>, <A HREF="../../libm3/src/os/Common/OSError.i3.html">OSError</A>, <A HREF="../../libm3/src/os/POSIX/OSErrorPosix.i3.html">OSErrorPosix</A>, <A HREF="../../libm3/src/os/Common/Pathname.i3.html">Pathname</A>,
  <A HREF="../../libm3/src/os/Common/Process.i3.html">Process</A>, <A HREF="../../suplib/src/RCSDate.i3.html">RCSDate</A>, <A HREF="../../suplib/src/RCSError.i3.html">RCSError</A>, <A HREF="../../suplib/src/RCSFile.i3.html">RCSFile</A>, <A HREF="../../suplib/src/RCSKeyword.i3.html">RCSKeyword</A>, <A HREF="../../suplib/src/RCSRevNum.i3.html">RCSRevNum</A>,
  <A HREF="RCSUpdater.i3.html">RCSUpdater</A>, <A HREF="../../libm3/src/rw/Rd.i3.html">Rd</A>, <A HREF="../../suplib/src/Reaper.i3.html">Reaper</A>, <A HREF="../../libm3/derived/RefSeq.i3.html">RefSeq</A>, <A HREF="RegularCreator.i3.html">RegularCreator</A>, <A HREF="RegularUpdater.i3.html">RegularUpdater</A>,
  <A HREF="RsyncUpdater.i3.html">RsyncUpdater</A>, <A HREF="../../suplib/src/StatusFile.i3.html">StatusFile</A>, <A HREF="../../tcp/src/common/StreamRd.i3.html">StreamRd</A>, <A HREF="../../suplib/src/SupFileRec.i3.html">SupFileRec</A>, <A HREF="#x1">SupFileRecSeq</A>,
  <A HREF="../../suplib/src/SupMisc.i3.html">SupMisc</A>, <A HREF="../derived/SyncFixupQueue.i3.html">SyncFixupQueue</A>, <A HREF="../../tempfiles/src/TempFiles.i3.html">TempFiles</A>, <A HREF="../../m3core/src/text/Text.i3.html">Text</A>, <A HREF="../../libm3/derived/TextSeq.i3.html">TextSeq</A>, <A HREF="../../m3core/src/thread/Common/Thread.i3.html">Thread</A>, <A HREF="../../m3core/src/time/Common/Time.i3.html">Time</A>,
  <A HREF="../../suplib/src/TokScan.i3.html">TokScan</A>, <A HREF="#x2">Uerror</A>, <A HREF="../../m3core/src/word/Word.i3.html">Word</A>, <A HREF="../../libm3/src/rw/Wr.i3.html">Wr</A>;

EXCEPTION Error(TEXT);

TYPE
  ParsedExecute = REF RECORD
    name: Pathname.T;
    command: TEXT;
  END;

REVEAL
  <A NAME="T">T</A> = Public BRANDED OBJECT
    proto: CVProto.T;
    wireRd: StreamRd.T;	(* Raw reader. *)
    rd: StreamRd.T;	(* Currently active reader. *)
    collections: SupFileRecSeq.T;
    reaper: Reaper.T;
    userDestDir: TEXT;  (* User-specified destination directory, or NIL. *)
    destDir: TEXT;      (* Set for each collection, never NIL. *)
    executes: RefSeq.T; (* ParsedExecute sequence for next file update. *)
    dirsCreated: TextSeq.T; (* A stack of directories that we've created. *)
    fixups: SyncFixupQueue.T;
    stats: Stats;
    trace: Logger.T;
    deleteLimit := -1;
    deleteCount := 0;
    statusFile: StatusFile.T;
    startingWireBytes: LONGREAL;
    startingCommBytes: LONGREAL;
  OVERRIDES
    apply := Apply;
    init := Init;
  END;

CONST
  DevInodeAttrs =
    FileAttr.AttrTypes{ FileAttr.AttrType.Dev, FileAttr.AttrType.Inode };

VAR  (*CONST*)
  EexistAtom := OSErrorPosix.ErrnoAtom(Uerror.EEXIST);
  EnoentAtom := OSErrorPosix.ErrnoAtom(Uerror.ENOENT);

PROCEDURE <A NAME="Apply"><procedure>Apply</procedure></A>(self: T): REFANY =
  BEGIN
    TRY
      IF self.stats # NIL THEN
	self.stats.start();
      END;
      TRY
	UpdateBatch(self, isFixups := FALSE);  (* Normal updates. *)
	self.fixups.close();
	UpdateBatch(self, isFixups := TRUE);  (* Fixups. *)
      FINALLY
	IF self.reaper # NIL THEN
	  Reaper.Dying(self.reaper);
	END;
	IF self.stats # NIL THEN
	  self.stats.finish();
	END;
      END;
      RETURN NEW(SupMisc.ThreadStatus,
	status := SupMisc.ExitCode.Success, message := NIL);
    EXCEPT
    | Error(msg) =&gt;
	RETURN NEW(SupMisc.ThreadStatus,
	  status := SupMisc.ExitCode.Failure,
	  message := &quot;Updater failed: &quot; &amp; msg);
    | Rd.EndOfFile =&gt;
	RETURN NEW(SupMisc.ThreadStatus,
	  status := SupMisc.ExitCode.TransientFailure,
	  message := &quot;Updater failed: Premature EOF from server&quot;);
    | Rd.Failure(list) =&gt;
	RETURN NEW(SupMisc.ThreadStatus,
	  status := SupMisc.ExitCode.TransientFailure,
	  message := &quot;Updater failed: Network read failure: &quot;
	    &amp; ErrMsg.StrError(list));
    | Thread.Alerted =&gt;
	RETURN NEW(SupMisc.ThreadStatus,
	  status := SupMisc.ExitCode.Failure,
	  message := &quot;Updater failed: Interrupted&quot;);
    | TokScan.Error(msg) =&gt;
	RETURN NEW(SupMisc.ThreadStatus,
	  status := SupMisc.ExitCode.Failure,
	  message := &quot;Updater failed: Protocol error: &quot; &amp; msg);
    END;
  END Apply;

PROCEDURE <A NAME="UpdateBatch"><procedure>UpdateBatch</procedure></A>(self: T;
                      isFixups: BOOLEAN)
  RAISES {Error, Rd.EndOfFile, Rd.Failure, Thread.Alerted, TokScan.Error} =
</PRE><BLOCKQUOTE><EM> Process all the collections or all the fixups. </EM></BLOCKQUOTE><PRE>
  VAR
    ts: TokScan.T;
    collection, release: TEXT;
  BEGIN
    FOR i := 0 TO self.collections.size()-1 DO
      WITH sfr = self.collections.get(i) DO
	IF NOT SupFileRec.Option.Skip IN sfr.options THEN
	  ts := self.proto.getCmd(self.rd);
	  ts.getFolded(&quot;COLL&quot;);
	  collection := ts.getToken(&quot;collection&quot;);
	  release := ts.getToken(&quot;release&quot;);
	  SupFileRec.Check(sfr, collection, release);

	  IF self.userDestDir # NIL THEN
	    self.destDir := self.userDestDir;
	  ELSE  (* Construct a destDir that is a no-op. *)
	    self.destDir := &quot;&quot;;
	  END;

	  UpdateCollection(self, sfr, isFixups);
	ELSE
	  IF NOT isFixups THEN
	    Trace(self, &quot;Skipping collection &quot; &amp; sfr.collection
		  &amp; &quot;/&quot; &amp; sfr.release);
	  END;
	END;
      END;
    END;
    ts := self.proto.getCmd(self.rd);
    ts.getLiteral(&quot;.&quot;);
  END UpdateBatch;

PROCEDURE <A NAME="UpdateCollection"><procedure>UpdateCollection</procedure></A>(self: T;
                           sfr: SupFileRec.T;
			   isFixups: BOOLEAN)
  RAISES {Error, Rd.EndOfFile, Rd.Failure, Thread.Alerted, TokScan.Error} =
  VAR
    ts: TokScan.T;
    cmd: TEXT;
    name: TEXT;
    cmdCh: CHAR;
    attr: FileAttr.T;
    linkTo: TEXT;
    fsType: FileStatus.Type;
    tag: TEXT;
    date: TEXT;
    revNum: TEXT;
    revDate: RCSDate.T;
    expand: RCSKeyword.ExpandMode;
    optStr: TEXT;
    cksum: TEXT;
    rcsOptions: RCSFile.Options;
    blockSize: CARDINAL;
    pos: CARDINAL;
    logLines: CARDINAL;
    fromAttic: BOOLEAN;
    needFixupTrace := isFixups;
    clearQueuedExecutes := TRUE;
  PROCEDURE AttrOrModTime(): FileAttr.T RAISES {TokScan.Error} =
    BEGIN
      IF self.proto.v.hasFileAttrs THEN
	RETURN DecodeAttr(self, ts.getToken(&quot;attributes&quot;));
      ELSE
	RETURN NEW(FileAttr.T).init(FileAttr.FileType.File,
	  modTime := ts.getTime(&quot;modTime&quot;));
      END;
    END AttrOrModTime;
  PROCEDURE AttrOrModTimeAndSize(): FileAttr.T RAISES {TokScan.Error} =
    BEGIN
      IF self.proto.v.hasFileAttrs THEN
	RETURN DecodeAttr(self, ts.getToken(&quot;attributes&quot;));
      ELSE
	WITH modTime = ts.getTime(&quot;modTime&quot;), size = ts.getInt(&quot;size&quot;) DO
	  RETURN NEW(FileAttr.T).init(FileAttr.FileType.File,
	    modTime := modTime,
	    size := size);
	END;
      END;
    END AttrOrModTimeAndSize;
  PROCEDURE DateToTime(date: RCSDate.T): Time.T RAISES {TokScan.Error} =
    BEGIN
      TRY
	RETURN RCSDate.ToTime(date);
      EXCEPT RCSError.E(msg) =&gt;
	RAISE TokScan.Error(&quot;Invalid RCS date: &quot; &amp; msg);
      END;
    END DateToTime;
  BEGIN
    IF SupFileRec.Option.Compress IN sfr.options THEN
      TRY
	IF self.stats = NIL THEN
	  self.rd := NEW(GzipRd.T).init(self.wireRd, closeChild := FALSE);
	ELSE
	  (* Use reduced read sizes in order to reduce the granularity of
	     the statistics. *)
	  self.rd := NEW(GzipRd.T).init(self.wireRd, maxChildRead := 512,
	    closeChild := FALSE);
	END;
      EXCEPT OSError.E(list) =&gt;
	RAISE Error(&quot;Cannot create Gzip reader: &quot; &amp; ErrMsg.StrError(list));
      END;
    END;
    TRY
      TRY
	self.statusFile := StatusFile.Open(sfr, sfr.scanTime, self.destDir,
	  readFromDestDir := isFixups);
	TRY
	  IF NOT isFixups THEN
	    Trace(self, &quot;Updating collection &quot; &amp; sfr.collection
	      &amp; &quot;/&quot; &amp; sfr.release);
	  END;
	  self.dirsCreated := NEW(TextSeq.T).init(30);
	  LOOP
	    IF Thread.TestAlert() THEN
	      RAISE Thread.Alerted;
	    END;
	    self.startingWireBytes := StreamRd.ByteCount(self.wireRd);
	    self.startingCommBytes := StreamRd.ByteCount(self.rd);
	    ts := self.proto.getCmd(self.rd);
	    cmdCh := ts.getChar(&quot;command&quot;);
	    IF cmdCh = '.' THEN EXIT END;
	    IF needFixupTrace THEN
	      Trace(self, &quot;Applying fixups for collection &quot; &amp; sfr.collection
		&amp; &quot;/&quot; &amp; sfr.release);
	      needFixupTrace := FALSE;
	    END;

	    (* Executes (shell commands to be executed on update
	       of certain files) arrive from the server immediately
	       before the associated file update commands.  We
	       clear the sequence of queued executes every time
	       through the command loop, except when the last
	       command was &quot;E&quot;, which adds an execute to the
	       sequence.  Thus we can accumulate a sequence of
	       executes with successive &quot;E&quot; commands, and they will
	       be applied to the first subsequent non-&quot;E&quot; command. *)
	    IF clearQueuedExecutes THEN
	      FOR i := 1 TO self.executes.size() DO
		EVAL self.executes.remlo();
	      END;
	    END;
	    clearQueuedExecutes := TRUE;  (* Assumption for the next time. *)

	    cmd := Text.FromChar(cmdCh);
	    CASE cmdCh OF
	    | 'A', 'a' =&gt;	(* Add file. *)
		name := ts.getToken(&quot;file name&quot;);
		IF self.proto.v.hasFileAttrs THEN
		  attr := DecodeAttr(self, ts.getToken(&quot;attributes&quot;));
		ELSE
		  WITH modTime = ts.getTime(&quot;modTime&quot;),
		       size = ts.getInt(&quot;size&quot;),
		       mode = ts.getInt(&quot;mode&quot;)
		  DO
		    attr := NEW(FileAttr.T).init(FileAttr.FileType.File,
		      modTime := modTime,
		      size := size,
		      mode := mode);
		  END;
		END;
		ts.getEnd(&quot;end of \&quot;&quot; &amp; cmd &amp; &quot;\&quot; command&quot;);
		UpdateFile(self, sfr, name,
		  toAttic := cmdCh = 'a',
		  fup := NEW(RegularCreator.T).init(
		    attr := attr,
		    isFixup := FALSE));
	    | 'C' =&gt;	(* Checkout file. *)
		name := ts.getToken(&quot;file name&quot;);
		tag := ts.getToken(&quot;tag&quot;);
		date := ts.getToken(&quot;date&quot;);
		revNum := ts.getToken(&quot;revision number&quot;);
		revDate := ts.getToken(&quot;revision date&quot;);
		attr := AttrOrModTime();
		ts.getEnd(&quot;end of \&quot;&quot; &amp; cmd &amp; &quot;\&quot; command&quot;);
		UpdateFile(self, sfr, name,
		  toAttic := FALSE,
		  fup := NEW(CheckoutCreator.T).init(
		    tag := tag,
		    date := date,
		    revNum := revNum,
		    revDate := revDate,
		    fileAttr := NEW(FileAttr.T).init(FileAttr.FileType.File,
		      modTime := DateToTime(revDate)),
		    rcsAttr := attr,
		    isFixup := FALSE));
	    | 'c' =&gt;	(* Checkout dead file. *)
		name := ts.getToken(&quot;file name&quot;);
		tag := ts.getToken(&quot;tag&quot;);
		date := ts.getToken(&quot;date&quot;);
		attr := AttrOrModTime();
		ts.getEnd(&quot;end of \&quot;&quot; &amp; cmd &amp; &quot;\&quot; command&quot;);
		WITH coName = SupMisc.CheckoutName(name) DO
		  (* Theoretically, the file does not exist on the client.
		     Just to make sure, we'll delete it here, if it exists. *)
		  TRY
		    EVAL FS.Status(SupMisc.CatPath(sfr.clientPrefix, coName));
		    Delete(self, sfr, coName);
		  EXCEPT OSError.E =&gt;
		    (* As expected, the file does not exist. *)
		  END;
		END;
		self.statusFile.put(NEW(FileStatus.T,
		  type := FileStatus.Type.CheckoutDead,
		  name := name,
		  tag := tag,
		  date := date,
		  serverAttr := attr));
	    | 'D' =&gt;	(* Delete file. *)
		(* This will delete the live and/or dead versions of the
		   file -- whichever ones exist. *)
		name := ts.getToken(&quot;file name&quot;);
		ts.getEnd(&quot;end of \&quot;&quot; &amp; cmd &amp; &quot;\&quot; command&quot;);
		IF SupFileRec.Option.CheckoutMode IN sfr.options THEN
		  WITH coName = SupMisc.CheckoutName(name) DO
		    Delete(self, sfr, coName);
		  END;
		ELSE
		  Delete(self, sfr, name);
		END;
		self.statusFile.delete(name);
	    | 'E' =&gt;  (* Save a shell command for after the next update. *)
		WITH pe = NEW(ParsedExecute) DO
		  pe.name := ts.getToken(&quot;file name&quot;);
		  pe.command := ts.getRest();
		  self.executes.addhi(pe);
		END;
		clearQueuedExecutes := FALSE;
	    | 'H', 'h' =&gt;  (* Make hard link. *)
		name := ts.getToken(&quot;file name&quot;);
		linkTo := ts.getToken(&quot;hard link target&quot;);
		ts.getEnd(&quot;end of \&quot;&quot; &amp; cmd &amp; &quot;\&quot; command&quot;);
		HardLink(self, sfr, name, linkTo, toAttic := cmdCh = 'h');
	    | 'I' =&gt;  (* Create directory. *)
		name := ts.getToken(&quot;directory name&quot;);
		ts.getEnd(&quot;End of \&quot;&quot; &amp; cmd &amp; &quot;\&quot; command&quot;);
		CreateDirectory(self, sfr, name);
	    | 'i' =&gt;  (* Remove DirDown listfile entry. *)
		name := ts.getToken(&quot;directory name&quot;);
		ts.getEnd(&quot;End of \&quot;&quot; &amp; cmd &amp; &quot;\&quot; command&quot;);
		self.statusFile.delete(name);
	    | 'J' =&gt;  (* Set directory attributes. *)
		name := ts.getToken(&quot;directory name&quot;);
		attr := AttrOrModTime();
		ts.getEnd(&quot;End of \&quot;&quot; &amp; cmd &amp; &quot;\&quot; command&quot;);
		SetDirectoryAttributes(self, sfr, name, attr);
	    | 'j' =&gt;  (* Remove directory. *)
		name := ts.getToken(&quot;directory name&quot;);
		ts.getEnd(&quot;End of \&quot;&quot; &amp; cmd &amp; &quot;\&quot; command&quot;);
		RemoveDirectory(self, sfr, name);
	    | 'L', 'l' =&gt;  (* Update recorded information for cvs file. *)
		name := ts.getToken(&quot;file name&quot;);
		attr := AttrOrModTime();
		ts.getEnd(&quot;end of \&quot;&quot; &amp; cmd &amp; &quot;\&quot; command&quot;);
		IF cmdCh = 'L' THEN
		  fsType := FileStatus.Type.FileLive;
		ELSE
		  fsType := FileStatus.Type.FileDead;
		END;
		self.statusFile.put(NEW(FileStatus.T,
		  type := fsType,
		  name := name,
		  clientAttr := attr,
		  serverAttr := attr));
	    | 'N', 'n' =&gt;  (* Create a node. *)
		name := ts.getToken(&quot;file name&quot;);
		attr := AttrOrModTime();
		ts.getEnd(&quot;end of \&quot;&quot; &amp; cmd &amp; &quot;\&quot; command&quot;);
		UpdateNode(self, sfr, name, attr, toAttic := cmdCh = 'n');
	    | 'R' =&gt;	(* Replace regular file. *)
		(* FIXME - Use 'A' for this, and use the 'R' command only
		   for legacy protocol support. *)
		name := ts.getToken(&quot;file name&quot;);
		attr := AttrOrModTimeAndSize();
		ts.getEnd(&quot;end of \&quot;&quot; &amp; cmd &amp; &quot;\&quot; command&quot;);
		UpdateFile(self, sfr, name,
		  toAttic := FALSE,
		  fup := NEW(RegularUpdater.T).init(
		    pos := 0,
		    attr := attr));
	    | 'r' =&gt;	(* Update regular file with rsync algorithm. *)
		name := ts.getToken(&quot;file name&quot;);
		attr := AttrOrModTime();
		blockSize := ts.getInt(&quot;blockSize&quot;);
		cksum := ts.getToken(&quot;checksum&quot;);
		ts.getEnd(&quot;end of \&quot;&quot; &amp; cmd &amp; &quot;\&quot; command&quot;);
		UpdateFile(self, sfr, name,
		  toAttic := FALSE,
		  fup := NEW(RsyncUpdater.T).init(
		    blockSize := blockSize,
		    attr := attr,
		    wantSum := cksum));
	    | 'T' =&gt;	(* Update recorded information for checked-out file. *)
		name := ts.getToken(&quot;file name&quot;);
		tag := ts.getToken(&quot;tag&quot;);
		date := ts.getToken(&quot;date&quot;);
		revNum := ts.getToken(&quot;revision number&quot;);
		IF self.proto.v.sendsRevDates THEN
		  revDate := ts.getToken(&quot;revision date&quot;);
		ELSE
		  revDate := &quot;.&quot;;
		END;
		attr := AttrOrModTime();
		ts.getEnd(&quot;end of \&quot;&quot; &amp; cmd &amp; &quot;\&quot; command&quot;);
		UpdateCheckoutInfo(self, sfr, name,
		  tag := tag,
		  date := date,
		  revNum := revNum,
		  revDate := revDate,
		  rcsAttr := attr);
	    | 'U' =&gt;	(* Update live checked-out file. *)
		name := ts.getToken(&quot;file name&quot;);
		tag := ts.getToken(&quot;tag&quot;);
		date := ts.getToken(&quot;date&quot;);
		revNum := ts.getToken(&quot;old revision number&quot;);
		fromAttic := NOT Text.Equal(ts.getToken(&quot;Attic flag&quot;), &quot;0&quot;);
		logLines := ts.getInt(&quot;log deletion count&quot;);
		WITH text = ts.getToken(&quot;expansion mode&quot;) DO
		  TRY
		    expand := RCSKeyword.DecodeExpand(text);
		  EXCEPT RCSError.E(msg) =&gt;
		    RAISE TokScan.Error(msg);
		  END;
		END;
		attr := AttrOrModTime();
		cksum := ts.getToken(&quot;checksum&quot;);
		ts.getEnd(&quot;end of \&quot;&quot; &amp; cmd &amp; &quot;\&quot; command&quot;);
		UpdateFile(self, sfr, name,
		  toAttic := FALSE,
		  fup := NEW(CheckoutUpdater.T).init(
		    tag := tag,
		    date := date,
		    oldRevNum := revNum,
		    oldLogLines := logLines,
		    fromAttic := fromAttic,
		    expand := expand,
		    rcsAttr := attr,
		    wantSum := cksum));
	    | 'u' =&gt;	(* Update dead checked-out file. *)
		name := ts.getToken(&quot;file name&quot;);
		tag := ts.getToken(&quot;tag&quot;);
		date := ts.getToken(&quot;date&quot;);
		attr := AttrOrModTime();
		ts.getEnd(&quot;end of \&quot;&quot; &amp; cmd &amp; &quot;\&quot; command&quot;);
		WITH coName = SupMisc.CheckoutName(name) DO
		  Delete(self, sfr, coName);
		END;
		self.statusFile.put(NEW(FileStatus.T,
		  type := FileStatus.Type.CheckoutDead,
		  name := name,
		  tag := tag,
		  date := date,
		  serverAttr := attr));
	    | 'V', 'v' =&gt;	(* Edit RCS file. *)
		name := ts.getToken(&quot;file name&quot;);
		attr := AttrOrModTime();
		optStr := ts.getToken(&quot;RCS file options&quot;);
		cksum := ts.getToken(&quot;checksum&quot;);
		ts.getEnd(&quot;end of \&quot;&quot; &amp; cmd &amp; &quot;\&quot; command&quot;);
		TRY
		  rcsOptions := RCSFile.DecodeOptions(optStr);
		EXCEPT RCSError.E =&gt;
		  RAISE TokScan.Error(&quot;Invalid RCS file options \&quot;&quot; &amp;
		    optStr &amp; &quot;\&quot;&quot;);
		END;
		UpdateFile(self, sfr, name,
		  toAttic := cmdCh = 'v',
		  fup := NEW(RCSUpdater.T).init(
		    attr := attr,
		    rcsOptions := rcsOptions,
		    wantSum := cksum));
	    | 'X', 'x' =&gt;	(* Receive RCS file fixup. *)
		name := ts.getToken(&quot;file name&quot;);
		attr := AttrOrModTimeAndSize();
		ts.getEnd(&quot;end of \&quot;&quot; &amp; cmd &amp; &quot;\&quot; command&quot;);
		UpdateFile(self, sfr, name,
		  toAttic := cmdCh = 'x',
		  fup := NEW(RegularCreator.T).init(
		    attr := attr,
		    isFixup := TRUE),
		  isFixup := TRUE);
	    | 'Y' =&gt;	(* Receive checkout-mode fixup. *)
		name := ts.getToken(&quot;file name&quot;);
		tag := ts.getToken(&quot;tag&quot;);
		date := ts.getToken(&quot;date&quot;);
		revNum := ts.getToken(&quot;revision number&quot;);
		revDate := ts.getToken(&quot;revision date&quot;);
		attr := AttrOrModTime();
		ts.getEnd(&quot;end of \&quot;&quot; &amp; cmd &amp; &quot;\&quot; command&quot;);
		UpdateFile(self, sfr, name,
		  toAttic := FALSE,
		  fup := NEW(CheckoutCreator.T).init(
		    tag := tag,
		    date := date,
		    revNum := revNum,
		    revDate := revDate,
		    fileAttr := NEW(FileAttr.T).init(FileAttr.FileType.File,
		      modTime := DateToTime(revDate)),
		    rcsAttr := attr,
		    isFixup := TRUE),
		  isFixup := TRUE);
	    | 'Z' =&gt;	(* Append to regular file. *)
		name := ts.getToken(&quot;file name&quot;);
		IF self.proto.v.hasFileAttrs THEN
		  attr := DecodeAttr(self, ts.getToken(&quot;attributes&quot;));
		  pos := ts.getInt(&quot;pos&quot;);
		ELSE
		  WITH modTime = ts.getTime(&quot;RCS file modTime&quot;),
		       nbytes = ts.getInt(&quot;nbytes&quot;)
		  DO
		    pos := ts.getInt(&quot;pos&quot;);
		    attr := NEW(FileAttr.T).init(FileAttr.FileType.File,
		      modTime := modTime,
		      size := pos + nbytes);
		  END;
		END;
		ts.getEnd(&quot;end of \&quot;&quot; &amp; cmd &amp; &quot;\&quot; command&quot;);
		UpdateFile(self, sfr, name,
		  toAttic := FALSE,
		  fup := NEW(RegularUpdater.T).init(
		    pos := pos,
		    attr := attr));
	    | '!' =&gt;	(* Warning from server. *)
		Warn(self, &quot;Server warning: &quot; &amp; ts.getRest());
	    ELSE
	      RAISE TokScan.Error(&quot;Invalid command \&quot;&quot; &amp; cmd &amp; &quot;\&quot;&quot;);
	    END;
	  END;
	FINALLY
	  self.statusFile.close();
	END;
      EXCEPT FileStatus.Error(msg) =&gt;
	RAISE Error(msg);
      END;
      IF SupFileRec.Option.Compress IN sfr.options THEN
	IF NOT Rd.EOF(self.rd) THEN
	  RAISE TokScan.Error(
	    &quot;Expected EOF from compressed stream, didn't get it&quot;);
	END;
	Rd.Close(self.rd);
      END;
    FINALLY
      IF SupFileRec.Option.Compress IN sfr.options THEN
	GzipRd.Cleanup(self.rd);
	self.rd := self.wireRd;
      END;
    END;
  END UpdateCollection;

PROCEDURE <A NAME="DecodeAttr"><procedure>DecodeAttr</procedure></A>(self: T; t: TEXT): FileAttr.T
  RAISES {TokScan.Error} =
  BEGIN
    LOOP
      TRY
	RETURN FileAttr.Decode(t);
      EXCEPT
      | FileAttr.UnknownGroup(name) =&gt;
	  Warn(self, &quot;Unknown group name \&quot;&quot; &amp; name
	    &amp; &quot;\&quot; received from server&quot;);
      | FileAttr.UnknownOwner(name) =&gt;
	  Warn(self, &quot;Unknown user name \&quot;&quot; &amp; name
	    &amp; &quot;\&quot; received from server&quot;);
      END;
    END;
  END DecodeAttr;

PROCEDURE <A NAME="UpdateFile"><procedure>UpdateFile</procedure></A>(self: T;
                     sfr: SupFileRec.T;
		     name: Pathname.T;
		     toAttic: BOOLEAN;
		     fup: FileUpdater.T;
		     isFixup := FALSE)
  RAISES {Error, FileStatus.Error, Rd.EndOfFile, Rd.Failure,
	  Thread.Alerted, TokScan.Error} =
  VAR
    destName := name;
    destPath, atticPath, finalPath, tempPath: Pathname.T;
    md5: MD5.T;
    wr: MD5Wr.T;
    size: CARDINAL;
    status: FileUpdater.Status;
  BEGIN
    IF SupFileRec.Option.CheckoutMode IN sfr.options THEN
      destName := SupMisc.CheckoutName(name);
    END;
    destPath := SupMisc.CatPath(self.destDir,
      SupMisc.CatPath(sfr.clientPrefix, destName));
    atticPath := SupMisc.AtticName(destPath);
    finalPath := destPath;
    IF toAttic THEN finalPath := atticPath END;

    tempPath := SupMisc.TempName(destPath);
    MakeDirectories(tempPath, sfr.umask);

    IF fup.isRCS()
    AND NOT SupFileRec.Option.StrictCheckRCS IN sfr.options
    AND self.proto.v.hasLooseRCSCheck THEN
      md5 := MD5.NewRCS();
    ELSE
      md5 := MD5.New();
    END;

    TRY
      TRY
	wr := NEW(MD5Wr.T).init(
	  wr := NEW(FileWr.T).init(FS.OpenFile(tempPath,
	    access := FS.AccessOption.OnlyOwnerCanRead)),
	  md5 := md5);
	TRY
	  TempFiles.Note(tempPath);
	  fup.update(
	    sfr := sfr,
	    name := name,
	    toAttic := toAttic,
	    proto := self.proto,
	    trace := self.trace,
	    protoRd := self.rd,
	    wr := wr,
	    status := status);
	  size := Wr.Index(wr);
	FINALLY
	  Wr.Close(wr);
	END;
      EXCEPT
      | FileUpdater.Error(msg) =&gt;
	  RAISE Error(destName &amp; &quot;: &quot; &amp; msg);
      | OSError.E(l) =&gt;
	  RAISE Error(tempPath &amp; &quot;: Cannot create: &quot; &amp; ErrMsg.StrError(l));
      | Wr.Failure(l) =&gt;
	  RAISE Error(tempPath &amp; &quot;: Write failure: &quot; &amp; ErrMsg.StrError(l));
      END;

      IF status.wantSum # NIL
      AND NOT Text.Equal(wr.getSignature(), status.wantSum) THEN
	RAISE FileUpdater.FixupNeeded(&quot;Checksum mismatch&quot;);
      END;

      IF status.fromAttic THEN
	MakeDirectories(atticPath, sfr.umask);
	destPath := atticPath;
      ELSIF toAttic THEN
	MakeDirectories(atticPath, sfr.umask);
      END;

      TempFiles.Forget(tempPath);
      status.fs.clientAttr := FileAttr.Umask(status.fs.clientAttr, sfr.umask);
      Install(status.fs.clientAttr,
	from := tempPath, via := destPath, to := finalPath);

      DoQueuedExecutes(self, sfr);

      (* We weren't necessarily able to set all the file attributes to the
	 desired values, and any executes may have altered the attributes.
	 To make sure we record the actual attribute values, we fetch
	 them from the file.

	 However, we preserve the link count as received from the
	 server.  This is important for preserving hard links in mirror
	 mode. *)
      TRY
        status.fs.clientAttr := FileAttr.Override(
          FileAttr.FromPathname(finalPath, follow := FALSE),
          status.fs.clientAttr,
          FileAttr.AttrTypes{FileAttr.AttrType.LinkCount});
      EXCEPT OSError.E(l) =&gt;
	RAISE Error(&quot;Cannot stat \&quot;&quot; &amp; finalPath &amp; &quot;\&quot;: &quot; &amp; ErrMsg.StrError(l));
	(* FIXME - This could happen if the execute deleted the file.  Maybe
	   we should simply delete its entry from the status file. *)
      END;

      (* To save space, don't write out the device and inode unless
	 the link count is greater than 1.  These attributes are used
	 only for detecting hard links.  If the link count is 1 then we
	 know there aren't any hard links. *)
      IF NOT FileAttr.AttrType.LinkCount IN
        FileAttr.GetMask(status.fs.clientAttr)
      OR FileAttr.GetLinkCount(status.fs.clientAttr) &lt;= 1 THEN
	status.fs.clientAttr :=
	  FileAttr.MaskOut(status.fs.clientAttr, DevInodeAttrs);
      END;

      (* In checkout mode, limit the attributes that we keep around. *)
      IF SupFileRec.Option.CheckoutMode IN sfr.options THEN
	status.fs.clientAttr :=
	  FileAttr.MaskOut(status.fs.clientAttr, FileAttr.CheckoutIgnore);
      END;

      self.statusFile.put(status.fs);

      IF status.modified THEN
	StatsUpdate(self, status.updateType, size);
      ELSE
	StatsUpdate(self, FileUpdater.UpdateType.Touch, size);
      END;
    EXCEPT FileUpdater.FixupNeeded(msg) =&gt;
      IF isFixup THEN
	Warn(self, destName &amp; &quot;: &quot; &amp; msg &amp; &quot; -- file not updated&quot;);
      ELSE
	Warn(self, destName &amp; &quot;: &quot; &amp; msg &amp; &quot; -- will transfer entire file&quot;);
	self.fixups.put(NEW(Fixup.T, sfr := sfr, name := name));
      END;
      TempFiles.Forget(tempPath);
      IF SupFileRec.Option.KeepBadFiles IN sfr.options THEN
	Warn(self, &quot;Bad version saved in &quot; &amp; tempPath);
      ELSE
	DeleteFile(self, tempPath);
      END;
    END;
  END UpdateFile;

PROCEDURE <A NAME="UpdateNode"><procedure>UpdateNode</procedure></A>(self: T;
                     sfr: SupFileRec.T;
		     name: Pathname.T;
		     attr: FileAttr.T;
		     toAttic: BOOLEAN)
  RAISES {Error, FileStatus.Error, Thread.Alerted} =
  VAR
    srcPath := SupMisc.CatPath(sfr.clientPrefix, name);
    origSrcPath := srcPath;
    destPath, atticPath, finalPath, tempPath: Pathname.T;
    fs: FileStatus.T;
    what: TEXT;
  BEGIN
    (* We don't bother mentioning the Attic here. *)
    IF attr.fileType = FileAttr.FileType.SymLink THEN
      Trace(self, &quot; Symlink &quot; &amp; name &amp; &quot; -&gt; &quot; &amp; FileAttr.GetLinkTarget(attr));
      what := &quot;symbolic link&quot;;
    ELSE
      Trace(self, &quot; Mknod &quot; &amp; name);
      what := &quot;node&quot;;
    END;

    destPath := SupMisc.CatPath(self.destDir,
      SupMisc.CatPath(sfr.clientPrefix, name));
    atticPath := SupMisc.AtticName(destPath);
    finalPath := destPath;
    IF toAttic THEN finalPath := atticPath END;

    tempPath := SupMisc.TempName(destPath);
    MakeDirectories(tempPath, sfr.umask);

    TRY
      FileAttr.MakeNode(attr, tempPath);
      TempFiles.Note(tempPath);
    EXCEPT OSError.E(l) =&gt;
      RAISE Error(tempPath &amp; &quot;: Cannot make &quot; &amp; what
	&amp; &quot;: &quot; &amp; ErrMsg.StrError(l));
    END;

    IF srcPath # origSrcPath THEN  (* Coming from the Attic. *)
      MakeDirectories(atticPath, sfr.umask);
      destPath := atticPath;
    ELSIF toAttic THEN
      MakeDirectories(atticPath, sfr.umask);
    END;

    TempFiles.Forget(tempPath);
    attr := FileAttr.Umask(attr, sfr.umask);
    Install(attr,
      from := tempPath, via := destPath, to := finalPath);

    DoQueuedExecutes(self, sfr);

    (* We weren't necessarily able to set all the file attributes to the
       desired values, and any executes may have altered the attributes.
       To make sure we record the actual attribute values, we fetch
       them from the file.

       However, we preserve the link count as received from the
       server.  This is important for preserving hard links in mirror
       mode. *)
    TRY
      attr := FileAttr.Override(
        FileAttr.FromPathname(finalPath, follow := FALSE), attr,
        FileAttr.AttrTypes{FileAttr.AttrType.LinkCount});
    EXCEPT OSError.E(l) =&gt;
      RAISE Error(&quot;Cannot stat \&quot;&quot; &amp; finalPath &amp; &quot;\&quot;: &quot; &amp; ErrMsg.StrError(l));
      (* FIXME - This could happen if the execute deleted the file.  Maybe
	 we should simply delete its entry from the status file. *)
    END;

    (* To save space, don't write out the device and inode unless
       the link count is greater than 1.  These attributes are used
       only for detecting hard links.  If the link count is 1 then we
       know there aren't any hard links. *)
    IF NOT FileAttr.AttrType.LinkCount IN FileAttr.GetMask(attr)
    OR FileAttr.GetLinkCount(attr) &lt;= 1 THEN
      attr := FileAttr.MaskOut(attr, DevInodeAttrs);
    END;

    fs := NEW(FileStatus.T,
      name := name,
      clientAttr := attr,
      serverAttr := attr);
    IF toAttic THEN
      fs.type := FileStatus.Type.FileDead;
    ELSE
      fs.type := FileStatus.Type.FileLive;
    END;
    self.statusFile.put(fs);

    StatsUpdate(self, FileUpdater.UpdateType.Other, 0);
  END UpdateNode;

PROCEDURE <A NAME="UpdateCheckoutInfo"><procedure>UpdateCheckoutInfo</procedure></A>(self: T;
                             sfr: SupFileRec.T;
			     name: Pathname.T;
			     tag: TEXT;
			     date: RCSDate.T;
			     revNum: RCSRevNum.T;
			     revDate: RCSDate.T;
			     rcsAttr: FileAttr.T)
  RAISES {Error, FileStatus.Error, Thread.Alerted} =
  VAR
    path := SupMisc.CatPath(sfr.clientPrefix,
      SupMisc.CheckoutName(name));
    destPath := SupMisc.CatPath(self.destDir, path);
    fileAttr: FileAttr.T;
  BEGIN
    TRY
      fileAttr := FileAttr.FromPathname(path, follow := FALSE);
      fileAttr := FileAttr.Override(fileAttr,
	FileAttr.ForCheckout(rcsAttr, sfr.umask));
      TRY
	IF FileAttr.Install(fileAttr, destPath) THEN
	  Trace(self, &quot; SetAttrs &quot; &amp; name);
	  fileAttr := FileAttr.FromPathname(destPath, follow := FALSE);
	END;
      EXCEPT OSError.E(l) =&gt;
	Trace(self, &quot; SetAttrs &quot; &amp; name);
	(* Ignore it if a different destination directory was specified
	   and the file doesn't exist. *)
	IF l.head # EnoentAtom OR Text.Equal(path, destPath) THEN
	  RAISE Error(&quot;Cannot set attributes for \&quot;&quot; &amp; destPath &amp; &quot;\&quot;: &quot; &amp;
	    ErrMsg.StrError(l));
	END;
      END;
      fileAttr := FileAttr.MaskOut(fileAttr, FileAttr.CheckoutIgnore);
      self.statusFile.put(NEW(FileStatus.T,
	type := FileStatus.Type.CheckoutLive,
	name := name,
	tag := tag,
	date := date,
	serverAttr := rcsAttr,
	revNum := revNum,
	revDate := revDate,
	clientAttr := fileAttr));
    EXCEPT OSError.E =&gt; (* The file has vanished. *)
      self.statusFile.delete(name);
    END;
  END UpdateCheckoutInfo;

PROCEDURE <A NAME="HardLink"><procedure>HardLink</procedure></A>(self: T;
                   sfr: SupFileRec.T;
		   name: Pathname.T;
		   linkTo: Pathname.T;
		   toAttic: BOOLEAN)
  RAISES {Error, FileStatus.Error, Thread.Alerted} =
  VAR
    srcPath := SupMisc.CatPath(sfr.clientPrefix, name);
    origSrcPath := srcPath;
    targetPath := SupMisc.CatPath(sfr.clientPrefix, linkTo);
    destPath, atticPath, finalPath, tempPath: Pathname.T;
    fromAttic := FALSE;
    idA, idB: FileID.T;
    attr: FileAttr.T;
    fs: FileStatus.T;
  BEGIN
    (* See if the link already exists in the right place. *)
    TRY
      idA :=
	FileID.FromAttr(Attic.FileAttrFromPathname(srcPath, follow := FALSE));
      idB :=
	FileID.FromAttr(FileAttr.FromPathname(targetPath, follow := FALSE));
      fromAttic := srcPath # origSrcPath;
      IF fromAttic = toAttic AND idA # NIL AND idB # NIL
      AND FileID.Equal(idA, idB) THEN
	RETURN;  (* Already linked. *)
      END;
    EXCEPT OSError.E =&gt; (* Ignore. *) END;

    (* We won't bother to mention the Attic here. *)
    Trace(self, &quot; Hardlink &quot; &amp; name &amp; &quot; -&gt; &quot; &amp; linkTo);

    destPath := SupMisc.CatPath(self.destDir,
      SupMisc.CatPath(sfr.clientPrefix, name));
    atticPath := SupMisc.AtticName(destPath);
    finalPath := destPath;
    IF toAttic THEN finalPath := atticPath END;

    tempPath := SupMisc.TempName(destPath);
    MakeDirectories(tempPath, sfr.umask);

    TRY
      attr := FileAttr.HardLink(path := tempPath, target := targetPath);
      TempFiles.Note(tempPath);
    EXCEPT OSError.E(l) =&gt;
      RAISE Error(tempPath &amp; &quot;: Cannot make node: &quot; &amp; ErrMsg.StrError(l));
    END;

    IF fromAttic THEN
      MakeDirectories(atticPath, sfr.umask);
      destPath := atticPath;
    ELSIF toAttic THEN
      MakeDirectories(atticPath, sfr.umask);
    END;

    TempFiles.Forget(tempPath);
    attr := FileAttr.Umask(attr, sfr.umask);
    Install(attr,
      from := tempPath, via := destPath, to := finalPath);

    DoQueuedExecutes(self, sfr);

    (* We weren't necessarily able to set all the file attributes to the
       desired values, and any executes may have altered the attributes.
       To make sure we record the actual attribute values, we fetch
       them from the file.

       However, we preserve the link count as received from the
       server.  This is important for preserving hard links in mirror
       mode. *)
    TRY
      attr := FileAttr.Override(
        FileAttr.FromPathname(finalPath, follow := FALSE), attr,
        FileAttr.AttrTypes{FileAttr.AttrType.LinkCount});
    EXCEPT OSError.E(l) =&gt;
      RAISE Error(&quot;Cannot stat \&quot;&quot; &amp; finalPath &amp; &quot;\&quot;: &quot; &amp; ErrMsg.StrError(l));
      (* FIXME - This could happen if the execute deleted the file.  Maybe
	 we should simply delete its entry from the status file. *)
    END;

    fs := NEW(FileStatus.T,
      name := name,
      clientAttr := attr,
      serverAttr := attr);
    IF toAttic THEN
      fs.type := FileStatus.Type.FileDead;
    ELSE
      fs.type := FileStatus.Type.FileLive;
    END;
    self.statusFile.put(fs);

    StatsUpdate(self, FileUpdater.UpdateType.Other, 0);
  END HardLink;

PROCEDURE <A NAME="CreateDirectory"><procedure>CreateDirectory</procedure></A>(self: T;
                          sfr: SupFileRec.T;
		          name: Pathname.T)
  RAISES {Error, FileStatus.Error, Thread.Alerted} =
  VAR
    path := SupMisc.CatPath(sfr.clientPrefix, name);
    destPath := SupMisc.CatPath(self.destDir, path);
    attr := NEW(FileAttr.T).init(FileAttr.FileType.Directory);
  BEGIN
    (* If we are not going to be setting the modes of the directory
       later, then create it with reasonable default modes.  Otherwise,
       let MakeNode create it with more restrictive modes initially. *)
    IF NOT SupFileRec.Option.SetMode IN sfr.options THEN
      attr := FileAttr.MergeDefault(attr);
    END;
    IF NOT Text.Equal(path, destPath) THEN  (* Testing -&gt; separate destDir. *)
      MakeDirectories(destPath, sfr.umask);
    END;
    TRY
      FileAttr.MakeNode(attr, destPath);
      Trace(self, &quot; Mkdir &quot; &amp; name);
      (* Push the directory name onto the stack so that we will be able
	 to find out that we created it when the matching
	 SetDirectoryAttributes call comes along. *)
      self.dirsCreated.addhi(name);
    EXCEPT OSError.E(l) =&gt;
      IF l.head # EexistAtom THEN
	Trace(self, &quot; Mkdir &quot; &amp; name);
	RAISE Error(&quot;Cannot create directory \&quot;&quot; &amp; destPath &amp; &quot;\&quot;: &quot; &amp;
	  ErrMsg.StrError(l));
      END;
    END;

    self.statusFile.put(NEW(FileStatus.T,
      name := name,
      type := FileStatus.Type.DirDown));

    StatsUpdate(self, FileUpdater.UpdateType.Other, 0);
  END CreateDirectory;

PROCEDURE <A NAME="SetDirectoryAttributes"><procedure>SetDirectoryAttributes</procedure></A>(self: T;
                                 sfr: SupFileRec.T;
		                 name: Pathname.T;
				 attr: FileAttr.T)
  RAISES {Error, FileStatus.Error, Thread.Alerted} =
  VAR
    path := SupMisc.CatPath(sfr.clientPrefix, name);
    destPath := SupMisc.CatPath(self.destDir, path);
    changed := FALSE;
  BEGIN
    IF self.dirsCreated.size() &gt; 0
    AND Text.Equal(name, self.dirsCreated.gethi()) THEN  (* We created it. *)
      changed := TRUE;
      EVAL self.dirsCreated.remhi();  (* Pop it from the stack. *)
    END;

    attr := FileAttr.MergeDefault(attr);
    attr := FileAttr.Umask(attr, sfr.umask);

    TRY
      IF FileAttr.Install(attr, destPath) THEN
	Trace(self, &quot; SetAttrs &quot; &amp; name);
	changed := TRUE;
      END;
    EXCEPT OSError.E(l) =&gt;
      Trace(self, &quot; SetAttrs &quot; &amp; name);
      (* Ignore it if a different destination directory was specified
	 and the file doesn't exist. *)
      IF l.head # EnoentAtom OR Text.Equal(path, destPath) THEN
	RAISE Error(&quot;Cannot set attributes for \&quot;&quot; &amp; destPath &amp; &quot;\&quot;: &quot; &amp;
	  ErrMsg.StrError(l));
      END;
    END;

    IF changed THEN  (* We changed the attributes and/or created it. *)
      DoQueuedExecutes(self, sfr);
    END;

    (* We weren't necessarily able to set all the directory attributes
       to the desired values, and any executes may have altered the
       attributes.  To make sure we record the actual attribute
       values, we fetch them from the directory. *)
    TRY
      attr := FileAttr.FromPathname(destPath, follow := FALSE);
    EXCEPT OSError.E(l) =&gt;
      RAISE Error(&quot;Cannot stat \&quot;&quot; &amp; destPath &amp; &quot;\&quot;: &quot; &amp; ErrMsg.StrError(l));
      (* FIXME - This could happen if the execute deleted the directory.
	 Maybe we should simply delete its entry from the status file. *)
    END;

    self.statusFile.put(NEW(FileStatus.T,
      name := name,
      type := FileStatus.Type.DirUp,
      clientAttr := attr,
      serverAttr := attr));

    StatsUpdate(self, FileUpdater.UpdateType.Other, 0);
  END SetDirectoryAttributes;

PROCEDURE <A NAME="RemoveDirectory"><procedure>RemoveDirectory</procedure></A>(self: T;
                          sfr: SupFileRec.T;
			  name: Pathname.T)
  RAISES {FileStatus.Error, Thread.Alerted} =
  VAR
    path := SupMisc.CatPath(sfr.clientPrefix, name);
    destPath := SupMisc.CatPath(self.destDir, path);
  BEGIN
    Trace(self, &quot; Rmdir &quot; &amp; name);
    IF Text.Equal(path, destPath) THEN
      TRY
	FS.DeleteDirectory(destPath);
      EXCEPT OSError.E(l) =&gt;
	IF l.head # EnoentAtom THEN
	  Warn(self, &quot;Cannot remove directory \&quot;&quot; &amp; destPath &amp; &quot;\&quot;: &quot; &amp;
	    ErrMsg.StrError(l));
	END;
      END;
    END;
    self.statusFile.delete(name, isDirUp := TRUE);

    StatsUpdate(self, FileUpdater.UpdateType.Other, 0);
  END RemoveDirectory;
</PRE>***************************************************************************

<P><PRE>PROCEDURE <A NAME="DoQueuedExecutes"><procedure>DoQueuedExecutes</procedure></A>(self: T;
                           sfr: SupFileRec.T)
  RAISES {Error} =
  BEGIN
    FOR i := 1 TO self.executes.size() DO
      WITH pe = NARROW(self.executes.remlo(), ParsedExecute) DO
	ExecCommand(self, sfr, pe.name, pe.command);
      END;
    END;
  END DoQueuedExecutes;

PROCEDURE <A NAME="ExecCommand"><procedure>ExecCommand</procedure></A>(self: T;
                      sfr: SupFileRec.T;
		      name: Pathname.T;
		      command: TEXT)
  RAISES {Error} =
  VAR
    path := SupMisc.CatPath(sfr.clientPrefix, name);
    pos := 0;
    len := Text.Length(command);
    expCmd := &quot;&quot;;
    pctPos: INTEGER;
    stdin, stdout, stderr: File.T;
    child: Process.T;
    status: Process.ExitCode;
    modTime: Time.T;
  BEGIN
    (* Expand &quot;%&quot; escapes. *)
    WHILE pos &lt; len DO
      pctPos := Text.FindChar(command, '%', pos);
      IF pctPos = -1 THEN pctPos := len END;
      expCmd := expCmd &amp; Text.Sub(command, pos, pctPos-pos);
      pos := pctPos;
      IF pos &lt; len THEN  (* Found a &quot;%&quot;. *)
	IF pos+1 &lt; len THEN
	  WITH ch = Text.GetChar(command, pos+1) DO
	    IF ch = 's' THEN
	      expCmd := expCmd &amp; path;
	    ELSIF ch = '%' THEN
	      expCmd := expCmd &amp; &quot;%&quot;;
	    ELSE
	      expCmd := expCmd &amp; &quot;%&quot; &amp; Text.FromChar(ch);
	    END;
	    INC(pos, 2);
	  END;
	ELSE
	  expCmd := expCmd &amp; &quot;%&quot;;
	  INC(pos);
	END;
      END;
    END;

    IF SupFileRec.Option.Execute IN sfr.options THEN
      Trace(self, &quot; Execute &quot; &amp; expCmd);
      TRY
	modTime := FS.Status(path).modificationTime;
      EXCEPT OSError.E =&gt; modTime := -1.0d0 END;
      Process.GetStandardFileHandles(stdin, stdout, stderr);
      TRY
	child := Process.Create(
	  cmd := SupMisc.ShellPath,
	  params := ARRAY [0..1] OF TEXT{&quot;-c&quot;, expCmd},
	  stdin := stdin, stdout := stdout, stderr := stderr);
      EXCEPT OSError.E(l) =&gt;
	RAISE Error(&quot;Cannot execute command: &quot; &amp; ErrMsg.StrError(l));
      END;
      status := Process.Wait(child);
      (* FIXME - This is Unix specific. *)
      WITH sig = Word.Extract(status, 0, 7),
	code = Word.Extract(status, 8, 8)
      DO
	IF sig # 0 THEN
	  Trace(self, &quot; [signal &quot; &amp; Fmt.Int(sig) &amp; &quot;]&quot;);
	ELSIF code # 0 THEN
	  Trace(self, &quot; [exit code &quot; &amp; Fmt.Int(code) &amp; &quot;]&quot;);
	END;
      END;
      IF modTime # -1.0d0 THEN
	TRY
	  FS.SetModificationTime(path, modTime)
	EXCEPT OSError.E =&gt; (* Ignore *) END;
      END;
    ELSE
      Trace(self, &quot; NoExecute &quot; &amp; expCmd);
    END;
  END ExecCommand;
</PRE>***************************************************************************

<P><PRE>PROCEDURE <A NAME="Init"><procedure>Init</procedure></A>(self: T;
	       proto: CVProto.T;
	       rd: StreamRd.T;
	       collections: SupFileRecSeq.T;
	       fixups: SyncFixupQueue.T;
	       deleteLimit := -1;
	       reaper: Reaper.T := NIL;
	       destDir: TEXT := NIL;
               stats: Stats := NIL;
	       trace: Logger.T := NIL): T =
  BEGIN
    self.proto := proto;
    self.userDestDir := destDir;
    self.wireRd := rd;
    self.rd := rd;
    self.collections := collections;
    self.executes := NEW(RefSeq.T).init();
    self.fixups := fixups;
    self.deleteLimit := deleteLimit;
    self.reaper := reaper;
    self.stats := stats;
    self.trace := trace;
    RETURN self;
  END Init;

PROCEDURE <A NAME="Delete"><procedure>Delete</procedure></A>(self: T;
                 sfr: SupFileRec.T;
		 name: Pathname.T)
  RAISES {Error} =
  VAR
    srcPath := SupMisc.CatPath(sfr.clientPrefix, name);
    destPath := SupMisc.CatPath(self.destDir, srcPath);
  BEGIN
    IF SupFileRec.Option.Delete IN sfr.options
    AND Text.Equal(destPath, srcPath) THEN
      Trace(self, &quot; Delete &quot; &amp; name);
      IF self.deleteLimit &gt;= 0 AND self.deleteCount &gt;= self.deleteLimit THEN
	RAISE Error(&quot;File deletion limit exceeded&quot;);
      END;
      INC(self.deleteCount);
      IF NOT SupFileRec.Option.CheckoutMode IN sfr.options THEN
	(* Try the attic. *)
	WITH atticName = SupMisc.AtticName(destPath) DO
	  DeleteFile(self, atticName);
	  (* We always delete the Attic directory when it becomes empty.
	     FIXME - Is that the right thing to do?. *)
	  TRY
	    FileAttr.Delete(SupMisc.PathPrefix(atticName));
	  EXCEPT OSError.E =&gt; (* Ignore. *) END;
	END;
      END;
      DeleteFile(self, destPath);
      IF SupFileRec.Option.CheckoutMode IN sfr.options
      OR NOT self.proto.v.dirsAreExplicit THEN
	(* Delete the directory automatically if it is now empty. *)
	DeleteAncestorDirectories(self, sfr, name);
      END;
    ELSE
      Trace(self, &quot; NoDelete &quot; &amp; name);
    END;
    StatsUpdate(self, FileUpdater.UpdateType.Delete, 0);
  END Delete;

PROCEDURE <A NAME="DeleteAncestorDirectories"><procedure>DeleteAncestorDirectories</procedure></A>(&lt;*UNUSED*&gt; self: T;
                                               sfr: SupFileRec.T;
			                       name: Pathname.T) =
</PRE><BLOCKQUOTE><EM> Delete the ancestor directories of <CODE>name</CODE> up to the first one that
   can't be deleted, but stop at the client prefix. </EM></BLOCKQUOTE><PRE>
  VAR
    path := SupMisc.CatPath(sfr.clientPrefix, name);
    prefixLen := Text.Length(sfr.clientPrefix);
  BEGIN
    TRY
      LOOP
	path := SupMisc.PathPrefix(path);
	IF Text.Length(path) &lt;= prefixLen THEN EXIT END;
	FS.DeleteDirectory(path);
      END;
    EXCEPT OSError.E =&gt; (* Break out of the loop. *) END;
  END DeleteAncestorDirectories;

PROCEDURE <A NAME="MakeDirectories"><procedure>MakeDirectories</procedure></A>(path: Pathname.T; umask: INTEGER)
  RAISES {Error} =
  BEGIN
    TRY
      SupMisc.MakeDirectories(path, umask);
    EXCEPT OSError.E(list) =&gt;
      RAISE Error(&quot;Cannot create directories leading to \&quot;&quot; &amp; path
	&amp; &quot;\&quot;: &quot; &amp; ErrMsg.StrError(list));
    END;
  END MakeDirectories;

PROCEDURE <A NAME="DeleteFile"><procedure>DeleteFile</procedure></A>(self: T; path: Pathname.T) =
  BEGIN
    TRY
      FileAttr.Delete(path);
    EXCEPT OSError.E(l) =&gt;
      IF l.head # EnoentAtom THEN
	Warn(self, &quot;Cannot delete \&quot;&quot; &amp; path &amp; &quot;\&quot;: &quot; &amp;
	  ErrMsg.StrError(l));
      END;
    END;
  END DeleteFile;

PROCEDURE <A NAME="Install"><procedure>Install</procedure></A>(attr: FileAttr.T;
                  from, via, to: Pathname.T)
  RAISES {Error} =
  BEGIN
    TRY
      EVAL FileAttr.Install(attr, from := from, to := via);
    EXCEPT OSError.E(list) =&gt;
      RAISE Error(&quot;Cannot install \&quot;&quot; &amp; from &amp; &quot;\&quot; to \&quot;&quot;
	&amp; via &amp; &quot;\&quot;: &quot; &amp; ErrMsg.StrError(list));
    END;
    IF NOT Text.Equal(via, to) THEN  (* Move file to or from the Attic. *)
      TRY
	WITH noAttr = FileAttr.MaskOut(attr, FileAttr.AllButFileType) DO
	  EVAL FileAttr.Install(noAttr, from := via, to := to);
	END;
      EXCEPT OSError.E(list) =&gt;
	RAISE Error(&quot;Cannot move \&quot;&quot; &amp; via &amp; &quot;\&quot; to \&quot;&quot;
	  &amp; to &amp; &quot;\&quot;: &quot; &amp; ErrMsg.StrError(list));
      END;
    END;
  END Install;
</PRE>***************************************************************************

<P><PRE>REVEAL
  <A NAME="Stats">Stats</A> = StatsPublic BRANDED OBJECT
  OVERRIDES
    init := StatsInit;
    start := StatsDoNothing0;
    update := StatsDoNothing1;
    finish := StatsDoNothing0;
  END;

PROCEDURE <A NAME="StatsInit"><procedure>StatsInit</procedure></A>(self: Stats): Stats =
  BEGIN
    RETURN self;
  END StatsInit;

PROCEDURE <A NAME="StatsDoNothing0"><procedure>StatsDoNothing0</procedure></A>(&lt;*UNUSED*&gt; self: Stats) =
  BEGIN
  END StatsDoNothing0;

PROCEDURE <A NAME="StatsDoNothing1"><procedure>StatsDoNothing1</procedure></A>(&lt;*UNUSED*&gt; self: Stats;
                          &lt;*UNUSED*&gt; type: FileUpdater.UpdateType) =
  BEGIN
  END StatsDoNothing1;

PROCEDURE <A NAME="StatsUpdate"><procedure>StatsUpdate</procedure></A>(self: T;
                      updateType: FileUpdater.UpdateType;
		      fileBytes: CARDINAL) =
  VAR
    commBytes, wireBytes: LONGREAL;
  BEGIN
    IF self.stats # NIL THEN
      wireBytes := StreamRd.ByteCount(self.wireRd) - self.startingWireBytes;
      commBytes := StreamRd.ByteCount(self.rd) - self.startingCommBytes;
      LOCK self.stats DO
	WITH info = self.stats.updateInfo[updateType] DO
	  INC(info.fileCount);
	  info.fileBytes := info.fileBytes + FLOAT(fileBytes, LONGREAL);
	  info.wireBytes := info.wireBytes + wireBytes;
	  info.commBytes := info.commBytes + commBytes;
	END;
	WITH info = self.stats.totals DO
	  INC(info.fileCount);
	  info.fileBytes := info.fileBytes + FLOAT(fileBytes, LONGREAL);
	  info.wireBytes := info.wireBytes + wireBytes;
	  info.commBytes := info.commBytes + commBytes;
	END;
      END;
      self.stats.update(updateType);
    END;
  END StatsUpdate;
</PRE>***************************************************************************

<P><PRE>PROCEDURE <A NAME="Warn"><procedure>Warn</procedure></A>(self: T; msg: TEXT) =
  BEGIN
    IF self.trace # NIL THEN
      Logger.Put(self.trace, Logger.Priority.Warning, msg);
    END;
  END Warn;

PROCEDURE <A NAME="Trace"><procedure>Trace</procedure></A>(self: T; msg: TEXT) =
  BEGIN
    IF self.trace # NIL THEN
      Logger.Put(self.trace, Logger.Priority.Notice, msg);
    END;
  END Trace;

BEGIN
END Updater.
</PRE>
</inModule>
<HR>
<A NAME="x1">interface SupFileRecSeq is in:
</A><UL>
<LI><A HREF="../derived/SupFileRecSeq.i3.html#0TOP0">client/derived/SupFileRecSeq.i3</A>
<LI><A HREF="../../server/derived/SupFileRecSeq.i3.html#0TOP0">server/derived/SupFileRecSeq.i3</A>
</UL>
<P>
<HR>
<A NAME="x2">interface Uerror is in:
</A><UL>
<LI><A HREF="../../m3core/src/unix/Common/Uerror.i3.html#0TOP0">m3core/src/unix/Common/Uerror.i3</A>
<LI><A HREF="../../m3core/src/unix/linux-common/Uerror.i3.html#0TOP0">m3core/src/unix/linux-common/Uerror.i3</A>
</UL>
<P>
<PRE>























</PRE>
</BODY>
</HTML>
