<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: client/src/SupFile.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>client/src/SupFile.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright 1996-2003 John D. Polstra.
 * All rights reserved.
 *
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions
 * are met:
 * 1. Redistributions of source code must retain the above copyright
 *    notice, this list of conditions and the following disclaimer.
 * 2. Redistributions in binary form must reproduce the above copyright
 *    notice, this list of conditions and the following disclaimer in the
 *    documentation and/or other materials provided with the distribution.
 * 3. All advertising materials mentioning features or use of this software
 *    must display the following acknowledgment:
 *      This product includes software developed by John D. Polstra.
 * 4. The name of the author may not be used to endorse or promote products
 *    derived from this software without specific prior written permission.
 *
 * THIS SOFTWARE IS PROVIDED BY THE AUTHOR ``AS IS'' AND ANY EXPRESS OR
 * IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES
 * OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED.
 * IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR ANY DIRECT, INDIRECT,
 * INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT
 * NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
 * DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
 * THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
 * (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF
 * THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 *
 * $Id: SupFile.m3.html,v 1.3 2010-04-29 17:17:58 wagner Exp $ 

<P><PRE>MODULE <module><implements><A HREF="SupFile.i3.html">SupFile</A></implements></module>;

IMPORT
  <A HREF="../../suplib/src/ErrMsg.i3.html">ErrMsg</A>, <A HREF="../../libm3/src/rw/FileRd.i3.html">FileRd</A>, <A HREF="../../libm3/src/os/Common/OSError.i3.html">OSError</A>, <A HREF="../../libm3/src/os/Common/Pathname.i3.html">Pathname</A>, <A HREF="../../suplib/src/RCSDate.i3.html">RCSDate</A>, <A HREF="../../libm3/src/rw/Rd.i3.html">Rd</A>, <A HREF="../../suplib/src/SupFileRec.i3.html">SupFileRec</A>,
  <A HREF="#x1">SupFileRecSeq</A>, <A HREF="../../suplib/src/SupMisc.i3.html">SupMisc</A>, <A HREF="../../m3core/src/text/Text.i3.html">Text</A>, <A HREF="../../m3core/src/thread/Common/Thread.i3.html">Thread</A>, <A HREF="../../suplib/src/TokScan.i3.html">TokScan</A>, <A HREF="../../suplib/src/UnixMisc.i3.html">UnixMisc</A>, <A HREF="#x2">Uugid</A>;

CONST
  PreserveOptions = SupFileRec.Options{  (* Options set for &quot;preserve&quot;. *)
    SupFileRec.Option.SetOwner,
    SupFileRec.Option.SetFlags };

PROCEDURE <A NAME="Parse"><procedure>Parse</procedure></A>(fileName: Pathname.T;
		override: SupFileRec.T := NIL;
		mask := SupFileRec.Options{}): SupFileRecSeq.T
  RAISES {Error} =
  &lt;* FATAL Thread.Alerted *&gt;
  VAR
    rd: Rd.T;
    collections: SupFileRecSeq.T;
    line, field: TEXT;
    ts, ts2: TokScan.T;
    coll: TEXT;
    default: SupFileRec.T := NIL;
    sfr: SupFileRec.T;
    name, value: TEXT;
    noCheckRCS: BOOLEAN;
    dfltNoCheckRCS := FALSE;
  BEGIN
    TRY
      rd := FileRd.Open(fileName);
    EXCEPT OSError.E(list) =&gt;
      RAISE Error(&quot;Cannot open \&quot;&quot; &amp; fileName &amp; &quot;\&quot;: &quot; &amp;
	ErrMsg.StrError(list));
    END;
    TRY
      collections := NEW(SupFileRecSeq.T).init();
      LOOP
	TRY
	  line := Rd.GetLine(rd);
	EXCEPT
	| Rd.EndOfFile =&gt;
	  EXIT;
	| Rd.Failure(list) =&gt;
	  RAISE Error(&quot;Read failure from \&quot;&quot; &amp; fileName &amp; &quot;\&quot;: &quot; &amp;
	    ErrMsg.StrError(list));
	END;
	WITH pos = Text.FindChar(line, '#') DO
	  IF pos &gt;= 0 THEN line := Text.Sub(line, 0, pos) END;
	END;
	ts := TokScan.New(line);
	TRY
	  IF ts.next(coll) THEN
	    sfr := NEW(SupFileRec.T).init(default);
	    sfr.collection := coll;
	    noCheckRCS := dfltNoCheckRCS;

	    WHILE ts.next(field) DO
	      IF Text.FindChar(field, '=') &gt;= 0 THEN
		ts2 := TokScan.New(field, SET OF CHAR{'='});
		name := ts2.getToken(&quot;field name&quot;);
		value := ts2.getToken(&quot;Field value&quot;);
		IF TokScan.EqualFolded(name, &quot;release&quot;) THEN
		  IF NOT Pathname.Valid(value) THEN
		    RAISE Error(&quot;Invalid release \&quot;&quot; &amp; value &amp; &quot;\&quot;&quot;);
		  END;
		  sfr.release := value;
		ELSIF TokScan.EqualFolded(name, &quot;host&quot;) THEN
		  sfr.serverHost := value;
		ELSIF TokScan.EqualFolded(name, &quot;base&quot;) THEN
		  IF NOT Pathname.Valid(value) THEN
		    RAISE Error(&quot;Invalid base \&quot;&quot; &amp; value &amp; &quot;\&quot;&quot;);
		  END;
		  sfr.clientBase := value;
		ELSIF TokScan.EqualFolded(name, &quot;prefix&quot;) THEN
		  IF NOT Pathname.Valid(value) THEN
		    RAISE Error(&quot;Invalid prefix \&quot;&quot; &amp; value &amp; &quot;\&quot;&quot;);
		  END;
		  sfr.clientPrefix := value;
		ELSIF TokScan.EqualFolded(name, &quot;tag&quot;) THEN
		  sfr.options := sfr.options +
		    SupFileRec.Options{SupFileRec.Option.CheckoutMode};
		  sfr.checkoutTag := value;
		ELSIF TokScan.EqualFolded(name, &quot;date&quot;) THEN
		  IF NOT Text.Equal(value, &quot;.&quot;)
		  AND NOT RCSDate.Valid(value) THEN
		    RAISE Error(&quot;Invalid date \&quot;&quot; &amp; value &amp; &quot;\&quot;&quot;);
		  END;
		  sfr.options := sfr.options +
		    SupFileRec.Options{SupFileRec.Option.CheckoutMode};
		  sfr.checkoutDate := value;
		ELSIF TokScan.EqualFolded(name, &quot;list&quot;) THEN
		  IF Text.FindChar(value, SupMisc.SlashChar) &gt;= 0 THEN
		    RAISE Error(&quot;\&quot;list\&quot; suffix must not contain slashes&quot;);
		  END;
		  sfr.listSuffix := value;
		ELSIF TokScan.EqualFolded(name, &quot;umask&quot;) THEN
		  sfr.umask := TokScan.AtoI(value, &quot;umask value&quot;, 8);
		END;
	      ELSE
		IF TokScan.EqualFolded(field, &quot;backup&quot;) THEN
		  sfr.options := sfr.options +
		    SupFileRec.Options{SupFileRec.Option.Backup};
		ELSIF TokScan.EqualFolded(field, &quot;delete&quot;) THEN
		  sfr.options := sfr.options + SupFileRec.Options{
		    SupFileRec.Option.Delete,
		    SupFileRec.Option.ExactRCS};
		ELSIF TokScan.EqualFolded(field, &quot;keep&quot;) THEN
		  sfr.options := sfr.options +
		    SupFileRec.Options{SupFileRec.Option.Keep};
		ELSIF TokScan.EqualFolded(field, &quot;old&quot;) THEN
		  sfr.options := sfr.options +
		    SupFileRec.Options{SupFileRec.Option.Old};
		ELSIF TokScan.EqualFolded(field, &quot;unlinkbusy&quot;) THEN
		  sfr.options := sfr.options +
		    SupFileRec.Options{SupFileRec.Option.UnlinkBusy};
		ELSIF TokScan.EqualFolded(field, &quot;noupdate&quot;) THEN
		  sfr.options := sfr.options +
		    SupFileRec.Options{SupFileRec.Option.NoUpdate};
		ELSIF TokScan.EqualFolded(field, &quot;compress&quot;) THEN
		  sfr.options := sfr.options +
		    SupFileRec.Options{SupFileRec.Option.Compress};
		ELSIF TokScan.EqualFolded(field, &quot;use-rel-suffix&quot;) THEN
		  sfr.options := sfr.options +
		    SupFileRec.Options{SupFileRec.Option.UseRelSuffix};
		ELSIF TokScan.EqualFolded(field, &quot;norsync&quot;) THEN
		  sfr.options := sfr.options +
		    SupFileRec.Options{SupFileRec.Option.NoRsync};
		ELSIF TokScan.EqualFolded(field, &quot;norcs&quot;) THEN
		  sfr.options := sfr.options +
		    SupFileRec.Options{SupFileRec.Option.NoRCS};
		ELSIF TokScan.EqualFolded(field, &quot;nocheckrcs&quot;) THEN
		  noCheckRCS := TRUE;
		ELSIF TokScan.EqualFolded(field, &quot;strictrcs&quot;) THEN
		  sfr.options := sfr.options +
		    SupFileRec.Options{SupFileRec.Option.StrictCheckRCS};
		ELSIF TokScan.EqualFolded(field, &quot;execute&quot;) THEN
		  sfr.options := sfr.options +
		    SupFileRec.Options{SupFileRec.Option.Execute};
		ELSIF TokScan.EqualFolded(field, &quot;preserve&quot;) THEN
		  sfr.options := sfr.options + PreserveOptions;
		END;
	      END;
	    END;

	    IF Text.Equal(sfr.collection, &quot;*default&quot;) THEN
	      default := sfr;
	      dfltNoCheckRCS := noCheckRCS;
	    ELSE
	      IF override # NIL THEN
		sfr.overrideFrom(override, mask);
	      END;

	      (* Determine whether to checksum RCS files or not. *)
	      IF SupFileRec.Option.ExactRCS IN sfr.options
	      AND NOT noCheckRCS THEN
		sfr.options := sfr.options +
		  SupFileRec.Options{SupFileRec.Option.CheckRCS};
	      ELSE
		sfr.options := sfr.options -
		  SupFileRec.Options{SupFileRec.Option.CheckRCS};
	      END;

	      (* In recent versions, we always try to set the file modes. *)
	      sfr.options := sfr.options + SupFileRec.Options{
		SupFileRec.Option.SetMode};
	      (* Ignore &quot;preserve&quot; in checkout mode. *)
	      IF SupFileRec.Option.CheckoutMode IN sfr.options THEN
		sfr.options := sfr.options - PreserveOptions;
	      END;
	      (* Preserve all mode bits if &quot;preserve&quot; is set. *)
	      IF sfr.options &gt;= PreserveOptions THEN
		sfr.umask := 0;
	      END;
	      (* If no umask was specified, default it to the OS umask. *)
	      IF sfr.umask = -1 THEN
		sfr.umask := UnixMisc.GetUmask();
	      END;
	      (* If the client is not the superuser, don't try to preserve
		 the owner, group, and flags.  FIXME - Unix specific. *)
	      IF Uugid.geteuid() # 0 THEN  (* FIXME - Unix specific. *)
		sfr.options := sfr.options - PreserveOptions;
	      END;

	      IF NOT Pathname.Valid(sfr.collection) THEN
		RAISE Error(&quot;Invalid collection \&quot;&quot; &amp; sfr.collection &amp; &quot;\&quot;&quot;);
	      END;
	      IF sfr.release = NIL THEN
		RAISE Error(&quot;Release not specified for collection \&quot;&quot; &amp;
		  sfr.collection &amp; &quot;\&quot;&quot;);
	      END;
	      IF sfr.serverHost = NIL THEN
		RAISE Error(&quot;Host not specified for collection \&quot;&quot; &amp;
		  sfr.collection &amp; &quot;\&quot;&quot;);
	      END;
	      IF collections.size() &gt; 0
	      AND NOT TokScan.EqualFolded(sfr.serverHost,
		collections.gethi().serverHost)
	      THEN
		RAISE
		  Error(&quot;All \&quot;host\&quot; fields in the supfile must be the same&quot;);
	      END;

	      IF sfr.clientBase = NIL THEN
		sfr.clientBase := SupMisc.DefaultClientBase;
	      END;
	      IF NOT SupMisc.IsDirectory(sfr.clientBase) THEN
		RAISE Error(&quot;Nonexistent base directory \&quot;&quot;
		  &amp; sfr.clientBase &amp; &quot;\&quot; for collection \&quot;&quot;
		  &amp; sfr.collection &amp; &quot;\&quot;&quot;);
	      END;

	      IF sfr.clientPrefix = NIL THEN
		sfr.clientPrefix := sfr.clientBase;
	      ELSE
		sfr.clientPrefix :=
		  SupMisc.ResolvePath(sfr.clientBase, sfr.clientPrefix);
	      END;

	      IF sfr.clientCollDir = NIL THEN
		sfr.clientCollDir := SupMisc.DefaultClientCollDir;
	      END;

	      ParseRefuseFiles(sfr);
	      collections.addhi(sfr);
	    END;
	  END;
	EXCEPT TokScan.Error(msg) =&gt;
	  RAISE Error(&quot;Parse error in \&quot;&quot; &amp; fileName &amp; &quot;\&quot;: &quot; &amp; msg);
	END;
      END;
      IF collections.size() = 0 THEN
	RAISE Error(&quot;Empty supfile&quot;);
      END;
    FINALLY
      TRY
	Rd.Close(rd);
      EXCEPT Rd.Failure(list) =&gt;
	RAISE Error(&quot;Cannot close \&quot;&quot; &amp; fileName &amp; &quot;\&quot;: &quot; &amp;
	  ErrMsg.StrError(list));
      END;
    END;
    RETURN collections;
  END Parse;

PROCEDURE <A NAME="ParseRefuseFile"><procedure>ParseRefuseFile</procedure></A>(sfr: SupFileRec.T; filename: Pathname.T)
  RAISES {Error} =
</PRE><BLOCKQUOTE><EM> Parses a <CODE>refuse</CODE> file, and records the relevant information
   in <CODE>sfr.refusals</CODE>.  If the file does not exist, it is silently
   ignored. </EM></BLOCKQUOTE><PRE>
  &lt;* FATAL Thread.Alerted *&gt;
  VAR
    rd: Rd.T;
    line: TEXT;
    ts: TokScan.T;
    tok: TEXT;
  BEGIN
    TRY
      rd := FileRd.Open(filename);
    EXCEPT OSError.E =&gt;
      RETURN;
    END;
    TRY
      LOOP
	TRY
	  line := Rd.GetLine(rd);
	EXCEPT
	| Rd.EndOfFile =&gt;
	  EXIT;
	| Rd.Failure(list) =&gt;
	  RAISE Error(&quot;Read failure from \&quot;&quot; &amp; filename &amp; &quot;\&quot;: &quot; &amp;
	    ErrMsg.StrError(list));
	END;
	ts := TokScan.New(line);
	&lt;* FATAL TokScan.Error *&gt;
	BEGIN
	  WHILE ts.next(tok) DO
	    sfr.refusals.addhi(tok);
	  END;
	END;
      END;
    FINALLY
      TRY
	Rd.Close(rd);
      EXCEPT Rd.Failure(list) =&gt;
	RAISE Error(&quot;Cannot close \&quot;&quot; &amp; filename &amp; &quot;\&quot;: &quot; &amp;
	  ErrMsg.StrError(list));
      END;
    END;
  END ParseRefuseFile;

PROCEDURE <A NAME="ParseRefuseFiles"><procedure>ParseRefuseFiles</procedure></A>(sfr: SupFileRec.T)
  RAISES {Error} =
  VAR
    supDir := SupMisc.ResolvePath(sfr.clientBase, sfr.clientCollDir);
    collStem := SupMisc.CatPath(supDir,
      SupMisc.CatPath(sfr.collection, &quot;refuse&quot;));
  BEGIN
    (* First the global refuse file that applies to all collections. *)
    ParseRefuseFile(sfr, SupMisc.CatPath(supDir, &quot;refuse&quot;));

    (* Next the per-collection refuse file that applies to all release/tag
       combinations. *)
    ParseRefuseFile(sfr, collStem);

    (* Finally, the per-release and per-tag refuse file. *)
    WITH suffix = SupMisc.StatusFileSuffix(sfr) DO
      IF NOT Text.Empty(suffix) THEN  (* Different from per-collection. *)
	ParseRefuseFile(sfr, collStem &amp; suffix);
      END;
    END;
  END ParseRefuseFiles;

BEGIN
END SupFile.
</PRE>
</inModule>
<HR>
<A NAME="x1">interface SupFileRecSeq is in:
</A><UL>
<LI><A HREF="../derived/SupFileRecSeq.i3.html#0TOP0">client/derived/SupFileRecSeq.i3</A>
<LI><A HREF="../../server/derived/SupFileRecSeq.i3.html#0TOP0">server/derived/SupFileRecSeq.i3</A>
</UL>
<P>
<HR>
<A NAME="x2">interface Uugid is in:
</A><UL>
<LI><A HREF="../../m3core/src/unix/Common/Uugid.i3.html#0TOP0">m3core/src/unix/Common/Uugid.i3</A>
<LI><A HREF="../../m3core/src/unix/linux-common/Uugid.i3.html#0TOP0">m3core/src/unix/linux-common/Uugid.i3</A>
</UL>
<P>
<PRE>























</PRE>
</BODY>
</HTML>
