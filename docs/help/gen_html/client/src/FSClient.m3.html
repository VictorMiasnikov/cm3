<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: client/src/FSClient.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>client/src/FSClient.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright 1996-2003 John D. Polstra.
 * All rights reserved.
 *
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions
 * are met:
 * 1. Redistributions of source code must retain the above copyright
 *    notice, this list of conditions and the following disclaimer.
 * 2. Redistributions in binary form must reproduce the above copyright
 *    notice, this list of conditions and the following disclaimer in the
 *    documentation and/or other materials provided with the distribution.
 * 3. All advertising materials mentioning features or use of this software
 *    must display the following acknowledgment:
 *      This product includes software developed by John D. Polstra.
 * 4. The name of the author may not be used to endorse or promote products
 *    derived from this software without specific prior written permission.
 *
 * THIS SOFTWARE IS PROVIDED BY THE AUTHOR ``AS IS'' AND ANY EXPRESS OR
 * IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES
 * OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED.
 * IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR ANY DIRECT, INDIRECT,
 * INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT
 * NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
 * DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
 * THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
 * (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF
 * THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 *
 * $Id: FSClient.m3.html,v 1.3 2010-04-29 17:17:57 wagner Exp $ 

<P><PRE>MODULE <module><implements><A HREF="FSClient.i3.html">FSClient</A></implements></module>;

IMPORT
  <A HREF="Auth.i3.html">Auth</A>, <A HREF="../../suplib/src/AuthMD5.i3.html">AuthMD5</A>, <A HREF="../../suplib/src/ChannelMux.i3.html">ChannelMux</A>, <A HREF="../../suplib/src/CVProto.i3.html">CVProto</A>, <A HREF="Detailer.i3.html">Detailer</A>, <A HREF="../../libm3/src/params/Env.i3.html">Env</A>, <A HREF="../../suplib/src/ErrMsg.i3.html">ErrMsg</A>, <A HREF="../../suplib/src/FileAttr.i3.html">FileAttr</A>,
  <A HREF="../../libm3/src/fmtlex/Fmt.i3.html">Fmt</A>, <A HREF="#x1">Glob</A>, <A HREF="#x2">GlobTree</A>, <A HREF="../../suplib/src/IOWatchDog.i3.html">IOWatchDog</A>, <A HREF="../../tcp/src/common/IP.i3.html">IP</A>, <A HREF="../../suplib/src/LockFile.i3.html">LockFile</A>, <A HREF="../../suplib/src/Logger.i3.html">Logger</A>, <A HREF="../../libm3/src/os/Common/OSError.i3.html">OSError</A>,
  <A HREF="../../libm3/src/os/Common/Pathname.i3.html">Pathname</A>, <A HREF="../../libm3/src/os/Common/Process.i3.html">Process</A>, <A HREF="../../suplib/src/RCSKeyword.i3.html">RCSKeyword</A>, <A HREF="../../libm3/src/rw/Rd.i3.html">Rd</A>, <A HREF="../../suplib/src/Reaper.i3.html">Reaper</A>, <A HREF="../../suplib/src/SigHandler.i3.html">SigHandler</A>, <A HREF="../../tcp/src/common/StreamRd.i3.html">StreamRd</A>,
  <A HREF="../../tcp/src/common/StreamWr.i3.html">StreamWr</A>, <A HREF="../../suplib/src/SupFileRec.i3.html">SupFileRec</A>, <A HREF="../../suplib/src/SupMisc.i3.html">SupMisc</A>, <A HREF="../derived/SyncFixupQueue.i3.html">SyncFixupQueue</A>, <A HREF="../../tcp/src/common/TCPMisc.i3.html">TCPMisc</A>, <A HREF="../../m3core/src/text/Text.i3.html">Text</A>,
  <A HREF="../../m3core/src/thread/Common/Thread.i3.html">Thread</A>, <A HREF="../../suplib/src/TokScan.i3.html">TokScan</A>, <A HREF="TreeList.i3.html">TreeList</A>, <A HREF="../../suplib/src/UnixMisc.i3.html">UnixMisc</A>, <A HREF="Updater.i3.html">Updater</A>, <A HREF="#x3">Usignal</A>, <A HREF="#x4">Version</A>,
  <A HREF="../../suplib/src/WatchDog.i3.html">WatchDog</A>, <A HREF="../../libm3/src/rw/Wr.i3.html">Wr</A>;

IMPORT <A HREF="../../tcp/src/common/ConnRW.i3.html">ConnRW</A>;
IMPORT <A HREF="../../tcp/src/common/TCP.i3.html">TCP</A>;

EXCEPTION
  Error(TEXT);
  TransientFailure(TEXT);

CONST
  AuthFile = &quot;.cvsup/auth&quot;;
  IdleTimeout = 15.0d0 * 60.0d0;
  S1GUrl = &quot;http://www.cvsup.org/s1g/&quot;;

REVEAL
  <A NAME="T">T</A> = Public BRANDED OBJECT
      config: Configuration;
      server: TEXT;
      peer: IP.Endpoint;
      proto: CVProto.T := NIL;
      tcp0, tcp1, tcp2, tcp3: TCP.T := NIL;
      mux: ChannelMux.T := NIL;
      rdA, rdB, oldRdA: StreamRd.T := NIL;
      wrA, wrB, oldWrA: StreamWr.T := NIL;
      watchDog: IOWatchDog.T := NIL;
      authPath: Pathname.T;
      authDB: Auth.DB := NIL;
    OVERRIDES
      init := Init;
      apply := Apply;
    END;

PROCEDURE <A NAME="Init"><procedure>Init</procedure></A>(self: T; config: Configuration): T =
  BEGIN
    self.config := config;
    IF self.config.hiDataPort = IP.NullPort THEN
      self.config.hiDataPort := self.config.loDataPort;
    END;
    RETURN self;
  END Init;

PROCEDURE <A NAME="Apply"><procedure>Apply</procedure></A>(self: T): REFANY =
  VAR
    fixups: SyncFixupQueue.T;
    reaper: Reaper.T;
    lister: TreeList.T;
    detailer: Detailer.T;
    updater: Updater.T;
    listerThread: Thread.T;
    detailerThread: Thread.T;
    updaterThread: Thread.T;
    threadStatus: SupMisc.ThreadStatus;
    lock: LockFile.T := NIL;
    homeDir: Pathname.T;
    killer: Killer := NIL;
    retVal: REFANY;
    thread: Thread.T;
  BEGIN
    TRY
      IF self.config.lockFile # NIL THEN
	TRY
	  lock := LockFile.Lock(self.config.lockFile);
	EXCEPT OSError.E(l) =&gt;
	  RAISE Error(&quot;Error locking \&quot;&quot; &amp; self.config.lockFile
	    &amp; &quot;\&quot;: &quot; &amp; ErrMsg.StrError(l));
	END;
	IF lock = NIL THEN  (* Already locked. *)
	  RAISE Error(&quot;\&quot;&quot; &amp; self.config.lockFile
	    &amp; &quot;\&quot; is already locked by another process&quot;);
	END;
      END;
      TRY
	homeDir := Env.Get(&quot;HOME&quot;);
	IF homeDir = NIL THEN
	  RAISE Error(&quot;Environment variable $\&quot;HOME\&quot; is not set&quot;);
	END;
	self.authPath := SupMisc.ResolvePath(homeDir, AuthFile);
	TRY
	  self.authDB := Auth.Open(self.authPath);
	EXCEPT Auth.Error(msg) =&gt;
	  IF self.config.authRequired THEN
	    RAISE Error(msg);
	  END;
	END;
	TRY
	  IF CheckCollections(self) = 0 THEN
	    RAISE Error(&quot;No collections selected&quot;);
	  END;

	  self.server := self.config.collections.get(0).serverHost;

	  TRY
	    EstablishControlConnection(self);

	    self.watchDog := NEW(IOWatchDog.T).init(timeout := IdleTimeout);
	    TRY
	      IOWatchDog.AddRd(self.watchDog, self.rdA);
	      IOWatchDog.AddWr(self.watchDog, self.wrA);

	      ShakeHands(self);
	      AdjustForProtocol(self);
	      Authorize(self);
	      ExchangeAttributeInfo(self);
	      ExchangeCollectionInfo(self);
	      EstablishDataConnection(self);

	      reaper := NEW(Reaper.T).init();
	      fixups := NEW(SyncFixupQueue.T).init();

	      lister := NEW(TreeList.T).init(
		proto := self.proto,
		wr := self.wrA,
		collections := self.config.collections,
		reaper := reaper,
		stats := self.config.listerStats,
		trace := self.config.listerTrace);

	      detailer := NEW(Detailer.T).init(
		proto := self.proto,
		rd := self.rdA,
		wr := self.wrB,
		collections := self.config.collections,
		fixups := fixups,
		reaper := reaper,
		stats := self.config.detailerStats,
		logger := self.config.detailerTrace);

	      updater := NEW(Updater.T).init(
		proto := self.proto,
		rd := self.rdB,
		collections := self.config.collections,
		fixups := fixups,
		deleteLimit := self.config.deleteLimit,
		reaper := reaper,
		destDir := self.config.destDir,
		stats := self.config.updaterStats,
		trace := self.config.updaterTrace);

	      TRY
		killer := NEW(Killer, thread := Thread.Self());
		IF NOT UnixMisc.SigIsIgnored(Usignal.SIGINT) THEN
		  SigHandler.Register(Usignal.SIGINT, killer);
		END;
		IF NOT UnixMisc.SigIsIgnored(Usignal.SIGHUP) THEN
		  SigHandler.Register(Usignal.SIGHUP, killer);
		END;
		IF NOT UnixMisc.SigIsIgnored(Usignal.SIGTERM) THEN
		  SigHandler.Register(Usignal.SIGTERM, killer);
		END;

		(* Start the worker threads. *)
		listerThread := Reaper.Fork(reaper, lister);
		detailerThread := Reaper.Fork(reaper, detailer);
		updaterThread := Reaper.Fork(reaper, updater);

		TRY
		  Trace(self, &quot;Running&quot;);

		  (* Wait until all the threads have finished, or until an
		     error is returned from one of them. *)
		  TRY
		    threadStatus := NIL;
		    WHILE Reaper.AlertJoinNext(reaper, thread, retVal) DO
		      threadStatus := retVal;
		      IF threadStatus.status # SupMisc.ExitCode.Success THEN
			Err(self, threadStatus.message);
			Reaper.AlertAll(reaper);  (* Kill remaining threads. *)
			EXIT
		      END;
		    END;
		  EXCEPT Thread.Alerted =&gt;
		    (* Interrupted from GUI or signal. *)
		    Reaper.AlertAll(reaper);
		    Trace(self, &quot;Cleaning up ...&quot;);
		    RAISE Thread.Alerted;
		  END;
		FINALLY
		  (* Reap all the remaining threads. *)
		  WHILE Reaper.JoinNext(reaper, thread, retVal) DO
		    (* Nothing *)
		  END;
		END;
	      FINALLY
		IF NOT UnixMisc.SigIsIgnored(Usignal.SIGINT) THEN
		  SigHandler.Register(Usignal.SIGINT, NIL);
		END;
		IF NOT UnixMisc.SigIsIgnored(Usignal.SIGHUP) THEN
		  SigHandler.Register(Usignal.SIGHUP, NIL);
		END;
		IF NOT UnixMisc.SigIsIgnored(Usignal.SIGTERM) THEN
		  SigHandler.Register(Usignal.SIGTERM, NIL);
		END;
	      END;

	      IF threadStatus.status = SupMisc.ExitCode.Success THEN
		Trace(self, &quot;Shutting down connection to server&quot;);
		Wr.Close(self.wrA);
		self.wrA := NIL;
		Wr.Close(self.wrB);
		self.wrB := NIL;
		IF NOT Rd.EOF(self.rdA) THEN
		  RAISE Error(&quot;Detailer protocol error: Expected EOF, &quot; &amp;
		      &quot;didn't get it&quot;);
		END;
		IF NOT Rd.EOF(self.rdB) THEN
		  RAISE Error(&quot;Updater protocol error: Expected EOF, &quot; &amp;
		    &quot;didn't get it&quot;);
		END;
		IF self.mux # NIL THEN
		  ChannelMux.Close(self.mux);
		  self.mux := NIL;
		END;
		IF self.oldWrA # NIL THEN
		  Wr.Close(self.oldWrA);
		  self.oldWrA := NIL;
		END;
		Notice(self, &quot;Finished successfully&quot;);
	      END;
	    FINALLY
	      IOWatchDog.Cancel(self.watchDog);
	    END;
	  FINALLY
	    ShutdownConnections(self);
	  END;
	  threadStatus.message := NIL;  (* Already printed. *)
	  RETURN threadStatus;
	FINALLY
	  IF self.authDB # NIL THEN
	    TRY
	      Auth.Close(self.authDB);
	    EXCEPT ELSE END;
	    self.authDB := NIL;
	  END;
	END;
      FINALLY
	IF lock # NIL THEN
	  TRY
	    LockFile.Unlock(lock);
	  EXCEPT OSError.E(l) =&gt;
	    RAISE Error(&quot;Error unlocking \&quot;&quot; &amp; self.config.lockFile
	      &amp; &quot;\&quot;: &quot; &amp; ErrMsg.StrError(l));
	  END;
	END;
      END;
    EXCEPT
    | Error(msg) =&gt;
	Err(self, msg);
	RETURN NEW(SupMisc.ThreadStatus,
	  status := SupMisc.ExitCode.Failure);
    | Rd.EndOfFile =&gt;
	Err(self, &quot;Premature EOF from server&quot;);
	RETURN NEW(SupMisc.ThreadStatus,
	  status := SupMisc.ExitCode.TransientFailure);
    | Rd.Failure(list) =&gt;
	Err(self, &quot;Network read failure: &quot; &amp; ErrMsg.StrError(list));
	RETURN NEW(SupMisc.ThreadStatus,
	  status := SupMisc.ExitCode.TransientFailure);
    | Thread.Alerted =&gt;
	IF self.watchDog # NIL AND IOWatchDog.Expired(self.watchDog) THEN
	  Err(self, &quot;Inactivity timeout&quot;);
	  RETURN NEW(SupMisc.ThreadStatus,
	    status := SupMisc.ExitCode.TransientFailure);
	ELSE
	  Err(self, &quot;Interrupted&quot;);
	  IF killer # NIL AND killer.killedBySignal # -1 THEN
	    EVAL Usignal.kill(Process.GetMyID(), killer.killedBySignal);
	  END;
	  RETURN NEW(SupMisc.ThreadStatus,
	    status := SupMisc.ExitCode.Failure);
	END;
    | TransientFailure(msg) =&gt;
	Err(self, msg);
	RETURN NEW(SupMisc.ThreadStatus,
	  status := SupMisc.ExitCode.TransientFailure);
    | Wr.Failure(list) =&gt;
	Err(self, &quot;Network write failure: &quot; &amp; ErrMsg.StrError(list));
	RETURN NEW(SupMisc.ThreadStatus,
	  status := SupMisc.ExitCode.TransientFailure);
    END;
  END Apply;

PROCEDURE <A NAME="Accept"><procedure>Accept</procedure></A>(self: T;
                 addr: IP.Address;
		 cmd: TEXT): TCP.T
  RAISES {Thread.Alerted, TransientFailure, Wr.Failure} =
</PRE><BLOCKQUOTE><EM> Creates a connector, binds it to an acceptable port on the given
   address, sends its endpoint with the command <CODE>cmd</CODE>, accepts one
   connection from the server, and closes the connector. </EM></BLOCKQUOTE><PRE>
  VAR
    conn: TCP.Connector;
    wd: WatchDog.T;
    epToks: ARRAY [0..4] OF TEXT;
  BEGIN
    conn := NewConnector(addr, self.config.loDataPort, self.config.hiDataPort);
    TRY
      TokScan.EncodeEndpoint(TCP.GetEndPoint(conn), epToks);
      self.proto.putCmd(self.wrA, cmd,
	epToks[0], epToks[1], epToks[2], epToks[3], epToks[4]);
      Wr.Flush(self.wrA);
      TRY
	wd := WatchDog.New(SupMisc.ListenTimeout);
	TRY
	  RETURN TCP.Accept(conn);
	FINALLY
	  WatchDog.Cancel(wd);
	END;
      EXCEPT
      | IP.Error(list) =&gt;
	  RAISE TransientFailure(&quot;Accept failed: &quot;
	    &amp; ErrMsg.StrError(list));
      | Thread.Alerted =&gt;
	  IF WatchDog.Expired(wd) THEN
	    RAISE TransientFailure(
	      &quot;Timed out waiting for connection from server.  Check your&quot;
	      &amp; &quot; firewall setup or try the \&quot;-P m\&quot; option&quot;);
	  ELSE
	    RAISE Thread.Alerted;
	  END;
      END;
    FINALLY
      TCP.CloseConnector(conn);
    END;
  END Accept;

PROCEDURE <A NAME="ShutdownConnections"><procedure>ShutdownConnections</procedure></A>(self: T)
  RAISES {Thread.Alerted} =
</PRE><BLOCKQUOTE><EM> Closes all the network connections, being careful to get it all done
   even if exceptions occur along the way. </EM></BLOCKQUOTE><PRE>
  BEGIN
    IF self.mux # NIL THEN ChannelMux.Close(self.mux) END;

    (* Close all of the TCP channels, being careful to avoid any delays.
       This will close the file descriptors right out from under the
       associated readers and writers.  Below, when we close the readers
       and writers themselves, we'll get some Rd.Failure and Wr.Failure
       exceptions.  But that is better than hanging up waiting for the
       buffer flushes to complete. *)

    IF self.tcp0 # NIL THEN
      TRY TurnOffLinger(self.tcp0) EXCEPT Error =&gt; (* Ignore *) END;
      TCP.Close(self.tcp0);
    END;
    IF self.tcp1 # NIL THEN
      TRY TurnOffLinger(self.tcp1) EXCEPT Error =&gt; (* Ignore *) END;
      TCP.Close(self.tcp1);
    END;
    IF self.tcp2 # NIL THEN
      TRY TurnOffLinger(self.tcp2) EXCEPT Error =&gt; (* Ignore *) END;
      TCP.Close(self.tcp2);
    END;
    IF self.tcp3 # NIL THEN
      TRY TurnOffLinger(self.tcp3) EXCEPT Error =&gt; (* Ignore *) END;
      TCP.Close(self.tcp3);
    END;

    (* Close the readers and writers. *)
    TRY
      IF self.oldRdA # NIL THEN
	TRY Rd.Close(self.oldRdA) EXCEPT Rd.Failure =&gt; (* Ignore *) END;
      END;
    FINALLY
      TRY
	IF self.rdA # NIL THEN
	  TRY Rd.Close(self.rdA) EXCEPT Rd.Failure =&gt; (* Ignore *) END;
	END;
      FINALLY
	TRY
	  IF self.rdB # NIL THEN
	    TRY Rd.Close(self.rdB) EXCEPT Rd.Failure =&gt; (* Ignore *) END;
	  END;
	FINALLY
	  TRY
	    IF self.oldWrA # NIL THEN
	      TRY Wr.Close(self.oldWrA) EXCEPT Wr.Failure =&gt; (* Ignore *) END;
	    END;
	  FINALLY
	    TRY
	      IF self.wrA # NIL THEN
		TRY Wr.Close(self.wrA) EXCEPT Wr.Failure =&gt; (* Ignore *) END;
	      END;
	    FINALLY
	      IF self.wrB # NIL THEN
		TRY Wr.Close(self.wrB) EXCEPT Wr.Failure =&gt; (* Ignore *) END;
	      END;
	    END;
	  END;
	END;
      END;
    END;
  END ShutdownConnections;
</PRE>***************************************************************************

<P><PRE>PROCEDURE <A NAME="GetSockName"><procedure>GetSockName</procedure></A>(tcp: TCP.T): IP.Endpoint
  RAISES {Error} =
  BEGIN
    TRY
      RETURN TCPMisc.GetSockName(tcp);
    EXCEPT IP.Error(list) =&gt;
      RAISE Error(&quot;GetSockName failed: &quot; &amp; ErrMsg.StrError(list));
    END;
  END GetSockName;

PROCEDURE <A NAME="NewConnector"><procedure>NewConnector</procedure></A>(addr: IP.Address;
                       loPort, hiPort: IP.Port): TCP.Connector
  RAISES {TransientFailure} =
  BEGIN
    TRY
      RETURN SupMisc.NewConnector(addr, loPort, hiPort);
    EXCEPT IP.Error(list) =&gt;
      RAISE TransientFailure(&quot;Listen failed: &quot; &amp; ErrMsg.StrError(list));
    END;
  END NewConnector;

PROCEDURE <A NAME="TurnOffLinger"><procedure>TurnOffLinger</procedure></A>(tcp: TCP.T)
  RAISES {Error} =
  BEGIN
    TRY
      TCPMisc.LingerOnClose(tcp, FALSE);
    EXCEPT IP.Error(list) =&gt;
      RAISE Error(&quot;Cannot turn off SO_LINGER: &quot; &amp;
	ErrMsg.StrError(list));
    END;
  END TurnOffLinger;

PROCEDURE <A NAME="TurnOffNoDelay"><procedure>TurnOffNoDelay</procedure></A>(tcp: TCP.T)
  RAISES {Error} =
  BEGIN
    TRY
      TCPMisc.CoalesceWrites(tcp, TRUE);
    EXCEPT IP.Error(list) =&gt;
      RAISE Error(&quot;Cannot turn off TCP_NODELAY: &quot; &amp;
	ErrMsg.StrError(list));
    END;
  END TurnOffNoDelay;
</PRE>***************************************************************************

<P><PRE>PROCEDURE <A NAME="Err"><procedure>Err</procedure></A>(self: T; msg: TEXT) =
  BEGIN
    IF self.config.trace # NIL THEN
      Logger.Put(self.config.trace, Logger.Priority.Err, msg);
    END;
  END Err;

PROCEDURE <A NAME="Notice"><procedure>Notice</procedure></A>(self: T; msg: TEXT) =
  BEGIN
    IF self.config.trace # NIL THEN
      Logger.Put(self.config.trace, Logger.Priority.Notice, msg);
    END;
  END Notice;

PROCEDURE <A NAME="Trace"><procedure>Trace</procedure></A>(self: T; msg: TEXT) =
  BEGIN
    IF self.config.trace # NIL THEN
      Logger.Put(self.config.trace, Logger.Priority.Info, msg);
    END;
  END Trace;

PROCEDURE <A NAME="Warn"><procedure>Warn</procedure></A>(self: T; msg: TEXT) =
  BEGIN
    IF self.config.trace # NIL THEN
      Logger.Put(self.config.trace, Logger.Priority.Warning, msg);
    END;
  END Warn;
</PRE>***************************************************************************

<P><PRE>PROCEDURE <A NAME="ShakeHands"><procedure>ShakeHands</procedure></A>(self: T)
  RAISES {Error, Rd.EndOfFile, Rd.Failure, Thread.Alerted, TransientFailure,
	  Wr.Failure} =
  VAR
    ts: TokScan.T;
    cmd: TEXT;
    serverMajor, serverMinor: CVProto.VersionNumber;
    actualMajor, actualMinor: CVProto.VersionNumber;
    serverSW: TEXT;
  BEGIN
    TRY
      ts := TokScan.New(SupMisc.GetCmdLine(self.rdA));
      cmd := ts.getToken(&quot;greeting&quot;);
      IF TokScan.EqualFolded(cmd, &quot;OK&quot;) THEN
	serverMajor := ts.getInt(&quot;server protocol major version&quot;);
	serverMinor := ts.getInt(&quot;server protocol minor version&quot;);
	IF NOT ts.next(serverSW) THEN serverSW := &quot;.&quot; END;
      ELSIF TokScan.EqualFolded(cmd, &quot;!&quot;) THEN
	RAISE TransientFailure(&quot;Rejected by server: &quot; &amp; ts.getRest());
      ELSE
	RAISE TokScan.Error(&quot;Invalid greeting from server&quot;);
      END;

      SupMisc.PutCmd(self.wrA, &quot;PROTO&quot;,
	Fmt.Int(CVProto.Current.major),
	Fmt.Int(CVProto.Current.minor),
	Version.Name);
      Wr.Flush(self.wrA);

      ts := TokScan.New(SupMisc.GetCmdLine(self.rdA));
      cmd := ts.getToken(&quot;PROTO&quot;);
      IF TokScan.EqualFolded(cmd, &quot;PROTO&quot;) THEN
	actualMajor := ts.getInt(&quot;actual protocol major version&quot;);
	actualMinor := ts.getInt(&quot;actual protocol minor version&quot;);
      ELSIF TokScan.EqualFolded(cmd, &quot;!&quot;) THEN
	RAISE Error(&quot;Protocol negotiation failed: &quot; &amp; ts.getRest());
      ELSE
	RAISE TokScan.Error(&quot;Invalid PROTO command from server&quot;);
      END;

      self.proto := CVProto.Lookup(actualMajor, actualMinor);

      IF self.proto.v.exchangesVersions THEN
	Trace(self, &quot;Server software version: &quot; &amp; serverSW);
      END;

      IF CVProto.HasS1GBug(self.proto, serverSW) THEN
	Err(self, &quot;Server &quot; &amp; self.server &amp; &quot; has the S1G bug&quot;);
	Err(self, &quot;See &quot; &amp; S1GUrl &amp; &quot; for details&quot;);
	Err(self, &quot;Please notify the maintainer of &quot; &amp; self.server);
	RAISE Error(&quot;Refusing update from server with S1G bug&quot;);
      END;

      IF self.proto.major # CVProto.Current.major
      OR self.proto.minor # CVProto.Current.minor THEN
	Notice(self, &quot;Falling back to protocol version &quot;
	  &amp; Fmt.Int(self.proto.major) &amp; &quot;.&quot; &amp; Fmt.Int(self.proto.minor));
      END;
    EXCEPT
    | TokScan.Error(msg) =&gt;
	RAISE Error(&quot;Startup protocol error: &quot; &amp; msg);
    | CVProto.NotSupported =&gt;
	RAISE Error(&quot;Server protocol version&quot;
	  &amp; &quot; &quot; &amp; Fmt.Int(serverMajor) &amp; &quot;.&quot; &amp; Fmt.Int(serverMinor)
	  &amp; &quot; &quot; &amp; &quot;not supported by client&quot;);
    END;
  END ShakeHands;

PROCEDURE <A NAME="AdjustForProtocol"><procedure>AdjustForProtocol</procedure></A>(self: T)
  RAISES {Error} =
</PRE><BLOCKQUOTE><EM> Make sure the client and server can work together, and adjust various
   options according to what has been negotiated. </EM></BLOCKQUOTE><PRE>
  BEGIN
    CASE self.config.connectMode OF
    | ConnectMode.Default =&gt;
	IF self.proto.v.hasMuxMode THEN
	  self.config.connectMode := ConnectMode.Mux;
	ELSE
	  self.config.connectMode := ConnectMode.Active;
	END;
    | ConnectMode.Active, ConnectMode.Passive, ConnectMode.Socks =&gt;
	(* Nothing special *)
    | ConnectMode.Mux =&gt;
	IF NOT self.proto.v.hasMuxMode THEN
	  RAISE Error(&quot;Server does not support multiplexed mode&quot;);
	END;
    END;

    FOR i := 0 TO self.config.collections.size()-1 DO
      WITH sfr = self.config.collections.get(i) DO
	IF NOT SupFileRec.Option.Skip IN sfr.options THEN
	  IF NOT self.proto.v.hasClientAccepts AND sfr.accepts.size() &gt; 0 THEN
	    RAISE Error(&quot;Server does not support the \&quot;-i pattern\&quot; option&quot;);
	  END;
	END;
      END;
    END;

    IF self.config.authRequired AND NOT self.proto.v.hasMD5Auth THEN
      RAISE Error(&quot;Server does not support authentication&quot;);
    END;
  END AdjustForProtocol;

PROCEDURE <A NAME="Authorize"><procedure>Authorize</procedure></A>(self: T)
  RAISES {Error, Rd.EndOfFile, Rd.Failure, Thread.Alerted, Wr.Failure} =
  VAR
    user: TEXT;
    host: TEXT;
    ts: TokScan.T;
  BEGIN
    TRY
      user := UnixMisc.GetLogin();
      IF user = NIL THEN user := &quot;?&quot; END;
      TRY host := UnixMisc.GetHostName() EXCEPT OSError.E =&gt; host := &quot;?&quot; END;
      (* Early servers expected only the user name, and not the host name.
         But no servers ever checked for the end of the line, so even early
	 ones can handle the host name.  Therefore we don't need a new
	 protocol version to distinguish between when it is sent and when
	 it is not. *)
      self.proto.putCmd(self.wrA, &quot;USER&quot;, user, host);
      Wr.Flush(self.wrA);
      IF self.proto.v.hasMD5Auth THEN
	DoMD5Auth(self);
      ELSE
	ts := self.proto.getCmd(self.rdA);
	ts.getFolded(&quot;OK&quot;);
      END;
    EXCEPT
    | TokScan.Error(msg) =&gt;
	RAISE Error(&quot;Authorization protocol error: &quot; &amp; msg);
    END;
  END Authorize;

PROCEDURE <A NAME="DoMD5Auth"><procedure>DoMD5Auth</procedure></A>(self: T)
  RAISES {Error, Rd.EndOfFile, Rd.Failure, Thread.Alerted, TokScan.Error,
    Wr.Failure} =
  VAR
    ts: TokScan.T;
    realm: TEXT;
    serverChallenge: TEXT;
    serverResponse: TEXT;
    client := &quot;.&quot;;
    clientResponse := &quot;.&quot;;
    clientChallenge := &quot;.&quot;;
    sharedSecret := &quot;*&quot;;
    auth: Auth.T;
    cmd: TEXT;
  BEGIN
    ts := self.proto.getCmd(self.rdA);
    ts.getFolded(&quot;AUTHMD5&quot;);
    realm := ts.getToken(&quot;realm&quot;);
    serverChallenge := ts.getToken(&quot;server challenge&quot;);
    ts.getEnd(&quot;end of AUTHMD5 command&quot;);
    IF self.config.authRequired OR NOT Text.Equal(serverChallenge, &quot;.&quot;) THEN
      IF self.authDB = NIL THEN
	RAISE Error(&quot;Authentication required, but could not open \&quot;&quot; &amp;
	  self.authPath &amp; &quot;\&quot;&quot;);
      END;
      IF Text.Equal(realm, &quot;.&quot;) THEN
	RAISE Error(&quot;Authentication required, but not enabled on server&quot;);
      END;
      TRY
	IF NOT Auth.Lookup(self.authDB, realm, auth) THEN
	  RAISE Error(&quot;No record for server \&quot;&quot; &amp; realm &amp; &quot;\&quot; in \&quot;&quot; &amp;
	    self.authPath &amp; &quot;\&quot;&quot;);
	END;
      EXCEPT Auth.Error(msg) =&gt;
	RAISE Error(msg);
      END;
      client := auth.client;
      sharedSecret := AuthMD5.MakeSecret(auth.server, auth.client,
	auth.password);
    END;
    IF NOT Text.Equal(serverChallenge, &quot;.&quot;) THEN
      clientResponse := AuthMD5.GenResponse(serverChallenge, sharedSecret);
    END;
    IF self.config.authRequired THEN
      clientChallenge := AuthMD5.GenChallenge(self.peer.addr, &quot;.&quot;);
    END;
    self.proto.putCmd(self.wrA, &quot;AUTHMD5&quot;, client, clientResponse,
      clientChallenge);
    Wr.Flush(self.wrA);
    ts := self.proto.getCmd(self.rdA);
    cmd := ts.getToken(&quot;server reply to AUTHMD5&quot;);
    IF TokScan.EqualFolded(cmd, &quot;OK&quot;) THEN
      serverResponse := ts.getToken(&quot;server auth response&quot;);
      IF self.config.authRequired
      AND NOT AuthMD5.CheckResponse(serverResponse, clientChallenge,
	sharedSecret)
      THEN
	RAISE Error(&quot;Server failed to authenticate itself to client&quot;);
      END;
    ELSIF Text.Equal(cmd, &quot;!&quot;) THEN
      RAISE Error(&quot;Server error: &quot; &amp; ts.getRest());
    ELSE
      RAISE Error(&quot;Invalid server reply to AUTHMD5&quot;);
    END;
  END DoMD5Auth;

PROCEDURE <A NAME="ExchangeAttributeInfo"><procedure>ExchangeAttributeInfo</procedure></A>(self: T)
  RAISES {Error, Rd.EndOfFile, Rd.Failure, Thread.Alerted, Wr.Failure} =
  VAR
    ts: TokScan.T;
    count: INTEGER;
    tok: TEXT;
  BEGIN
    IF self.proto.v.hasFileAttrs THEN
      TRY
	Trace(self, &quot;Negotiating file attribute support&quot;);
	self.proto.v.attrSupport := FileAttr.Supported;
	self.proto.putCmd(self.wrA, &quot;ATTR&quot;,
	  Fmt.Int(NUMBER(self.proto.v.attrSupport)));
	FOR i := FIRST(self.proto.v.attrSupport) TO
	  LAST(self.proto.v.attrSupport)
	DO
	  Wr.PutText(self.wrA,
	    FileAttr.EncodeAttrTypes(self.proto.v.attrSupport[i]) &amp; &quot;\n&quot;);
	END;
	self.proto.putCmd(self.wrA, &quot;.&quot;);
	Wr.Flush(self.wrA);

	ts := self.proto.getCmd(self.rdA);
	ts.getFolded(&quot;ATTR&quot;);
	count := ts.getInt(&quot;number of file types&quot;);
	ts.getEnd(&quot;end of \&quot;ATTR\&quot; command&quot;);

	IF count &gt; NUMBER(self.proto.v.attrSupport) THEN
	  RAISE TokScan.Error(&quot;Invalid file type count&quot;);
	END;

	FOR i := 0 TO count-1 DO
	  ts := self.proto.getCmd(self.rdA);
	  tok := ts.getToken(&quot;attrTypes&quot;);
	  ts.getEnd(&quot;end of attrTypes&quot;);
	  WITH as = self.proto.v.attrSupport[VAL(i, FileAttr.FileType)] DO
	    as := as * FileAttr.DecodeAttrTypes(tok);
	  END;
	END;
	ts := self.proto.getCmd(self.rdA);
	ts.getLiteral(&quot;.&quot;);
	ts.getEnd(&quot;end of \&quot;.\&quot;&quot;);

	FOR i := count TO NUMBER(self.proto.v.attrSupport)-1 DO
	  self.proto.v.attrSupport[VAL(i, FileAttr.FileType)] :=
	    FileAttr.AttrTypes{};
	END;
      EXCEPT TokScan.Error(msg) =&gt;
	RAISE Error(&quot;Protocol error negotiating attribute support: &quot; &amp; msg);
      END;
    ELSE
      self.proto.v.attrSupport := FileAttr.Historical;
    END;
  END ExchangeAttributeInfo;

PROCEDURE <A NAME="ExchangeCollectionInfo"><procedure>ExchangeCollectionInfo</procedure></A>(self: T)
  RAISES {Error, Rd.EndOfFile, Rd.Failure, Thread.Alerted, Wr.Failure} =
</PRE><BLOCKQUOTE><EM> All <CODE>Rd</CODE> and <CODE>Wr</CODE> exceptions relate to the network connection. </EM></BLOCKQUOTE><PRE>
  CONST
    ServerMaySet = SupFileRec.Options{
      SupFileRec.Option.Skip,
      SupFileRec.Option.NoRsync,
      SupFileRec.Option.NoRCS};
    ServerMayClear = SupFileRec.Options{
      SupFileRec.Option.CheckRCS};
  VAR (* CONST *)
    NoWS              := GlobTree.Not(GlobTree.Match(&quot;*[ \t\r\n]*&quot;));
  VAR
    sfr: SupFileRec.T;
    ts: TokScan.T;
    cmd: TEXT;
    dirAccept     := GlobTree.True;
    fileAccept    := GlobTree.True;
    dirRefuse     := GlobTree.False;
    fileRefuse    := GlobTree.False;
    pat: TEXT;
    aliasName: TEXT;
    keyName: TEXT;
  BEGIN
    Trace(self, &quot;Exchanging collection information&quot;);
    TRY
      (* Send all the supfile information to the server. *)
      FOR i := 0 TO self.config.collections.size()-1 DO
	sfr := self.config.collections.get(i);
	IF NOT SupFileRec.Option.Skip IN sfr.options THEN
	  self.proto.putCmd(self.wrA, &quot;COLL&quot;,
	    sfr.collection,
	    sfr.release,
	    more := TRUE);
	  IF self.proto.v.clientSendsUmask THEN
	    self.proto.putCmd(self.wrA, NIL,
	      Fmt.Unsigned(sfr.umask, base := 8),
	      more := TRUE);
	  END;
	  self.proto.putCmd(self.wrA, NIL,
	    SupFileRec.EncodeOptions(sfr.options));
	  IF self.proto.v.hasClientAccepts THEN
	    FOR j := 0 TO sfr.accepts.size()-1 DO
	      self.proto.putCmd(self.wrA, &quot;ACC&quot;, sfr.accepts.get(j));
	    END;
	  END;
	  FOR j := 0 TO sfr.refusals.size()-1 DO
	    self.proto.putCmd(self.wrA, &quot;REF&quot;, sfr.refusals.get(j));
	  END;
	  self.proto.putCmd(self.wrA, &quot;.&quot;);
	END;
      END;
      self.proto.putCmd(self.wrA, &quot;.&quot;);
      Wr.Flush(self.wrA);

      (* Read back the filtering details. *)
      FOR i := 0 TO self.config.collections.size()-1 DO
	sfr := self.config.collections.get(i);
	IF NOT SupFileRec.Option.Skip IN sfr.options THEN
	  ts := self.proto.getCmd(self.rdA);
	  ts.getFolded(&quot;COLL&quot;);
	  WITH collection = ts.getToken(&quot;collection name&quot;) DO
	    IF NOT Text.Equal(collection, sfr.collection) THEN
	      RAISE TokScan.Error(&quot;Expected collection \&quot;&quot; &amp;
		sfr.collection &amp; &quot;\&quot;, got \&quot;&quot; &amp; collection &amp; &quot;\&quot;&quot;);
	    END;
	  END;
	  WITH release = ts.getToken(&quot;release name&quot;) DO
	    IF NOT Text.Equal(release, sfr.release) THEN
	      RAISE TokScan.Error(&quot;Expected release \&quot;&quot; &amp;
		sfr.release &amp; &quot;\&quot;, got \&quot;&quot; &amp; release &amp; &quot;\&quot;&quot;);
	    END;
	  END;
	  WITH so = SupFileRec.DecodeOptions(ts.getToken(&quot;options&quot;)) DO
	    sfr.options := sfr.options +
	      ServerMaySet * so -
	      ServerMayClear * (SupFileRec.AllOptions - so);
	  END;

	  LOOP
	    ts := self.proto.getCmd(self.rdA);
	    cmd := ts.getToken(&quot;command&quot;);
	    IF Text.Equal(cmd, &quot;.&quot;) THEN EXIT END;
	    IF TokScan.EqualFolded(cmd, &quot;PRFX&quot;) THEN
	      sfr.keywordPrefix := ts.getToken(&quot;keyword prefix&quot;);
	    ELSIF TokScan.EqualFolded(cmd, &quot;KEYALIAS&quot;) THEN
	      aliasName := ts.getToken(&quot;alias name&quot;);
	      keyName := ts.getToken(&quot;RCS keyword&quot;);
	      TRY
		sfr.expander.alias(aliasName, keyName);
	      EXCEPT RCSKeyword.Unknown =&gt;
		Warn(self, &quot;Attempt by server to alias unknown RCS keyword \&quot;&quot;
		  &amp; keyName &amp; &quot;\&quot;&quot;);
	      END;
	    ELSIF TokScan.EqualFolded(cmd, &quot;KEYON&quot;) THEN
	      keyName := ts.getToken(&quot;RCS keyword&quot;);
	      IF Text.Equal(keyName, &quot;.&quot;) THEN
		sfr.expander.enableAll();
	      ELSE
		TRY
		  sfr.expander.enable(keyName);
		EXCEPT RCSKeyword.Unknown =&gt;
		  Warn(self, &quot;Attempt by server to enable unknown RCS&quot;
		    &amp; &quot; keyword \&quot;&quot; &amp; keyName &amp; &quot;\&quot;&quot;);
		END;
	      END;
	    ELSIF TokScan.EqualFolded(cmd, &quot;KEYOFF&quot;) THEN
	      keyName := ts.getToken(&quot;RCS keyword&quot;);
	      IF Text.Equal(keyName, &quot;.&quot;) THEN
		sfr.expander.enableAll(enabled := FALSE);
	      ELSE
		TRY
		  sfr.expander.enable(keyName, enabled := FALSE);
		EXCEPT RCSKeyword.Unknown =&gt;
		  Warn(self, &quot;Attempt by server to disable unknown RCS&quot;
		    &amp; &quot; keyword \&quot;&quot; &amp; keyName &amp; &quot;\&quot;&quot;);
		END;
	      END;
	    ELSIF TokScan.EqualFolded(cmd, &quot;NORS&quot;) THEN
	      pat := ts.getToken(&quot;pattern&quot;);
	      sfr.noRsync := GlobTree.Or(sfr.noRsync,
		GlobTree.Match(pat, Glob.MatchOptions{
		  Glob.MatchOption.Pathname }));
	    ELSIF TokScan.EqualFolded(cmd, &quot;RNORS&quot;) THEN
	      pat := ts.getToken(&quot;pattern&quot;);
	      sfr.noRsync := GlobTree.Or(sfr.noRsync,
		GlobTree.Match(pat, Glob.MatchOptions{
		  Glob.MatchOption.Pathname,
		  Glob.MatchOption.LeadingDir }));
	    ELSIF TokScan.EqualFolded(cmd, &quot;!&quot;) THEN
	      sfr.options := sfr.options +
		SupFileRec.Options{SupFileRec.Option.Skip};
	      Warn(self, &quot;Server message: &quot; &amp; ts.getRest());
	    ELSIF TokScan.EqualFolded(cmd, &quot;UPGR&quot;)
	    OR TokScan.EqualFolded(cmd, &quot;ALWS&quot;)
	    OR TokScan.EqualFolded(cmd, &quot;OANY&quot;) THEN
	      EVAL ts.getToken(&quot;pattern&quot;);  (* Not used any more. *)
	    ELSE
	      RAISE TokScan.Error(&quot;Invalid command \&quot;&quot; &amp; cmd &amp; &quot;\&quot;&quot;);
	    END;
	  END;

	  IF sfr.accepts.size() &gt; 0 THEN
	    dirAccept := GlobTree.False;
	    fileAccept := GlobTree.False;
	    FOR i := 0 TO sfr.accepts.size()-1 DO
	      pat := sfr.accepts.get(i);
	      dirAccept := GlobTree.Or(dirAccept,
		GlobTree.Match(pat, Glob.MatchOptions{
		  Glob.MatchOption.Pathname,
		  Glob.MatchOption.LeadingDir,
		  Glob.MatchOption.PrefixDirs }));
	      IF SupFileRec.Option.CheckoutMode IN sfr.options THEN
		(* We must modify the pattern so that it refers
		   to the RCS file, rather than the checked-out file. *)
		WITH len = Text.Length(pat) DO
		  IF len = 0 OR Text.GetChar(pat, len-1) # '*' THEN
		    pat := pat &amp; SupMisc.RCSSuffix;
		  END;
		END;
	      END;
	      fileAccept := GlobTree.Or(fileAccept,
		GlobTree.Match(pat, Glob.MatchOptions{
		  Glob.MatchOption.Pathname,
		  Glob.MatchOption.LeadingDir }));
	    END;
	  END;

	  FOR j := 0 TO sfr.refusals.size()-1 DO
	    pat := sfr.refusals.get(j);
	    dirRefuse := GlobTree.Or(dirRefuse, GlobTree.Match(pat));
	    IF SupFileRec.Option.CheckoutMode IN sfr.options THEN
	      (* We must modify the pattern so that it refers
		 to the RCS file, rather than the checked-out file. *)
	      WITH len = Text.Length(pat) DO
		IF len = 0 OR Text.GetChar(pat, len-1) # '*' THEN
		  pat := pat &amp; SupMisc.RCSSuffix;
		END;
	      END;
	    END;
	    fileRefuse := GlobTree.Or(fileRefuse, GlobTree.Match(pat));
	  END;

	  sfr.dirFilter := GlobTree.And(dirAccept, GlobTree.Not(dirRefuse));
	  sfr.fileFilter := GlobTree.And(fileAccept, GlobTree.Not(fileRefuse));

	  (* If the server can't handle white space in file names, make sure
	     we don't send him any. *)
	  IF NOT self.proto.v.handlesWhiteSpace THEN
	    sfr.dirFilter := GlobTree.And(sfr.dirFilter, NoWS);
	    sfr.fileFilter := GlobTree.And(sfr.fileFilter, NoWS);
	  END;

	  (* Set up a mask of file attribes that we don't want to sync
	     with the server. *)
	  IF NOT SupFileRec.Option.SetOwner IN sfr.options THEN
	    sfr.attrIgnore := sfr.attrIgnore + FileAttr.AttrTypes{
	      FileAttr.AttrType.Owner, FileAttr.AttrType.Group };
	  END;
	  IF NOT SupFileRec.Option.SetMode IN sfr.options THEN
	    sfr.attrIgnore := sfr.attrIgnore + FileAttr.AttrTypes{
	      FileAttr.AttrType.Mode };
	  END;
	  IF NOT SupFileRec.Option.SetFlags IN sfr.options THEN
	    sfr.attrIgnore := sfr.attrIgnore + FileAttr.AttrTypes{
	      FileAttr.AttrType.Flags };
	  END;
	END;
      END;
      ts := self.proto.getCmd(self.rdA);
      ts.getLiteral(&quot;.&quot;);
    EXCEPT TokScan.Error(msg) =&gt;
      RAISE Error(&quot;Protocol error exchanging collection info: &quot; &amp; msg);
    END;
  END ExchangeCollectionInfo;

PROCEDURE <A NAME="EstablishControlConnection"><procedure>EstablishControlConnection</procedure></A>(self: T)
  RAISES {Error, Thread.Alerted, TransientFailure} =
  BEGIN
    TRY
      IF NOT SupMisc.ParseHost(self.server, self.peer.addr) THEN
	RAISE Error(&quot;Unknown host \&quot;&quot; &amp; self.server &amp; &quot;\&quot;&quot;);
      END;
    EXCEPT IP.Error(list) =&gt;
      RAISE TransientFailure(&quot;Name lookup failure for \&quot;&quot;
	&amp; self.server &amp; &quot;\&quot;: &quot; &amp; ErrMsg.StrError(list));
    END;
    self.peer.port := self.config.port;

    Trace(self, &quot;Connecting to &quot; &amp; self.server);
    TRY
      self.tcp0 := TCPMisc.ConnectFrom(self.peer, self.config.localEndpoint);
    EXCEPT IP.Error(list) =&gt;
      RAISE TransientFailure(&quot;Cannot connect to &quot; &amp; self.server &amp; &quot;: &quot; &amp;
	ErrMsg.StrError(list));
    END;
    TurnOffNoDelay(self.tcp0);
    self.rdA := ConnRW.NewRd(self.tcp0);
    self.wrA := ConnRW.NewWr(self.tcp0);

    Notice(self, &quot;Connected to &quot; &amp; self.server);
  END EstablishControlConnection;

PROCEDURE <A NAME="EstablishDataConnection"><procedure>EstablishDataConnection</procedure></A>(self: T)
  RAISES {Error, Rd.EndOfFile, Rd.Failure, Thread.Alerted, TransientFailure,
	  Wr.Failure} =
  VAR
    ts: TokScan.T;
    addr: IP.Address;
    ep: IP.Endpoint;
    id: ChannelMux.ChannelID;
    chan0, chan1: ChannelMux.Channel;
  BEGIN
    TRY
      CASE self.config.connectMode OF
      | ConnectMode.Active =&gt;
	  Trace(self, &quot;Establishing active-mode data connection&quot;);
	  addr := GetSockName(self.tcp0).addr;
	  self.tcp1 := Accept(self, addr, &quot;PORT&quot;);

	  TurnOffNoDelay(self.tcp1);
	  self.rdB := ConnRW.NewRd(self.tcp1);
	  IOWatchDog.AddRd(self.watchDog, self.rdB);
	  self.wrB := ConnRW.NewWr(self.tcp1);
	  IOWatchDog.AddWr(self.watchDog, self.wrB);
      | ConnectMode.Socks =&gt;
	  Trace(self, &quot;Establishing SOCKS-mode data connection&quot;);
	  addr := GetSockName(self.tcp0).addr;
	  self.tcp1 := Accept(self, addr, &quot;SOCKS1&quot;);
	  self.tcp2 := Accept(self, addr, &quot;SOCKS2&quot;);
	  self.tcp3 := Accept(self, addr, &quot;SOCKS3&quot;);

	  (* We would like to do a &quot;Rd.Close(self.rdA)&quot; at this point, since
	     we aren't going to use that direction any more.  But that causes
	     SOCKS to drop the entire connection. *)
	  self.oldRdA := self.rdA;

	  TurnOffNoDelay(self.tcp1);
	  self.rdA := ConnRW.NewRd(self.tcp1);
	  IOWatchDog.AddRd(self.watchDog, self.rdA);
	  TurnOffNoDelay(self.tcp2);
	  self.wrB := ConnRW.NewWr(self.tcp2);
	  IOWatchDog.AddWr(self.watchDog, self.wrB);
	  TurnOffNoDelay(self.tcp3);
	  self.rdB := ConnRW.NewRd(self.tcp3);
	  IOWatchDog.AddRd(self.watchDog, self.rdB);
      | ConnectMode.Passive =&gt;
	  Trace(self, &quot;Establishing passive-mode data connection&quot;);
	  self.proto.putCmd(self.wrA, &quot;PASV&quot;);
	  Wr.Flush(self.wrA);

	  ts := self.proto.getCmd(self.rdA);
	  ts.getFolded(&quot;PORT&quot;);
	  ep := ts.getEndpoint();
	  ts.getEnd();

	  TRY
	    self.tcp1 := TCPMisc.ConnectFrom(ep, self.config.localEndpoint);
	  EXCEPT IP.Error(list) =&gt;
	    RAISE TransientFailure(&quot;Cannot connect to data port: &quot;
	      &amp; ErrMsg.StrError(list));
	  END;

	  TurnOffNoDelay(self.tcp1);
	  self.rdB := ConnRW.NewRd(self.tcp1);
	  IOWatchDog.AddRd(self.watchDog, self.rdB);
	  self.wrB := ConnRW.NewWr(self.tcp1);
	  IOWatchDog.AddWr(self.watchDog, self.wrB);
      | ConnectMode.Mux =&gt;
	  Trace(self, &quot;Establishing multiplexed-mode data connection&quot;);
	  self.proto.putCmd(self.wrA, &quot;MUX&quot;);
	  Wr.Flush(self.wrA);
	  TRY
	    self.mux :=
	      ChannelMux.Open(self.rdA, self.wrA, chan0, active := TRUE);
	  EXCEPT IP.Error(l) =&gt;
	    RAISE Error(&quot;ChannelMux.Open failed: &quot; &amp; ErrMsg.StrError(l));
	  END;

	  self.oldRdA := self.rdA;
	  self.oldWrA := self.wrA;

	  self.rdA := ConnRW.NewRd(chan0);
	  IOWatchDog.AddRd(self.watchDog, self.rdA);
	  self.wrA := ConnRW.NewWr(chan0);
	  IOWatchDog.AddWr(self.watchDog, self.wrA);

	  TRY
	    id := ChannelMux.Listen(self.mux);
	  EXCEPT IP.Error(l) =&gt;
	    RAISE Error(&quot;ChannelMux.Listen failed: &quot; &amp; ErrMsg.StrError(l));
	  END;
	  self.proto.putCmd(self.wrA, &quot;CHAN&quot;, Fmt.Int(id));
	  Wr.Flush(self.wrA);
	  TRY
	    chan1 := ChannelMux.Accept(self.mux, id);
	  EXCEPT IP.Error(l) =&gt;
	    RAISE Error(&quot;ChannelMux.Accept failed: &quot; &amp; ErrMsg.StrError(l));
	  END;
	  self.rdB := ConnRW.NewRd(chan1);
	  IOWatchDog.AddRd(self.watchDog, self.rdB);
	  self.wrB := ConnRW.NewWr(chan1);
	  IOWatchDog.AddWr(self.watchDog, self.wrB);
      | ConnectMode.Default =&gt;
	  (* It should have been set to a concrete value by now. *)
	  &lt;* ASSERT FALSE *&gt;	
      END;
    EXCEPT
    | TokScan.Error(msg) =&gt;
	RAISE Error(&quot;Protocol error establishing data connection: &quot; &amp; msg);
    END;
  END EstablishDataConnection;

PROCEDURE <A NAME="CheckCollections"><procedure>CheckCollections</procedure></A>(self: T): CARDINAL =
  VAR
    numValid: CARDINAL := 0;
    sfr: SupFileRec.T;
    link: TEXT;
  BEGIN
    FOR i := 0 TO self.config.collections.size()-1 DO
      sfr := self.config.collections.get(i);
      IF NOT SupMisc.IsDirectory(sfr.clientPrefix) THEN
	(* Skip this collection, and warn about it unless its prefix is
	   a symbolic link pointing to &quot;SKIP&quot;. *)
	sfr.options := sfr.options + SupFileRec.Options{SupFileRec.Option.Skip};
	TRY
	  link := UnixMisc.ReadLink(sfr.clientPrefix)
	EXCEPT OSError.E =&gt;
	  link := NIL;
	END;
	IF link = NIL OR NOT Text.Equal(link, &quot;SKIP&quot;) THEN
	  Warn(self, &quot;Nonexistent prefix \&quot;&quot;
	    &amp; sfr.clientPrefix &amp; &quot;\&quot; for &quot;
	    &amp; sfr.collection &amp; &quot;/&quot; &amp; sfr.release);
	END;
      END;
      IF NOT SupFileRec.Option.Skip IN sfr.options THEN
	INC(numValid);
      END;
    END;
    RETURN numValid;
  END CheckCollections;
</PRE>***************************************************************************

<P><PRE>TYPE
  Killer = SigHandler.T OBJECT
    thread: Thread.T;
    killedBySignal := -1;
  OVERRIDES
    apply := KillerApply;
  END;

PROCEDURE <A NAME="KillerApply"><procedure>KillerApply</procedure></A>(self: Killer; sig: INTEGER) =
  BEGIN
    self.killedBySignal := sig;
    Thread.Alert(self.thread);
  END KillerApply;

BEGIN
END FSClient.
</PRE>
</inModule>
<HR>
<A NAME="x1">interface Glob is in:
</A><UL>
<LI><A HREF="../../patternmatching/src/Glob.i3.html#0TOP0">patternmatching/src/Glob.i3</A>
<LI><A HREF="../../suplib/src/Glob.i3.html#0TOP0">suplib/src/Glob.i3</A>
</UL>
<P>
<HR>
<A NAME="x2">interface GlobTree is in:
</A><UL>
<LI><A HREF="../../patternmatching/src/GlobTree.i3.html#0TOP0">patternmatching/src/GlobTree.i3</A>
<LI><A HREF="../../suplib/src/GlobTree.i3.html#0TOP0">suplib/src/GlobTree.i3</A>
</UL>
<P>
<HR>
<A NAME="x3">interface Usignal is in:
</A><UL>
<LI><A HREF="../../m3core/src/unix/Common/Usignal.i3.html#0TOP0">m3core/src/unix/Common/Usignal.i3</A>
<LI><A HREF="../../m3core/src/unix/linux-common/Usignal.i3.html#0TOP0">m3core/src/unix/linux-common/Usignal.i3</A>
</UL>
<P>
<HR>
<A NAME="x4">interface Version is in:
</A><UL>
<LI><A HREF="Version.i3.html#0TOP0">client/src/Version.i3</A>
<LI><A HREF="../../cm3/derived/Version.i3.html#0TOP0">cm3/derived/Version.i3</A>
<LI><A HREF="../../server/src/Version.i3.html#0TOP0">server/src/Version.i3</A>
</UL>
<P>
<PRE>























</PRE>
</BODY>
</HTML>
