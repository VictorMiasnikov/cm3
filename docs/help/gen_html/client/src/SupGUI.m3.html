<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: client/src/SupGUI.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>client/src/SupGUI.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright 1996-2003 John D. Polstra.
 * All rights reserved.
 *
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions
 * are met:
 * 1. Redistributions of source code must retain the above copyright
 *    notice, this list of conditions and the following disclaimer.
 * 2. Redistributions in binary form must reproduce the above copyright
 *    notice, this list of conditions and the following disclaimer in the
 *    documentation and/or other materials provided with the distribution.
 * 3. All advertising materials mentioning features or use of this software
 *    must display the following acknowledgment:
 *      This product includes software developed by John D. Polstra.
 * 4. The name of the author may not be used to endorse or promote products
 *    derived from this software without specific prior written permission.
 *
 * THIS SOFTWARE IS PROVIDED BY THE AUTHOR ``AS IS'' AND ANY EXPRESS OR
 * IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES
 * OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED.
 * IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR ANY DIRECT, INDIRECT,
 * INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT
 * NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
 * DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
 * THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
 * (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF
 * THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 *
 * $Id: SupGUI.m3.html,v 1.3 2010-04-29 17:17:58 wagner Exp $ 
<PRE>&lt;* PRAGMA LL *&gt;

MODULE <module><implements><A HREF="SupGUI.i3.html">SupGUI</A></implements></module>;

IMPORT
  <A HREF="../../vbtkit/src/vbtkitutils/AutoRepeat.i3.html">AutoRepeat</A>, <A HREF="../../suplib/src/CVProto.i3.html">CVProto</A>, <A HREF="Detailer.i3.html">Detailer</A>, <A HREF="../../suplib/src/ErrMsg.i3.html">ErrMsg</A>, <A HREF="EventSync.i3.html">EventSync</A>, <A HREF="FileUpdater.i3.html">FileUpdater</A>,
  <A HREF="../../libm3/src/rw/FileWr.i3.html">FileWr</A>, <A HREF="../../libm3/src/fmtlex/Fmt.i3.html">Fmt</A>, <A HREF="../../formsvbt/src/FormsVBT.i3.html">FormsVBT</A>, <A HREF="FSClient.i3.html">FSClient</A>, <A HREF="../../tcp/src/common/IP.i3.html">IP</A>, <A HREF="../../suplib/src/Logger.i3.html">Logger</A>, <A HREF="../../libm3/src/os/Common/OSError.i3.html">OSError</A>, <A HREF="../../libm3/src/os/Common/Pathname.i3.html">Pathname</A>, <A HREF="../../libm3/src/rw/Rd.i3.html">Rd</A>,
  <A HREF="../../vbtkit/src/vbtkitutils/Rsrc.i3.html">Rsrc</A>, <A HREF="../../suplib/src/SplitLogger.i3.html">SplitLogger</A>, <A HREF="SupFile.i3.html">SupFile</A>, <A HREF="../derived/SupGUIBundle.i3.html">SupGUIBundle</A>, <A HREF="../../suplib/src/SupMisc.i3.html">SupMisc</A>, <A HREF="../../m3core/src/text/Text.i3.html">Text</A>, <A HREF="../../vbtkit/src/etext/TextPort.i3.html">TextPort</A>,
  <A HREF="TextPortLogger.i3.html">TextPortLogger</A>, <A HREF="../../vbtkit/src/etext/TextEditVBT.i3.html">TextEditVBT</A>, <A HREF="../../ui/src/split/TextVBT.i3.html">TextVBT</A>, <A HREF="TextVBTLogger.i3.html">TextVBTLogger</A>, <A HREF="../../m3core/src/thread/Common/Thread.i3.html">Thread</A>, <A HREF="../../m3core/src/time/Common/Time.i3.html">Time</A>,
  <A HREF="../../suplib/src/TokScan.i3.html">TokScan</A>, <A HREF="TreeList.i3.html">TreeList</A>, <A HREF="../../ui/src/vbt/Trestle.i3.html">Trestle</A>, <A HREF="../../ui/src/vbt/TrestleComm.i3.html">TrestleComm</A>, <A HREF="../../vbtkit/src/etext/TypeinVBT.i3.html">TypeinVBT</A>, <A HREF="../../suplib/src/UnixMisc.i3.html">UnixMisc</A>,
  <A HREF="Updater.i3.html">Updater</A>, <A HREF="../../ui/src/vbt/VBT.i3.html">VBT</A>, <A HREF="#x1">Version</A>, <A HREF="../../libm3/src/rw/Wr.i3.html">Wr</A>;

CONST
  LogName = &quot;cvsup.log&quot;;

&lt;* FATAL FormsVBT.Unimplemented *&gt;
</PRE>***************************************************************************
 Main Controller. 
***************************************************************************

<P><PRE>TYPE
  Controller = Thread.Closure OBJECT
    fv: FormsVBT.T;
    supFile: Pathname.T;
    config: FSClient.Configuration;
  METHODS
    init(fv: FormsVBT.T;
	 supFile: Pathname.T;
	 config: FSClient.Configuration): Controller := ControllerInit;
  OVERRIDES
    apply := ControllerApply;
  END;

PROCEDURE <A NAME="ControllerInit"><procedure>ControllerInit</procedure></A>(self: Controller;
                         fv: FormsVBT.T;
			 supFile: Pathname.T;
			 config: FSClient.Configuration): Controller =
  BEGIN
    self.fv := fv;
    self.supFile := supFile;
    self.config := config;
    RETURN self;
  END ControllerInit;

PROCEDURE <A NAME="ControllerApply"><procedure>ControllerApply</procedure></A>(self: Controller): REFANY =
  &lt;* FATAL EventSync.Error, FormsVBT.Error, Thread.Alerted *&gt;
  VAR
    totalStats: TotalStats;
    clock: Clock;
    listerVBT: TextVBT.T;
    detailerVBT: TextVBT.T;
    messagesVBT: TextPort.T;
    filterVBT: TypeinVBT.T;
    statusLogger: Logger.T;
    listerTrace: Logger.T;
    detailerTrace: Logger.T;
    messagesLogger: Logger.T;
    client: FSClient.T;
    clientThread: Thread.T;
    aborterThread: Thread.T;
    clientStatus: SupMisc.ThreadStatus;
    filterText := &quot;&quot;;
    ts: TokScan.T;
  BEGIN
    listerVBT := FormsVBT.GetVBT(self.fv, &quot;lister&quot;);
    detailerVBT := FormsVBT.GetVBT(self.fv, &quot;detailer&quot;);
    messagesVBT :=
      NARROW(FormsVBT.GetVBT(self.fv, &quot;messages&quot;), TextEditVBT.T).tp;
    filterVBT :=
      NARROW(FormsVBT.GetVBT(self.fv, &quot;filter&quot;), TypeinVBT.T);

    (* Translate the command-line accept filter into a text string for
       the display. *)
    WITH accepts = self.config.override.accepts DO
      FOR i := 0 TO accepts.size()-1 DO
	IF i = 0 THEN
	  filterText := accepts.get(i);
	ELSE
	  filterText := filterText &amp; &quot; &quot; &amp; accepts.get(i);
	END;
      END;
    END;

    LOCK VBT.mu DO
      messagesVBT.setModel(TextPort.Model.Xterm);
      filterVBT.setModel(TextPort.Model.Xterm);
      FormsVBT.PutText(self.fv, &quot;filter&quot;, filterText);
    END;

    listerTrace := NEW(TextVBTLogger.T).init(
      listerVBT, level := Logger.Priority.Info);
    detailerTrace := NEW(TextVBTLogger.T).init(
      detailerVBT, level := Logger.Priority.Info);

    statusLogger := NEW(TextVBTLogger.T).init(
      FormsVBT.GetVBT(self.fv, &quot;status&quot;), level := Logger.Priority.Info);
    messagesLogger := NEW(TextPortLogger.T).init(
      messagesVBT, level := Logger.Priority.Info);

    self.config.trace := NEW(SplitLogger.T).init(messagesLogger, statusLogger,
      SET OF Logger.Priority{Logger.Priority.Emerg..Logger.Priority.Warning},
      SET OF Logger.Priority{Logger.Priority.Emerg..Logger.Priority.Debug});
    self.config.listerTrace :=
      NEW(SplitLogger.T).init(messagesLogger, listerTrace);
    self.config.detailerTrace :=
      NEW(SplitLogger.T).init(messagesLogger, detailerTrace);
    self.config.updaterTrace := messagesLogger;

    (* Wait for the initial start command. *)
    IF EventSync.Wait(self.fv, &quot;quit=0 start=1 filter=1&quot;) = 1 THEN  (* start *)
      LOOP
	(* Clear the trace and statistics displays. *)
	LOCK VBT.mu DO
	  TextVBT.Put(listerVBT, &quot;&quot;);
	  TextVBT.Put(detailerVBT, &quot;&quot;);
	  TextPort.SetText(messagesVBT, &quot;&quot;);
	  ResetStatsDisplay(self);
	  VBT.Release(filterVBT, VBT.KBFocus);
	END;

	totalStats := NEW(TotalStats).init(self.fv);

	self.config.listerStats := NEW(ListerStats).init(self.fv, totalStats);
	self.config.detailerStats :=
	  NEW(DetailerStats).init(self.fv, totalStats);
	self.config.updaterStats := NEW(UpdaterStats).init(self.fv, totalStats);

	totalStats.ls := self.config.listerStats;
	totalStats.ds := self.config.detailerStats;
	totalStats.us := self.config.updaterStats;

	(* Build a filter list from the text in the TypeIn. *)
	WITH accepts = self.config.override.accepts DO
	  FOR i := 0 TO accepts.size()-1 DO EVAL accepts.remhi() END;
	  ts := TokScan.New(FormsVBT.GetText(self.fv, &quot;filter&quot;));
	  &lt;* FATAL TokScan.Error *&gt;
	  BEGIN
	    WHILE ts.next(filterText) DO
	      accepts.addhi(filterText);
	    END;
	  END;
	END;

	clock := NEW(Clock).init(self.fv, &quot;clock&quot;, self.config.updaterStats);

	TRY
	  Msg(self, &quot;Parsing supfile \&quot;&quot; &amp; self.supFile &amp; &quot;\&quot;&quot;);
	  self.config.collections := SupFile.Parse(self.supFile,
	    override := self.config.override,
	    mask := self.config.overrideMask);

	  client := NEW(FSClient.T).init(self.config);

	  (* Start the client. *)
	  clientThread := Thread.Fork(client);
	  AutoRepeat.Start(clock);
	  aborterThread :=
	    Thread.Fork(NEW(Aborter).init(self.fv, clientThread));

	  (* Wait for the client to finish. *)
	  clientStatus := Thread.Join(clientThread);
	  AutoRepeat.Stop(clock);
	  Thread.Alert(aborterThread);
	  EVAL Thread.Join(aborterThread);
	EXCEPT SupFile.Error(msg) =&gt;
	  Msg(self, msg);
	END;

	CASE EventSync.Wait(self.fv, &quot;quit=0 start=1 filter=1 save=2&quot;) OF
	| 0 =&gt; (* quit *)
	    EXIT;
	| 1 =&gt; (* start *)
	    (* Continue *)
	| 2 =&gt; (* save *)
	    TRY
	      SaveTrace(messagesVBT, LogName);
	      Msg(self, &quot;Saved message window to \&quot;&quot; &amp; LogName &amp; &quot;\&quot;&quot;);
	    EXCEPT Error(msg) =&gt;
	      Msg(self, msg);
	    END;
	    IF EventSync.Wait(self.fv, &quot;quit=0 start=1 filter=1&quot;) = 0 THEN
	      (* quit *)
	      EXIT;
	    END;
	ELSE &lt;* ASSERT FALSE *&gt; END;
      END;
    END;

    LOCK VBT.mu DO Trestle.Delete(self.fv) END;
    RETURN NIL;
  END ControllerApply;

PROCEDURE <A NAME="Msg"><procedure>Msg</procedure></A>(self: Controller;
              msg: TEXT;
	      append := FALSE) =
  &lt;* FATAL FormsVBT.Error, FormsVBT.Unimplemented *&gt;
  BEGIN
    LOCK VBT.mu DO
      FormsVBT.PutText(self.fv, &quot;status&quot;, msg, append);
    END;
  END Msg;

PROCEDURE <A NAME="ResetStatsDisplay"><procedure>ResetStatsDisplay</procedure></A>(self: Controller) =
  &lt;* LL = VBT.mu *&gt;
  &lt;* FATAL FormsVBT.Error, FormsVBT.Unimplemented *&gt;
  CONST
    UpdateRows = ARRAY OF TEXT{
      &quot;edit&quot;, &quot;co&quot;, &quot;rsync&quot;, &quot;app&quot;, &quot;touch&quot;,
      &quot;add&quot;, &quot;del&quot;, &quot;repl&quot;, &quot;fixup&quot;, &quot;other&quot;, &quot;tot&quot;
    };
    UpdateCols = ARRAY OF TEXT{
      &quot;Count&quot;, &quot;FileKB&quot;, &quot;WireKB&quot;, &quot;Compr&quot;
    };
    CommRows = ARRAY OF TEXT{
      &quot;lister&quot;, &quot;detailer&quot;, &quot;updater&quot;, &quot;tot&quot;
    };
    CommCols = ARRAY OF TEXT{
      &quot;CommIn&quot;, &quot;CommOut&quot;, &quot;WireIn&quot;, &quot;WireOut&quot;, &quot;WireTot&quot;
    };
  VAR
    name, value: TEXT;
  BEGIN
    FOR i := FIRST(UpdateRows) TO LAST(UpdateRows) DO
      FOR j := FIRST(UpdateCols) TO LAST(UpdateCols) DO
	name := UpdateRows[i] &amp; UpdateCols[j];
	IF Text.Equal(UpdateCols[j], &quot;Compr&quot;) THEN
	  value := &quot;0.0&quot;;
	ELSE
	  value := &quot;0&quot;;
	END;
	FormsVBT.PutText(self.fv, name, value);
      END;
    END;

    FOR i := FIRST(CommRows) TO LAST(CommRows) DO
      FOR j := FIRST(CommCols) TO LAST(CommCols) DO
	name := CommRows[i] &amp; CommCols[j];
	IF Text.Equal(name, &quot;listerCommIn&quot;)
	OR Text.Equal(name, &quot;listerWireIn&quot;)
	OR Text.Equal(name, &quot;updaterCommOut&quot;)
	OR Text.Equal(name, &quot;updaterWireOut&quot;) THEN
	  value := &quot;-&quot;;
	ELSE
	  value := &quot;0&quot;;
	END;
	FormsVBT.PutText(self.fv, name, value);
      END;
    END;

    FormsVBT.PutText(self.fv, &quot;clock&quot;, &quot;00:00:00&quot;);
    FormsVBT.PutText(self.fv, &quot;xferRate&quot;, &quot;    0.0 KB/sec&quot;);
  END ResetStatsDisplay;

PROCEDURE <A NAME="SaveTrace"><procedure>SaveTrace</procedure></A>(tp: TextPort.T; fileName: TEXT)
  RAISES {Error, Thread.Alerted} =
  CONST
    ChunkSize = 8 * 1024;
  VAR
    wr: Wr.T;
    pos, limit, len: CARDINAL;
  BEGIN
    TRY
      wr := FileWr.Open(fileName);
    EXCEPT OSError.E(list) =&gt;
      RAISE Error(&quot;Cannot create \&quot;&quot; &amp; fileName &amp; &quot;\&quot;: &quot; &amp;
	ErrMsg.StrError(list));
    END;
    TRY
      LOCK VBT.mu DO
	len := TextPort.Length(tp);
	pos := 0;
	WHILE pos &lt; len DO
	  limit := MIN(pos+ChunkSize, len);
	  TRY
	    Wr.PutText(wr, TextPort.GetText(tp, pos, limit));
	  EXCEPT Wr.Failure(list) =&gt;
	    RAISE Error(&quot;Write failure on \&quot;&quot; &amp; fileName &amp; &quot;\&quot;: &quot; &amp;
	      ErrMsg.StrError(list));
	  END;
	  pos := limit;
	END;
      END;
    FINALLY
      TRY
	Wr.Close(wr);
      EXCEPT Wr.Failure(list) =&gt;
	RAISE Error(&quot;Cannot close \&quot;&quot; &amp; fileName &amp; &quot;\&quot;: &quot; &amp;
	  ErrMsg.StrError(list));
      END;
    END;
  END SaveTrace;
</PRE>***************************************************************************
 Aborter. 
***************************************************************************

<P><PRE>TYPE
  Aborter = Thread.Closure OBJECT
    fv: FormsVBT.T;
    clientThread: Thread.T;
  METHODS
    init(fv: FormsVBT.T;
	 clientThread: Thread.T): Aborter := AborterInit;
  OVERRIDES
    apply := AborterApply;
  END;

PROCEDURE <A NAME="AborterInit"><procedure>AborterInit</procedure></A>(self: Aborter;
                      fv: FormsVBT.T;
		      clientThread: Thread.T): Aborter =
  BEGIN
    self.fv := fv;
    self.clientThread := clientThread;
    RETURN self;
  END AborterInit;

PROCEDURE <A NAME="AborterApply"><procedure>AborterApply</procedure></A>(self: Aborter): REFANY =
  &lt;* FATAL EventSync.Error, FormsVBT.Error *&gt;
  BEGIN
    TRY
      EVAL EventSync.Wait(self.fv, &quot;abort=0&quot;);
      Thread.Alert(self.clientThread);
    EXCEPT Thread.Alerted =&gt;
      (* Just exit. *)
    END;
    RETURN NIL;
  END AborterApply;
</PRE>***************************************************************************
 Lister Statistics Display. 
***************************************************************************

<P><PRE>TYPE
  ListerStats = TreeList.Stats OBJECT
    fv: FormsVBT.T;
    ts: TotalStats;
  METHODS
    init(fv: FormsVBT.T; ts: TotalStats): ListerStats := LSInit;
  OVERRIDES
    update := LSUpdate;
  END;

PROCEDURE <A NAME="LSInit"><procedure>LSInit</procedure></A>(self: ListerStats;
                 fv: FormsVBT.T;
		 ts: TotalStats): ListerStats =
  BEGIN
    EVAL TreeList.Stats.init(self);
    self.fv := fv;
    self.ts := ts;
    RETURN self;
  END LSInit;

PROCEDURE <A NAME="LSUpdate"><procedure>LSUpdate</procedure></A>(self: ListerStats) =
  &lt;* FATAL FormsVBT.Error *&gt;
  BEGIN
    LOCK VBT.mu DO
      FormsVBT.PutText(self.fv, &quot;listerCommOut&quot;,
	Fmt.LongReal(self.bytesOut / 1024.0d0, Fmt.Style.Fix, 0));
      FormsVBT.PutText(self.fv, &quot;listerWireOut&quot;,
	Fmt.LongReal(self.wireBytesOut / 1024.0d0, Fmt.Style.Fix, 0));
    END;
    self.ts.update();
  END LSUpdate;
</PRE>***************************************************************************
 Detailer Statistics Display. 
***************************************************************************

<P><PRE>TYPE
  DetailerStats = Detailer.Stats OBJECT
    fv: FormsVBT.T;
    ts: TotalStats;
  METHODS
    init(fv: FormsVBT.T; ts: TotalStats): DetailerStats := DSInit;
  OVERRIDES
    update := DSUpdate;
  END;

PROCEDURE <A NAME="DSInit"><procedure>DSInit</procedure></A>(self: DetailerStats;
                 fv: FormsVBT.T;
		 ts: TotalStats): DetailerStats =
  BEGIN
    EVAL Detailer.Stats.init(self);
    self.fv := fv;
    self.ts := ts;
    RETURN self;
  END DSInit;

PROCEDURE <A NAME="DSUpdate"><procedure>DSUpdate</procedure></A>(self: DetailerStats) =
  &lt;* FATAL FormsVBT.Error *&gt;
  BEGIN
    LOCK VBT.mu DO
      FormsVBT.PutText(self.fv, &quot;detailerCommIn&quot;,
	Fmt.LongReal(self.bytesIn / 1024.0d0, Fmt.Style.Fix, 0));
      FormsVBT.PutText(self.fv, &quot;detailerWireIn&quot;,
	Fmt.LongReal(self.wireBytesIn / 1024.0d0, Fmt.Style.Fix, 0));
      FormsVBT.PutText(self.fv, &quot;detailerCommOut&quot;,
	Fmt.LongReal(self.bytesOut / 1024.0d0, Fmt.Style.Fix, 0));
      FormsVBT.PutText(self.fv, &quot;detailerWireOut&quot;,
	Fmt.LongReal(self.wireBytesOut / 1024.0d0, Fmt.Style.Fix, 0));
    END;
    self.ts.update();
  END DSUpdate;
</PRE>***************************************************************************
 Updater Statistics Display. 
***************************************************************************

<P><PRE>TYPE
  UpdaterStats = Updater.Stats OBJECT
    fv: FormsVBT.T;
    ts: TotalStats;
  METHODS
    init(fv: FormsVBT.T; ts: TotalStats): UpdaterStats := USInit;
  OVERRIDES
    start := USStart;
    update := USUpdate;
  END;

PROCEDURE <A NAME="USInit"><procedure>USInit</procedure></A>(self: UpdaterStats;
                 fv: FormsVBT.T;
		 ts: TotalStats): UpdaterStats =
  BEGIN
    EVAL Updater.Stats.init(self);
    self.fv := fv;
    self.ts := ts;
    RETURN self;
  END USInit;

PROCEDURE <A NAME="USStart"><procedure>USStart</procedure></A>(&lt;*UNUSED*&gt; self: UpdaterStats) =
  BEGIN
  END USStart;

PROCEDURE <A NAME="USUpdate"><procedure>USUpdate</procedure></A>(self: UpdaterStats; type: FileUpdater.UpdateType) =
  &lt;* FATAL FormsVBT.Error *&gt;
  VAR
    name: TEXT;
  BEGIN
    CASE type OF
    | FileUpdater.UpdateType.Edit =&gt;     name := &quot;edit&quot;;
    | FileUpdater.UpdateType.Checkout =&gt; name := &quot;co&quot;;
    | FileUpdater.UpdateType.Rsync =&gt;    name := &quot;rsync&quot;;
    | FileUpdater.UpdateType.Append =&gt;   name := &quot;app&quot;;
    | FileUpdater.UpdateType.Touch =&gt;    name := &quot;touch&quot;;
    | FileUpdater.UpdateType.Create =&gt;   name := &quot;add&quot;;
    | FileUpdater.UpdateType.Delete =&gt;   name := &quot;del&quot;;
    | FileUpdater.UpdateType.Replace =&gt;  name := &quot;repl&quot;;
    | FileUpdater.UpdateType.Fixup =&gt;    name := &quot;fixup&quot;;
    | FileUpdater.UpdateType.Other =&gt;    name := &quot;other&quot;;
    ELSE
      name := NIL;
    END;

    LOCK VBT.mu DO
      IF name # NIL THEN
	WITH info = self.updateInfo[type] DO
	  FormsVBT.PutText(self.fv, name &amp; &quot;Count&quot;,
	    Fmt.Int(info.fileCount));
	  FormsVBT.PutText(self.fv, name &amp; &quot;WireKB&quot;,
	    Fmt.LongReal(info.wireBytes / 1024.0d0, Fmt.Style.Fix, 0));
	  FormsVBT.PutText(self.fv, name &amp; &quot;FileKB&quot;,
	    Fmt.LongReal(info.fileBytes / 1024.0d0, Fmt.Style.Fix, 0));
	  IF info.fileBytes # 0.0d0 THEN
	    FormsVBT.PutText(self.fv, name &amp; &quot;Compr&quot;,
	      Fmt.LongReal(100.0d0 *
		(info.fileBytes - info.wireBytes) / info.fileBytes,
		style := Fmt.Style.Fix, prec := 1));
	  END;
	END;
      END;
      WITH info = self.totals DO
	FormsVBT.PutText(self.fv, &quot;totCount&quot;,
	  Fmt.Int(info.fileCount));
	FormsVBT.PutText(self.fv, &quot;totWireKB&quot;,
	  Fmt.LongReal(info.wireBytes / 1024.0d0, Fmt.Style.Fix, 0));
	FormsVBT.PutText(self.fv, &quot;totFileKB&quot;,
	  Fmt.LongReal(info.fileBytes / 1024.0d0, Fmt.Style.Fix, 0));
	IF info.fileBytes # 0.0d0 THEN
	  FormsVBT.PutText(self.fv, &quot;totCompr&quot;,
	    Fmt.LongReal(100.0d0 *
	      (info.fileBytes - info.wireBytes) / info.fileBytes,
	      style := Fmt.Style.Fix, prec := 1));
	END;

	FormsVBT.PutText(self.fv, &quot;updaterCommIn&quot;,
	  Fmt.LongReal(info.commBytes / 1024.0d0, Fmt.Style.Fix, 0));
	FormsVBT.PutText(self.fv, &quot;updaterWireIn&quot;,
	  Fmt.LongReal(info.wireBytes / 1024.0d0, Fmt.Style.Fix, 0));
      END;
    END;
    self.ts.update();
  END USUpdate;
</PRE>***************************************************************************
 Total Statistics Display. 
***************************************************************************

<P><PRE>TYPE
  TotalStats = MUTEX OBJECT
    fv: FormsVBT.T;
    ls: ListerStats := NIL;
    ds: DetailerStats := NIL;
    us: UpdaterStats := NIL;
  METHODS
    init(fv: FormsVBT.T): TotalStats := TSInit;
    update() := TSUpdate;
  END;

PROCEDURE <A NAME="TSInit"><procedure>TSInit</procedure></A>(self: TotalStats;
                 fv: FormsVBT.T): TotalStats =
  BEGIN
    self.fv := fv;
    (* The other fields have to be filled in manually. *)
    RETURN self;
  END TSInit;

PROCEDURE <A NAME="TSUpdate"><procedure>TSUpdate</procedure></A>(self: TotalStats) =
  &lt;* FATAL FormsVBT.Error *&gt;
  VAR
    commIn, wireIn, commOut, wireOut := 0.0d0;
    listTot, detTot, updTot, wireTot: LONGREAL;
  BEGIN
    LOCK self.ls DO
      commOut := commOut + self.ls.bytesOut;
      wireOut := wireOut + self.ls.wireBytesOut;
      listTot := self.ls.wireBytesOut;
    END;
    LOCK self.ds DO
      commIn := commIn + self.ds.bytesIn;
      wireIn := wireIn + self.ds.wireBytesIn;
      commOut := commOut + self.ds.bytesOut;
      wireOut := wireOut + self.ds.wireBytesOut;
      detTot := self.ds.wireBytesIn + self.ds.wireBytesOut;
    END;
    LOCK self.us DO
      commIn := commIn + self.us.totals.commBytes;
      wireIn := wireIn + self.us.totals.wireBytes;
      updTot := self.us.totals.wireBytes;
    END;
    wireTot := listTot + detTot + updTot;

    LOCK VBT.mu DO
      FormsVBT.PutText(self.fv, &quot;listerWireTot&quot;,
	Fmt.LongReal(listTot / 1024.0d0, Fmt.Style.Fix, 0));
      FormsVBT.PutText(self.fv, &quot;detailerWireTot&quot;,
	Fmt.LongReal(detTot / 1024.0d0, Fmt.Style.Fix, 0));
      FormsVBT.PutText(self.fv, &quot;updaterWireTot&quot;,
	Fmt.LongReal(updTot / 1024.0d0, Fmt.Style.Fix, 0));

      FormsVBT.PutText(self.fv, &quot;totCommIn&quot;,
	Fmt.LongReal(commIn / 1024.0d0, Fmt.Style.Fix, 0));
      FormsVBT.PutText(self.fv, &quot;totWireIn&quot;,
	Fmt.LongReal(wireIn / 1024.0d0, Fmt.Style.Fix, 0));
      FormsVBT.PutText(self.fv, &quot;totCommOut&quot;,
	Fmt.LongReal(commOut / 1024.0d0, Fmt.Style.Fix, 0));
      FormsVBT.PutText(self.fv, &quot;totWireOut&quot;,
	Fmt.LongReal(wireOut / 1024.0d0, Fmt.Style.Fix, 0));

      FormsVBT.PutText(self.fv, &quot;totWireTot&quot;,
	Fmt.LongReal(wireTot / 1024.0d0, Fmt.Style.Fix, 0));
    END;
  END TSUpdate;
</PRE>***************************************************************************
 Clock. 
***************************************************************************

<P><PRE>TYPE
  Clock = AutoRepeat.T OBJECT
    fv: FormsVBT.T;
    name: TEXT;
    updaterStats: UpdaterStats;
    started := FALSE;
    startTime: Time.T;
    seconds: CARDINAL;
  METHODS
    init(fv: FormsVBT.T; name: TEXT; updaterStats: UpdaterStats): Clock
      := ClockInit;
  OVERRIDES
    repeat := ClockTick;
  END;

PROCEDURE <A NAME="ClockInit"><procedure>ClockInit</procedure></A>(self: Clock;
                    fv: FormsVBT.T;
		    name: TEXT;
		    updaterStats: UpdaterStats): Clock =
  BEGIN
    EVAL NARROW(self, AutoRepeat.T).init(0, 500);
    self.fv := fv;
    self.name := name;
    self.updaterStats := updaterStats;
    RETURN self;
  END ClockInit;

PROCEDURE <A NAME="ClockTick"><procedure>ClockTick</procedure></A>(self: Clock) =
  &lt;* FATAL FormsVBT.Error *&gt;
  VAR
    elapsed: Time.T;
    hours, minutes, seconds: CARDINAL;
    clockText: TEXT;
    fileBytes: LONGREAL;
    xferRate: LONGREAL;
    xferRateText: TEXT;
  BEGIN
    IF NOT self.started THEN
      self.startTime := Time.Now();
      self.seconds := 0;
      self.started := TRUE;
      RETURN;
    END;

    elapsed := Time.Now() - self.startTime;
    seconds := FLOOR(elapsed);
    IF seconds # self.seconds THEN
      self.seconds := seconds;

      hours := seconds DIV 3600;
      seconds := seconds MOD 3600;
      minutes := seconds DIV 60;
      seconds := seconds MOD 60;
      clockText := Fmt.Pad(Fmt.Int(hours), 2, '0') &amp; &quot;:&quot; &amp;
	Fmt.Pad(Fmt.Int(minutes), 2, '0') &amp; &quot;:&quot; &amp;
	Fmt.Pad(Fmt.Int(seconds), 2, '0');

      LOCK self.updaterStats DO
	fileBytes := self.updaterStats.totals.fileBytes;
      END;

      xferRate := fileBytes / 1024.0d0 / elapsed;
      xferRateText := Fmt.Pad(Fmt.LongReal(xferRate, Fmt.Style.Fix, 1), 7) &amp;
	&quot; KB/sec&quot;;

      LOCK VBT.mu DO
	FormsVBT.PutText(self.fv, self.name, clockText);
	FormsVBT.PutText(self.fv, &quot;xferRate&quot;, xferRateText);
      END;
    END;
  END ClockTick;
</PRE>***************************************************************************

<P><PRE>PROCEDURE <A NAME="Run"><procedure>Run</procedure></A>(supFile: Pathname.T;
              config: FSClient.Configuration)
  RAISES {Error, Thread.Alerted} =
CONST
  FormFile = &quot;SupGUI.fv&quot;;
  PathVar = &quot;$CVSupPath&quot;;
VAR
  fv: FormsVBT.T;
  controller: Controller;
BEGIN
  (* If the client host's hostname is set up wrong, such that it is
     impossible to find out its IP address, then we will get an
     &quot;IP.FatalError&quot; exception from the X11 initialization code.  Find
     out about it now, so that we can produce an intelligent error
     message rather than a core dump. *)
  VAR
    ok := FALSE;
    addr: IP.Address;
  BEGIN
    TRY
      ok := IP.GetHostByName(UnixMisc.GetHostName(), addr);
    EXCEPT ELSE END;
    IF NOT ok THEN
      RAISE Error(&quot;Cannot get IP address of my own host&quot;
	&amp; &quot; -- is its hostname correct?&quot;);
    END;
  END;

  TRY
    TRY
      WITH path = Rsrc.BuildPath(PathVar, SupGUIBundle.Get()) DO
	fv := NEW(FormsVBT.T).initFromRsrc(FormFile, path);
      END;
    EXCEPT
    | Rd.Failure(list) =&gt;
      RAISE Error(&quot;Cannot read \&quot;&quot; &amp; FormFile &amp; &quot;\&quot;: &quot; &amp;
	ErrMsg.StrError(list));
    | Rsrc.NotFound =&gt;
      RAISE Error(&quot;Cannot find the \&quot;&quot; &amp; FormFile &amp; &quot;\&quot; resource&quot;);
    END;

    LOCK VBT.mu DO
      FormsVBT.PutText(fv, &quot;clientVersion&quot;, Version.Name);
      FormsVBT.PutText(fv, &quot;protoVersion&quot;,
	Fmt.Int(CVProto.Current.major) &amp; &quot;.&quot; &amp;
	Fmt.Int(CVProto.Current.minor));
      FormsVBT.PutText(fv, &quot;target&quot;, Version.Target);
    END;

    controller := NEW(Controller).init(
      fv := fv,
      supFile := supFile,
      config := config);
    Trestle.Install(fv);
    EVAL Thread.Fork(controller);
    Trestle.AwaitDelete(fv);
  EXCEPT
  | FormsVBT.Error(msg) =&gt;
    RAISE Error(&quot;FormsVBT error: &quot; &amp; msg);
  | TrestleComm.Failure =&gt;
    RAISE Error(&quot;Lost the connection to the X server&quot;);
  END;
END Run;

BEGIN
  Supported := TRUE;
END SupGUI.
</PRE>
</inModule>
<HR>
<A NAME="x1">interface Version is in:
</A><UL>
<LI><A HREF="Version.i3.html#0TOP0">client/src/Version.i3</A>
<LI><A HREF="../../cm3/derived/Version.i3.html#0TOP0">cm3/derived/Version.i3</A>
<LI><A HREF="../../server/src/Version.i3.html#0TOP0">server/src/Version.i3</A>
</UL>
<P>
<PRE>























</PRE>
</BODY>
</HTML>
