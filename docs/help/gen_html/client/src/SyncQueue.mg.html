<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: client/src/SyncQueue.mg</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>client/src/SyncQueue.mg</H2></A><HR>
<inModule>
<PRE></PRE> Copyright 1996-2003 John D. Polstra.
 * All rights reserved.
 *
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions
 * are met:
 * 1. Redistributions of source code must retain the above copyright
 *    notice, this list of conditions and the following disclaimer.
 * 2. Redistributions in binary form must reproduce the above copyright
 *    notice, this list of conditions and the following disclaimer in the
 *    documentation and/or other materials provided with the distribution.
 * 3. All advertising materials mentioning features or use of this software
 *    must display the following acknowledgment:
 *      This product includes software developed by John D. Polstra.
 * 4. The name of the author may not be used to endorse or promote products
 *    derived from this software without specific prior written permission.
 *
 * THIS SOFTWARE IS PROVIDED BY THE AUTHOR ``AS IS'' AND ANY EXPRESS OR
 * IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES
 * OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED.
 * IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR ANY DIRECT, INDIRECT,
 * INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT
 * NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
 * DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
 * THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
 * (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF
 * THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 *
 * $Id: SyncQueue.mg.html,v 1.3 2010-04-29 17:17:58 wagner Exp $ 

<P><PRE>GENERIC MODULE <genericModule><A HREF="SyncQueue.ig.html">SyncQueue</A></genericModule>(Elem, Seq);
</PRE><BLOCKQUOTE><EM> Where <CODE>Seq</CODE> is a generic instance <CODE>Sequence(Elem)</CODE>. </EM></BLOCKQUOTE><PRE>

IMPORT <A HREF="../../m3core/src/thread/Common/Thread.i3.html">Thread</A>;

REVEAL
  <A NAME="Private">Private</A> = MUTEX BRANDED OBJECT END;

  <A NAME="T">T</A> = Public BRANDED OBJECT
    seq: Seq.T;
    cond: Thread.Condition;
    qsize: CARDINAL := 0;
    closed: BOOLEAN := FALSE;
  OVERRIDES
    init := Init;
    put := Put;
    close := Close;
    get := Get;
    EOF := TestEOF;
    size := Size;
  END;

PROCEDURE <A NAME="Close"><procedure>Close</procedure></A>(self: T) =
  VAR
    doSignal: BOOLEAN;
  BEGIN
    LOCK self DO
      doSignal := self.qsize = 0 AND NOT self.closed;
      self.closed := TRUE;
    END;
    IF doSignal THEN Thread.Signal(self.cond) END;
  END Close;

PROCEDURE <A NAME="Init"><procedure>Init</procedure></A>(self: T; sizeHint: CARDINAL := 5): T =
  BEGIN
    self.seq := NEW(Seq.T).init(sizeHint);
    self.cond := NEW(Thread.Condition);
    RETURN self;
  END Init;

PROCEDURE <A NAME="Get"><procedure>Get</procedure></A>(self: T): Elem.T
  RAISES {EndOfFile, Thread.Alerted} =
  BEGIN
    LOCK self DO
      WHILE self.qsize = 0 AND NOT self.closed DO
	Thread.AlertWait(self, self.cond);
      END;
      IF self.qsize = 0 THEN RAISE EndOfFile END;
      DEC(self.qsize);
      RETURN self.seq.remlo();
    END;
  END Get;

PROCEDURE <A NAME="Put"><procedure>Put</procedure></A>(self: T; READONLY e: Elem.T) =
  VAR
    doSignal: BOOLEAN;
  BEGIN
    LOCK self DO
      &lt;* ASSERT NOT self.closed *&gt;
      doSignal := self.qsize = 0;
      self.seq.addhi(e);
      INC(self.qsize);
    END;
    IF doSignal THEN Thread.Signal(self.cond) END;
  END Put;

PROCEDURE <A NAME="Size"><procedure>Size</procedure></A>(self: T): CARDINAL =
  VAR
    size: CARDINAL;
  BEGIN
    LOCK self DO
      size := self.qsize;
      IF self.closed THEN INC(size) END;
    END;
    RETURN size;
  END Size;

PROCEDURE <A NAME="TestEOF"><procedure>TestEOF</procedure></A>(self: T): BOOLEAN
  RAISES {Thread.Alerted} =
  BEGIN
    LOCK self DO
      WHILE self.qsize = 0 AND NOT self.closed DO
	Thread.AlertWait(self, self.cond);
      END;
      RETURN self.qsize = 0;
    END;
  END TestEOF;

BEGIN
END SyncQueue.
</PRE>
</inModule>
<PRE>























</PRE>
</BODY>
</HTML>
