<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: client/src/RCSUpdater.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>client/src/RCSUpdater.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright 1997-2003 John D. Polstra.
 * All rights reserved.
 *
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions
 * are met:
 * 1. Redistributions of source code must retain the above copyright
 *    notice, this list of conditions and the following disclaimer.
 * 2. Redistributions in binary form must reproduce the above copyright
 *    notice, this list of conditions and the following disclaimer in the
 *    documentation and/or other materials provided with the distribution.
 * 3. All advertising materials mentioning features or use of this software
 *    must display the following acknowledgment:
 *      This product includes software developed by John D. Polstra.
 * 4. The name of the author may not be used to endorse or promote products
 *    derived from this software without specific prior written permission.
 *
 * THIS SOFTWARE IS PROVIDED BY THE AUTHOR ``AS IS'' AND ANY EXPRESS OR
 * IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES
 * OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED.
 * IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR ANY DIRECT, INDIRECT,
 * INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT
 * NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
 * DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
 * THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
 * (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF
 * THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 *
 * $Id: RCSUpdater.m3.html,v 1.3 2010-04-29 17:17:58 wagner Exp $ 

<P><PRE>MODULE <module><implements><A HREF="RCSUpdater.i3.html">RCSUpdater</A></implements></module>;

IMPORT
  <A HREF="../../suplib/src/Attic.i3.html">Attic</A>, <A HREF="../../suplib/src/CVProto.i3.html">CVProto</A>, <A HREF="../../suplib/src/ErrMsg.i3.html">ErrMsg</A>, <A HREF="../../suplib/src/FileAttr.i3.html">FileAttr</A>, <A HREF="../../suplib/src/FileStatus.i3.html">FileStatus</A>, <A HREF="FileUpdater.i3.html">FileUpdater</A>, <A HREF="../../suplib/src/Logger.i3.html">Logger</A>,
  <A HREF="../../libm3/src/os/Common/OSError.i3.html">OSError</A>, <A HREF="../../libm3/src/os/Common/Pathname.i3.html">Pathname</A>, <A HREF="../../suplib/src/RCSError.i3.html">RCSError</A>, <A HREF="../../suplib/src/RCSFile.i3.html">RCSFile</A>, <A HREF="../../suplib/src/RCSKeyword.i3.html">RCSKeyword</A>, <A HREF="../../suplib/src/RCSRevNum.i3.html">RCSRevNum</A>, <A HREF="../../libm3/src/rw/Rd.i3.html">Rd</A>,
  <A HREF="Receive.i3.html">Receive</A>, <A HREF="../../suplib/src/SupFileRec.i3.html">SupFileRec</A>, <A HREF="../../suplib/src/SupMisc.i3.html">SupMisc</A>, <A HREF="../../m3core/src/text/Text.i3.html">Text</A>, <A HREF="../../m3core/src/thread/Common/Thread.i3.html">Thread</A>, <A HREF="../../suplib/src/TokScan.i3.html">TokScan</A>, <A HREF="../../libm3/src/rw/Wr.i3.html">Wr</A>;

REVEAL
  <A NAME="T">T</A> = Public BRANDED OBJECT
    attr: FileAttr.T;
    rcsOptions: RCSFile.Options;
    wantSum: TEXT;
  OVERRIDES
    init := Init;
    update := Update;
    isRCS := IsRCS;
  END;

PROCEDURE <A NAME="Init"><procedure>Init</procedure></A>(self: T;
	       attr: FileAttr.T;
	       rcsOptions: RCSFile.Options;
	       wantSum: TEXT): T =
  BEGIN
    self.attr := attr;
    self.rcsOptions := rcsOptions;
    self.wantSum := wantSum;
    RETURN self;
  END Init;

PROCEDURE <A NAME="IsRCS"><procedure>IsRCS</procedure></A>(&lt;*UNUSED*&gt; self: T): BOOLEAN =
  BEGIN
    RETURN TRUE;
  END IsRCS;

PROCEDURE <A NAME="Update"><procedure>Update</procedure></A>(self: T;
                 sfr: SupFileRec.T;
		 name: Pathname.T;
		 toAttic: BOOLEAN;
                 proto: CVProto.T;
		 trace: Logger.T;
		 protoRd: Rd.T;
	         wr: Wr.T;
		 VAR status: FileUpdater.Status)
      RAISES {FileUpdater.Error, FileUpdater.FixupNeeded, Rd.EndOfFile,
	      Rd.Failure, Thread.Alerted, TokScan.Error, Wr.Failure} =
  VAR
    srcPath := SupMisc.CatPath(sfr.clientPrefix, name);
    origSrcPath := srcPath;
    fileChanged := FALSE;
    oldAttr: FileAttr.T;
    rf: RCSFile.T;
    ts: TokScan.T;
    cmd: TEXT;
    cmdCh: CHAR;
    branch: TEXT;
    tagName: TEXT;
    revNum: RCSRevNum.T;
    diffBase: RCSRevNum.T;
    date: TEXT;
    author: TEXT;
    expandText: TEXT;
    expandMode: RCSKeyword.ExpandMode;
  PROCEDURE NoteChange() =
    BEGIN
      IF NOT fileChanged THEN
	IF toAttic THEN
	  Logger.Notice(trace, &quot; Edit &quot; &amp; name &amp; &quot; -&gt; Attic&quot;);
	ELSE
	  Logger.Notice(trace, &quot; Edit &quot; &amp; name);
	END;
	fileChanged := TRUE;
      END;
    END NoteChange;
  BEGIN
    TRY
      rf := Attic.RCSFileOpenReadonly(srcPath);
      TRY
	oldAttr := RCSFile.GetAttr(rf);
	self.attr := FileAttr.Merge(self.attr, oldAttr);
	(* If the user wants to keep his files exactly the same as
	   the server, then we aim for byte-for-byte equality.  In
	   that case, we have to write the file using the same
	   whitespace conventions that were used in the server's
	   file.  Otherwise, we stick with whatever format is already
	   being used in the client's file. *)
	IF SupFileRec.Option.ExactRCS IN sfr.options
	AND rf.options # self.rcsOptions THEN
	  rf.options := self.rcsOptions;
	  NoteChange();
	END;

	LOOP
	  ts := proto.getCmd(protoRd);
	  cmdCh := ts.getChar(&quot;edit command&quot;);
	  cmd := Text.FromChar(cmdCh);
	  CASE cmdCh OF
	  | '.' =&gt;
	      EXIT;
	  | 'B' =&gt;  (* Set default branch. *)
	      NoteChange();
	      branch := ts.getToken(&quot;default branch&quot;);
	      ts.getEnd(&quot;end of \&quot;&quot; &amp; cmd &amp; &quot;\&quot; command&quot;);
	      Logger.Info(trace, &quot;  Set default branch to &quot; &amp; branch);
	      rf.branch := branch;
	  | 'b' =&gt;  (* Clear default branch. *)
	      NoteChange();
	      ts.getEnd(&quot;end of \&quot;&quot; &amp; cmd &amp; &quot;\&quot; command&quot;);
	      Logger.Info(trace, &quot;  Clear default branch&quot;);
	      rf.branch := NIL;
	  | 'D' =&gt;  (* Add delta. *)
	      NoteChange();
	      revNum := ts.getToken(&quot;revision number&quot;);
	      diffBase := ts.getToken(&quot;diffBase&quot;);
	      date := ts.getToken(&quot;date&quot;);
	      author := ts.getToken(&quot;author&quot;);
	      ts.getEnd(&quot;end of \&quot;&quot; &amp; cmd &amp; &quot;\&quot; command&quot;);
	      Logger.Info(trace, &quot;  Add delta &quot; &amp; revNum &amp; &quot; &quot;
		&amp; date &amp; &quot; &quot; &amp; author);
	      TRY
		EVAL Receive.Delta(protoRd, rf, revNum, diffBase,
		  date, author);
	      EXCEPT RCSError.E(msg) =&gt;
		RAISE FileUpdater.Error(&quot;Error adding delta: &quot; &amp; msg);
	      END;
	  | 'd' =&gt;  (* Delete delta. *)
	      NoteChange();
	      revNum := ts.getToken(&quot;revision number&quot;);
	      ts.getEnd(&quot;end of \&quot;&quot; &amp; cmd &amp; &quot;\&quot; command&quot;);
	      Logger.Info(trace, &quot;  Delete delta &quot; &amp; revNum);
	      TRY
		RCSFile.DeleteDelta(rf, RCSFile.GetDelta(rf, revNum));
	      EXCEPT RCSError.E(msg) =&gt;
		RAISE FileUpdater.Error(&quot;Error deleting delta: &quot; &amp; msg);
	      END;
	  | 'E' =&gt;  (* Change keyword expansion mode. *)
	      NoteChange();
	      expandText := ts.getToken(&quot;expand mode&quot;);
	      ts.getEnd(&quot;end of \&quot;&quot; &amp; cmd &amp; &quot;\&quot; command&quot;);
	      TRY
		expandMode := RCSKeyword.DecodeExpand(expandText);
	      EXCEPT RCSError.E(msg) =&gt; RAISE TokScan.Error(msg) END;
	      IF expandMode = RCSKeyword.ExpandMode.Default THEN
		Logger.Info(trace, &quot;  Set keyword expansion to default&quot;);
	      ELSE
		Logger.Info(trace, &quot;  Set keyword expansion to \&quot;&quot;
		  &amp; expandText &amp; &quot;\&quot;&quot;);
	      END;
	      rf.expand := expandMode;
	  | 'T' =&gt;  (* Add tag. *)
	      NoteChange();
	      tagName := ts.getToken(&quot;tag name&quot;);
	      revNum := ts.getToken(&quot;revision number&quot;);
	      ts.getEnd(&quot;end of \&quot;&quot; &amp; cmd &amp; &quot;\&quot; command&quot;);
	      Logger.Info(trace, &quot;  Add tag &quot; &amp; tagName &amp; &quot; -&gt; &quot; &amp; revNum);
	      TRY
		EVAL RCSFile.AddTag(rf, tagName, revNum);
	      EXCEPT RCSError.E(msg) =&gt;
		RAISE FileUpdater.Error(&quot;Error adding tag: &quot; &amp; msg);
	      END;
	  | 't' =&gt;  (* Delete tag. *)
	      NoteChange();
	      tagName := ts.getToken(&quot;tag name&quot;);
	      revNum := ts.getToken(&quot;revision number&quot;);
	      ts.getEnd(&quot;end of \&quot;&quot; &amp; cmd &amp; &quot;\&quot; command&quot;);
	      Logger.Info(trace, &quot;  Delete tag &quot; &amp; tagName
		&amp; &quot; -&gt; &quot; &amp; revNum);
	      TRY
		RCSFile.DeleteTag(rf, tagName, revNum);
	      EXCEPT RCSError.E(msg) =&gt;
		RAISE FileUpdater.Error(&quot;Error deleting tag: &quot; &amp; msg);
	      END;
	  ELSE
	    RAISE TokScan.Error(&quot;Invalid edit command \&quot;&quot; &amp; cmd &amp; &quot;\&quot;&quot;);
	  END;
	END;

	IF fileChanged THEN
	  status.updateType := FileUpdater.UpdateType.Edit;
	ELSE
	  VAR
	    msg: TEXT;
	  BEGIN
	    status.updateType := FileUpdater.UpdateType.Touch;
	    IF FileAttr.Equal(self.attr,
	    FileAttr.MaskOut(oldAttr, FileAttr.AllButModTime)) THEN
	      msg := &quot; SetAttrs &quot;;
	    ELSE
	      msg := &quot; Touch &quot;;
	    END;
	    msg := msg &amp; name;
	    IF toAttic THEN msg := msg &amp; &quot; -&gt; Attic&quot; END;
	    Logger.Notice(trace, msg);
	  END;
	END;

	TRY
	  RCSFile.ToWr(rf, wr);
	EXCEPT RCSError.E(msg) =&gt;
	  WITH errMsg = &quot;Invalid RCS file: &quot; &amp; msg DO
	    IF SupFileRec.Option.ExactRCS IN sfr.options THEN
	      RAISE FileUpdater.FixupNeeded(errMsg);
	    ELSE  (* No fixups allowed if local mods might be present. *)
	      RAISE FileUpdater.Error(errMsg);
	    END;
	  END;
	END;

	status.fs := NEW(FileStatus.T, name := name,
	  clientAttr := self.attr, serverAttr := self.attr);
	IF toAttic THEN
	  status.fs.type := FileStatus.Type.FileDead;
	ELSE
	  status.fs.type := FileStatus.Type.FileLive;
	END;
	status.fromAttic := srcPath # origSrcPath;
	status.modified := fileChanged;
	IF SupFileRec.Option.CheckRCS IN sfr.options THEN
	  status.wantSum := self.wantSum;
	ELSE
	  status.wantSum := NIL;
	END;
      FINALLY
	RCSFile.Close(rf);
      END;
    EXCEPT
    | OSError.E(l) =&gt;
</PRE><BLOCKQUOTE><EM> FIXME - This exception can come from the Close().  We should change it
   to raise RCSError.E instead, analogous to Wr.Close(). </EM></BLOCKQUOTE><PRE>
	RAISE FileUpdater.Error(&quot;Cannot open: &quot; &amp; ErrMsg.StrError(l));
    | RCSError.E(msg) =&gt;
	RAISE FileUpdater.Error(&quot;RCS file error: &quot; &amp; msg);
    END;
  END Update;

BEGIN
END RCSUpdater.
</PRE>
</inModule>
<PRE>























</PRE>
</BODY>
</HTML>
