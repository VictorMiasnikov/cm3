<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: obliqlibemb/src/ObSharedObj.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>obliqlibemb/src/ObSharedObj.m3</H2></A><HR>
<inModule>
<PRE></PRE> <P>
 * This library is free software; you can redistribute it and/or          
 * modify it under the terms of the GNU Library General Public            
 * License as published by the Free Software Foundation.                  
 * This library is distributed in the hope that it will be useful,        
 * but WITHOUT ANY WARRANTY; without even the implied warranty of         
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU      
 * Library General Public License for more details.                       
 * If you do not have a copy of the GNU Library General Public            
 * License, write to The Free Software Foundation, Inc.,                  
 * 675 Mass Ave, Cambridge, MA 02139, USA.                                
 *                                                                        
 * For more information on this program, contact Blair MacIntyre          
 * (bm@cs.columbia.edu) or Steven Feiner (feiner@cs.columbia.edu)         
 * at the Computer Science Dept., Columbia University,                    
 * 500 W 120th St, Room 450, New York, NY, 10027.                         
 *                                                                        
 * Copyright (C) Blair MacIntyre 1995, Columbia University 1995           
 * 
 * Author          : Blair MacIntyre
 * Created On      : Tue Aug  8 16:29:55 1995
 * Last Modified By: Blair MacIntyre
 * Last Modified On: Sun Feb 18 15:02:20 1996
 * Update Count    : 24
 *
 * SCCS Status     : %W%	%G%
 * 
 * HISTORY
 

<P> TODO: create picklers that will not duplicate the obliq object when
   passed to a host more than once.  Simply send a receive the real
   object first.  If it already exists, there will be a proxy, which
   can just be used. 

<P><PRE>MODULE <module><implements><A HREF="ObSharedObj.i3.html">ObSharedObj</A></implements></module>;

IMPORT <A HREF="../../obliqrt/src/ObCommand.i3.html">ObCommand</A>, <A HREF="../../obliqrt/src/ObLib.i3.html">ObLib</A>, <A HREF="ObLoader.i3.html">ObLoader</A>, <A HREF="../../obliqrt/src/ObValue.i3.html">ObValue</A>, <A HREF="../../obliqrt/src/Obliq.i3.html">Obliq</A>, <A HREF="../../libm3/src/params/Env.i3.html">Env</A>,
       <A HREF="ObEmbProxiedObj.i3.html">ObEmbProxiedObj</A>, <A HREF="../../synloc/src/SynLocation.i3.html">SynLocation</A>, <A HREF="../../sharedobj/src/SharedObj.i3.html">SharedObj</A>, <A HREF="../../sharedobj/src/SharedObjRT.i3.html">SharedObjRT</A>, <A HREF="../../m3core/src/text/Text.i3.html">Text</A>,
       <A HREF="#x1">ObText</A>, <A HREF="#x2">ObInt</A>, <A HREF="../../m3core/src/thread/Common/Thread.i3.html">Thread</A>, <A HREF="../../tcp/src/common/IP.i3.html">IP</A>, <A HREF="../../sharedobj/src/ObjectSpace.i3.html">ObjectSpace</A>, <A HREF="ObError.i3.html">ObError</A>;
</PRE> When a new proxied object is created, we first want to check to see
   if there already is a proxied obj.  If so, just use it.
   This can happen if the obliq value is pickled across the network.
   If the shared object exists, it will be returned from the pickle
   reader.  Thus, we should use the existing proxy, too. 

<P><PRE>REVEAL
  <A NAME="T">T</A> = ObEmbProxiedObj.T BRANDED &quot;ObSharedObj.T&quot; OBJECT END;

PROCEDURE <A NAME="GetArg"><procedure>GetArg</procedure></A> (args    : ObValue.ArgArray;
                  idx     : INTEGER;
                  package : ObLib.T;
                  opCode  : ObLib.OpCode;
                  loc     : SynLocation.T) : SharedObj.T
    RAISES {ObValue.Error, ObValue.Exception} =
  BEGIN
    WITH raw = Obliq.ObjectSelect (args[idx], &quot;raw&quot;) DO
      TYPECASE raw OF
      | T (node) =&gt;
        RETURN node.po;
      ELSE
        ObValue.BadArgType (idx, pkgname, package.name, opCode.name, loc);
        RETURN NIL;      (* ... only to suppress compiler warning *)
      END;
    END;
  END GetArg;
</PRE>***************************************************************************
 Setup procedures                                                          
***************************************************************************

<P><PRE>PROCEDURE <A NAME="SetupPackage"><procedure>SetupPackage</procedure></A>() =
  PROCEDURE NewOpCode (name: TEXT; arity: INTEGER; code: Code) : OpCode =
    BEGIN
      RETURN NEW (OpCode, name := name, arity := arity, code := code);
    END NewOpCode;

  VAR opCodes: REF ObLib.OpCodes;
  BEGIN
    opCodes := NEW(REF ObLib.OpCodes, NUMBER(Code));
    opCodes^ :=
        ObLib.OpCodes{
          NewOpCode (&quot;Error&quot;,              -1, Code.Error),
          NewOpCode (&quot;Fatal&quot;,              -1, Code.Fatal),
          NewOpCode (&quot;AcquireGlobalLock&quot;,   1, Code.AcquireGlobalLock),
          NewOpCode (&quot;ReleaseGlobalLock&quot;,   1, Code.ReleaseGlobalLock),
          NewOpCode (&quot;Own&quot;,                 2, Code.Own),
          NewOpCode (&quot;Disown&quot;,              1, Code.Disown),
          NewOpCode (&quot;SetTimeliness&quot;,       2, Code.SetTimeliness),
          NewOpCode (&quot;SetNodeName&quot;,         1, Code.SetNodeName),
          NewOpCode (&quot;SetDefaultSequencer&quot;, 2, Code.SetDefaultSequencer),
          NewOpCode (&quot;DebugLevel&quot;,          1, Code.DebugLevel),
          NewOpCode (&quot;PullObject&quot;,          1, Code.PullObject)
        };
    errorException := Obliq.NewException(&quot;SharedObj_Error&quot;);
    fatalException := Obliq.NewException(&quot;SharedObj_Fatal&quot;);
    ObLib.Register(NEW(Package, name:=pkgname, opCodes:=opCodes));
    ObLib.RegisterHelp (pkgname, Help);
  END SetupPackage;

PROCEDURE <A NAME="SetupModule"><procedure>SetupModule</procedure></A> (loader: ObLoader.T) =
  BEGIN
    pkgloader := loader;
    pkgloader.load (&quot;SharedObj.obl&quot;);
  END SetupModule;
</PRE>***************************************************************************
 Execution machinery                                                       
***************************************************************************

<P><PRE>TYPE
  Code = {Error, Fatal, AcquireGlobalLock, ReleaseGlobalLock, Own,
          Disown, SetTimeliness, SetNodeName, SetDefaultSequencer,
          DebugLevel, PullObject};

  OpCode = ObLib.OpCode OBJECT
    code: Code;
  END;

  Package = ObLib.T OBJECT OVERRIDES
    Eval := Eval;
  END;

CONST
  pkgname = &quot;SharedObj&quot;;

VAR
  nodename: TEXT;
</PRE> PullObject uses a very cute trick.
   The theory is this.  By obtaining the <CODE>raw</CODE> field, and
   from it the proxy, if the object is remote we will now have
   a locate copy, created when the modula-3 object is pickled
   across, a new one is created and the appropriate mkproxy is
   called.   If the object is locate, we will return the same
   obliq object as before.
<P>
   Unlike the anim3D ObProxiedObj.Extend, we do not allow the
   object to be extended: the various copies would them
   potentially become different.
<P>
   One of the two methods will end up being more useful.  I'll kill
   one eventually.
 

<P><PRE>PROCEDURE <A NAME="Eval"><procedure>Eval</procedure></A>(self: Package; opCode: ObLib.OpCode;
               &lt;* UNUSED *&gt; arity        : ObLib.OpArity;
               READONLY args: ObValue.ArgArray;
               &lt;* UNUSED *&gt; temp: BOOLEAN; loc: SynLocation.T)
    : ObValue.Val RAISES {ObValue.Error, ObValue.Exception} =
  BEGIN
    TRY
      CASE NARROW(opCode, OpCode).code OF
      | Code.Error =&gt;
        RETURN errorException;
      | Code.Fatal =&gt;
        RETURN fatalException;
      | Code.AcquireGlobalLock =&gt;
        WITH obj = GetArg(args, 1, self, opCode, loc) DO
          SharedObj.AcquireGlobalLock(obj);
        END;
        RETURN ObValue.valOk;
      | Code.ReleaseGlobalLock =&gt;
        WITH obj = GetArg(args, 1, self, opCode, loc) DO
          SharedObj.ReleaseGlobalLock(obj);
        END;
        RETURN ObValue.valOk;
      | Code.Own =&gt;
        WITH obj = GetArg(args, 1, self, opCode, loc),
             arg1 = ObInt.GetArg(args, 2, self, opCode, loc) DO
          IF arg1 &lt; FIRST(SharedObj.Timeliness) OR
            arg1 &gt; LAST(SharedObj.Timeliness) THEN
            ObValue.BadArgVal(2, &quot;invalid timeliness&quot;, self.name,
                              opCode.name, loc);
          END;
          SharedObj.Own(obj, arg1);
        END;
        RETURN ObValue.valOk;
      | Code.Disown =&gt;
        WITH obj = GetArg(args, 1, self, opCode, loc) DO
          SharedObj.Disown(obj);
        END;
        RETURN ObValue.valOk;
      | Code.SetTimeliness =&gt;
        WITH obj = GetArg(args, 1, self, opCode, loc),
             arg1 = ObInt.GetArg(args, 2, self, opCode, loc) DO
          IF arg1 &lt; FIRST(SharedObj.Timeliness) OR
            arg1 &gt; LAST(SharedObj.Timeliness) THEN
            ObValue.BadArgVal(2, &quot;invalid timeliness&quot;, self.name,
                              opCode.name, loc);
          END;
          SharedObj.SetTimeliness(obj, arg1);
        END;
        RETURN ObValue.valOk;
      | Code.SetNodeName =&gt;
        nodename := ObText.GetArg(args, 1, self, opCode, loc);
        IF Text.Equal(nodename, &quot;&quot;) THEN
          nodename := IP.GetCanonicalByAddr(IP.GetHostAddr());
        END;
        SharedObjRT.ExportSpace(nodename);
        RETURN ObText.M3ToObliq(nodename);
      | Code.SetDefaultSequencer =&gt;
        VAR space: ObjectSpace.T;
            host := ObText.GetArg(args, 1, self, opCode, loc);
            name := ObText.GetArg(args, 2, self, opCode, loc);
        BEGIN
          IF Text.Equal(&quot;&quot;, host) THEN
            WITH defhost = Env.Get(&quot;SEQUENCERHOST&quot;) DO
              IF defhost # NIL THEN
                host := defhost;
              END;
            END;
          END;
          IF Text.Equal(&quot;&quot;, name) THEN
            WITH defname = Env.Get(&quot;SEQUENCERNAME&quot;) DO
              IF defname # NIL THEN
                name := defname;
              END;
            END;
          END;

          IF NOT Text.Equal(&quot;&quot;, host) OR NOT Text.Equal(&quot;&quot;, name) THEN
            space := SharedObjRT.ImportSpace(host, name);
            IF space = NIL THEN
              ObValue.RaiseException(errorException,
                                     self.name &amp; &quot;_&quot; &amp; opCode.name &amp;
                                     &quot;: node &quot; &amp; name &amp; &quot;@&quot; &amp; host &amp;
                                     &quot; is unavailable&quot;, loc);
            END;
          ELSE
            space := SharedObjRT.LocalSpace();
          END;
          SharedObjRT.SetDfltSequencer(space);
        END;
        RETURN ObValue.valOk;
      | Code.DebugLevel =&gt;
        WITH arg1 = ObInt.GetArg(args, 1, self, opCode, loc) DO
          SharedObjRT.DebugLevel(arg1);
        END;
        RETURN ObValue.valOk;
      | Code.PullObject =&gt;
        WITH po = GetArg (args, 1, self, opCode, loc) DO
          RETURN po.proxy.obj;
        END;
      ELSE
        ObValue.BadOp(self.name, opCode.name, loc);
        RETURN NIL; (* to shut up warning in compiler about no return value *)
      END;
    EXCEPT
    | IP.Error(ec) =&gt;
      ObValue.RaiseException(ObValue.netException,
                             self.name &amp; &quot;_&quot; &amp; opCode.name &amp;
                             &quot;: &quot; &amp; ObError.AtomListToText(ec), loc);
      RETURN NIL; (* to shut up warning in compiler about no return value *)
    | Thread.Alerted =&gt;
      ObValue.RaiseException(ObValue.threadAlerted,
                             self.name &amp; &quot;_&quot; &amp; opCode.name &amp; &quot;: &quot;, loc);
      RETURN NIL; (* to shut up warning in compiler about no return value *)
    | SharedObj.Error(ec) =&gt;
      ObValue.RaiseException(errorException,
                             self.name &amp; &quot;_&quot; &amp; opCode.name &amp;
                             &quot;: &quot; &amp; ObError.AtomListToText(ec), loc);
      RETURN NIL; (* to shut up warning in compiler about no return value *)
    END;
  END Eval;
</PRE>***************************************************************************
 Help                                                                      
***************************************************************************

<P><PRE>PROCEDURE <A NAME="Help"><procedure>Help</procedure></A> (self : ObCommand.T; arg : TEXT; &lt;* UNUSED *&gt; data : REFANY) =
  BEGIN
    IF pkgloader # NIL THEN
      pkgloader.help (self, arg, pkgname);
    END;
  END Help;

VAR
  pkgloader: ObLoader.T := NIL;

BEGIN
END ObSharedObj.
</PRE>
</inModule>
<HR>
<A NAME="x1">interface ObText is in:
</A><UL>
<LI><A HREF="../../obliqlib3D/src/ObText.i3.html#0TOP0">obliqlib3D/src/ObText.i3</A>
<LI><A HREF="ObText.i3.html#0TOP0">obliqlibemb/src/ObText.i3</A>
</UL>
<P>
<HR>
<A NAME="x2">interface ObInt is in:
</A><UL>
<LI><A HREF="../../obliqlib3D/src/ObInt.i3.html#0TOP0">obliqlib3D/src/ObInt.i3</A>
<LI><A HREF="ObInt.i3.html#0TOP0">obliqlibemb/src/ObInt.i3</A>
</UL>
<P>
<PRE>























</PRE>
</BODY>
</HTML>
