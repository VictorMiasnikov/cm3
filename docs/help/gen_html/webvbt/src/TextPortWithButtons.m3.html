<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: webvbt/src/TextPortWithButtons.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>webvbt/src/TextPortWithButtons.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright (C) 1995, Digital Equipment Corporation                         
 All rights reserved.                                                      
 See the file COPYRIGHT for a full description.                            
                                                                           
 Last modified on Tue Jul  4 10:00:25 PDT 1995 by mhb                      

<P><PRE>MODULE <module><implements><A HREF="TextPortWithButtons.i3.html">TextPortWithButtons</A></implements></module>;

IMPORT <A HREF="../../vbtkit/src/etext/TextPort.i3.html">TextPort</A>, <A HREF="../../ui/src/vbt/PaintOp.i3.html">PaintOp</A>, <A HREF="../../ui/src/vbt/VBT.i3.html">VBT</A>, <A HREF="../../vbtkit/src/vtext/VText.i3.html">VText</A>, <A HREF="../../vbtkit/src/vtext/VTDef.i3.html">VTDef</A>, <A HREF="../../libm3/src/geometry/Rect.i3.html">Rect</A>;
IMPORT <A HREF="TextPortButton.i3.html">TextPortButton</A>, <A HREF="../derived/TextPortButtonSeq.i3.html">TextPortButtonSeq</A>, <A HREF="../../libm3/src/rw/Rd.i3.html">Rd</A>, <A HREF="../../m3core/src/thread/Common/Thread.i3.html">Thread</A>;

REVEAL
  <A NAME="T">T</A> = Public BRANDED OBJECT
    buttons: TextPortButtonSeq.T;
    regular, highlight: VText.IntervalOptions;
  OVERRIDES
    init := Init;
    insertButton := InsertButton;
    numberOfButtons := NumberOfButtons;
    getButton := GetButton;
    clearButtons := ClearButtons;
    mouse := Mouse;
  END;

PROCEDURE <A NAME="Init"><procedure>Init</procedure></A>(self: T; readOnly: BOOLEAN): T =
  VAR
    regularScheme :=
       PaintOp.MakeColorScheme(PaintOp.FromRGB(0.425, 0.765, 0.85,
                                               mode := PaintOp.Mode.Accurate),
                               PaintOp.FromRGB(0.0, 0.0, 0.0));
    highlightScheme :=
       PaintOp.MakeColorScheme(PaintOp.FromRGB(0.0, 0.0, 0.0),
                               PaintOp.FromRGB(0.425, 0.765, 0.85,
                                               mode := PaintOp.Mode.Accurate));
  BEGIN
    EVAL TextPort.T.init(self, readOnly := readOnly);
    self.buttons := NEW(TextPortButtonSeq.T).init(1);
    self.regular := VText.MakeIntervalOptions(
                        style := VText.IntervalStyle.BoxStyle,
                        whiteBlack := regularScheme,
                        whiteStroke := regularScheme,
                        leading := regularScheme.bg);
    self.highlight := VText.MakeIntervalOptions(
                          style := VText.IntervalStyle.BoxStyle,
                          whiteBlack := highlightScheme,
                          whiteStroke := highlightScheme,
                          leading := highlightScheme.bg);
    RETURN self
  END Init;

PROCEDURE <A NAME="InsertButton"><procedure>InsertButton</procedure></A>(self: T; b: TextPortButton.T) =
  VAR
    start := TextPort.Index(self);
  &lt;*FATAL VTDef.Error*&gt;
  BEGIN
    TextPort.Insert(self, &quot;  &quot; &amp; b.label &amp; &quot;  &quot;);
    WITH vtext = TextPort.GetVText(self),
         end = TextPort.Index(self) DO
      b.interval := VText.CreateInterval(vtext, start+1, end-1, self.regular);
      self.buttons.addhi(b);
      VText.SwitchInterval(b.interval, VText.OnOffState.On);
    END;
  END InsertButton;

PROCEDURE <A NAME="NumberOfButtons"><procedure>NumberOfButtons</procedure></A>(self: T): CARDINAL =
  BEGIN
    RETURN self.buttons.size()
  END NumberOfButtons;

PROCEDURE <A NAME="GetButton"><procedure>GetButton</procedure></A>(self: T; i: CARDINAL): TextPortButton.T =
  BEGIN
    RETURN self.buttons.get(i)
  END GetButton;

PROCEDURE <A NAME="ClearButtons"><procedure>ClearButtons</procedure></A>(self: T) =
  BEGIN
    self.buttons := NEW(TextPortButtonSeq.T).init(1);
  END ClearButtons;

PROCEDURE <A NAME="Mouse"><procedure>Mouse</procedure></A>(self: T; READONLY cd: VBT.MouseRec) =
  VAR
    index, left, right: VText.Index;
    button: TextPortButton.T;
    found := FALSE;
    vtext := TextPort.GetVText(self);
  &lt;*FATAL VTDef.Error*&gt;

  PROCEDURE CurrentIndex(): VText.Index =
    VAR
      left, middle, right: VText.Index;
      cage: Rect.T;
    BEGIN
      TRY
        EVAL VText.Pounce(vtext, 0, cd.cp.pt,
                          VText.SelectionMode.CharSelection,
                          left, middle, right, cage);
      EXCEPT Rd.Failure, Rd.EndOfFile, Thread.Alerted =&gt; (* skip *)
      END;
      RETURN middle
    END CurrentIndex;

  BEGIN
    index := CurrentIndex();
    FOR i := 0 TO self.buttons.size()-1 DO
      button := self.buttons.get(i);
      TextPortButton.GetExtents(button, left, right);
      IF left &lt;= index AND index &lt;= right THEN
        found := TRUE;
        EXIT
      END;
    END;
    IF found THEN
      CASE cd.clickType OF
      | VBT.ClickType.FirstDown =&gt;
        VText.ChangeIntervalOptions(button.interval, self.highlight);
        TRY VText.Update(vtext) EXCEPT Rd.Failure, Rd.EndOfFile, Thread.Alerted =&gt;
          (* skip *)
        END;
      | VBT.ClickType.LastUp =&gt;
        VText.ChangeIntervalOptions(button.interval, self.regular);
        button.callback(cd);
        TRY VText.Update(vtext) EXCEPT Rd.Failure, Rd.EndOfFile, Thread.Alerted =&gt;
          (* skip *)
        END;
      ELSE (* skip *)
      END;
    ELSE
      Public.mouse(self, cd);
    END;
  END Mouse;

BEGIN
END TextPortWithButtons.
</PRE>
</inModule>
<PRE>























</PRE>
</BODY>
</HTML>
