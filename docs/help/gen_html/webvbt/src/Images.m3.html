<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: webvbt/src/Images.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>webvbt/src/Images.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright (C) 1995, Digital Equipment Corporation                         
 All rights reserved.                                                      
 See the file COPYRIGHT for a full description.                            
                                                                           
 Last modified on Tue Aug 27 13:22:06 PDT 1996 by najork                   
      modified on Wed Aug 16 15:59:32 PDT 1995 by mhb                      

<P><PRE>MODULE <module><implements><A HREF="Images.i3.html">Images</A></implements></module>;

IMPORT <A HREF="../../libm3/src/rw/FileRd.i3.html">FileRd</A>, <A HREF="../../libm3/src/rw/FileWr.i3.html">FileWr</A>, <A HREF="../../vbtkit/src/lego/Image.i3.html">Image</A>, <A HREF="ImageUtils.i3.html">ImageUtils</A>, <A HREF="../../libm3/src/os/Common/OSError.i3.html">OSError</A>, <A HREF="../../libm3/src/os/Common/Pipe.i3.html">Pipe</A>, <A HREF="../../ui/src/vbt/Pixmap.i3.html">Pixmap</A>, <A HREF="../../libm3/src/rw/Rd.i3.html">Rd</A>,
       <A HREF="../../libm3/derived/TextRefTbl.i3.html">TextRefTbl</A>, <A HREF="../../libm3/src/rw/TextRd.i3.html">TextRd</A>, <A HREF="../../libm3/src/rw/TextWr.i3.html">TextWr</A>, <A HREF="../../m3core/src/thread/Common/Thread.i3.html">Thread</A>, <A HREF="../../libm3/src/rw/Wr.i3.html">Wr</A>;

TYPE Format = {JPEG, GIF, XBM};

PROCEDURE <A NAME="FromJPEG"><procedure>FromJPEG</procedure></A> (bits: TEXT): Pixmap.T
  RAISES {Error, Thread.Alerted} =
  BEGIN
    RETURN GetPixmap (bits, Format.JPEG)
  END FromJPEG;

PROCEDURE <A NAME="FromGIF"><procedure>FromGIF</procedure></A> (bits: TEXT): Pixmap.T
  RAISES {Error, Thread.Alerted} =
  BEGIN
    RETURN GetPixmap (bits, Format.GIF)
  END FromGIF;

PROCEDURE <A NAME="FromXBM"><procedure>FromXBM</procedure></A> (bits: TEXT): Pixmap.T
  RAISES {Error, Thread.Alerted} =
  BEGIN
    RETURN GetPixmap (bits, Format.XBM)
  END FromXBM;
</PRE> This simple version of GetPixmap doesn't use pipes, which means that the
   bits get fondled an extra time as they go into a TEXT after being
   converted into pbm format. On the other hand, there isn't any thread
   creation, and the version with pipes seems fragile (crashes with a
   <CODE>Thread.Alerted not handled</CODE>) so perhaps this is the version to use. --mhb 7/22/95 
<PRE>PROCEDURE <A NAME="GetPixmap"><procedure>GetPixmap</procedure></A> (bits: TEXT; format: Format): Pixmap.T
  RAISES {Error, Thread.Alerted} =
  VAR
    rd : Rd.T;
    wr : Wr.T;
    raw: Image.Raw;
  BEGIN
    rd := TextRd.New(bits);
    wr := TextWr.New();
    TRY
      CASE format OF
      | Format.JPEG =&gt; ImageUtils.jpegtopbm(rd, wr);
      | Format.GIF =&gt; ImageUtils.giftopbm(rd, wr);
      | Format.XBM =&gt; ImageUtils.xbmtopbm(rd, wr);
      END;
      rd := TextRd.New(TextWr.ToText(wr));
      raw := Image.FromRd(rd);
      RETURN Image.Unscaled(raw)
    EXCEPT
      ImageUtils.Error, Image.Error, Rd.Failure =&gt; RAISE Error
    END
  END GetPixmap;

&lt;*UNUSED*&gt;
PROCEDURE <A NAME="GetPixmapWithPipes"><procedure>GetPixmapWithPipes</procedure></A> (bits: TEXT; format: Format): Pixmap.T
  RAISES {Error, Thread.Alerted} =
  VAR
    rd    : Rd.T;
    destWr: FileWr.T;
    destRd: FileRd.T;
    hr, hw: Pipe.T;
    pm    : Pixmap.T;
    raw   : Image.Raw;
  BEGIN
    rd := TextRd.New(bits);
    TRY Pipe.Open(hr, hw) EXCEPT OSError.E =&gt; RAISE Error END;
    TRY
      TRY
        destWr := NEW(FileWr.T).init(hw);
        destRd := NEW(FileRd.T).init(hr);
        WITH t = Thread.Fork(
                   NEW(Closure, rd := rd, wr := destWr, fmt := format)) DO
          TRY
            raw := Image.FromRd(destRd);
          EXCEPT
          | Rd.Failure, Image.Error =&gt; Thread.Alert(t); RAISE Error
          | Thread.Alerted =&gt; Thread.Alert(t); RAISE Thread.Alerted
          END;
          EVAL Thread.AlertJoin(t);
        END;
        pm := Image.Unscaled(raw);
        RETURN pm
      EXCEPT
        OSError.E =&gt; RAISE Error
      END
    FINALLY
      CloseRd(destRd)
    END;
  END GetPixmapWithPipes;

TYPE
  Closure = Thread.Closure OBJECT
              rd : Rd.T;
              wr : Wr.T;
              fmt: Format;
            OVERRIDES
              apply := PumpBits
            END;

PROCEDURE <A NAME="PumpBits"><procedure>PumpBits</procedure></A> (cl: Closure): REFANY =
  BEGIN
    TRY
      CASE cl.fmt OF
      | Format.JPEG =&gt; ImageUtils.jpegtopbm(cl.rd, cl.wr);
      | Format.GIF  =&gt; ImageUtils.giftopbm(cl.rd, cl.wr);
      | Format.XBM  =&gt; ImageUtils.xbmtopbm(cl.rd, cl.wr);
      END;
      CloseWr(cl.wr)
    EXCEPT
      ImageUtils.Error, Thread.Alerted =&gt;
    END;
    RETURN NIL
  END PumpBits;

PROCEDURE <A NAME="CloseWr"><procedure>CloseWr</procedure></A> (wr: Wr.T) RAISES {Thread.Alerted} =
  BEGIN
    TRY Wr.Close(wr) EXCEPT Wr.Failure =&gt; END;
  END CloseWr;

PROCEDURE <A NAME="CloseRd"><procedure>CloseRd</procedure></A> (rd: Rd.T) RAISES {Thread.Alerted} =
  BEGIN
    TRY Rd.Close(rd) EXCEPT Rd.Failure =&gt; END;
  END CloseRd;

VAR
  mu := NEW(MUTEX);
  cache := NEW(TextRefTbl.Default).init();

PROCEDURE <A NAME="FromCache"><procedure>FromCache</procedure></A> (url: TEXT; VAR pm: Pixmap.T): BOOLEAN =
  VAR ref: REFANY; found: BOOLEAN;
  BEGIN
    LOCK mu DO
      found := cache.get(url, ref);
      IF found THEN pm := NARROW(ref, REF Pixmap.T)^ END
    END;
    RETURN found
  END FromCache;

PROCEDURE <A NAME="ToCache"><procedure>ToCache</procedure></A> (url: TEXT; pm: Pixmap.T) =
  VAR ref := NEW(REF Pixmap.T);
  BEGIN
    ref^ := pm;
    LOCK mu DO EVAL cache.put(url, ref) END
  END ToCache;

BEGIN
END Images.
</PRE>
</inModule>
<PRE>























</PRE>
</BODY>
</HTML>
