<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: webvbt/src/WebVBT.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>webvbt/src/WebVBT.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright (C) 1995, Digital Equipment Corporation                         
 All rights reserved.                                                      
 See the file COPYRIGHT for a full description.                            
                                                                           
 Last modified on Thu May 16 15:59:05 PDT 1996 by mhb                      
      modified on Wed Jan 17 14:34:14 PST 1996 by najork                   
<PRE>&lt;* PRAGMA LL *&gt;

MODULE <module><implements><A HREF="WebVBT.i3.html">WebVBT</A></implements></module>;

IMPORT <A HREF="CIText.i3.html">CIText</A>, <A HREF="../../ui/src/split/Filter.i3.html">Filter</A>, <A HREF="../../libm3/src/fmtlex/Fmt.i3.html">Fmt</A>, <A HREF="../../ui/src/vbt/Font.i3.html">Font</A>, <A HREF="#x1">HTML</A>, <A HREF="HTMLVBT.i3.html">HTMLVBT</A>, <A HREF="HTMLVBTText.i3.html">HTMLVBTText</A>, <A HREF="HTMLVBTG.i3.html">HTMLVBTG</A>,
       <A HREF="Images.i3.html">Images</A>, <A HREF="../../vbtkit/src/lego/MultiClass.i3.html">MultiClass</A>, <A HREF="../../vbtkit/src/lego/MultiSplit.i3.html">MultiSplit</A>, <A HREF="../../ui/src/vbt/Pixmap.i3.html">Pixmap</A>, <A HREF="../../vbtkit/src/lego/PixmapVBT.i3.html">PixmapVBT</A>, <A HREF="../../libm3/src/geometry/Point.i3.html">Point</A>,
       <A HREF="../../libm3/derived/RefList.i3.html">RefList</A>, <A HREF="SimpleWeb.i3.html">SimpleWeb</A>, <A HREF="../../ui/src/split/Split.i3.html">Split</A>, <A HREF="../../libm3/src/text/TextExtras.i3.html">TextExtras</A>, <A HREF="../../libm3/derived/TextList.i3.html">TextList</A>, <A HREF="../../vbtkit/src/etext/TextEditVBT.i3.html">TextEditVBT</A>,
       <A HREF="../../vbtkit/src/etext/TextPort.i3.html">TextPort</A>, <A HREF="../../ui/src/split/TextureVBT.i3.html">TextureVBT</A>, <A HREF="../../ui/src/split/TextVBT.i3.html">TextVBT</A>, <A HREF="../../m3core/src/thread/Common/Thread.i3.html">Thread</A>, <A HREF="URLCache.i3.html">URLCache</A>, <A HREF="../../ui/src/vbt/VBT.i3.html">VBT</A>, <A HREF="../../web/src/Web.i3.html">Web</A>, <A HREF="../../vbtkit/src/lego/Image.i3.html">Image</A>,
       <A HREF="../../libm3/src/rw/Rd.i3.html">Rd</A>, <A HREF="../../libm3/src/rw/TextRd.i3.html">TextRd</A>, <A HREF="../../ui/src/vbt/PaintOp.i3.html">PaintOp</A>;

REVEAL <A NAME="Private">Private</A> = Filter.T BRANDED OBJECT END;

REVEAL
  <A NAME="T">T</A> = Public BRANDED OBJECT
        &lt;* LL=VBT.mu *&gt;
        t: Thread.T := NIL;
      OVERRIDES
        init     := Init;
        fetch    := Fetch;
        fromText := FromText;
        stop     := Stop;
        getLinks := GetLinks;
        search   := Search;
        ready    := Ready;
        hotlink  := Hotlink;
        isindex  := Isindex;
        ismap    := Ismap;
        form     := Form;
      END;

PROCEDURE <A NAME="Init"><procedure>Init</procedure></A> (v: T): T =
  BEGIN
    RETURN Filter.T.init(v, TextureVBT.New(txt:=Pixmap.Gray))
  END Init;

PROCEDURE <A NAME="FromText"><procedure>FromText</procedure></A> (v             : T;
                    contents      : TEXT;
                    contentType   : Web.MIMEType := Web.MIMEType.Text;
                    contentSubType: TEXT         := &quot;html&quot;;
                    url           : TEXT         := &quot;text:&quot;;
                    style         : Style        := Style.Normal;
                    zippers       : BOOLEAN      := FALSE;
                    reload        : BOOLEAN      := FALSE;
                    server        : Web.T        := NIL;
                    scrollBar     : BOOLEAN      := TRUE) =

  VAR webpage := NEW(Web.Page);
  BEGIN
    v.stop();
    webpage.header.contentType := contentType;
    webpage.header.contentSubType := contentSubType;
    webpage.header.location := url;
    webpage.contents := contents;
    v.t :=
      Thread.Fork(NEW(FromTextClosure, v := v, webpage := webpage,
                      url := url, style := style, zippers := zippers,
                      reload := reload, server := server,
                      scrollBar := scrollBar))
  END FromText;

TYPE
  Closure = Thread.Closure OBJECT
                      v        : T;
                      style    : Style;
                      zippers  : BOOLEAN;
                      reload   : BOOLEAN;
                      server   : Web.T;
                      scrollBar: BOOLEAN;
                    END;

TYPE
  FromTextClosure = Closure OBJECT
                      webpage : Web.Page;
                      url     : TEXT;
                    OVERRIDES
                      apply := FromTextWrapper;
                    END;

PROCEDURE <A NAME="FromTextWrapper"><procedure>FromTextWrapper</procedure></A> (cl: FromTextClosure): REFANY =
  BEGIN
    Display(cl.webpage, cl.v, cl.url, cl.style, cl.zippers, cl.reload,
            cl.server, cl.scrollBar);
    RETURN NIL
  END FromTextWrapper;

PROCEDURE <A NAME="Fetch"><procedure>Fetch</procedure></A> (v        : T;
                 url      : TEXT;
                 style    : Style   := Style.Normal;
                 zippers  : BOOLEAN := FALSE;
                 reload   : BOOLEAN := FALSE;
                 server   : Web.T   := NIL;
                 scrollBar: BOOLEAN := TRUE) =
  BEGIN
    v.stop();
    v.t := Thread.Fork(
             NEW(FetchClosure, v := v, url := url, style := style,
                 zippers := zippers, reload := reload, server := server,
                 scrollBar := scrollBar))
  END Fetch;

TYPE
  FetchClosure = Closure OBJECT
                   url    : TEXT;
                 OVERRIDES
                   apply := FetchWrapper;
                 END;

PROCEDURE <A NAME="FetchWrapper"><procedure>FetchWrapper</procedure></A> (cl: FetchClosure): REFANY =
  VAR webpage: Web.Page; base: TEXT;
  BEGIN
    TRY
      webpage :=
          SimpleWeb.Fetch(cl.url, reload := cl.reload, server := cl.server);
      base := webpage.header.location;
                   (* SimpleWeb.Fetch always fills in header.location *)
      Display(webpage, cl.v, base, cl.style, cl.zippers, cl.reload, cl.server,
              cl.scrollBar);
    EXCEPT
      Thread.Alerted =&gt;
    END;
    RETURN NIL
  END FetchWrapper;

CONST
    FontName = &quot;-*-fixed-medium-r-semicondensed-*-*-120-*-*-*-*-iso8859-1&quot;;

PROCEDURE <A NAME="Display"><procedure>Display</procedure></A> (webpage  : Web.Page;
                   v        : T;
                   base     : TEXT;
                   style    : Style;
                   zippers  : BOOLEAN;
                   reload   : BOOLEAN;
                   server   : Web.T;
                   scrollBar: BOOLEAN) =

  PROCEDURE NewTextPage (t: TEXT) RAISES {Thread.Alerted} =
    VAR page := NEW(TextPage);
    BEGIN
      page.vbt := NEW(TextEditVBT.T).init();
      WITH tp = page.vbt.tp DO
        TextPort.SetText(tp, t);
        tp.setReadOnly(TRUE);
        tp.setFont(Font.FromName(ARRAY OF TEXT{FontName}));
      END;
      NewPage(page, page.vbt)
    END NewTextPage;

  PROCEDURE NewHTMLPage (h: HTML.T) RAISES {Thread.Alerted} =
    VAR
      page              := NEW(HTMLPage);
      toLoad: RefList.T;

    BEGIN
      IF h.base = NIL THEN h.base := base END;
      page.html := h;

      URLCache.PutHTML (base, h);

      CASE style OF

      | Style.Ugly =&gt;
          page.vbt := NEW(TextHTMLVBT, parent := v).init(page.html);
          NewPage(page, page.vbt);

      | Style.NoImages =&gt;
          page.vbt := NEW(GraphicsHTMLVBT, parent := v).init(
                        page.html, TRUE, zippers, toLoad, scrollBar);
          NewPage(page, page.vbt);

      | Style.Normal =&gt;
          page.vbt := NEW(GraphicsHTMLVBT, parent := v).init(
                        page.html, FALSE, zippers, toLoad, scrollBar);
          LoadResources(v, reload, server, toLoad, FALSE);
          NewPage(page, page.vbt);

      | Style.Background =&gt;
          page.vbt := NEW(GraphicsHTMLVBT, parent := v).init(
                        page.html, FALSE, zippers, toLoad, scrollBar);
          NewPage(page, page.vbt, RefList.Length(toLoad));
          LoadResources(v, reload, server, toLoad, TRUE);

      END;
    END NewHTMLPage;

  PROCEDURE NewImagePage (pm: Pixmap.T) RAISES {Thread.Alerted} =
    VAR
      page := NEW(ImagePage);
      (* op := PaintOp.BgFg; *)
      op := PaintOp.Copy;
    BEGIN
      page.vbt := NEW(PixmapVBT.T).init(pm, op := op);
      NewPage(page, page.vbt);
    END NewImagePage;

  PROCEDURE NewPage (page: Page; vbt: VBT.T; imageCt := 0)
    RAISES {Thread.Alerted} =
    BEGIN
      LOCK VBT.mu DO
        IF Thread.TestAlert() THEN RAISE Thread.Alerted END;
        IF v.t # Thread.Self() THEN RETURN END;
        EVAL Filter.Replace(v, vbt);
        page.header := webpage.header;
        page.contents := webpage.contents;
        v.url := base;
        v.page := page;
        v.ready(imageCt);
      END
    END NewPage;

  BEGIN
    TRY
      WITH hdr   = webpage.header,
           stuff = webpage.contents DO
        IF hdr.contentType = Web.MIMEType.Text THEN
          IF CIText.Equal(hdr.contentSubType, &quot;html&quot;) THEN
            NewHTMLPage(HTML.FromRd(TextRd.New(stuff)))
          ELSE
            NewTextPage(stuff);
          END;
        ELSIF hdr.contentType = Web.MIMEType.Image THEN
          TRY
            IF CIText.Equal(hdr.contentSubType, &quot;jpeg&quot;) THEN
              NewImagePage(Images.FromJPEG(stuff));
            ELSIF CIText.Equal(hdr.contentSubType, &quot;gif&quot;) THEN
              NewImagePage(Images.FromGIF(stuff))
            ELSIF CIText.Equal(hdr.contentSubType, &quot;ppm&quot;) OR
                  CIText.Equal(hdr.contentSubType, &quot;pnm&quot;) OR
                  CIText.Equal(hdr.contentSubType, &quot;pbm&quot;) OR
                  CIText.Equal(hdr.contentSubType, &quot;pgm&quot;) THEN
              WITH rd = TextRd.New(stuff) DO
                NewImagePage(Image.Unscaled(Image.FromRd(rd)));
              END;
            ELSE
              NewTextPage(&quot;cannot handle '&quot; &amp; hdr.contentSubType &amp; &quot;'&quot;);
            END
          EXCEPT
            Rd.Failure, Image.Error, Images.Error =&gt;
                NewTextPage(&quot;cannot display image&quot;);
          END
        END
      END
    EXCEPT
      Thread.Alerted =&gt;
    END
  END Display;

PROCEDURE <A NAME="LoadResources"><procedure>LoadResources</procedure></A> (v              : T;
                         reload         : BOOLEAN;
                         server         : Web.T;
                         list           : RefList.T;
                         callReadyMethod: BOOLEAN    )
  RAISES {Thread.Alerted} =
  VAR
    ct  : INTEGER;
    info: HTMLVBTG.Info;
    page: Web.Page;
  BEGIN
    ct := RefList.Length(list);
    WHILE list # NIL DO
      info := list.head;
      page :=
        SimpleWeb.Fetch(info.url, reload := reload, server := server);
      info.load(page);
      DEC(ct);
      IF callReadyMethod THEN InvokeReadyMethod(v, ct) END;
      list := list.tail;
    END;
    IF callReadyMethod THEN InvokeReadyMethod(v, 0) END;
  END LoadResources;

PROCEDURE <A NAME="InvokeReadyMethod"><procedure>InvokeReadyMethod</procedure></A> (v: T; arg: INTEGER) RAISES {Thread.Alerted} =
  BEGIN
    LOCK VBT.mu DO
      IF Thread.TestAlert() THEN RAISE Thread.Alerted END;
      IF v.t # Thread.Self() THEN RETURN END;
      v.ready(arg)
    END;
  END InvokeReadyMethod;

TYPE GraphicsHTMLVBT = HTMLVBTG.T OBJECT
    parent: T;
  OVERRIDES
    hotlink := HTMLVBTHotlink;
    ismap   := HTMLVBTIsmap;
    isindex := HTMLVBTIsindex;
  END;

TYPE TextHTMLVBT = HTMLVBTText.T OBJECT
    parent: T;
  OVERRIDES
    hotlink := HTMLVBTHotlink;
    ismap   := HTMLVBTIsmap;
    isindex := HTMLVBTIsindex;
  END;

PROCEDURE <A NAME="HTMLVBTHotlink"><procedure>HTMLVBTHotlink</procedure></A> (         ch : HTMLVBT.T;
                                   url: TEXT;
                          READONLY cd : VBT.MouseRec) =
  BEGIN
    TYPECASE ch OF
    | GraphicsHTMLVBT (v) =&gt; v.parent.hotlink(url, cd)
    ELSE
    END
  END HTMLVBTHotlink;

PROCEDURE <A NAME="HTMLVBTIsmap"><procedure>HTMLVBTIsmap</procedure></A> (         ch : HTMLVBT.T;
                                 url: TEXT;
                        READONLY pt : Point.T;
                        READONLY cd : VBT.MouseRec) =
  BEGIN
    TYPECASE ch OF
    | GraphicsHTMLVBT (v) =&gt;
        v.parent.ismap(url &amp; &quot;?&quot; &amp; Fmt.Int(pt.h) &amp; &quot;,&quot; &amp; Fmt.Int(pt.v), cd)
    ELSE
    END
  END HTMLVBTIsmap;

PROCEDURE <A NAME="HTMLVBTIsindex"><procedure>HTMLVBTIsindex</procedure></A> (ch: HTMLVBT.T; typein: TEXT) =
  VAR p: T;
  BEGIN
    TYPECASE ch OF
    | GraphicsHTMLVBT (v) =&gt; p := v.parent;
    | TextHTMLVBT (v) =&gt; p := v.parent;
    ELSE &lt;* ASSERT FALSE *&gt;
    END;
    p.isindex(p.url &amp; &quot;?&quot; &amp; typein);
  END HTMLVBTIsindex;

PROCEDURE <A NAME="Stop"><procedure>Stop</procedure></A> (self: T) =
  BEGIN
    IF self.t # NIL THEN Thread.Alert(self.t) END
  END Stop;

PROCEDURE <A NAME="Hotlink"><procedure>Hotlink</procedure></A> (&lt;* UNUSED *&gt; self: T;
                   &lt;* UNUSED *&gt; link: TEXT;
                   &lt;* UNUSED *&gt; READONLY cd: VBT.MouseRec) =
  BEGIN
  END Hotlink;

PROCEDURE <A NAME="Isindex"><procedure>Isindex</procedure></A> (&lt;* UNUSED *&gt; self: T; &lt;* UNUSED *&gt; typein: TEXT) =
  BEGIN
  END Isindex;

PROCEDURE <A NAME="Ismap"><procedure>Ismap</procedure></A> (&lt;* UNUSED *&gt;          self  : T;
                 &lt;* UNUSED *&gt;          absURL: TEXT;
                 &lt;* UNUSED *&gt; READONLY cd    : VBT.MouseRec) =
  BEGIN
  END Ismap;

PROCEDURE <A NAME="Form"><procedure>Form</procedure></A> (&lt;* UNUSED *&gt; self: T) =
  BEGIN
  END Form;

PROCEDURE <A NAME="Ready"><procedure>Ready</procedure></A> (&lt;* UNUSED *&gt; self: T; &lt;* UNUSED *&gt; remImages: CARDINAL) =
  BEGIN
  END Ready;

PROCEDURE <A NAME="GetLinks"><procedure>GetLinks</procedure></A> (self: T): TextList.T =
  BEGIN
    TYPECASE self.page OF
    | NULL =&gt;
    | HTMLPage (h) =&gt; RETURN HTML.GetLinks(h.html)
    ELSE
    END;
    RETURN NIL
  END GetLinks;

PROCEDURE <A NAME="Search"><procedure>Search</procedure></A> (self: T; pattern: TEXT): BOOLEAN =
  BEGIN
    TYPECASE self.page OF
    | NULL =&gt;
    | TextPage (t) =&gt; RETURN SearchVBTTree(t.vbt, pattern)
    | HTMLPage (h) =&gt; RETURN SearchVBTTree(h.vbt, pattern)
    ELSE
    END;
    RETURN FALSE
  END Search;

PROCEDURE <A NAME="SearchVBTTree"><procedure>SearchVBTTree</procedure></A> (v: VBT.T; pattern: TEXT): BOOLEAN =
  &lt;* FATAL MultiSplit.NotAChild *&gt;
  BEGIN
    TYPECASE v OF
    | TextVBT.T (textvbt) =&gt;
        WITH text = TextVBT.Get(textvbt) DO
          RETURN TextSearch(text, pattern)
        END;
    | TextPort.T (textport) =&gt;
        WITH text = TextPort.GetText(textport) DO
          RETURN TextSearch(text, pattern)
        END
    ELSE
      IF MultiClass.Resolve(v) # NIL OR ISTYPE(v, Split.T) THEN
        VAR ch := MultiSplit.Succ(v, NIL);
        BEGIN
          WHILE ch # NIL DO
            IF SearchVBTTree(ch, pattern) THEN
              RETURN TRUE
            ELSE
              ch := MultiSplit.Succ(v, ch)
            END
          END;
          RETURN FALSE
        END
      ELSE
        RETURN FALSE
      END
    END;
  END SearchVBTTree;

PROCEDURE <A NAME="TextSearch"><procedure>TextSearch</procedure></A> (text, pattern: TEXT): BOOLEAN =
  VAR index: CARDINAL := 0;
  BEGIN
    RETURN TextExtras.FindSub(text, pattern, index)
  END TextSearch;

BEGIN
END WebVBT.
</PRE>
</inModule>
<HR>
<A NAME="x1">interface HTML is in:
</A><UL>
<LI><A HREF="../../cm3ide/src/misc/HTML.i3.html#0TOP0">cm3ide/src/misc/HTML.i3</A>
<LI><A HREF="HTML.i3.html#0TOP0">webvbt/src/HTML.i3</A>
</UL>
<P>
<PRE>























</PRE>
</BODY>
</HTML>
