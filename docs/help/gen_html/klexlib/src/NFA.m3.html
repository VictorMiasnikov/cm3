<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: klexlib/src/NFA.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>klexlib/src/NFA.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright (c) 2000 California Institute of Technology 
 All rights reserved. See the file COPYRIGHT for a full description. 
 $Id: NFA.m3.html,v 1.3 2010-04-29 17:18:49 wagner Exp $ 

<P><PRE>MODULE <module><implements><A HREF="NFA.i3.html">NFA</A></implements></module>;
IMPORT <A HREF="../../libm3/src/geometry/Interval.i3.html">Interval</A>;
IMPORT <A HREF="../../ktoklib/src/CharRange.i3.html">CharRange</A>;
IMPORT <A HREF="../../m3core/src/text/Text.i3.html">Text</A>;
IMPORT <A HREF="NFANode.i3.html">NFANode</A>;
TYPE
  Node = NFANode.T;
  NodeArray = REF ARRAY OF Node;

PROCEDURE <A NAME="AssignIDs"><procedure>AssignIDs</procedure></A>(a: T): INTEGER =
  VAR
    cur := a.start;
    ID := 0;
  BEGIN
    WHILE cur # NIL DO
      cur.ID := ID;
      INC(ID);
      cur := cur.next;
    END;
    RETURN ID;
  END AssignIDs;

PROCEDURE <A NAME="InitReplacements"><procedure>InitReplacements</procedure></A>(old: Node; news: NodeArray) =
  VAR
    new: Node;
  PROCEDURE Replace(n: Node): Node =
    BEGIN
      IF n = NIL THEN
        RETURN NIL
      ELSE
        RETURN news[n.ID];
      END;
    END Replace;
  BEGIN
    IF old # NIL THEN
      new := Replace(old);
      new.targ1 := Replace(old.targ1);
      new.targ2 := Replace(old.targ2);
      new.keyBegin := old.keyBegin;
      new.keyEnd := old.keyEnd;
      new.output := old.output;
      new.next := Replace(old.next);
      InitReplacements(old.next, news);
    END;
  END InitReplacements;

PROCEDURE <A NAME="Copy"><procedure>Copy</procedure></A>(a: T): T =
  VAR
    result := NEW(T);
    nodes: NodeArray;
  BEGIN
    &lt;* ASSERT a.end.next = NIL *&gt;
    &lt;* ASSERT a.start # NIL *&gt;
    nodes := NEW(NodeArray, AssignIDs(a));
    FOR i := 0 TO LAST(nodes^) DO
      nodes[i] := NEW(Node);
    END;
    InitReplacements(a.start, nodes);
    result.start := nodes[a.start.ID];
    result.end := nodes[a.end.ID];
    &lt;* ASSERT result.end.next = NIL *&gt;
    &lt;* ASSERT result.start # NIL *&gt;
    RETURN result;
  END Copy;

PROCEDURE <A NAME="FromChar"><procedure>FromChar</procedure></A>(c: CHAR): T =
  VAR
    result := NEW(T);
  BEGIN
    result.end := NEW(Node, keyBegin := c, keyEnd := c);
    result.start := result.end;
    RETURN result;
  END FromChar;

PROCEDURE <A NAME="Empty"><procedure>Empty</procedure></A>(): T =
  BEGIN
    RETURN FromChar('\000');
  END Empty;

PROCEDURE <A NAME="Concat"><procedure>Concat</procedure></A>(a, b: T): T =
  BEGIN
    &lt;* ASSERT a.end.next = NIL *&gt;
    &lt;* ASSERT a.start # NIL *&gt;
    &lt;* ASSERT b.end.next = NIL *&gt;
    &lt;* ASSERT b.start # NIL *&gt;
    &lt;* ASSERT a.end.targ2 = NIL *&gt;
    a.end.targ2 := b.start;
    a.end.next :=  b.start;
    a.end := b.end;
    &lt;* ASSERT a.end.next = NIL *&gt;
    &lt;* ASSERT a.start # NIL *&gt;
    RETURN a;
  END Concat;

PROCEDURE <A NAME="Or"><procedure>Or</procedure></A>(a, b: T; endCap: BOOLEAN := TRUE): T =
  VAR
    start := NEW(Node);
    end: Node;
  BEGIN
    &lt;* ASSERT a.end.next = NIL *&gt;
    &lt;* ASSERT a.start # NIL *&gt;
    &lt;* ASSERT b.end.next = NIL *&gt;
    &lt;* ASSERT b.start # NIL *&gt;

    (* append nodeLists *)
    start.next := a.start;
    a.end.next := b.start;

    (* set start targets *)
    start.targ1 := a.start;
    start.targ2 := b.start;
    &lt;* ASSERT a.end.targ2 = NIL *&gt;
    &lt;* ASSERT b.end.targ2 = NIL *&gt;

    (* set new start *)
    a.start := start;
    IF endCap THEN
      end := NEW(Node);
      (* set end targets *)
      a.end.targ2 := end;
      b.end.targ2 := end;
      (* set new end *)
      a.end := end;
      (* append nodeLists *)
      b.end.next := end;
    ELSE
      a.end := b.end;
    END;
    &lt;* ASSERT a.start # NIL *&gt;
    &lt;* ASSERT a.end.next = NIL *&gt;
    RETURN a;
  END Or;

PROCEDURE <A NAME="Plus"><procedure>Plus</procedure></A>(a: T): T =
  BEGIN
    IF a.end.targ1 = NIL THEN
      a.end.targ1 := a.start;
    ELSE
      &lt;* ASSERT a.end.targ2 = NIL *&gt;
      a.end.targ2 := NEW(Node, targ1 := a.start);
      a.end.next := a.end.targ2;
      a.end := a.end.targ2;
    END;
    RETURN a;
  END Plus;

PROCEDURE <A NAME="Output"><procedure>Output</procedure></A>(a: T; code: INTEGER): T =
  BEGIN
    &lt;* ASSERT a.end.output = NFANode.NoOutput *&gt;
    a.end.output := code;
    RETURN a;
  END Output;
</PRE> extra constructors 

<P><PRE>PROCEDURE <A NAME="FromString"><procedure>FromString</procedure></A>(s: TEXT): T =
  VAR
    result: T := Empty();
  BEGIN
    FOR i := 0 TO Text.Length(s)-1 DO
      result := Concat(result, FromChar(Text.GetChar(s, i)));
    END;
    RETURN result;
  END FromString;

PROCEDURE <A NAME="FromRange"><procedure>FromRange</procedure></A>(c: CharRange.T): T =
  VAR
    block: T;
    result: T := NIL;
    keyBegin, keyEnd: CHAR;
  BEGIN
    keyBegin := VAL(1, CHAR);
    REPEAT
      WHILE keyBegin # LAST(CHAR) AND NOT keyBegin IN c DO
        INC(keyBegin);
      END;
      IF keyBegin IN c THEN
        keyEnd := keyBegin;
        WHILE keyEnd # LAST(CHAR) AND keyEnd IN c DO
          INC(keyEnd);
        END;
        block := FromChar(keyBegin);
        keyBegin := keyEnd; (* for the next block *)
        IF NOT keyEnd IN c THEN
          DEC(keyEnd);
        END;
        block.start.keyEnd := keyEnd;
        IF result = NIL THEN
          result := block;
        ELSE
          result := Or(result, block);
        END;
      END;
    UNTIL keyBegin = LAST(CHAR);
</PRE>    FOR i := FIRST(CHAR) TO LAST(CHAR) DO
      IF i IN c THEN
        IF result = NIL THEN
          result := FromChar(i);
        ELSE
          result := Or(result, FromChar(i));
        END;
      END; 
    END; 

<P>    <PRE>RETURN result;
  END FromRange;

PROCEDURE <A NAME="Rept"><procedure>Rept</procedure></A>(a: T; count: Interval.T): T =
  BEGIN
    IF count.lo = 1 AND count.hi &gt;= OrMore DIV 2 THEN
      RETURN Plus(a);
    ELSIF count.lo = 1 AND count.hi=1 THEN
      RETURN a;
    ELSIF count.lo # 0 THEN
      RETURN Concat(Copy(a), Rept(a, Interval.T{count.lo-1,count.hi-1}));
    ELSIF count.hi &gt;= OrMore DIV 2 THEN
      RETURN Or(Empty(), Plus(a));
    ELSIF count.hi &lt;= 0 THEN
      RETURN Empty();
    ELSE
      RETURN Or(Empty(), Concat(Copy(a),
                                Rept(a, Interval.T{0, count.hi-1})));
    END;
  END Rept;

BEGIN
END NFA.
</PRE>
</inModule>
<PRE>























</PRE>
</BODY>
</HTML>
