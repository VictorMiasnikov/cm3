<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: m3tk/src/sem/M3CImportS.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>m3tk/src/sem/M3CImportS.m3</H2></A><HR>
<inModule>
<PRE>MODULE <module><implements><A HREF="M3CImportS.i3.html">M3CImportS</A></implements></module>;
</PRE>*************************************************************************
                      Copyright (C) Olivetti 1989                        
                          All Rights reserved                            
                                                                         
 Use and copy of this software and preparation of derivative works based 
 upon this software are permitted to any person, provided this same      
 copyright notice and the following Olivetti warranty disclaimer are      
 included in any copy of the software or any modification thereof or     
 derivative work therefrom made by any person.                           
                                                                         
 This software is made available AS IS and Olivetti disclaims all        
 warranties with respect to this software, whether expressed or implied  
 under any law, including all implied warranties of merchantibility and  
 fitness for any purpose. In no event shall Olivetti be liable for any   
 damages whatsoever resulting from loss of use, data or profits or       
 otherwise arising out of or in connection with the use or performance   
 of this software.                                                       
*************************************************************************

<P> Copyright (C) 1991, Digital Equipment Corporation           
 All rights reserved.                                        
 See the file COPYRIGHT for a full description.              

<P><PRE>IMPORT <A HREF="#x1">M3AST</A>, <A HREF="../ast/M3AST_AS.i3.html">M3AST_AS</A>;

IMPORT <A HREF="../ast/M3AST_LX_F.i3.html">M3AST_LX_F</A>, <A HREF="../ast/M3AST_AS_F.i3.html">M3AST_AS_F</A>, <A HREF="../ast/M3AST_SM_F.i3.html">M3AST_SM_F</A>;

IMPORT <A HREF="../../derived/SeqM3AST_AS_Used_interface_id.i3.html">SeqM3AST_AS_Used_interface_id</A>, <A HREF="../../derived/SeqM3AST_AS_IMPORTED.i3.html">SeqM3AST_AS_IMPORTED</A>;

IMPORT <A HREF="M3ASTNext.i3.html">M3ASTNext</A>;
IMPORT <A HREF="../toolmisc/M3Context.i3.html">M3Context</A>, <A HREF="../syn/M3CSrcPos.i3.html">M3CSrcPos</A>; (* for Standard *)

PROCEDURE <A NAME="Set"><procedure>Set</procedure></A>(an: M3AST.NODE) RAISES {}=
  VAR
    unit: M3AST_AS.UNIT_NORMAL;
    m: M3AST_AS.Module;
    used_intf_id: M3AST_AS.Used_interface_id;
    iter: SeqM3AST_AS_Used_interface_id.Iter;
  BEGIN
    TYPECASE an OF
    | M3AST_AS.Module(module) =&gt;
        m := module;
        unit := module;
    | M3AST_AS.Interface(interface) =&gt;
        m := NIL;
        unit := interface;
    ELSE
      RETURN
    END;

    (* Everybody imports Standard - ugh not next time *)
    AddStandard(unit);

    IF m # NIL THEN
      iter := SeqM3AST_AS_Used_interface_id.NewIter(m.sm_export_s);
      WHILE SeqM3AST_AS_Used_interface_id.Next(iter, used_intf_id) DO
        AddInterface(unit, used_intf_id);
      END; (* while *)
    END; (* if *)
    AddImports(unit, unit.as_import_s);
  END Set;

PROCEDURE <A NAME="AddInterface"><procedure>AddInterface</procedure></A>(
    unit: M3AST_AS.UNIT_NORMAL;
    used_intf_id: M3AST_AS.Used_interface_id)
    RAISES {}=
  VAR
    id := used_intf_id.lx_symrep;
    xused_intf_id: M3AST_AS.Used_interface_id;
    iter := SeqM3AST_AS_Used_interface_id.NewIter(unit.sm_import_s);
  BEGIN
    (* Don't add errant ids *)
    IF id = NIL THEN RETURN END;
    (* sm_import_s is really a set, no duplicates, no self *)
    WHILE SeqM3AST_AS_Used_interface_id.Next(iter, xused_intf_id) DO
      IF id = xused_intf_id.lx_symrep THEN
        RETURN
      END; (* if *)
    END; (* while *)
    (* new; add it and everything it imports, but be graceful about
    unresolved names. Do not add self *)
    IF used_intf_id.sm_def # unit.as_id THEN
      SeqM3AST_AS_Used_interface_id.AddRear(unit.sm_import_s, used_intf_id);
      IF used_intf_id.sm_def # NIL THEN
        VAR spec := NARROW(used_intf_id.sm_def, M3AST_AS.UNIT_ID).sm_spec;
        BEGIN
          IF spec # NIL THEN
            TYPECASE spec OF
            | M3AST_AS.UNIT_NORMAL(unit_normal) =&gt;
                AddImports(unit, unit_normal.as_import_s);
            ELSE (* error elsewhere, (import of generic) *)
            END;
          END;
        END;
      END; (* if *)
    END; (* if *)
  END AddInterface;

PROCEDURE <A NAME="AddStandard"><procedure>AddStandard</procedure></A>(unit: M3AST_AS.UNIT_NORMAL) RAISES {}=
  VAR
    cu: M3AST_AS.Compilation_Unit;
    std_id: M3AST_AS.Interface_id;
    used_intf_id: M3AST_AS.Used_interface_id;
  BEGIN
    cu := M3Context.Standard();
    IF unit = cu.as_root THEN RETURN END; (* this is Standard *)
    std_id := cu.as_root.as_id;
    used_intf_id := NEW(M3AST_AS.Used_interface_id).init();
    used_intf_id.lx_symrep := std_id.lx_symrep;
    used_intf_id.sm_def := std_id;
    used_intf_id.lx_srcpos := M3CSrcPos.Null;
    AddInterface(unit, used_intf_id);
  END AddStandard;

PROCEDURE <A NAME="AddImports"><procedure>AddImports</procedure></A>(
    unit: M3AST_AS.UNIT_NORMAL;
    seqImported: SeqM3AST_AS_IMPORTED.T)
    RAISES {}=
  VAR
    iter : M3ASTNext.IterImportedId;
    used_interface_id: M3AST_AS.Used_interface_id;
  BEGIN
    iter := M3ASTNext.NewIterImportedId(seqImported);
    WHILE M3ASTNext.ImportedId(iter, used_interface_id) DO
      (* The case &quot;IMPORT I AS J; FROM J IMPORT ...&quot; will cause
         &quot;used_interface_id&quot; to bind to an &quot;Interface_AS_id&quot;, which
         doesnt provide any new information, so we ignore it here.
      *)
      TYPECASE used_interface_id.sm_def OF
      | NULL =&gt;
      | M3AST_AS.Interface_AS_id =&gt;
      | M3AST_AS.Interface_id =&gt;
          AddInterface(unit, used_interface_id);
      ELSE
      END; (* typecase *)
    END; (* while *)
  END AddImports;

BEGIN
END M3CImportS.
</PRE>
</inModule>
<HR>
<A NAME="x1">interface M3AST is in:
</A><UL>
<LI><A HREF="../ast/M3AST.i3.html#0TOP0">m3tk/src/ast/M3AST.i3</A>
<LI><A HREF="../../../m3tools/src/M3AST.i3.html#0TOP0">m3tools/src/M3AST.i3</A>
</UL>
<P>
<PRE>























</PRE>
</BODY>
</HTML>
