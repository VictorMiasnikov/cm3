<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: m3tk/src/target/M3CBackEnd_Int.mg</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>m3tk/src/target/M3CBackEnd_Int.mg</H2></A><HR>
<inModule>
<PRE>GENERIC MODULE <genericModule><A HREF="M3CBackEnd_Int.ig.html">M3CBackEnd_Int</A></genericModule>(IntType, WordType);
</PRE> Copyright (C) 1991, Digital Equipment Corporation           
 All rights reserved.                                        
 See the file COPYRIGHT for a full description.              

<P><PRE>IMPORT <A HREF="../ast/M3AST_AS.i3.html">M3AST_AS</A>, <A HREF="../ast/M3AST_SM.i3.html">M3AST_SM</A>, <A HREF="../sem/M3CStdProcs.i3.html">M3CStdProcs</A>;

IMPORT <A HREF="../sem/M3CBackEnd.i3.html">M3CBackEnd</A>, <A HREF="M3CBackEnd_C.i3.html">M3CBackEnd_C</A>, <A HREF="../sem/M3CWordProcs.i3.html">M3CWordProcs</A>;
IMPORT <A HREF="M3CBackEnd_Float_Real.i3.html">M3CBackEnd_Float_Real</A>, <A HREF="M3CBackEnd_Float_LongReal.i3.html">M3CBackEnd_Float_LongReal</A>,
       <A HREF="M3CBackEnd_Float_Extended.i3.html">M3CBackEnd_Float_Extended</A>;
IMPORT <A HREF="M3CBackEnd_Int_Integer.i3.html">M3CBackEnd_Int_Integer</A> AS Integer;

CONST
  False = VAL(ORD(FALSE), IntType.T);
  True = VAL(ORD(TRUE), IntType.T);
  Zero = VAL(0, IntType.T);

VAR
  small := ARRAY [0..255] OF T {NIL, ..};
  first := NEW(T, sm_value := FIRST(IntType.T));
  last := NEW(T, sm_value := LAST(IntType.T));

PROCEDURE <A NAME="New_value"><procedure>New_value</procedure></A>(i: IntType.T): T RAISES {} =
  BEGIN
    IF VAL(FIRST(small), IntType.T) &lt;= i
      AND i &lt;= VAL(LAST(small), IntType.T) THEN
      WITH result = small[VAL(i, INTEGER)] DO
        IF result = NIL THEN result := NEW(T, sm_value := i) END;
        RETURN result;
      END;
    ELSIF i = FIRST(IntType.T) THEN RETURN first;
    ELSIF i = LAST(IntType.T)  THEN RETURN last;
    ELSE                            RETURN NEW(T, sm_value := i);
    END;
  END New_value;

PROCEDURE <A NAME="UnaryOp"><procedure>UnaryOp</procedure></A>(
    op: M3AST_AS.UNARY;
    e: T;
    VAR (*out*) er: M3AST_SM.Exp_value)
  : M3CBackEnd.NumStatus RAISES {} =
  BEGIN
    TYPECASE op OF
    | M3AST_AS.Unaryplus =&gt;
        er := New_value(e.sm_value);
    | M3AST_AS.Unaryminus =&gt;
        er := New_value(-e.sm_value);
    | M3AST_AS.Not =&gt;
        er := Integer.New_value(ORD(e.sm_value = False));
    ELSE
        RETURN M3CBackEnd.NumStatus.Unknown;
    END; (* case *)
    RETURN M3CBackEnd.NumStatus.Valid;
  END UnaryOp;

PROCEDURE <A NAME="StdUnaryOp"><procedure>StdUnaryOp</procedure></A>(
    f: M3CStdProcs.Func;
    e: T;
    VAR (*out*) er: M3AST_SM.Exp_value;
    &lt;*UNUSED*&gt; it: M3AST_AS.INT_TYPE := NIL;
    ft: M3AST_AS.FLOAT_TYPE := NIL)
  : M3CBackEnd.NumStatus RAISES {} =
  VAR
    int: IntType.T := e.sm_value;
  BEGIN
    CASE f OF
    | M3CStdProcs.T.Abs =&gt;
        er := New_value(ABS(int));
    | M3CStdProcs.T.Float =&gt;
        TYPECASE ft OF &lt;*NOWARN*&gt;
        | M3AST_AS.Real_type =&gt;
            er := M3CBackEnd_Float_Real.New_value(FLOAT(int, REAL));
        | M3AST_AS.LongReal_type =&gt;
            er := M3CBackEnd_Float_LongReal.New_value(FLOAT(int, LONGREAL));
        | M3AST_AS.Extended_type =&gt;
            er := M3CBackEnd_Float_Extended.New_value(FLOAT(int, EXTENDED));
        END; (* typecase *)
    ELSE
        RETURN M3CBackEnd.NumStatus.Unknown;
    END; (* case *)
    RETURN M3CBackEnd.NumStatus.Valid;
  END StdUnaryOp;

PROCEDURE <A NAME="BinaryOp"><procedure>BinaryOp</procedure></A>(
    op: M3AST_AS.BINARY;
    e1, e2: T;
    VAR (*out*) er: M3AST_SM.Exp_value)
  : M3CBackEnd.NumStatus RAISES {} =
  VAR
    i1 := e1.sm_value;
    i2 := e2.sm_value;
    r: IntType.T;
    bool: BOOLEAN;
  BEGIN
    TYPECASE op OF
    | M3AST_AS.Plus =&gt;  r := i1 + i2;
    | M3AST_AS.Minus =&gt; r := i1 - i2;
    | M3AST_AS.Times =&gt; r := i1 * i2;
    | M3AST_AS.Div =&gt;
      IF i2 = Zero THEN RETURN M3CBackEnd.NumStatus.Overflow END;
      r := i1 DIV i2;
    | M3AST_AS.Mod =&gt;
      IF i2 = Zero THEN RETURN M3CBackEnd.NumStatus.Overflow END;
      r := i1 MOD i2;
    ELSE
      TYPECASE op OF
      | M3AST_AS.Eq =&gt;  bool := i1 = i2;
      | M3AST_AS.Ne =&gt;  bool := i1 # i2;
      | M3AST_AS.Gt =&gt;  bool := i1 &gt; i2;
      | M3AST_AS.Lt =&gt;  bool := i1 &lt; i2;
      | M3AST_AS.Ge =&gt;  bool := i1 &gt;= i2;
      | M3AST_AS.Le =&gt;  bool := i1 &lt;= i2;
      | M3AST_AS.And =&gt; bool := (i1 = True) AND (i2 = True);
      | M3AST_AS.Or =&gt;  bool := (i1 = True) OR (i2 = True);
      ELSE RETURN M3CBackEnd.NumStatus.Unknown;
      END; (* case *)
      er := Integer.New_value(ORD(bool));
      RETURN M3CBackEnd.NumStatus.Valid;
    END;
    er := New_value(r);
    RETURN M3CBackEnd.NumStatus.Valid;
  END BinaryOp;

PROCEDURE <A NAME="StdBinaryOp"><procedure>StdBinaryOp</procedure></A>(f: M3CStdProcs.Func;
    e1, e2: T;
    VAR (*out*) er: M3AST_SM.Exp_value)
  : M3CBackEnd.NumStatus RAISES {} =
  BEGIN
    CASE f OF &lt;*NOWARN*&gt;
    | M3CStdProcs.T.Min =&gt;
      IF e1.sm_value &lt; e2.sm_value
        THEN er := e1;
        ELSE er := e2;
      END;
    | M3CStdProcs.T.Max =&gt;
      IF e1.sm_value &gt; e2.sm_value
        THEN er := e1;
        ELSE er := e2;
      END;
    END; (* case *)
    RETURN M3CBackEnd.NumStatus.Valid;
  END StdBinaryOp;

PROCEDURE <A NAME="WordOp"><procedure>WordOp</procedure></A>(
    w: M3CWordProcs.T;
    READONLY args: ARRAY OF M3AST_SM.Exp_value;
    VAR (* out *) er: M3AST_SM.Exp_value)
  : M3CBackEnd.NumStatus RAISES {} =
  VAR
    a0 := NARROW(args[0], T).sm_value;
    r: IntType.T;
    bool: BOOLEAN;
  BEGIN
    CASE w OF
    | M3CWordProcs.T.Not =&gt;  r := WordType.Not(a0);
    | M3CWordProcs.T.Plus =&gt;
      r := WordType.Plus(a0, NARROW(args[1], T).sm_value);
    | M3CWordProcs.T.Times =&gt;
      r := WordType.Times(a0, NARROW(args[1], T).sm_value);
    | M3CWordProcs.T.Minus =&gt;
      r := WordType.Minus(a0, NARROW(args[1], T).sm_value);
    | M3CWordProcs.T.Divide =&gt;
      r := WordType.Divide(a0, NARROW(args[1], T).sm_value);
    | M3CWordProcs.T.Mod =&gt;
      r := WordType.Mod(a0, NARROW(args[1], T).sm_value);
    | M3CWordProcs.T.And =&gt;
      r := WordType.And(a0, NARROW(args[1], T).sm_value);
    | M3CWordProcs.T.Or =&gt;
      r := WordType.Or(a0, NARROW(args[1], T).sm_value);
    | M3CWordProcs.T.Xor =&gt;
      r := WordType.Xor(a0, NARROW(args[1], T).sm_value);
    | M3CWordProcs.T.Shift =&gt;
      r := WordType.Shift(a0, NARROW(args[1], Integer.T).sm_value);
    | M3CWordProcs.T.RightShift =&gt;
      r := WordType.RightShift(a0, NARROW(args[1], Integer.T).sm_value);
    | M3CWordProcs.T.Rotate =&gt;
      r := WordType.Rotate(a0, NARROW(args[1], Integer.T).sm_value);
    | M3CWordProcs.T.RightRotate =&gt;
      r := WordType.RightRotate(a0, NARROW(args[1], Integer.T).sm_value);
    | M3CWordProcs.T.Extract =&gt;
      WITH a1 = NARROW(args[1], Integer.T).sm_value,
           a2 = NARROW(args[2], Integer.T).sm_value DO
        IF a1 &lt; 0 OR a2 &lt; 0 OR a1 + a2 &gt; WordType.Size THEN
          RETURN M3CBackEnd.NumStatus.Unknown;
        END;
        r := WordType.Extract(a0, a1, a2);
      END;
    | M3CWordProcs.T.Insert =&gt;
      WITH a1 = NARROW(args[1], T).sm_value,
           a2 = NARROW(args[2], Integer.T).sm_value,
           a3 = NARROW(args[3], Integer.T).sm_value DO
        IF a2 &lt; 0 OR a3 &lt; 0 OR a2 + a3 &gt; WordType.Size THEN
          RETURN M3CBackEnd.NumStatus.Unknown;
        END;
        r := WordType.Insert(a0, a1, a2, a3);
      END;
    ELSE
      CASE w OF
      | M3CWordProcs.T.LT =&gt;
        bool := WordType.LT(a0, NARROW(args[1], T).sm_value);
      | M3CWordProcs.T.LE =&gt;
        bool := WordType.LE(a0, NARROW(args[1], T).sm_value);
      | M3CWordProcs.T.GT =&gt;
        bool := WordType.GT(a0, NARROW(args[1], T).sm_value);
      | M3CWordProcs.T.GE =&gt;
        bool := WordType.GE(a0, NARROW(args[1], T).sm_value);
      ELSE RETURN M3CBackEnd.NumStatus.Unknown;
      END;
      er := Integer.New_value(ORD(bool));
      RETURN M3CBackEnd.NumStatus.Valid;
    END;
    er := New_value(r);
    RETURN M3CBackEnd.NumStatus.Valid;
  END WordOp;

BEGIN
END M3CBackEnd_Int.
</PRE>
</inModule>
<PRE>























</PRE>
</BODY>
</HTML>
