<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: m3tools/src/M3Lexer.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>m3tools/src/M3Lexer.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright 1996-2000 Critical Mass, Inc. All rights reserved.    
 See file COPYRIGHT-CMASS for details. 

<P><PRE>MODULE <module><implements><A HREF="M3Lexer.i3.html">M3Lexer</A></implements></module>;

IMPORT <A HREF="../../libm3/src/rw/Rd.i3.html">Rd</A>, <A HREF="../../m3middle/src/Target.i3.html">Target</A>, <A HREF="../../m3middle/src/TInt.i3.html">TInt</A>, <A HREF="../../m3middle/src/TFloat.i3.html">TFloat</A>, <A HREF="../../m3middle/src/TWord.i3.html">TWord</A>;
IMPORT <A HREF="#x1">M3ID</A>, <A HREF="#x2">M3Scanner</A>, <A HREF="../../m3core/src/text/Text.i3.html">Text</A>;

FROM <A HREF="#x2">M3Scanner</A> IMPORT TK_Comment, TK_Error, TK_EOF,
  TK_Ident, TK_Card_const, TK_Long_const, TK_Real_const, TK_Longreal_const,
  TK_Extended_const, TK_Char_const, TK_Text_const,
  TK_Begin_pragma, TK_End_pragma, TK_Comma;

CONST
  Backslash = '\134';  (* ASCII / Unicode value *)

TYPE
  SUPER = M3Scanner.Default;

REVEAL
  <A NAME="T">T</A> = T_ BRANDED &quot;M3Lexer.T&quot; OBJECT
    saved        : BOOLEAN := FALSE;
    next_token   : CARDINAL  := TK_Comment;
    next_start   : CARDINAL  := 0;
    next_length  : CARDINAL  := 0;
    next_offset  : CARDINAL  := 0;
    next_line    : CARDINAL  := 0;
    next_column  : CARDINAL  := 0;
    next_msg     : TEXT      := NIL;
  OVERRIDES
    next        := NextToken;
    className   := ClassName;
    initFromRd  := InitFromRd;
    initFromBuf := InitFromBuf;
  END;

TYPE
  Pragma = REF RECORD
    id   : M3ID.T;
    val  : INTEGER;
    next : Pragma;
  END;

VAR
  pragmas: Pragma := NIL;

CONST
  CNames = ARRAY [TK_Inline .. TK_Fatal] OF TEXT {
    &quot;&lt;*INLINE*&gt;&quot;, &quot;&lt;*EXTERNAL*&gt;&quot;, &quot;&lt;*ASSERT*&gt;&quot;, &quot;&lt;*UNUSED*&gt;&quot;,
    &quot;&lt;*OBSOLETE*&gt;&quot;, &quot;&lt;*TRACE*&gt;&quot;, &quot;&lt;* calling convention *&gt;&quot;,
    &quot;&lt;*FATAL*&gt;&quot;
  };

PROCEDURE <A NAME="ClassName"><procedure>ClassName</procedure></A> (t: T;  tk: INTEGER): TEXT =
  BEGIN
    IF (FIRST (CNames) &lt;= tk) AND (tk &lt;= LAST (CNames))
      THEN RETURN CNames [tk];
      ELSE RETURN SUPER.className (t, tk);
    END;
  END ClassName;

PROCEDURE <A NAME="InitFromRd"><procedure>InitFromRd</procedure></A> (t: T;  rd: Rd.T): T =
  BEGIN
    pragmas := NIL;
    RETURN SUPER.initFromRd (t, rd,
             skip_comments := TRUE, split_pragmas := TRUE);
  END InitFromRd;

PROCEDURE <A NAME="InitFromBuf"><procedure>InitFromBuf</procedure></A> (t: T;  buf: Buf): T =
  BEGIN
    pragmas := NIL;
    RETURN SUPER.initFromBuf (t, buf,
             skip_comments := TRUE, split_pragmas := TRUE);
  END InitFromBuf;

PROCEDURE <A NAME="NextToken"><procedure>NextToken</procedure></A> (t: T) =
  BEGIN
    IF (t.saved) THEN
      t.saved  := FALSE;
      t.token  := t.next_token;
      t.start  := t.next_start;
      t.length := t.next_length;
      t.offset := t.next_offset;
      t.line   := t.next_line;
      t.column := t.next_column;
      t.msg    := t.next_msg;
    ELSE
      REPEAT
        SUPER.next (t);
        CASE t.token OF
        | TK_Ident          =&gt; FixID (t);
        | TK_Card_const     =&gt; FixInt (t, Target.Integer.bytes);
        | TK_Long_const     =&gt; FixInt (t, Target.Longint.bytes);
        | TK_Real_const     =&gt; FixFloat (t, Target.Precision.Short);
        | TK_Longreal_const =&gt; FixFloat (t, Target.Precision.Long);
        | TK_Extended_const =&gt; FixFloat (t, Target.Precision.Extended);
        | TK_Char_const     =&gt; FixChar (t);
        | TK_Text_const     =&gt; FixText (t);
        | TK_Begin_pragma   =&gt; FixPragma (t);
        ELSE (* let the token through... *)
        END;
      UNTIL (t.token # TK_Comment);
    END;
  END NextToken;

PROCEDURE <A NAME="FixID"><procedure>FixID</procedure></A> (t: T) =
  BEGIN
    t.id := M3ID.FromStr (SUBARRAY (t.buffer^, t.offset, t.length));
  END FixID;

PROCEDURE <A NAME="FixInt"><procedure>FixInt</procedure></A> (t: T;  bytes: CARDINAL) =
  VAR break := -1;  base: INTEGER;
  BEGIN
    FOR i := t.offset TO t.offset + t.length - 1 DO
      IF (t.buffer [i] = '_') THEN
        (* we have a based integer... *)
        break := i;
        EXIT;
      END;
    END;

    IF (break &lt; 0) THEN (* scan a simple integer *)
      IF NOT TInt.New (SUBARRAY (t.buffer^, t.offset, t.length), bytes, t.int) THEN
        Err (t, &quot;illegal integer literal&quot;);
      END;
    ELSIF NOT TInt.New (SUBARRAY (t.buffer^, t.offset, break - t.offset), bytes, t.int)
       OR NOT TInt.ToInt (t.int, base)
       OR (base &lt; 2) OR (16 &lt; base) THEN
      Err (t, &quot;illegal base for integer literal&quot;);
    ELSIF NOT TWord.New (SUBARRAY (t.buffer^, break+1,
                                   t.offset + t.length - break - 1),
                         base, bytes, t.int) THEN
      Err (t, &quot;illegal based integer literal&quot;);
    END;
  END FixInt;

PROCEDURE <A NAME="FixFloat"><procedure>FixFloat</procedure></A> (t: T;  prec: Target.Precision) =
  BEGIN
    IF NOT TFloat.New (SUBARRAY (t.buffer^, t.offset, t.length),
                       prec, t.float) THEN
      Err (t, &quot;illegal floating-point literal&quot;);
    END;
  END FixFloat;

PROCEDURE <A NAME="FixChar"><procedure>FixChar</procedure></A> (t: T) =
  VAR ch: CHAR;
  BEGIN
    ch := t.buffer [t.offset+1];
    IF (t.length = 3) THEN (* ok *)
    ELSIF ConvertEscape (t, t.offset+1, t.length-2, ch) THEN (* ok *)
    ELSE Err (t, &quot;illegal character literal&quot;);
    END;
    t.char := ORD (ch);
  END FixChar;

PROCEDURE <A NAME="FixText"><procedure>FixText</procedure></A> (t: T) =
  VAR
    len  := 0;
    cur  := t.offset + 1;        (* ignore the surrounding quotes *)
    stop := cur + t.length - 2;  (* ignore the surrounding quotes *)
    txt  := &quot;&quot;;
    ch   : CHAR;
    buf  : ARRAY [0..255] OF CHAR;
  BEGIN
    WHILE (cur &lt; stop) DO
      ch := t.buffer [cur];
      IF (ch = Backslash) THEN
        IF (cur+1 &lt; stop) AND ConvertEscape (t, cur, 2, ch) THEN
          INC (cur);
        ELSIF (cur+3 &lt; stop) AND ConvertEscape (t, cur, 4, ch) THEN
          INC (cur, 3);
        ELSE
          Err (t, &quot;unrecognized escape sequence in text literal&quot;);
          RETURN;
        END;
      END;
      IF (len &gt;= NUMBER (buf)) THEN
        txt := txt &amp; Text.FromChars (buf);
        len := 0;
      END;
      buf [len] := ch;  INC (len);
      INC (cur);
    END;
    IF (len &gt; 0) THEN
      txt := txt &amp; Text.FromChars (SUBARRAY (buf, 0, len));
    END;
    t.text :=txt;
  END FixText;

PROCEDURE <A NAME="ConvertEscape"><procedure>ConvertEscape</procedure></A> (t: T;  offs, len: INTEGER;  VAR ch: CHAR): BOOLEAN =
  VAR c0, c1, c2: CHAR;
  BEGIN
    IF t.buffer[offs] # Backslash THEN RETURN FALSE; END;

    IF (len = 2) THEN
      c0 := t.buffer [offs+1];
      IF    (c0 = 'n')       THEN ch := '\n';       RETURN TRUE;
      ELSIF (c0 = 't')       THEN ch := '\t';       RETURN TRUE;
      ELSIF (c0 = 'r')       THEN ch := '\r';       RETURN TRUE;
      ELSIF (c0 = 'f')       THEN ch := '\f';       RETURN TRUE;
      ELSIF (c0 = '\'')      THEN ch := '\'';       RETURN TRUE;
      ELSIF (c0 = '\&quot;')      THEN ch := '\&quot;';       RETURN TRUE;
      ELSIF (c0 = Backslash) THEN ch := Backslash;  RETURN TRUE;
      END;

    ELSIF (len = 4) THEN
      c0 := t.buffer [offs+1];
      c1 := t.buffer [offs+2];
      c2 := t.buffer [offs+3];
      IF    ('0' &lt;= c0) AND (c0 &lt;= '7')
        AND ('0' &lt;= c1) AND (c1 &lt;= '7')
        AND ('0' &lt;= c2) AND (c2 &lt;= '7') THEN
        ch := VAL ( (ORD (c0) - ORD ('0')) * 64
                  + (ORD (c1) - ORD ('0')) * 8
                  + (ORD (c2) - ORD ('0')), CHAR);
        RETURN TRUE;
      END;
    END;

    RETURN FALSE;
  END ConvertEscape;

PROCEDURE <A NAME="FixPragma"><procedure>FixPragma</procedure></A> (t: T) =
  VAR
    p: Pragma;
    save_start   : CARDINAL;
    save_length  : CARDINAL;
    save_offset  : CARDINAL;
    save_line    : CARDINAL;
    save_column  : CARDINAL;
  BEGIN
    (* just in case we need to back up, save the current token *)
    &lt;*ASSERT t.token = TK_Begin_pragma *&gt;
    save_start  := t.start;
    save_length := t.length;
    save_offset := t.offset;
    save_line   := t.line;
    save_column := t.column;

    (* get the ID that defines the pragma *)
    SUPER.next (t);

    IF t.token # TK_Ident THEN
      (* oops, it's a badly formed pragma *)
      t.saved       := TRUE;
      t.next_token  := t.token;
      t.next_start  := t.start;
      t.next_length := t.length;
      t.next_offset := t.offset;
      t.next_line   := t.line;
      t.next_column := t.column;
      t.next_msg    := t.msg;

      t.token  := TK_Begin_pragma;
      t.start  := save_start;
      t.length := save_length;
      t.offset := save_offset;
      t.line   := save_line;
      t.column := save_column;
      t.msg    := NIL;
      RETURN;
    END;

    t.id := M3ID.FromStr (SUBARRAY (t.buffer^, t.offset, t.length));

    p := LookUpPragma (t.id);

    IF (p = NIL) AND Target.FindConvention (M3ID.ToText (t.id)) # NIL THEN
      (* it's a legit calling convention on the current target *)
      t.token := TK_CallConv;
      AddPragma (M3ID.ToText (t.id), TK_CallConv);

    ELSIF (p = NIL) THEN
      (* unknown pragma, restore the begin_pragma/id token sequence... *)
      t.saved       := TRUE;
      t.next_token  := t.token;
      t.next_start  := t.start;
      t.next_length := t.length;
      t.next_offset := t.offset;
      t.next_line   := t.line;
      t.next_column := t.column;
      t.next_msg    := t.msg;

      t.token  := TK_Begin_pragma;
      t.start  := save_start;
      t.length := save_length;
      t.offset := save_offset;
      t.line   := save_line;
      t.column := save_column;
      t.msg    := NIL;

    ELSIF (p.val &gt; 0) THEN
      (* it's a recognized pragma to pass through *)
      t.token := p.val;

    ELSIF (p.val = IGNORE_PRAGMA) THEN
      EatPragma (t);

    ELSIF (p.val = LINE_PRAGMA) THEN
      EatPragma (t);  (* until we need something better... *)

    ELSIF (p.val = PRAGMA_PRAGMA) THEN
      SUPER.next (t);  (* PRAGMA *)
      WHILE (t.token = TK_Ident)
        OR ((M3Scanner.First_Keyword &lt;= t.token)
            AND (t.token &lt;= M3Scanner.Last_Keyword)) DO
        FixID (t);
        IF LookUpPragma (t.id) = NIL THEN
          AddPragma (M3ID.ToText (t.id), IGNORE_PRAGMA);
        END;
        SUPER.next (t);  (* Ident *)
        IF (t.token # TK_Comma) THEN EXIT END;
        SUPER.next (t); (* , *)
      END;
      IF (t.token # TK_End_pragma) THEN
        t.token := TK_Error;
        t.msg   := &quot;missing '*&gt;' on &lt;*PRAGMA*&gt; pragma&quot;;
      ELSE
        t.token := TK_Comment; (* fetch the next real token in the outer loop *)
      END;

    ELSE &lt;*ASSERT FALSE*&gt;
    END;

  END FixPragma;

PROCEDURE <A NAME="LookUpPragma"><procedure>LookUpPragma</procedure></A> (id: M3ID.T): Pragma =
  VAR p: Pragma;
  BEGIN
    IF (pragmas = NIL) THEN InitPragmas () END;
    p := pragmas;
    WHILE (p # NIL) AND (p.id # id) DO p := p.next; END;
    RETURN p;
  END LookUpPragma;

PROCEDURE <A NAME="EatPragma"><procedure>EatPragma</procedure></A> (t: T) =
  VAR key := t.id;  line := t.line;  offs := t.offset;  col := t.column;
  BEGIN
    LOOP
      IF (t.token = TK_End_pragma) THEN
        t.token := TK_Comment; (* so the top-level NextToken() cycles *)
        EXIT;
      ELSIF (t.token = TK_Error) THEN
        EXIT;
      ELSIF (t.token = TK_EOF) THEN
        t.token  := TK_Error;
        t.line   := line;
        t.offset := offs;
        t.column := col;
        t.msg := &quot;&lt;*&quot; &amp; M3ID.ToText(key) &amp; &quot;*&gt; pragma is missing its closing '*&gt;'&quot;;
        EXIT;
      ELSE
        SUPER.next (t);
      END;
    END;
  END EatPragma;

CONST
  IGNORE_PRAGMA = -1;
  PRAGMA_PRAGMA = -2;
  LINE_PRAGMA   = -3;

PROCEDURE <A NAME="InitPragmas"><procedure>InitPragmas</procedure></A> () =
  BEGIN
    AddPragma (&quot;TRACE&quot;,     TK_Trace);
    AddPragma (&quot;OBSOLETE&quot;,  TK_Obsolete);
    AddPragma (&quot;LINE&quot;,      LINE_PRAGMA);
    AddPragma (&quot;PRAGMA&quot;,    PRAGMA_PRAGMA);
    AddPragma (&quot;NOWARN&quot;,    IGNORE_PRAGMA);
    AddPragma (&quot;INLINE&quot;,    TK_Inline);
    AddPragma (&quot;UNUSED&quot;,    TK_Unused);
    AddPragma (&quot;FATAL&quot;,     TK_Fatal);
    AddPragma (&quot;EXTERNAL&quot;,  TK_External);
    AddPragma (&quot;ASSERT&quot;,    TK_Assert);
  END InitPragmas;

PROCEDURE <A NAME="AddPragma"><procedure>AddPragma</procedure></A> (txt: TEXT;  val: INTEGER) =
  BEGIN
    pragmas := NEW (Pragma, id := M3ID.Add (txt), val := val, next := pragmas);
  END AddPragma;

PROCEDURE <A NAME="Err"><procedure>Err</procedure></A> (t: T;  msg: TEXT) =
  BEGIN
    t.msg := msg &amp; &quot;: &quot; &amp; t.toText ();
    t.token := TK_Error;
  END Err;

BEGIN
END M3Lexer.
</PRE>
</inModule>
<HR>
<A NAME="x1">interface M3ID is in:
</A><UL>
<LI><A HREF="../../m3middle/src/M3ID.i3.html#0TOP0">m3middle/src/M3ID.i3</A>
<LI><A HREF="../../m3scan/src/M3ID.i3.html#0TOP0">m3scan/src/M3ID.i3</A>
</UL>
<P>
<HR>
<A NAME="x2">interface M3Scanner is in:
</A><UL>
<LI><A HREF="../../m3scan/src/M3Scanner.i3.html#0TOP0">m3scan/src/M3Scanner.i3</A>
<LI><A HREF="../../m3scanner/src/M3Scanner.i3.html#0TOP0">m3scanner/src/M3Scanner.i3</A>
</UL>
<P>
<PRE>























</PRE>
</BODY>
</HTML>
