<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: m3tools/src/M3Scope.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>m3tools/src/M3Scope.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright 1996-2000 Critical Mass, Inc. All rights reserved.    
 See file COPYRIGHT-CMASS for details. 

<P><PRE>MODULE <module><implements><A HREF="M3Scope.i3.html">M3Scope</A></implements></module>;

IMPORT <A HREF="#x1">M3ID</A>, <A HREF="#x2">M3AST</A>;

TYPE
  ScopeInfo = BRANDED &quot;M3Scope.ScopeInfo&quot; REF RECORD
    ast  : M3AST.T;
    n_scopes : CARDINAL;
    scopes   : REF ARRAY OF ScopeDesc;
    n_defns  : CARDINAL;
    defns    : REF ARRAY OF SymDef;
  END;

  ScopeDesc = RECORD
    loc    : Range;
    defn   : Range;
    parent : CARDINAL;  (* index into scopes, LAST(CARD) for root scope *)
  END;

  Range = RECORD start, stop: CARDINAL; END;

  SymDef = RECORD
    sym   : M3ID.T;
    class : Class;
    loc   : M3AST.NodeIndex;
    info  : REFANY;
  END;

PROCEDURE <A NAME="LookUp"><procedure>LookUp</procedure></A> (ast: M3AST.T;  loc: M3AST.NodeIndex;  sym: M3ID.T;
                  VAR(*OUT*) defn: Defn): BOOLEAN =
  VAR si: ScopeInfo;  sx: CARDINAL;
  BEGIN
    IF (loc &lt; 0) OR (loc &gt;= NUMBER (ast.nodes^)) THEN
      RETURN FALSE;
    END;

    IF (ast.scope_info = NIL) THEN Bind (ast); END;
    si := ast.scope_info;

    sx := FindScope (si, loc);
    WHILE (sx &lt; si.n_scopes) DO
      WITH scope = si.scopes [sx] DO
        FOR i := scope.defn.start TO scope.defn.stop DO
          WITH z = si.defns[i] DO
            IF (z.sym = sym) THEN
              defn.ast   := ast;
              defn.loc   := z.loc;
              defn.class := z.class;
              defn.info  := z.info;
              defn.uid   := i;
              RETURN TRUE;
            END;
          END;
        END;
        sx := scope.parent;
      END; (* WITH *)
    END;

    RETURN FALSE;
  END LookUp;

PROCEDURE <A NAME="FindScope"><procedure>FindScope</procedure></A> (si: ScopeInfo;  loc: M3AST.NodeIndex): CARDINAL =
  VAR
    lo  : CARDINAL := 0;
    hi  : CARDINAL := si.n_scopes;
    mid : CARDINAL;
  BEGIN
    IF (loc = 0) THEN
      (* skip the outermost scope that includes the imports *)
      IF    hi &gt; 1 THEN  loc := si.scopes[1].loc.start;
      ELSIF hi &gt; 0 THEN  loc := si.scopes[0].loc.start;
      END;
    END;

    (* find the first scope begining after &quot;loc&quot; *)
    WHILE (lo &lt; hi) DO
      mid := (lo + hi) DIV 2;
      IF loc &lt; si.scopes[mid].loc.start
        THEN hi := mid;
        ELSE lo := mid + 1;
      END;
    END;

    (* search backwards until we find a scope containing &quot;loc&quot; *)
    FOR i := MIN (lo, si.n_scopes-1) TO 0 BY -1 DO
      WITH z = si.scopes [i] DO
        IF (z.loc.start &lt;= loc) AND (loc &lt;= z.loc.stop) THEN RETURN i; END;
      END;
    END;

    (* failed, return an out-of-range index *)
    RETURN si.n_scopes;
  END FindScope;

PROCEDURE <A NAME="Define"><procedure>Define</procedure></A> (READONLY defn: Defn;  info: REFANY) =
  VAR si: ScopeInfo;  ast := defn.ast;
  BEGIN
    IF (ast.scope_info = NIL) THEN Bind (ast); END;
    si := ast.scope_info;
    &lt;*ASSERT (0 &lt;= defn.uid) AND (defn.uid &lt; si.n_defns) *&gt;
    si.defns[defn.uid].info := info;
  END Define;
</PRE>----------------------------------------------------- scope construction ---

<P><PRE>TYPE
  BindState = RECORD
    ast     : M3AST.T;
    si      : ScopeInfo;
    n_temps : CARDINAL;
    temps   : REF ARRAY OF SymDef;
  END;

PROCEDURE <A NAME="Bind"><procedure>Bind</procedure></A> (ast: M3AST.T) =
  VAR s: BindState;
  BEGIN
    s.ast         := ast;
    s.si          := NEW (ScopeInfo);
    s.si.ast      := ast;
    s.si.n_scopes := 0;
    s.si.scopes   := NEW (REF ARRAY OF ScopeDesc, 16);
    s.si.n_defns  := 1;
    s.si.defns    := NEW (REF ARRAY OF SymDef, 32);
    s.n_temps     := 0;
    s.temps       := NEW (REF ARRAY OF SymDef, 32);

    BindScope (s, 0, NUMBER (ast.nodes^), LAST (CARDINAL));
    ast.scope_info := s.si;
  END Bind;

PROCEDURE <A NAME="BindScope"><procedure>BindScope</procedure></A> (VAR s: BindState;  start, stop: M3AST.NodeIndex;
                     parent: CARDINAL) =
  VAR
    self := s.si.n_scopes;
    si   := s.si;
  BEGIN
    (* push the new scope *)
    IF si.n_scopes &gt;= NUMBER (si.scopes^) THEN ExpandScopes (si); END;
    WITH z = si.scopes [self] DO
      z.loc.start  := start;
      z.loc.stop   := stop-1;
      z.defn.start := s.n_temps;
      z.defn.stop  := s.n_temps;
      z.parent     := parent;
    END;
    INC (si.n_scopes);

    BindNodes (s, start, stop, self);

    (* pop the new scope &amp; all of its definitions *)
    WITH z = si.scopes [self], cnt = s.n_temps - z.defn.start DO
      IF (cnt &gt; 0) THEN
        WHILE (si.n_defns+cnt &gt;= NUMBER (si.defns^)) DO ExpandDefns (si); END;
        SUBARRAY (si.defns^, si.n_defns, cnt)
          := SUBARRAY (s.temps^, z.defn.start, cnt);
      END;
      s.n_temps := z.defn.start;
      z.defn.start := si.n_defns;
      z.defn.stop  := si.n_defns + cnt - 1;
      INC (si.n_defns, cnt);
    END;
  END BindScope;

PROCEDURE <A NAME="BindNodes"><procedure>BindNodes</procedure></A> (VAR s: BindState;  start, stop: M3AST.NodeIndex;
                     self: CARDINAL) =
  VAR x:= start;  op: M3AST.OP;
  BEGIN
    WHILE (x &lt; stop) DO
      WITH z = s.ast.nodes[x] DO
        op := z.op;  (* force a range check here so we know the CASE
                        is handling all the legal values *)
        CASE op OF
        | M3AST.OP_Block       =&gt; BindScope (s, x+1, x + z.width, self);
        | M3AST.OP_GenericArg  =&gt; PushDefn (s, x, z.info, Class.GenericArg);
        | M3AST.OP_Import      =&gt; PushDefn (s, x, z.info, Class.Import);
        | M3AST.OP_ImportAs    =&gt; PushDefn (s, x, z.info, Class.Import);
        | M3AST.OP_FromImport  =&gt; PushDefn (s, x, z.info, Class.Import);
        | M3AST.OP_ConstDecl   =&gt; PushDefn (s, x, z.info, Class.Const);
        | M3AST.OP_TypeDecl    =&gt; PushDefn (s, x, z.info, Class.Type);
        | M3AST.OP_OpaqueDecl  =&gt; PushDefn (s, x, z.info, Class.Type);
        | M3AST.OP_ExceptDecl  =&gt; PushDefn (s, x, z.info, Class.Exception);
        | M3AST.OP_VarDefn     =&gt; PushDefn (s, x, z.info, Class.Var);
        | M3AST.OP_FormalDefn  =&gt; PushDefn (s, x, z.info, Class.Formal);

        | M3AST.OP_ProcDecl    =&gt; PushDefn (s, x, z.info, Class.Procedure);
                                  BindScope (s, x+1, x + z.width, self);

        | M3AST.OP_For1, M3AST.OP_ForN, M3AST.OP_TryHandlerVar,
          M3AST.OP_TypeCaseVar, M3AST.OP_With =&gt;
            IF (start = x)
              THEN PushDefn (s, x, z.info, Class.Var);
              ELSE BindScope (s, x, x + z.width, self);
            END;

        | M3AST.OP_Unit, M3AST.OP_Generic, M3AST.OP_GenInstance, M3AST.OP_VarDecl,
          M3AST.OP_Case, M3AST.OP_CaseBranch, M3AST.OP_CaseElse,
          M3AST.OP_If, M3AST.OP_IfClause, M3AST.OP_IfElse, M3AST.OP_Lock,
          M3AST.OP_Loop, M3AST.OP_Repeat, M3AST.OP_TryFinally,
          M3AST.OP_TryExcept, M3AST.OP_TryHandler, M3AST.OP_TryElse,
          M3AST.OP_TypeCase, M3AST.OP_TypeCaseArm, M3AST.OP_TypeCaseElse,
          M3AST.OP_While, M3AST.OP_ProcType, M3AST.OP_Formal, M3AST.OP_StmtList =&gt;
            (* visit the subtrees *)
            BindNodes (s, x + 1, x + z.width, self);

        | M3AST.OP_Empty, M3AST.OP_Export,
          M3AST.OP_Reveal, M3AST.OP_RevealPartial,
          M3AST.OP_Assign, M3AST.OP_Assert, M3AST.OP_CallStmt,
          M3AST.OP_CaseLabel, M3AST.OP_CaseRange, M3AST.OP_Exit,
          M3AST.OP_Eval, M3AST.OP_Raise, M3AST.OP_RaiseValue,
          M3AST.OP_Return, M3AST.OP_ReturnValue, M3AST.OP_Array,
          M3AST.OP_OpenArray, M3AST.OP_Enum, M3AST.OP_EnumDefn,
          M3AST.OP_NamedType, M3AST.OP_Method, M3AST.OP_Override,
          M3AST.OP_Packed, M3AST.OP_Field, M3AST.OP_FieldDefn,
          M3AST.OP_Raises, M3AST.OP_RaisesAny, M3AST.OP_Object,
          M3AST.OP_NoBrand, M3AST.OP_DefaultBrand, M3AST.OP_Record,
          M3AST.OP_Ref, M3AST.OP_Root, M3AST.OP_Set, M3AST.OP_Subrange,
          M3AST.OP_UntracedRef, M3AST.OP_UntracedRoot,
          M3AST.OP_Or .. M3AST.OP_Qualify,          (* expr operators *)
          M3AST.OP_Id ..M3AST.OP_Text,              (* literals *)
          M3AST.OP_Attributes .. M3AST.OP_FatalAny  (* pragmas *)
          =&gt;
            (* skip this node and any subtrees *)

        END; (* CASE *)
        x := x + z.width;
      END;
    END;
  END BindNodes;

PROCEDURE <A NAME="PushDefn"><procedure>PushDefn</procedure></A> (VAR s: BindState;  loc: M3AST.NodeIndex;
                    sym: M3ID.T;  class: Class) =
  BEGIN
    IF (s.n_temps &gt;= NUMBER (s.temps^)) THEN ExpandTemps (s); END;
    WITH z = s.temps [s.n_temps] DO
      &lt;*ASSERT sym # M3ID.NoID *&gt;
      z.sym   := sym;
      z.class := class;
      z.loc   := loc;
    END;
    INC (s.n_temps);
  END PushDefn;

PROCEDURE <A NAME="ExpandScopes"><procedure>ExpandScopes</procedure></A> (si: ScopeInfo) =
  VAR n := NUMBER (si.scopes^);  xx := NEW (REF ARRAY OF ScopeDesc, n+n);
  BEGIN
    SUBARRAY (xx^, 0, n) := si.scopes^;
    si.scopes := xx;
  END ExpandScopes;

PROCEDURE <A NAME="ExpandDefns"><procedure>ExpandDefns</procedure></A> (si: ScopeInfo) =
  VAR n := NUMBER (si.defns^);  xx := NEW (REF ARRAY OF SymDef, n+n);
  BEGIN
    SUBARRAY (xx^, 0, n) := si.defns^;
    si.defns := xx;
  END ExpandDefns;

PROCEDURE <A NAME="ExpandTemps"><procedure>ExpandTemps</procedure></A> (VAR s: BindState) =
  VAR n := NUMBER (s.temps^);  xx := NEW (REF ARRAY OF SymDef, n+n);
  BEGIN
    SUBARRAY (xx^, 0, n) := s.temps^;
    s.temps := xx;
  END ExpandTemps;

BEGIN
END M3Scope.
</PRE>
</inModule>
<HR>
<A NAME="x1">interface M3ID is in:
</A><UL>
<LI><A HREF="../../m3middle/src/M3ID.i3.html#0TOP0">m3middle/src/M3ID.i3</A>
<LI><A HREF="../../m3scan/src/M3ID.i3.html#0TOP0">m3scan/src/M3ID.i3</A>
</UL>
<P>
<HR>
<A NAME="x2">interface M3AST is in:
</A><UL>
<LI><A HREF="../../m3tk/src/ast/M3AST.i3.html#0TOP0">m3tk/src/ast/M3AST.i3</A>
<LI><A HREF="M3AST.i3.html#0TOP0">m3tools/src/M3AST.i3</A>
</UL>
<P>
<PRE>























</PRE>
</BODY>
</HTML>
