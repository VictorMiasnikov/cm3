<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: webcat/src/HTTPCat.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>webcat/src/HTTPCat.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright (C) 1994, Digital Equipment Corporation. 
 All rights reserved. 
 Last modified on Tue Aug 27 16:00:14 PDT 1996 by steveg   
      modified on Tue Jul 25 15:59:38 PDT 1995 by glassman 
 modified on Thu Sep 29 12:18:36 PDT 1994 by mhb 

<P><PRE>MODULE <module>HTTPCat</module> EXPORTS <A HREF="../../m3core/src/main/Main.i3.html"><implements>Main</A></implements>;
IMPORT <A HREF="../../http/src/App.i3.html">App</A>, <A HREF="../../m3core/src/float/IEEE-default/FloatMode.i3.html">FloatMode</A>, <A HREF="../../libm3/src/fmtlex/Fmt.i3.html">Fmt</A>, <A HREF="../../http/src/HTTP.i3.html">HTTP</A>, <A HREF="../../http/src/HTTPApp.i3.html">HTTPApp</A>, <A HREF="../../libm3/src/fmtlex/Lex.i3.html">Lex</A>, <A HREF="../../libm3/src/rw/Rd.i3.html">Rd</A>, <A HREF="../../libm3/src/rw/Stdio.i3.html">Stdio</A>, <A HREF="../../m3core/src/text/Text.i3.html">Text</A>, <A HREF="../../libm3/src/rw/TextRd.i3.html">TextRd</A>,
       <A HREF="../../m3core/src/thread/Common/Thread.i3.html">Thread</A>, <A HREF="../../libm3/src/rw/Wr.i3.html">Wr</A>;

VAR
  method := HTTP.Method.Get;
  noCache := FALSE;
  post: TEXT := NIL;
  url: TEXT := NIL;
  fieldName: TEXT := NIL;
  fieldValue: TEXT := NIL;
  authName, authPassword: TEXT := NIL;
  proxy: HTTPApp.Proxy;
  version := HTTP.Version1_1;

TYPE
  Arg = {Auth, Field, Head, NoCache, Post, URL, Version};

  ArgHandler = App.ArgHandler OBJECT
  OVERRIDES
    set := SetArg;
  END;

CONST
  NonColon = SET OF CHAR{'\000'..'\377'} - SET OF CHAR{':'};

PROCEDURE <A NAME="SetArg"><procedure>SetArg</procedure></A> (self : ArgHandler;
                  src  : App.ArgSource;
                  value: TEXT;
                  log  : App.Log        ) RAISES {App.Error} =
  BEGIN
    CASE VAL(self.id, Arg) OF
    | Arg.Head =&gt;
        IF src # App.ArgSource.Default THEN method := HTTP.Method.Head END;
    | Arg.NoCache =&gt; noCache := src # App.ArgSource.Default;
    | Arg.Field =&gt;
        TRY
          WITH trd = TextRd.New(value) DO
            fieldName := Lex.Scan(trd);
            Lex.Skip(trd);
            fieldValue := Lex.Scan(trd);
          END;
        EXCEPT
        | Rd.Failure, Thread.Alerted =&gt;
            log.log(
              Fmt.F(&quot;Bad field argument: %s&quot;, value), App.LogStatus.Error);
        END;
    | Arg.Version =&gt;
        TRY
          WITH trd = TextRd.New(value) DO
            version.major := Lex.Int(trd);
            EVAL Rd.GetChar(trd);
            version.minor := Lex.Int(trd);
          END;
        EXCEPT
        | FloatMode.Trap, Lex.Error, Rd.EndOfFile, Rd.Failure, Thread.Alerted =&gt;
            log.log(Fmt.F(&quot;Bad version argument: %s&quot;, value),
                    App.LogStatus.Error);
        END;
    | Arg.Auth =&gt;
        IF src # App.ArgSource.Default THEN
          TRY
            WITH trd = TextRd.New(value) DO
              authName := Lex.Scan(trd, NonColon);
              EVAL Rd.GetChar(trd);
              authPassword := Rd.GetText(trd, LAST(INTEGER));
            END;
          EXCEPT
          | Rd.EndOfFile, Rd.Failure, Thread.Alerted =&gt;
              log.log(
                Fmt.F(&quot;Bad auth argument: %s&quot;, value), App.LogStatus.Error);
          END;
        END;
    | Arg.Post =&gt;
        IF src # App.ArgSource.Default THEN
          method := HTTP.Method.Post;
          post := value;
        END;
    | Arg.URL =&gt;
        url := value;
        IF src = App.ArgSource.Default AND value = NIL THEN
          log.log(&quot;Must give URL&quot;, App.LogStatus.Error);
        END;
    END;
  END SetArg;

TYPE
  ReplyHandler = HTTPApp.ReplyHandler OBJECT
  OVERRIDES
    reply := Reply;
  END;

PROCEDURE <A NAME="Reply"><procedure>Reply</procedure></A>(&lt;* UNUSED *&gt; self: HTTPApp.ReplyHandler; reply: HTTP.Reply;
                rd: Rd.T; wr: Wr.T; log: App.Log)
          RAISES {App.Error} =
  BEGIN
    IF App.Debug() OR method = HTTP.Method.Head THEN
      reply.write(wr, HTTP.DefaultStyle(version), log);
    END;
    IF App.Debug() OR method # HTTP.Method.Head THEN
      HTTP.WriteBody(reply, wr, NEW(HTTP.RdSrc).init(rd), log);
    END;
  END Reply;

BEGIN
  EVAL NEW(ArgHandler, id := ORD(Arg.Head), hasParam := FALSE).init(
         switchName := &quot;head&quot;);
  EVAL NEW(ArgHandler, id := ORD(Arg.NoCache), hasParam := FALSE).init(
         switchName := &quot;noCache&quot;);
  EVAL NEW(ArgHandler, id := ORD(Arg.Post), hasParam := FALSE).init(
         switchName := &quot;post&quot;);
  EVAL
    NEW(ArgHandler, id := ORD(Arg.Version), paramName := &quot;&lt;major.minor&gt;&quot;,
        default := &quot;1.1&quot;).init(switchName := &quot;version&quot;);
  EVAL NEW(ArgHandler, id := ORD(Arg.Field),
           paramName := &quot;&lt;HTTP field: value&gt;&quot;).init(switchName := &quot;field&quot;);
  EVAL
    NEW(
      ArgHandler, id := ORD(Arg.Auth), paramName := &quot;&lt;name:password&gt;&quot;).init(
      switchName := &quot;auth&quot;);
  EVAL NEW(ArgHandler, id := ORD(Arg.URL), hasParam := FALSE).init(
         switchName := App.AnyArgument);

  TRY
    App.InitializeArguments(App.defaultLog, &quot;/proj/m3/pkg/webcat/config&quot;, FALSE);
    proxy := HTTPApp.DefaultProxy();
    HTTP.SetProgramInfo(
      HTTP.ProgramInfo{type := HTTP.ProgramType.Client, name :=
                       &quot;webcat/1.1 (SRC Modula-3)&quot;});
    VAR
      rd       : Rd.T         := NIL;
      request  : HTTP.Request;
      urlParsed: HTTP.URL;
    BEGIN
      TRY
        urlParsed := NEW(HTTP.URL).init(url, App.nullLog);
        IF Text.Length(urlParsed.host) = 0 THEN RAISE App.Error(NIL) END;
      EXCEPT
      | App.Error =&gt;
          TRY
            (* try again, to handle www.foo.bar/xxx URL's *)
            urlParsed := NEW(HTTP.URL).init(&quot;http://&quot; &amp; url, App.nullLog);
          EXCEPT
          | App.Error =&gt;
              (* still no go, then complain about the original *)
              urlParsed := NEW(HTTP.URL).init(url, App.defaultLog);
          END;
      END;

      request := NEW(HTTP.Request, method := method, url := urlParsed);
      IF noCache THEN
        EVAL
          request.addField(
            NEW(HTTP.Field).init(name := &quot;Pragma&quot;, value := &quot;no-cache&quot;));
      END;
      IF fieldName # NIL AND Text.Length(fieldName) # 0 THEN
        EVAL request.addField(NEW(HTTP.Field).init(
                                name := fieldName, value := fieldValue));
      END;

      IF authName # NIL THEN
        EVAL request.addField(
               HTTP.BasicAuthField(
                 authName &amp; &quot;:&quot; &amp; authPassword, HTTP.AuthType.Server));
      END;

      IF method = HTTP.Method.Post THEN
        rd := TextRd.New(post);
      ELSE
        rd := TextRd.New(&quot;&quot;);
      END;

      IF App.Verbose() THEN
        request.write(
          Stdio.stderr, HTTP.DefaultStyle(version), FALSE, App.defaultLog);
      END;

      HTTPApp.Client(request, proxy, HTTP.DefaultStyle(version), rd,
                     Stdio.stdout, NEW(ReplyHandler),
                     HTTPApp.AnyService, App.defaultLog);

    END;
  EXCEPT
  | App.Error =&gt;
  END;
END HTTPCat.
</PRE>
</inModule>
<PRE>























</PRE>
</BODY>
</HTML>
